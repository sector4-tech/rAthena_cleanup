//===== rAthena Script =======================================
//= War of Emperium (Geffen) - Yesnelph
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= NPCs that relate to Geffen Guild Castle 3
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Removed dialog from interior flags. [L0ne_W0lf]
//= 1.2 Partially rewrote Script. [Masao]
//============================================================

// Guild Flag (Outside Castle)
//============================================================
-	script	Yesnelph#1::OutsideFlagsG3	722,{

	set .@GID, GetCastleData("gefg_cas03",1);

	if (.@GID == 0) {
		mes "[ คำสั่งของอาณาจักรรูนศักดิ์สิทธิ์-มิดการ์ทส์ ]";
		mes " ";
		mes "1. เราประกาศว่าไม่มีผู้ปกครองอย่างเป็นทางการของปราสาทแห่งนี้";
		mes " ";
		mes "2. สำหรับผู้ที่สามารถเอาชนะการทดลองทั้งหมดและทำลาย Emperium ได้ กษัตริย์จะมอบสิทธิ์การครอบครองปราสาทให้กับผู้นั้น";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[เสียงสะท้อน]";
		mes "ท่านผู้กล้า...";
		mes "ท่านปรารถนาที่จะกลับไปยังตำแหน่งอันมีเกียรติของท่านหรือไม่?";
		next;
		if(select("กลับสู่ปราสาทกิลด์:ออก") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("gefg_cas03",1)) {
				warp "gefg_cas03",221,30;
				end;
			}
			end;
		}
		close;
	}
	mes "[ คำสั่งของอาณาจักรรูนศักดิ์สิทธิ์-มิดการ์ทส์ ]";
	mes " ";
	mes "1. ปฏิบัติตามคำสั่งของอาณาจักรมิดการ์ตรูนศักดิ์สิทธิ์ เรารับรองว่าสถานที่นี้เป็นกรรมสิทธิ์ส่วนตัวของ ^ff0000"+GetGuildName(.@GID)+"^000000 Guild.";
	mes " ";
	mes "2. หัวหน้ากิลด์ของ ^ff0000"+GetGuildName(.@GID)+"^000000 คือ ^ff0000"+GetGuildMaster(.@GID)+"^000000 หากมีผู้ใดคัดค้านเรื่องนี้ จงพิสูจน์ความแข็งแกร่งและเกียรติยศของคุณด้วยดาบเหล็กในมือของคุณ";
	close;

OnRecvCastleG03:
	FlagEmblem GetCastleData("gefg_cas03",1);
	end;
}

gef_fild13,78,182,4	duplicate(OutsideFlagsG3)	Yesnelph#1-2	722
gef_fild13,87,182,4	duplicate(OutsideFlagsG3)	Yesnelph#2	722
gef_fild13,73,295,7	duplicate(OutsideFlagsG3)	Yesnelph#3	722
gef_fild13,113,274,7	duplicate(OutsideFlagsG3)	Yesnelph#4	722
gef_fild13,144,235,6	duplicate(OutsideFlagsG3)	Yesnelph#5	722
gef_fild13,144,244,6	duplicate(OutsideFlagsG3)	Yesnelph#6	722

// Guild Flag (Inside Castle)
//============================================================
-	script	Yesnelph#7::InsideFlagsG3	722,{
	end;

OnRecvCastleG03:
	FlagEmblem GetCastleData("gefg_cas03",1);
	end;
}

gefg_cas03,122,220,6	duplicate(InsideFlagsG3)	Yesnelph#7-2	722
gefg_cas03,122,229,6	duplicate(InsideFlagsG3)	Yesnelph#8	722
gefg_cas03,91,257,7	duplicate(InsideFlagsG3)	Yesnelph#9	722
gefg_cas03,52,276,7	duplicate(InsideFlagsG3)	Yesnelph#10	722
gefg_cas03,56,164,4	duplicate(InsideFlagsG3)	Yesnelph#11	722
gefg_cas03,65,164,4	duplicate(InsideFlagsG3)	Yesnelph#12	722
gefg_cas03,37,214,1	duplicate(InsideFlagsG3)	Yesnelph#13	722
gefg_cas03,34,208,1	duplicate(InsideFlagsG3)	Yesnelph#14	722

// Guild Flag (Town)
//============================================================
geffen,120,132,8	duplicate(InsideFlagsG3)	Yesnelph#15	722

// Kafra Employee
//============================================================
gefg_cas03,116,89,5	duplicate(guildkafra)	Kafra Staff#gefg_cas03	117

// Steward
//============================================================
gefg_cas03,106,23,5	duplicate(cm)	Jyang#gefg_cas03	55

// Treasure Room Exit
//============================================================
gefg_cas03,275,289,0	script	#lever_G03	111,{
	mes " ";
	mes "มีคันโยกเล็กๆ อยู่ค่ะ ช่วยดึงหน่อยได้ไหมค่ะ";
	next;
	if(select("ดึง : อย่า.") == 1) {
		close2;
		warp "gefg_cas03",106,24;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
gefg_cas03,221,43,0	duplicate(gdlever)	#gefg_cas03	111

// Guild Treasure Spawns
//============================================================
gefg_cas03,271,290,0	duplicate(Gld_Trea_Spawn)	Treasure#gefg_cas03	-1

// AGIT Manager
//============================================================
gefg_cas03,245,167,0	duplicate(Gld_Agit_Manager)	Agit#gefg_cas03	-1

// Guardians
//============================================================
gefg_cas03,106,23,5	duplicate(Gld_Guard_Template)	Guardian#gefg_cas03	-1

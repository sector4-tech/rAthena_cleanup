//===== rAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 3.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=     Removed all functions as they are no longer required.
//=     Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=     scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 3.0 Added quest log commands. [L0ne_W0lf]
//= 3.1 Added Izlude Renewal coordinates.
//============================================================ 

valkyrie,48,86,4	script	Valkyrie#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "ยินดีต้อนรับสู่วัลฮัลลา หอเกียรติยศ"; 
		next;
		mes "[Valkyrie]";
		mes "โปรดทำให้ตัวเองรู้สึกสบายขณะที่อยู่ที่นี่"; 
		mes "ขอเกียรติแด่เหล่านักรบ!";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "ยินดีต้อนรับสู่วัลฮัลลา หอเกียรติยศ";
		next;
		mes "[Valkyrie]";
		mes "บัดนี้เจ้าจะต้องจบชีวิตในปัจจุบันของเจ้าและเริ่มต้นชีวิตใหม่โดยสิ้นเชิง";
		mes "ขอแสดงความเคารพต่อเหล่านักรบ!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valkyrie]";
			mes "มีบางสิ่งที่คุณต้องทำก่อนที่เราจะเริ่ม คุณต้องปล่อยใจและร่างกายของคุณให้ว่างเปล่าเสียก่อน";
			mes "เกียรติยศจะมาถึงเมื่อคุณละทิ้งความปรารถนาเห็นแก่ตัวทั้งหมดของคุณ...";
			next;
			mes "[Valkyrie]";
			mes "คุณไม่สามารถนำสิ่งใดติดตัวไปยังชีวิตหน้าได้ สิ่งของ เซนี สัตว์เลี้ยง และรถเข็นของคุณต้องทิ้งไว้เบื้องหลัง";
			next;
			mes "[Valkyrie]";
			mes "เมื่อคุณพร้อมแล้ว โปรดกลับมาหาฉัน นักผจญภัยที่กล้าหาญ";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "ข้าพเจ้าเห็นท่านได้ละจากความยึดติดทางโลกทั้งปวงแล้ว";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[Valkyrie]";
			mes "นั่นเป็นทัศนคติที่น่าชื่นชมสำหรับนักผจญภัยเช่นคุณ เกียรติยศมาถึงเมื่อคุณละทิ้งความปรารถนาส่วนตัวทั้งหมดเพื่อประโยชน์ของมนุษยชาติ";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "อืม... ฉันรู้สึกว่าคุณยังมีความผูกพันหรือเรื่องที่ยังค้างคาอยู่ในชีวิตปัจจุบันของคุณอยู่ จัดการเรื่องนั้นซะ และปิดฉากชีวิตปัจจุบันของคุณซะ";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "ตอนนี้ ขอให้ฉันลบความทรงจำทั้งหมดของคุณออกไป...";
			mes "อย่างไรก็ตาม คุณจะสามารถจดจำช่วงเวลาอันน่ายกย่องที่สุดในชีวิตนี้ได้";
			next;
			mes "[Valkyrie]";
			mes "ด้วยหนึ่งเดียว ฉันจะขอให้เทพธิดา Urd ลบความทรงจำทั้งหมดของคุณออกไป";
			next;
			mes "[Valkyrie]";
			mes "ด้วยสองเดียว ฉันจะขอให้เทพธิดา Verdandi เก็บและบันทึกช่วงเวลาอันน่ายกย่องที่สุดในชีวิตปัจจุบันของคุณไว้";
			next;
			mes "[Valkyrie]";
			mes "ด้วยสามเดียว ฉันจะขอให้เทพธิดา Skuld นำทางคุณไปสู่ชีวิตต่อไป";
			next;
			mes "[Valkyrie]";
			mes "หนึ่ง...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "หนึ่ง...";
			mes "สอง......";
			next;
			mes "[Valkyrie]";
			mes "หนึ่ง...";
			mes "สอง......";
			mes "และสาม.";
			set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST & ~1024;//<-reset Skill Reset Event
			skill "NV_FIRSTAID",1,SKILL_PERM;
			skill "NV_TRICKDEAD",1,SKILL_PERM;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "ขอแสดงความยินดีด้วย";
			mes "ตอนนี้คุณได้เกิดใหม่ในชีวิตใหม่แล้ว";
			mes "โปรดรับของขวัญเล็กๆ น้อยๆ เหล่านี้เพื่อเตรียมพร้อมสำหรับการผจญภัยครั้งใหม่ของคุณ";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[Valkyrie]";
			mes "ข้าหวังว่าการปลดปล่อยที่เทพีอูร์ดมอบให้เจ้าจะเป็นพรแก่เจ้า ข้าหวังว่าความทรงจำที่แวร์ดานดีบันทึกไว้จะให้เกียรติเจ้าตลอดไป";
			next;
			mes "[Valkyrie]";
			mes "และข้าขออธิษฐานให้ชีวิตใหม่ที่เทพีสกัลด์จะนำทางเจ้าจะมีเกียรติยิ่งกว่าชีวิตเก่าของเจ้า";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (checkre(0)) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "ยินดีต้อนรับสู่วัลฮัลลา ห้องโถงแห่งเกียรติยศ";
		next;
		mes "[Valkyrie]";
		mes "ขออภัย คุณยังไม่ได้รับเชิญมาที่นี่ ฉันขอให้คุณรีบออกไปทันที ขอแสดงความเคารพต่อเหล่านักรบ!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Metheus Sylphe#Library	742,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Metheus Sylphe]";
			mes "ยินดีต้อนรับสู่ห้องสมุดของ Schweicherbil Magic Academy";
			mes "ฉันคิดว่าคุณมาที่นี่เพื่ออ่าน 'Book of Ymir'";
			next;
			mes "[Metheus Sylphe]";
			mes "ขออภัย สำเนาต้นฉบับของหนังสือได้รับความเสียหายตามกาลเวลา ปัจจุบันเราอนุญาตให้สาธารณชนดูสำเนาหนังสือเท่านั้น";
			next;
			mes "[Metheus Sylphe]";
			mes "นอกจากนี้ เพื่อรักษา 'Book of Ymir' ต้นฉบับไว้ เราจึงตัดสินใจที่จะรับบริจาคจากผู้ที่ต้องการอ่านสำเนาที่เรามอบให้";
			next;
			mes "[Metheus Sylphe]";
			mes "จำนวนเงินบริจาคที่แนะนำคือ";
			mes "1,285,000 zeny.";
			next;
			if (select("บริจาค:ยกเลิก.") == 1) {
				if (Zeny >= 1285000) {
					set Zeny, Zeny-1285000;
					set valkyrie_Q,1;
					mes "[Metheus Sylphe]";
					mes "ขอบคุณครับ เงินบริจาคของคุณจะถูกนำไปใช้เพื่อสาธารณประโยชน์ ขณะนี้คุณสามารถเข้าไปอ่านหนังสือได้แล้ว";
					close;
				}
				mes "[Metheus Sylphe]";
				mes "ขออภัย ดูเหมือนว่าคุณจะมีเงิน Zeny ไม่เพียงพอในขณะนี้ โปรดตรวจสอบเงินของคุณแล้วกลับมาใหม่อีกครั้ง";
				close;
			}
			mes "[Metheus Sylphe]";
			mes "ใช้เวลาของคุณให้เต็มที่ และเพลิดเพลินไปกับการเดินทางของคุณ";
			close;
		}
		mes "[Metheus Sylphe]";
		mes "ขอขอบคุณอีกครั้งสำหรับการบริจาคอันมีน้ำใจของคุณ โปรดอ่าน 'หนังสือแห่ง Ymir' ฉบับคาร์บอนได้ตามสบาย";
		close;
	}
	mes "[Metheus Sylphe]";
	mes "ยินดีต้อนรับสู่ห้องสมุดของ Schweicherbil Magic Academy";
	mes "ที่นี่เรามีหนังสือมากมายนับไม่ถ้วน โปรดใช้เวลาของคุณในการค้นหาหนังสือ";
	close;
}

yuno_in02,93,207,1	script	Book of Ymir	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[The Book of Ymir]";
		mes "...ทางเข้า Hall of Honor เปิดให้กับทุกคนที่ได้ก้าวไปสู่ชีวิตต่อไปแล้ว ทางเข้ามีไว้เพื่อช่วยให้ฮีโร่ตัดสินใจว่าพวกเขาต้องการทำอะไร และสามารถนำพวกเขาไปที่ใดก็ได้ในโลกนี้";
		next;
		mes "[The Book of Ymir]";
		mes "ใน Hall of Honor ทุกสิ่งได้รับการเตรียมไว้สำหรับฮีโร่ มีข่าวลือว่าความปรารถนาใดๆ ที่ไม่สามารถเติมเต็มได้ในความเป็นจริงของเราสามารถเป็นจริงได้ใน Hall of Honor";
		next;
		if (select("หยุดอ่าน:อ่านต่อ.") == 1) {
			mes "[The Book of Ymir]";
			mes ".....";
			close;
		}
		mes "[The Book of Ymir]";
		mes "มีทางที่ถูกลืมซึ่งนำไปสู่หอเกียรติยศ สถานที่ที่ใกล้ที่สุดกับสวรรค์ คนธรรมดาจะไม่มีวันค้นพบสถานที่นี้...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[The Book of Ymir]";
			mes "...ดังนั้น วีรบุรุษในสมัยโบราณจึงอยู่ในความทุกข์ระทมอยู่เสมอ โดยรู้ดีว่าในท้ายที่สุด พวกเขาก็เป็นเพียงมนุษย์และจะต้องจากโลกนี้ไป...";
			next;
			mes "[The Book of Ymir]";
			mes "ไม่มีเอกสาร เพลง หรือเรื่องเล่าพื้นบ้านใดๆ ที่ยังหลงเหลืออยู่ซึ่งมีข้อมูลเกี่ยวกับชีวิตหลังความตาย อย่างไรก็ตาม เมื่อไม่นานมานี้ ฉันได้ค้นพบม้วนกระดาษเก่าเกี่ยวกับวาลคิรี...";
			next;
			mes "[The Book of Ymir]";
			mes "Valkyrie...";
			mes "นางฟ้าผู้พิทักษ์ในตำนาน";
			mes "นางฟ้าแห่งแร็กนาร็อก";
			next;
			mes "[The Book of Ymir]";
			mes "นักผจญภัยที่มีพละกำลังและความกล้าหาญอันยิ่งใหญ่จะได้รับการนำโดยวาลคิรีไปยังวัลฮัลลา ห้องโถงแห่งเกียรติยศ ที่นั่น พวกเขาจะได้รับชีวิตใหม่";
			next;
			mes "[The Book of Ymir]";
			mes "เกิดใหม่อีกครั้ง พวกเขาจะมีชีวิตอีกครั้งในฐานะฮีโร่ที่ยิ่งใหญ่กว่าที่จะทำให้โลกสดใส ร่างกายที่อ่อนล้าจะเต็มไปด้วยพลังงาน...";
			next;
			mes "[The Book of Ymir]";
			mes "และวิญญาณของพวกเขาจะได้รับความสามารถจากหัวใจของ Ymir อย่างไรก็ตาม หัวใจของ Ymir ถูกทำลายจนหมดสิ้นและกระจัดกระจายไปทั่วโลกหลังจากการต่อสู้เพื่อ Rune-Midgarts";
			next;
			mes "[The Book of Ymir]";
			mes "ฉันพบชิ้นส่วนหัวใจของ Ymir จำนวนเล็กน้อยในช่วงเวลาอันยาวนาน แต่ฉันไม่สามารถยืนยันได้ว่าเรื่องราวของ Valkyrie และ Valhalla เป็นเรื่องจริงหรือไม่ จากการทดสอบทางวิทยาศาสตร์";
			next;
			mes "[The Book of Ymir]";
			mes "ฉันจึงฝากบันทึกนี้ไว้ด้วยความหวังว่าจะมีใครสักคนในอนาคตที่สามารถยืนยันได้ว่า Valkyrie และ Valhalla มีอยู่จริง...";
			next;
			set valkyrie_Q,2;
			if(checkquest(1000) == -1) {
				setquest 1000;
			}
			mes "[The Book of Ymir]";
			mes "ปล่อยให้เหล่าฮีโร่มีชีวิตใหม่เพื่อที่พวกเขาจะสามารถปกป้องโลกจากอันตรายได้ แล้วจากนั้น...";
			close;
		}
		mes "[The Book of Ymir]";
		mes "...";
		close;
	}
	mes "[The Book of Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	111,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporter]";
			mes "ขอแสดงความยินดี";
			mes "ขอแสดงความนับถือต่อเหล่านักรบ!";
			close;
		}
		mes "[Teleporter]";
		mes "โปรดหลีกเลี่ยงการสัมผัสส่วนนิทรรศการใด ๆ.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporter]";
		mes "ท่านผู้มีเกียรติ ท่านอยากไปที่ไหน?";
		next;
		switch(select("Prontera:Morocc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (checkre(0)) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[Teleporter]";
	mes "เที่ยวให้สนุกนะ";
	close2;
	savepoint getarg(0),getarg(1),getarg(2),1,1;
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75


//===== rAthena Script =======================================
//= Buwaya Cave
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Defeat Buwaya in his cave.
//= Part of the "Secret in the Woods" quest.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//============================================================

// Instance Creation :: buwaya_dun
//============================================================
ma_fild02,312,317,5	script	Guard#buwaya_cave	570,{
		if (BaseLevel < 130) {
				mes "[อารักขา]";
				mes "คนที่อยู่ใน ^ff0000ระดับ 130^000000";
				mes "ไม่สามารถเข้าสถานที่นี้ได้";
				mes "ที่นี่มันอันตราย กรุณากลับไป";
				close;
		}

		set .@party_id,getcharid(1);
		set .@md_name$,"Buwaya Cave";

		if (!.@party_id) {
				mes "[อารักขา]";
				mes "บูวายาเป็นอันตราย";
				mes "กรุณากลับมาอีกครั้งหลังจากที่คุณสร้าง";
				mes "ทีมงานร่วมกับคนอื่นๆ";
				close;
		}
		if (is_party_leader() == true) {
				mes "[อารักขา]";
				mes "ที่นี่มันอันตราย กรุณากลับไป";
				mes "หากคุณไม่มีธุระอะไรที่นี่ โปรดกลับมา";
				next;
				while(1) {
						switch(select("Why is it so dangerous?:I'm here to hunt down Buwaya.:End conversation.")) {
						case 1:
								mes "[อารักขา]";
								mes "เมื่อเร็วๆ นี้มีกรณีเกิดขึ้น";
								mes "ที่ชาวบ้านหายไป";
								mes "หลังจากตรวจสอบร่องรอยแล้ว";
								mes "เราพบสัตว์ประหลาดยักษ์ บุวายา";
								mes "มีความรับผิดชอบ.";
								next;
								mes "[อารักขา]";
								mes "ทหารและทหารรับจ้างได้ถูกส่งไป";
								mes "ตามล่าบูวายาแต่ก็ไม่เคย";
								mes "ประสบความสำเร็จอย่างสมบูรณ์";
								next;
								mes "[อารักขา]";
								mes "บุวายายังลักพาตัวอยู่";
								mes "ชาวบ้านจนทุกวันนี้.";
								mes "โปรดรวบรวมคนไปกำจัดบุวายาซะ";
								next;
								break;
						case 2:
								set .@playtime, checkquest(4229,PLAYTIME);
								if (.@playtime == -1) {
										// fall through
								} else if (.@playtime == 0 || .@playtime == 1) {
										mes "[อารักขา]";
										mes "บุวายายังคงซ่อนตัวอยู่";
										mes "ถึงจะเข้าตอนนี้ บุวายาก็ไม่ออก";
										mes "โปรดกลับมาใหม่ภายหลัง.";
										close;
								} else {
										erasequest 4229;
										// fall through
								}
								if (instance_create(.@md_name$) < 0) {
										mes "[อารักขา]";
										mes "ชื่อปาร์ตี้คือ..."+getpartyname(.@party_id)+"-";
										mes "หัวหน้าพรรคคือ..."+strcharinfo(0);
										mes "^0000ff"+.@md_name$+"^000000 ไม่สามารถเปิดได้ในขณะนี้";
										mes "โปรดลองอีกครั้งในภายหลัง";
										close;
								}
								mes "[อารักขา]";
								mes "ฉันจะเปิดอุโมงค์ให้ ^0000ff"+.@md_name$+"^000000.";
								mes "ขอให้โชคดีนะคะ.";
								close;
						case 3:
								mes "[อารักขา]";
								mes "ที่นี่มันอันตราย ห้ามเข้านะครับ";
								close;
						}
				}
		} else {
				mes "[อารักขา]";
				mes "ขอผมคุยกับหัวหน้าพรรคหน่อย.";
				mes "กรุณาเรียกผู้นำมา.";
				close;
		}
}

ma_fild02,315,323,0	script	Cave Entrance#buwaya	45,2,2,{
		end;
OnTouch:
		if (BaseLevel < 130) {
				mes "[อารักขา]";
				mes "คนที่อยู่ใน ^ff0000ระดับ 130^000000";
				mes "ไม่สามารถเข้าสถานที่นี้ได้";
				mes "ที่นี่มันอันตราย กรุณากลับไป";
				close;
		}
		set .@playtime, checkquest(4229,PLAYTIME);
		if (.@playtime == -1) {
				// fall through
		} else if (.@playtime == 0 || .@playtime == 1) {
				mes "[อารักขา]";
				mes "บุวายายังคงซ่อนตัวอยู่";
				mes "ถึงจะเข้าตอนนี้ บุวายาก็ไม่ออก";
				mes "โปรดกลับมาใหม่ภายหลัง.";
				close;
		} else {
				erasequest 4229;
				// fall through
		}
		switch(select("Enter.:Turn back.")) {
		case 1:
				switch(instance_enter("Buwaya Cave")) {
				case IE_OTHER:
						mes "[อารักขา]";
						mes "โอ้ ตอนนี้ไม่ใช่เวลาที่ดี";
						mes "โปรดลองอีกครั้งในภายหลัง";
						close;
				case IE_NOINSTANCE:
				case IE_NOMEMBER:
						mes "[อารักขา]";
						mes "ที่นี่อันตราย กรุณาอย่าเข้าไป";
						close;
				case IE_OK:
						mapannounce "ma_fild02",getpartyname(getcharid(1))+" party's "+strcharinfo(0)+" member began hunting Buwaya in Buwaya Cave.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
						setquest 4229;
						//warp "1@ma_c",35,57;
						end;
				default:
						mes "- ไม่สามารถเข้าได้เนื่องจากมีกองกำลังไม่ทราบฝ่าย -";
						close;
				}
		case 2:
				mes "[อารักขา]";
				mes "คุณจะต้องเห็นคุณค่าของชีวิตของคุณ";
				mes "จะดีกว่าถ้าคิดให้รอบคอบก่อนจะกระทำ";
				close;
		}
}

// Instance Scripts :: buwaya_in
//============================================================
1@ma_c,33,112,0	script	#damage	139,7,7,{
		end;
OnInstanceInit:
		initnpctimer;
		disablenpc instance_npcname("#damage");
		end;
OnTimer1000:
		enablenpc instance_npcname("#damage");
		specialeffect EF_POISONHIT;
		end;
OnTimer2000:
		stopnpctimer;
		initnpctimer;
		disablenpc instance_npcname("#damage");
		end;
OnTouch:
		percentheal -10,-10;
		sc_start SC_BLEEDING,60000,0;
		sc_start SC_POISON,60000,0;
		end;
}

1@ma_c,29,110,5	script	Kidnapped People#1	575,{
		mes "[ชาวบ้านถูกจับตัวไป]";
		mes "บุวายาก็วิ่งไปวิ่งมา";
		mes "และมีคนถูกจับคนใหม่";
		mes "เราจะติดอยู่ที่นี่ไปตลอดชีวิต";
		next;
		switch(select("Get me outta here!!:Ca...can't escape?")) {
		case 1:
				mes "[ชาวบ้านถูกจับตัวไป]";
				mes "เราไม่มีพลังพอที่จะทำลายมัน แต่บุวายามีจุดอ่อน";
				mes "มองดูกำแพงตรงนั้นสิ";
				mes "หากคุณเคาะตรงนั้น Buywaya จะรู้สึกเจ็บปวดและคายคุณออกมา";
				donpcevent instance_npcname("#box_mob_call")+"::OnEnable";
				close2;
				disablenpc instance_npcname("Kidnapped People#1");
				end;
		case 2:
				mes "[คนถูกจับตัวไป]";
				mes "มีบางอย่างผิดปกติที่นี่...";
				mes "คุณเคยประสบกับสิ่งนี้มาก่อนหรือไม่?";
				close;
		}
		end;
OnInstanceInit:
		disablenpc instance_npcname("Kidnapped People#1");
		end;
OnEnable:
		enablenpc instance_npcname("Kidnapped People#1");
		donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
		end;
}

1@ma_c,36,110,5	script	Kidnapped People#2	574,{
		mes "[ชาวบ้านถูกจับตัวไป]";
		mes "อ๋อ! อยากให้มีพลังมากกว่านี้อีกนิด...";
		mes "ฉันรู้จุดอ่อนของมัน...";
		next;
		switch(select("Tell me.:Do you really know?")) {
		case 1:
				mes "[ชาวบ้านถูกจับตัวไป]";
				mes "คุณแน่ใจว่าจะทำได้หรอ...?";
				mes "โอเค ฟังอย่างตั้งใจ";
				mes "จุดอ่อนของมันคือ...";
				next;
				mes "[ชาวบ้านถูกจับตัวไป]";
				mes "...ข้างหลังเราบนกำแพง";
				sc_start SC_ATKPOTION,60000,45;
				sc_start SC_MATKPOTION,60000,45;
				disablenpc instance_npcname("Kidnapped People#2");
				close;
		case 2:
				mes "[ชาวบ้านถูกจับตัวไป]";
				mes "อะไร?!";
				mes "คุณเพิกเฉยต่อฉันใช่ไหม";
				mes "เพราะฉันถูกจับอยู่ที่นี่เหรอ?";
				close;
		}
		end;
OnInstanceInit:
		disablenpc instance_npcname("Kidnapped People#2");
		end;
OnEnable:
		enablenpc instance_npcname("Kidnapped People#2");
		end;
}

1@ma_c,3,3,0	script	#box_mob_call	139,1,1,{
		end;
OnInstanceInit:
		setcell instance_mapname("1@ma_c"),30,118,35,118,cell_shootable,1; //custom
		disablenpc instance_npcname("#box_mob_call");
		end;
OnEnable:
		enablenpc instance_npcname("#box_mob_call");
		set .@label$, instance_npcname("#box_mob_call")+"::OnMyMobDead";
		set .@map$, instance_mapname("1@ma_c");
		monster .@map$,30,118,"Buwaya's Weakness",2333,1,.@label$;
		monster .@map$,35,118,"Buwaya's Weakness",2333,1,.@label$;
		end;
OnDisable:
		killmonster instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead";
		disablenpc instance_npcname("#box_mob_call");
		end;
OnMyMobDead:
		if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead") < 1)
				donpcevent instance_npcname("#box_out")+"::OnEnable";
		end;
}

1@ma_c,33,118,0	script	#box_out	45,2,2,{
OnInstanceInit:
OnDisable:
		disablenpc instance_npcname("#box_out");
		end;
OnEnable:
		enablenpc instance_npcname("#box_out");
		end;
OnTouch:
		set .@x, rand(1,20) + 97;
		set .@y, rand(1,20) + 74;
		warp instance_mapname("1@ma_c"),.@x,.@y;
		end;
}

1@ma_c,97,74,0	script	#box_call	139,50,50,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#box_call");
		initnpctimer;
		end;
OnTimer30000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : I will put you in my treasure box!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		// Should execute OnTimer33000, but client doesn't render the effect fast enough.
		for(set .@i,1; .@i<=9; set .@i,.@i+1)
				donpcevent instance_npcname("#yunobi"+.@i)+"::OnEnable";
		end;
OnTimer33000:
		donpcevent instance_npcname("#box_out")+"::OnDisable";
		donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
		donpcevent instance_npcname("Kidnapped People#1")+"::OnEnable";
		donpcevent instance_npcname("Kidnapped People#2")+"::OnEnable";
		end;
OnTimer34000:
		enablenpc instance_npcname("#box_call");
		end;
OnTimer35000:
		stopnpctimer;
		initnpctimer;
		disablenpc instance_npcname("#box_call");
		end;
OnTouch_:
		specialeffect2 EF_GUIDEDATTACK;
		warp instance_mapname("1@ma_c"),33,112;
		end;
OnDisable:
		stopnpctimer;
		disablenpc instance_npcname("#box_call");
		end;
}

1@ma_c,97,74,0	script	#yunobi1	139,{
		end;
OnInstanceInit:
		hideonnpc instance_npcname(strnpcinfo(0));
		end;
OnEnable:
		specialeffect EF_MAPPILLAR2;
		end;
}
1@ma_c,97,94,0	duplicate(#yunobi1)	#yunobi2	139
1@ma_c,117,94,0	duplicate(#yunobi1)	#yunobi3	139
1@ma_c,117,74,0	duplicate(#yunobi1)	#yunobi4	139
1@ma_c,117,54,0	duplicate(#yunobi1)	#yunobi5	139
1@ma_c,97,54,0	duplicate(#yunobi1)	#yunobi6	139
1@ma_c,77,54,0	duplicate(#yunobi1)	#yunobi7	139
1@ma_c,77,74,0	duplicate(#yunobi1)	#yunobi8	139
1@ma_c,77,94,0	duplicate(#yunobi1)	#yunobi9	139

1@ma_c,1,1,0	script	#bunshin	139,{
		end;
OnInstanceInit:
		initnpctimer;
		end;
OnTimer58000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : I had enough with this, let's go till the end!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer61000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : This is!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer62000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer63000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY...Deadly!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer64000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY...Deadly... ATTACK!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer65000:
		set .@label$, instance_npcname("#bunshin")+"::OnMyMobDead";
		set .@map$, instance_mapname("1@ma_c");
		areamonster .@map$,112,89,122,99,"Buwaya",2332,1,.@label$;
		areamonster .@map$,112,49,122,59,"Buwaya",2332,1,.@label$;
		areamonster .@map$,72,49,82,59,"Buwaya",2332,1,.@label$;
		areamonster .@map$,72,89,82,99,"Buwaya",2332,1,.@label$;
		end;
OnTimer66000:
		mapannounce instance_mapname("1@ma_c"),"Buwaya : Are you scared?",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		end;
OnTimer105000:
		killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
		stopnpctimer;
		initnpctimer;
		end;
OnMyMobDead:
		if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead") < 1) {
				stopnpctimer;
				initnpctimer;
		}
		end;
OnDisable:
		stopnpctimer;
		killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
		disablenpc instance_npcname("#bunshin");
		end;
}

1@ma_c,2,2,0	script	#buwaya_con	139,{
		end;
OnInstanceInit:
		areamonster instance_mapname("1@ma_c"),90,67,104,81,"Buwaya",2319,1,instance_npcname("#buwaya_con")+"::OnMyMobDead";
		end;
OnMyMobDead:
		set .@map$, instance_mapname("1@ma_c");
		if (mobcount(.@map$,instance_npcname("#buwaya_con")+"::OnMyMobDead") < 1) {
				donpcevent instance_npcname("#box_call")+"::OnDisable";
				donpcevent instance_npcname("#bunshin")+"::OnDisable";
				donpcevent instance_npcname("#exit_mob")+"::OnDisable";
				donpcevent instance_npcname("#cave_out")+"::OnEnable";
				mapannounce .@map$,"Guard : You did great work. Please hurry up and escape to the way you came in!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		}
		end;
}

1@ma_c,3,3,0	script	#exit_mob	139,{
		end;
OnInstanceInit:
		initnpctimer;
		end;
OnTimer60000:
		set .@label$, instance_npcname("#exit_mob")+"::OnMyMobDead";
		set .@map$, instance_mapname("1@ma_c");
		if (mobcount(.@map$,.@label$) < 30)
				set .@amount,10;
		else
				set .@amount,1;
		areamonster .@map$,43,58,47,60,"Water Plant",2331,.@amount,.@label$;
		areamonster .@map$,43,58,47,60,"Egg",2329,.@amount,.@label$;
		stopnpctimer;
		initnpctimer;
		end;
OnDisable:
		stopnpctimer;
		killmonster instance_mapname("1@ma_c"),instance_npcname("#exit_mob")+"::OnMyMobDead";
		disablenpc instance_npcname("#exit_mob");
		end;
OnMyMobDead:
		end;
}

1@ma_c,28,57,0	script	#cave_out	45,2,2,{
OnInstanceInit:
		disablenpc instance_npcname("#cave_out");
		end;
OnEnable:
		enablenpc instance_npcname("#cave_out");
		end;
OnTouch:
		mes "อยากออกไปมั้ย?";
		next;
		if(select("Yes!:No, I will stay.") == 1)
				warp "ma_fild02",315,315;
		close;
}

1@ma_c,1,1,0	script	#buwaya_spawn_mobs	-1,{
OnInstanceInit:
		set .@map$, instance_mapname("1@ma_c");
		areamonster .@map$,73,81,93,101,"Seaweed",2331,18;
		areamonster .@map$,110,97,116,103,"Seaweed",2331,8;
		areamonster .@map$,59,63,63,67,"Seaweed",2331,8;
		areamonster .@map$,73,55,77,59,"Seaweed",2331,4;
		areamonster .@map$,103,69,107,73,"Seaweed",2331,4;
		areamonster .@map$,108,45,122,63,"Seaweed",2331,15;
		areamonster .@map$,73,81,93,101,"Buwaya's Egg",2329,10;
		areamonster .@map$,110,97,116,103,"Buwaya's Egg",2329,8;
		areamonster .@map$,59,63,63,67,"Buwaya's Egg",2329,4;
		areamonster .@map$,73,55,77,59,"Buwaya's Egg",2329,3;
		areamonster .@map$,103,69,107,73,"Buwaya's Egg",2329,3;
		areamonster .@map$,108,45,122,63,"Buwaya's Egg",2329,15;
		monster .@map$,0,0,"Seaweed",2331,5;
		monster .@map$,0,0,"Buwaya's Slave",2330,5;
		end;
}
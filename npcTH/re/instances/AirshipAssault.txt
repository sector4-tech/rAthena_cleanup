//===== rAthena Script =======================================
//= Airship Assault Instance Script
//===== Description ==========================================
//= Heroes Trail Part-III 'Airship Assault' instance
//===== Additional Comments: =================================
//= [Official Conversion]
//= 1.0 First Version [Ziu]
//= 1.1 Fix Explosion Effect and add respawn attack visual mob [Ziu]
//= 1.1a Change close;-> end; [Ziu]
//= 2.0 English version [Secretdataz/Capuche]
//= 2.1 Clean-up [Secretdataz]
//= 2.2 Dialogues clean-ups [Aleos]
//============================================================

dali02,136,79,3	script	Adventurer Leon#air1	4_M_DST_GRAND,{
		mes "[นักผจญภัย เลออน]";
		mes "อืม...หลังจากสังเกตอยู่หลายวัน สถานที่แห่งนี้ดูเหมือนเป็นอุปกรณ์ที่ประทับความทรงจำถึงเรือเหาะ";
		next;
		mes "[นักผจญภัย เลออน]";
		mes "...นั่นมันต่างกัน ฉันไม่มีข้อมูลเพิ่มเติม...";
		close;
}

dali02,142,81,3	script	Dimensional Device#air1	PORTAL,{
		if (checkquest(15050,PLAYTIME) == 2) {
				erasequest 15050;// Memorial Dungeon: Airship Assault
				erasequest 15051;
		}
		else if (checkquest(15051,PLAYTIME) == 2) {// Memorial Dungeon: Airship Assault
				mes "^ff0000ไม่สามารถกลับเข้าสู่ดันเจี้ยนอนุสรณ์ได้อีกครั้งภายใน 23 ชั่วโมงหลังจากที่คุณเข้าครั้งสุดท้าย^000000";
				close;
		}
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Assault on the Airship";

		if (BaseLevel < 125) {
				mes "^ff0000ผู้ใช้ที่มีระดับต่ำกว่า 125 ไม่สามารถเข้าดันเจี้ยนนี้ได้^000000";
				close;
		}
		if (.@party_id < 1) {
				mes "^ff0000ในการเข้าใช้งาน คุณต้องสร้างหรือเข้าร่วมปาร์ตี้ที่มีสมาชิก 1 คนขึ้นไป^000000";
				close;
		}
		if (is_party_leader() == false) {
				mes "^ff0000ดันเจี้ยนแห่งความทรงจำนี้เปิดให้เข้าได้เพียง 1 คนเท่านั้น เฉพาะหัวหน้าปาร์ตี้เท่านั้นที่สามารถเข้าได้^000000";
				close;
		}
		.@s = select( "Power Up the Device", "Use the Device", "Cancel" );
		if (is_party_leader(.@party_id) == false) {
				mes "^ff0000ดันเจี้ยนแห่งความทรงจำนี้เปิดให้เข้าได้เพียง 1 คนเท่านั้น เฉพาะหัวหน้าปาร์ตี้เท่านั้นที่สามารถเข้าได้^000000";
				close;
		}
		switch(.@s) {
		case 1:
				if (instance_create(.@md_name$) < 0) {
						mes "ชื่อปาร์ตี้ :" + getpartyname( getcharid(1) );
						mes "ชื่อตัวละคร :" + strcharinfo(0);
						mes "^0000ff" + .@md_name$ + "^000000 - การสร้างล้มเหลว!";
						close;
				}
				mes "^ff0000หลังจากอุปกรณ์มิติได้ก่อตัว^000000";
				mes "^ff0000คลิกเพื่อเข้าใหม่อีกครั้ง^000000";
				close;
		case 2:
				switch( instance_enter(.@md_name$) ) {
				case IE_OK:
						if (isbegin_quest(15050) == 0) {
								setquest 15050;// Memorial Dungeon: Airship Assault
								setquest 15051;// Memorial Dungeon: Airship Assault
						}
						mapannounce "dali02", "" + strcharinfo(0) + ", member of the party " + getpartyname( getcharid(1) ) + " entered the instance " + .@md_name$ + ".", bc_map,"0x00ff99";
						specialeffect2 EF_WARP;
						// warp "1@air1",244,73;
						end;
				case IE_NOMEMBER:
						mes "^ff0000ในการเข้าใช้งาน คุณต้องสร้างหรือเข้าร่วมปาร์ตี้ที่มีสมาชิก 1 คนขึ้นไป^000000";
						close;
				case IE_NOINSTANCE:
						mes "^ff0000หัวหน้าปาร์ตี้ไม่ได้ใช้อุปกรณ์นี้^000000";
						close;
				case IE_OTHER:
						mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";// Custom
						close;
				}
		case 3:
				end;
		}
}

// Warps
1@air1,253,54,0	warp	warp1#air1	2,2,1@air1,91,68
1@air1,104,72,0	warp	warp2a#air1	2,2,1@air1,101,200
1@air1,104,52,0	warp	warp2b#air1	2,2,1@air1,101,176
1@air1,103,200,0	warp	warp3a#air1	2,2,1@air2,104,52
1@air1,103,176,0	warp	warp3b#air1	2,2,1@air2,104,52
1@air2,88,67,0	warp	warp4a#air2	2,2,1@air2,247,53
1@air2,244,73,0	warp	warp5a#air2	2,2,dali02,137,82

// Entrance
1@air1,240,57,3	script	Loki#air1	4_M_ROKI2,{
		if ('airship_status == 0) {
				cutin "ep14_roki02",2;
				mes "[โลกิ]";
				mes "...ดีใจจังที่ไม่มีสิ่งน่ารำคาญพวกนั้นอยู่ที่นี่...";
				close3;
		}
		cutin "ep14_roki02",2;
		mes "[โลกิ]";
		mes "...ฉันจะปกป้องที่นี่ไม่ว่าจะเกิดอะไรขึ้นก็ตาม ลงไปในเรือเหาะซะ...!!";
		close3;
}

1@air1,235,57,5	script	Fenrir#air1	4_F_FENRIR,{
		if ('airship_status == 0) {
				cutin "fenrir_a",2;
				mes "[เฟนริล]";
				mes "บินอยู่กลางอากาศด้วยความแข็งแกร่งของเครื่องจักร...การพัฒนาทางเทคโนโลยีนับว่ามหัศจรรย์จริงๆ";
				next;
				mes "[เฟนริล]";
				mes "แต่ผมรู้สึกว่ามันมีพลังบางอย่างนะ...ผมคิดว่าเรือเหาะลำนี้คงไม่บินได้ด้วยเทคโนโลยีเพียงอย่างเดียวหรอก";
				close3;
		}
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "ข้าจะเผชิญหน้าพวกมันที่นี่ โกลาหล ลงไปชั้นล่างของเรือเหาะกับไอริส!";
		close3;
}

1@air1,215,67,5	script	Iris#air1	4_F_IRIS,{ end; }
1@air1,214,66,5	duplicate(Iris#air1)	Chaos#air1	4_M_CHAOS

1@air1,217,67,3	script	Captain Ferlock#air1	4_M_REINDEER,{
		if ('airship_status == 0) {
				cutin "h_iris02",2;
				mes "[ไอริส]";
				mes "โห~ เยี่ยมเลย!! ลมเย็นสบายจริงๆ~!!";
				mes "เย้~ บินทะลุฟ้าแล้ว แถมฟรีอีกต่างหาก!";
				next;
				mes "[ไอริส]";
				mes "มันช่างน่าอัศจรรย์จริงๆ ที่เรือลำใหญ่ขนาดนี้สามารถบินได้โดยไม่ต้องใช้เวทมนตร์ นี่มันโกลาหลชัดๆ";
				next;
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes "-";
				next;
				cutin "h_iris02",2;
				mes "[ไอริส]";
				mes "...?? ความวุ่นวาย?";
				next;
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes ".....เซจ คาดมอน..";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "คุณทำอะไรอยู่แทนที่จะฟังฉัน!!!!";
				next;
				cutin "h_chaos03",2;
				mes "[ความวุ่นวาย]";
				mes "อ๊ากกก! อ๊า~!!";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "ฮึ่ม! นั่นคือสิ่งที่คุณได้รับจากการขี้ลืม!";
				next;
				cutin "h_chaos03",2;
				mes "[ความวุ่นวาย]";
				mes "อ่า...ไอริส!! เธอพยายามจะฆ่าฉันเหรอ~!!";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "ฮึ่ม! ฉันไม่สนหรอกว่าเธอจะตายหรือเปล่า...";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "เฮ้ย!! ห้ามเล่นตลกบนเรือนะ!!";
				mes "นี่คือสิ่งที่ผิดกับมนุษย์วัยรุ่น พวกเขาไม่มีมารยาท! ถ้าคุณทำตัวไม่ดี ฉันจะไล่คุณออกทั้งหมด!!";
				next;
				cutin "h_iris02",2;
				mes "[ไอริส]";
				mes "อุ๊ย... กัปตันครับ เรือเหาะลำใหญ่ลำนี้บินผ่านฟ้าได้ยังไงเนี่ย มหัศจรรย์จริงๆ ครับ!";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "ห๊ะ? นั่นเป็นคำถามที่ดี";
				mes "อย่างที่คุณเห็น เราให้ความร้อนกับหม้อต้มน้ำขนาดใหญ่เพื่อต้มน้ำ จากนั้นใช้แรงดันของไอน้ำเพื่อเคลื่อนลูกสูบซึ่งจะหมุนเฟือง และเคลื่อนกังหันเพื่อหมุนใบพัด";
				next;
				cutin "h_iris02",2;
				mes "[ไอริส]";
				mes "เอ่อ... เรือลำใหญ่ลอยน้ำได้อย่างนั้นเอง... แต่ตอนนี้ความรู้สึกลึกลับนั้นหายไปแล้ว นึกว่าจะมีอุปกรณ์ลึกลับอะไรสักอย่าง... ผิดหวังนิดหน่อย...";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "ใช่แล้ว เรือลำใหญ่ลำนี้ไม่สามารถแล่นบนท้องฟ้าได้ด้วยพลังไอน้ำเพียงอย่างเดียวได้ จึงต้องใช้เครื่องขยายสัญญาณที่เรียกว่า 'Rune Engine'";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "Rune Engine คืออะไรเหรอ?";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "มันเป็นอุปกรณ์ที่ใช้หินวิเศษ";
				mes "อะไรบางอย่างเกี่ยวกับเศษชิ้นส่วนที่แตกสลายของหัวใจตามตำนาน... ตำนานที่ไร้สาระแต่ก็ไม่สำคัญ...";
				next;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "หินนี้คือสิ่งที่เรียกกันว่า 'น้ำตาของ Ymir' หากนำไปวางรวมกับเครื่องยนต์จักรไอน้ำ รูนที่แผ่ออกมาจากหินจะขยายพลังงาน";
				next;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "พูดตามตรงแล้ว คงไม่เกินจริงเลยหากจะบอกว่าก้อนหินขนาดเท่าหัวแม่มือกำลังเคลื่อนย้ายเรือลำนี้ เรือลำนี้มีพลังมหาศาล";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "สิ่งเล็กๆ น้อยๆ นั่น...!!!";
				next;
				cutin "ep14_roki02",2;
				mes "[โลกิ]";
				mes "......ฮะ?";
				mes "มีอะไรบางอย่างกำลังมาทางนี้...";
				next;
				cutin "",255;
				mes "[อารักขา]";
				mes "แจ้งเตือน!! แจ้งเตือน!! หน่วยไวเวิร์นขนาดใหญ่ที่เวลา 7 นาฬิกา!!";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "อะไร?!";
				next;
				cutin "",255;
				mes "[อารักขา]";
				mes "เกร...เกร็มลิน!! เกร็มลินได้ลงมาจากไวเวิร์นแล้ว!!";
				for ( .@i = 1; .@i <= 8; .@i++ )
						enablenpc instance_npcname("Gremlin#" + .@i + "air1");
				enablenpc instance_npcname("Rotar Zairo#1air1");
				enablenpc instance_npcname("Rotar Zairo#2air1");
				enablenpc instance_npcname("Acidus#1air1");
				enablenpc instance_npcname("Acidus#2air1");
				enablenpc instance_npcname("Wywern#air1");
				'airship_status = 1;
				next;
		}
		if ('airship_status == 1) {
				cutin "h_chaos01",2;
				mes "[ความวุ่นวาย]";
				mes "หืม...นี่มันอะไรอีกเนี่ย?";
				next;
				cutin "h_arcana02",2;
				mes "[อาร์คานา]";
				mes "ฮ่าๆๆๆ~ รอคอยมานานเลย!!";
				mes "โชว์สัตว์ประหลาดบินเหนืออากาศสุดเซอร์ไพรส์ของแม่มดสาวแสนสวย อาร์คานา!!";
				next;
				cutin "h_chaos03",2;
				mes "[ความวุ่นวาย]";
				mes "อะไรนะ...รายการอะไรนะ?";
				next;
				cutin "h_arcana02",2;
				mes "[อาร์คานา]";
				mes "เอาล่ะ มาเริ่มกันเลยดีกว่า! Gremlins~!!";
				mes "ถึงเวลาปาร์ตี้แล้ว~!!";
				next;
				cutin "",255;
				mes "[อารักขา]";
				mes "กัปตัน..กัปตัน! นี่มันซีเรียสนะ!";
				mes "มี Gremlins อยู่ทั่วเรือเลย!!";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "อะไรนะ! พวกนี้เป็น Gremlins เหรอ!";
				mes "ไอ้เวรเอ๊ย!! ลงมาจากตรงนั้นเดี๋ยวนี้!!";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "กัปตันครับ ไม่ต้องวิตกกังวลขนาดนั้นครับ";
				mes "เกรมลินเป็นเพียงสัตว์ประหลาดที่เข้าไปยุ่งเกี่ยวกับเครื่องจักร...";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "คุณไม่รู้ว่าคุณกำลังพูดถึงอะไร!!";
				mes "ตอนนี้เราอยู่ที่ระดับความสูง 8,000 เมตรแล้ว!! แม้แต่ความผิดพลาดเพียงเล็กน้อยก็อาจก่อให้เกิดอุบัติเหตุร้ายแรงได้!";
				next;
				cutin "",255;
				mes "[อารักขา]";
				mes "อ๊าก!! เครื่องท้ายหมายเลข 1!!";
				mes "...นี่มันร้ายแรงมาก! พวกมันกำลังมุ่งหน้าไปที่ห้องเครื่อง! พวกเราต้องปกป้องเครื่องยนต์รูน!!";
				next;
				cutin "fly_felrock2",2;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "บ้าเอ้ย... ฉันเอาอาวุธไปไว้ไหนวะ...";
				mes "เฮ้ มนุษย์!! บล็อคสิ่งกวนใจนั่นซะ ขณะที่ฉันจะไปที่ห้องกัปตันเพื่อหาอาวุธของฉัน!!";
				next;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "เราต้องหยุดพวกมันไม่ให้เข้าถึง Rune Engine โดยเฉพาะ!! ถ้ามันถูกทำลาย เรือก็จะบินไม่ได้อีกต่อไป!!";
				next;
				mes "[กัปตันเฟอร์ล็อค]";
				mes "ชิบหาย... ถ้าฉันมีอาวุธก็คงดี...";
				next;
				disablenpc instance_npcname("Captain Ferlock#air1");
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes "ไอริส!! ลงไปห้องเครื่องกันเถอะ! โลกิและเฟนริล คอยดูแลที่นี่ไว้!";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "โอเค...โอเค! โอเค...ไปกันเถอะ!!";
				close2;
				cutin "",255;
				if ('airship_status == 1)
						donpcevent instance_npcname("#airshipputmob1") + "::OnStart";
		}
		end;
}

1@air1,236,57,3	duplicate(Iris#air1)	Gremlin#1air1	3176
1@air1,239,57,5	duplicate(Iris#air1)	Gremlin#2air1	3176
1@air1,230,50,5	duplicate(Iris#air1)	Gremlin#3air1	3176
1@air1,226,62,3	duplicate(Iris#air1)	Gremlin#4air1	3176
1@air1,241,48,3	duplicate(Iris#air1)	Gremlin#5air1	3176
1@air1,225,40,7	duplicate(Iris#air1)	Gremlin#6air1	3176
1@air1,236,48,7	duplicate(Iris#air1)	Gremlin#7air1	3176
1@air1,224,66,5	duplicate(Iris#air1)	Gremlin#8air1	3176
1@air1,213,75,5	duplicate(Iris#air1)	Wywern#air1	3180
1@air1,218,78,3	duplicate(Iris#air1)	Acidus#1air1	3178
1@air1,208,78,5	duplicate(Iris#air1)	Acidus#2air1	3179
1@air1,200,78,5	duplicate(Iris#air1)	Rotar Zairo#1air1	3175
1@air1,223,78,3	duplicate(Iris#air1)	Rotar Zairo#2air1	3175

// Step 1
1@air1,0,0,0	script	#airshipputmob1	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#airshipputmob1");
		enablenpc instance_npcname("Traveling Knight#air1");
		enablenpc instance_npcname("Passed out Drunkard#air1");
		enablenpc instance_npcname("Wealthy man at a meal#a");
		enablenpc instance_npcname("Free Riding Bard#air1");
		enablenpc instance_npcname("Airship Cafeteria Manager#air1");
		enablenpc instance_npcname("Gambling Spirit#air1");
		'airship_status = 2;
		initnpctimer;
		end;
OnTimer500:
		disablenpc instance_npcname("Iris#air1");
		disablenpc instance_npcname("Chaos#air1");
		mapannounce 'map_air1$, "Enemies have suddenly appeared and are attempting to destroy the Airship.", bc_map,"0xFFFF00";

		enablenpc instance_npcname("warp1#air1");
		.@label$ = instance_npcname("#airshipputmob1") + "::OnMyMobDead";
		monster 'map_air1$,113,66, "Beholder",3177,1, .@label$;
		monster 'map_air1$,113,58, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 94,76, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 96,46, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 67,69, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 67,54, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 67,30, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 46,57, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 44,64, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 35,45, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 25,65, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 25,63, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 25,61, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 36,79, "Beholder",3177,1, .@label$;
		monster 'map_air1$, 63,91, "Beholder",3177,1, .@label$;

		monster 'map_air1$,106,56, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$,115,68, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 95,64, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 96,56, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 82,77, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 63,30, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 44,61, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 42,53, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 33,54, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 18,65, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 18,61, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 18,56, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 32,71, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 69,90, "Rotar Zairo",3175,1, .@label$;
		monster 'map_air1$, 84,45, "Rotar Zairo",3175,1, .@label$;
		end;
OnTimer1500:
		mapannounce 'map_air1$, "Go to the bottom of the Airship and defeat the enemy.", bc_map,"0xFFFF00";
		end;
OnTimer2500:
		mapannounce 'map_air1$, "All the enemies on one floor have to be defeated in order to progress.", bc_map,"0xFFFF00";
		stopnpctimer;
		end;
OnMyMobDead:
		if (mobcount( 'map_air1$, instance_npcname("#airshipputmob1") + "::OnMyMobDead" ) < 1) {
				mapannounce 'map_air1$, "All the enemies on this floor have been defeated. Go down to the next floor.", bc_map,"0xFFFF00";
				disablenpc instance_npcname("#airshipputmob1");
				donpcevent instance_npcname("#airshipputmob2") + "::OnStart";
				'airship_status = 3;
		}
		end;
}

1@air1,120,62,3	script	Traveling Knight#air1	4_M_JOB_KNIGHT1,{
		mes "[อัศวินเดินทาง]";
		mes "สิ่งเหล่านี้คืออะไร?";
		mes "ฉันต้องตัดพวกมันทั้งหมดเลยไหม? ...ไม่หรอก เงินไม่พอให้ฉันเข้าไปเกี่ยวข้องหรอก...";
		close2;
		emotion ET_OHNO;
		end;
}

1@air1,95,47,3	script	Passed out Drunkard#air1	4_M_LIEMAN,{
		mes "[หมดสติไป ไอ้ขี้เมา]";
		mes "โอ๊ย...เมาแล้ว...สะอึก";
		mes "....กรน...Zzz";
		close2;
		emotion ET_CONFUSE;
		end;
}

1@air1,75,71,5	script	Wealthy man at a meal#a	1_M_JOBTESTER,{
		mes "[ชายร่ำรวยกำลังรับประทานอาหาร]";
		mes "อะไรนะ... พวกนี้มันตัวอะไรกันนะ!?";
		mes "แกกล้าดียังไงมาขัดจังหวะเวลาอาหารอันมีค่าของฉัน! ยาม! แกอยู่ไหน!";
		close;
}

1@air1,61,88,5	script	Free Riding Bard#air1	1_M_BARD,{
		mes "[บาร์ดขี่ฟรี]";
		mes "โอ้โห...สวยจังเลย~ สุดยอด~";
		mes "ขนาดเรือระเบิดยังน่าเหลือเชื่อเลย~ โห~";
		close;
}

1@air1,46,61,5	script	Airship Cafeteria Manager#air1	1_M_PUBMASTER,{
		mes "[ผู้จัดการโรงอาหารเรือเหาะ]";
		mes "พวกมันอาจดูคุกคาม แต่เจ้าสัตว์ประหลาดพวกนั้นก็เป็นลูกค้าของโรงอาหารของฉันเหมือนกัน";
		mes "คุณอยากได้ลูกค้ามอนสเตอร์แบบไหน?";
		next;
		mes "[ผู้จัดการโรงอาหารเรือเหาะ]";
		mes "อ๊าก... ช่วยฉันด้วย!!";
		close;
}

1@air1,28,67,5	script	Gambling Spirit#air1	4_M_YURI,{
		mes "[จิตวิญญาณแห่งการพนัน]";
		mes "เฮ้! อย่าแตะไพ่ของฉัน!";
		mes "ถอดมือและคอออกซะ!";
		next;
		mes "[จิตวิญญาณแห่งการพนัน]";
		mes "ฮะ..?!";
		mes "ฉันเพิ่งเริ่มรู้สึกตัวว่ามีสัตว์ประหลาดพวกนี้โผล่มางั้นเหรอ?! นายจะขโมยเงินของฉันเพราะฉันได้เงินมานิดหน่อยน่ะเหรอ? ห๊ะ?!";
		close;
}

// Step 2
1@air1,0,0,0	script	#airshipputmob2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#airshipputmob2");
		enablenpc instance_npcname("Airship Crew#1air1");
		enablenpc instance_npcname("Airship Crew#2air1");
		enablenpc instance_npcname("King's Escort#1air1");
		enablenpc instance_npcname("King's Escort#2air1");
		enablenpc instance_npcname("Tourist#1air1");
		enablenpc instance_npcname("Tourist#2air1");
		enablenpc instance_npcname("King of a Small Country#1air1");
		enablenpc instance_npcname("warp2a#air1");
		enablenpc instance_npcname("warp2b#air1");
		.@label$ = instance_npcname("#airshipputmob2") + "::OnMyMobDead";

		monster 'map_air1$,111,190, "Gremlins",3176,1, .@label$;
		monster 'map_air1$,120,190, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 97,198, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 97,201, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 82,191, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 82,199, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 66,197, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 67,161, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 54,173, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 69,213, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 43,175, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 36,190, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 41,202, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 26,195, "Gremlins",3176,1, .@label$;
		monster 'map_air1$, 20,193, "Gremlins",3176,1, .@label$;

		monster 'map_air1$, 82,186, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 81,202, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 97,178, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 97,175, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 67,180, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 65,157, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 64,212, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 37,173, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 37,201, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 32,186, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 20,181, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 18,186, "Acidus",3179,1, .@label$;
		monster 'map_air1$, 55,200, "Acidus",3179,1, .@label$;
		monster 'map_air1$,115,184, "Acidus",3179,1, .@label$;
		monster 'map_air1$,115,189, "Acidus",3179,1, .@label$;
		end;
OnMyMobDead:
		if (mobcount( 'map_air1$, instance_npcname("#airshipputmob2") + "::OnMyMobDead" ) < 1) {
				mapannounce 'map_air1$, "All the enemies on this floor have been defeated. Head to the engine room entrance.", bc_map,"0xFFFF00";
				enablenpc instance_npcname("Iris#2air1");
				enablenpc instance_npcname("Chaos#2air1");
				enablenpc instance_npcname("Gremlin#2aair1");
				disablenpc instance_npcname("#airshipputmob2");
				'airship_status = 4;
		}
		end;
}

1@air1,93,193,3	script	Airship Crew#1air1	4_M_EIN_SOLDIER,{
		mes "[ลูกเรือเรือเหาะ]";
		mes "นี่คือทางเข้าห้องเครื่อง อย่าให้พวกมันเข้ามาได้!";
		close;
}

1@air1,22,182,7	script	Airship Crew#2air1	4_M_EIN_SOLDIER,{
		mes "[ลูกเรือเรือเหาะ]";
		mes "โห...มันเยอะเกิน ห้องเครื่องจะพังเอานะ!!";
		close;
}

1@air1,43,191,4	script	King's Escort#1air1	8W_SOLDIER,{
		mes "[คิงส์เอสคอร์ท]";
		mes "เราจะต้องไม่ปลุกกษัตริย์ที่หลับใหลของเราไม่ว่าจะเกิดอะไรขึ้นก็ตาม!";
		close;
}
1@air1,39,191,4	duplicate(King's Escort#1air1)	King's Escort#2air1	8W_SOLDIER

1@air1,58,197,3	script	Tourist#1air1	4_M_LIEMAN,{
		mes "-" + strnpcinfo(1) + "-";
		mes "....กรน...Zzz";
		close;
}
1@air1,49,169,5	duplicate(Tourist#1air1)	Tourist#2air1	4_M_LIEMAN
1@air1,42,205,3	duplicate(Tourist#1air1)	King of a Small Country#1air1	4_M_TRISTAN

1@air1,88,187,5	duplicate(Iris#air1)	Chaos#2air1	4_M_CHAOS
1@air1,93,188,5	duplicate(Iris#air1)	Gremlin#2aair1	3176

1@air1,88,189,5	script	Iris#2air1	4_F_IRIS,{
		if ('airship_status == 4) {
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes "บ้าเอ้ย... พวกเขามาถึงจุดนี้ได้แล้ว! ขอบคุณคุณที่ทำให้ Rune Engine ได้รับการปกป้อง";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "เอ่อ...โอ้!! มีเกรmlin กำลังมุ่งหน้าไปที่ห้องเครื่อง!!";
				next;
				cutin "h_chaos03",2;
				mes "[ความวุ่นวาย]";
				mes ".....!! เราต้องหยุดมันให้ได้!!";
				next;
				cutin "",255;
				mes "[เกร็มลิน]";
				mes "กรี๊ด..!!";
				if ('airship_status == 4) {
						hideonnpc instance_npcname("Gremlin#2aair1");
						donpcevent instance_npcname("#airshipputmob3") + "::OnStart";
						donpcevent instance_npcname("firecontrol#air2") + "::OnStart";
						'airship_status = 5;
				}
				next;
		}
		if ('airship_status == 5) {
				specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Gremlin#2aair1");
				mes "- ป๊อป! ป๊อป ป๊อป!";
				next;
				cutin "h_iris01",2;
				mes "[ไอริส]";
				mes "อี๊ยย!!";
				next;
				cutin "",255;
				mes "[สมาชิกลูกเรือ]";
				mes "อ๊าก! เครื่องยนต์รูน!! ห้องเครื่องระเบิด!! ทีมดับเพลิง! ดับไฟซะ!!";
				next;
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes "บ้าเอ๊ย!! ที่นี่มันอันตรายนะ! รีบออกไปจากที่นี่ก่อนเถอะ! ไอริส รีบไปเถอะ!!";
				close2;
				// 'airship_status = 6;// no consequence
				cutin "",255;
				disablenpc instance_npcname("Iris#2air1");
				disablenpc instance_npcname("Chaos#2air1");
				initnpctimer;
		}
		end;
OnTimer1000:
		mapannounce 'map_air1$, "You must go up to the deck of the airship while defeating the enemy.", bc_map,"0xFFFF00";
		stopnpctimer;
		end;
}

// Step 3
1@air2,0,0,0	script	#airshipputmob3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#airshipputmob3");
		enablenpc instance_npcname("warp3a#air1");
		enablenpc instance_npcname("warp3b#air1");
		enablenpc instance_npcname("Passed out Drunkard#air2");
		enablenpc instance_npcname("Wealthy man at a meal#b");
		enablenpc instance_npcname("Traveling Knight#air2");
		enablenpc instance_npcname("Free Riding Bard#air2");
		enablenpc instance_npcname("Gambling Spirit#air2");
		enablenpc instance_npcname("Airship Cafeteria Manager#air2");

		.@label$ = instance_npcname("#airshipputmob3") + "::OnMyMobDead";
		monster 'map_air2$,115,68, "Beholder",3177,1, .@label$;
		monster 'map_air2$,106,56, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 95,64, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 96,56, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 84,45, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 63,30, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 76,77, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 69,90, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 50,57, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 42,53, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 32,71, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 18,65, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 18,61, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 18,56, "Beholder",3177,1, .@label$;
		monster 'map_air2$, 33,54, "Beholder",3177,1, .@label$;

		monster 'map_air2$,113,66, "Acidus",3178,1, .@label$;
		monster 'map_air2$,113,58, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 94,76, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 96,46, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 67,30, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 67,54, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 67,69, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 63,91, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 51,50, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 36,79, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 25,65, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 25,63, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 25,61, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 30,45, "Acidus",3178,1, .@label$;
		monster 'map_air2$, 38,72, "Acidus",3178,1, .@label$;
		end;
OnMyMobDead:
		if (mobcount( 'map_air2$, instance_npcname("#airshipputmob3") + "::OnMyMobDead" ) < 1) {
				mapannounce 'map_air2$, "All the enemies on this floor have been defeated. Go up to the deck.", bc_map,"0xFFFF00";
				enablenpc instance_npcname("warp4a#air2");
				enablenpc instance_npcname("Iris#air2");
				enablenpc instance_npcname("Fenrir#air2");
				enablenpc instance_npcname("Wywern#air2");
				enablenpc instance_npcname("Wywern#2air2");
				enablenpc instance_npcname("Wywern#3air2");
				enablenpc instance_npcname("Acidus#1air2");
				enablenpc instance_npcname("Acidus#2air2");
				enablenpc instance_npcname("Rotar Zairo#1air2");
				enablenpc instance_npcname("Rotar Zairo#2air2");
				donpcevent instance_npcname("firecontrol2#air2") + "::OnStart";
				disablenpc instance_npcname("#airshipputmob3");
				'airship_status = 6;
		}
		end;
}

1@air2,95,47,3	duplicate(Passed out Drunkard#air1)	Passed out Drunkard#air2	4_M_LIEMAN
1@air2,75,71,5	duplicate(Wealthy man at a meal#a)	Wealthy man at a meal#b	1_M_JOBTESTER

1@air2,120,62,3	script	Traveling Knight#air2	4_M_JOB_KNIGHT1,{
		mes "[อัศวินเดินทาง]";
		mes "อ่า.. อ๊าก!!";
		mes "จะระเบิดแล้ว! ต้องไป...ต้องไปที่เรือเหาะอีกฝั่งนึง!!";
		close;
}

1@air2,61,88,5	script	Free Riding Bard#air2	1_M_BARD,{
		mes "[บาร์ดขี่ฟรี]";
		mes "โอ้โห...สวยจังเลย~ สุดยอด~";
		mes "ขนาดเรือระเบิดยังน่าเหลือเชื่อเลย~ โห~";
		close;
}

1@air2,28,67,5	script	Gambling Spirit#air2	4_M_YURI,{
		mes "[จิตวิญญาณแห่งการพนัน]";
		mes "นี่มันอะไร ไฟเหรอ?";
		mes "อ้าาาา~";
		close;
}

1@air2,46,61,5	script	Airship Cafeteria Manager#air2	1_M_PUBMASTER,{
		mes "[ผู้จัดการโรงอาหารเรือเหาะ]";
		mes "เรือลำนี้..จะล่มมั้ย..";
		mes "อย่างไรก็ตาม ฉันจะแบ่งปันชะตากรรมกับเรือลำนี้ เรือลำนี้และฉันเป็นหนึ่งเดียวกัน!";
		next;
		mes "[ผู้จัดการโรงอาหารเรือเหาะ]";
		mes "อ๊าก... ช่วยฉันด้วย!!";
		close;
}

// Step 4
1@air2,236,57,3	script	Gremlin#1air2	3176,{
		end;
OnExplosion1:
		specialeffect EF_SUI_EXPLOSION;
		sleep 500;
		specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Gremlin#2air2");
		sleep 500;
		specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Gremlin#3air2");
		end;
OnExplosion2:
		specialeffect EF_SUI_EXPLOSION;
		hideonnpc instance_npcname("Gremlin#1air2");
		sleep 500;
		specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Gremlin#2air2");
		hideonnpc instance_npcname("Gremlin#2air2");
		sleep 500;
		specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Gremlin#3air2");
		hideonnpc instance_npcname("Gremlin#3air2");
		end;
}

1@air2,239,57,5	duplicate(Iris#air1)	Gremlin#2air2	3176
1@air2,230,50,5	duplicate(Iris#air1)	Gremlin#3air2	3176
1@air2,226,62,3	duplicate(Iris#air1)	Gremlin#4air2	3176
1@air2,241,48,3	duplicate(Iris#air1)	Gremlin#5air2	3176
1@air2,225,40,7	duplicate(Iris#air1)	Gremlin#6air2	3176
1@air2,236,48,7	duplicate(Iris#air1)	Gremlin#7air2	3176
1@air2,224,66,5	duplicate(Iris#air1)	Gremlin#8air2	3176

1@air2,229,56,5	duplicate(Iris#air1)	Loki#air2	4_M_ROKI2
1@air2,241,54,3	duplicate(Iris#air1)	Iris#air2	4_F_IRIS
1@air2,213,75,5	duplicate(Iris#air1)	Wywern#air2	3180
1@air2,235,59,3	duplicate(Iris#air1)	Wywern#2air2	3180
1@air2,235,49,3	duplicate(Iris#air1)	Wywern#3air2	3180
1@air2,218,78,3	duplicate(Iris#air1)	Acidus#1air2	3178
1@air2,208,78,5	duplicate(Iris#air1)	Acidus#2air2	3179
1@air2,200,78,5	duplicate(Iris#air1)	Rotar Zairo#1air2	3175
1@air2,223,78,3	duplicate(Iris#air1)	Rotar Zairo#2air2	3175

1@air2,229,54,5	script	Fenrir#air2	4_F_FENRIR,{
		if ('airship_status != 6)
				end;
		cutin "fenrir_a",2;
		mes "[เฟนริล]";
		mes "...ไอ้สารเลวพวกนี้มันยังมาอีก";
		next;
		cutin "h_guardian2",1;
		mes "-";
		mes "ตามที่คาดไว้ สัญชาตญาณของเราไม่ผิด การตามกระแสแห่งความชั่วร้ายมาได้ไกลขนาดนี้ถือเป็นผลดี";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "...?? ใครไปบ้าง!!";
		next;
		cutin "h_guardian2",1;
		mes "-";
		mes "ขอแนะนำตัวก่อนนะคะ...";
		next;
		mes "-";
		mes "พระศิวะ! บาศรา! ลักษมี!";
		mes "พวกเราคือเหล่าเรนเจอร์ผู้ไร้เทียมทาน!";
		next;
		cutin "h_guardian1",1;
		mes "[ฮีโร่สาวผู้พิทักษ์]";
		mes "ฮีโร่ผู้พิทักษ์!!";
		next;
		cutin "h_chaos02",2;
		mes "[ความวุ่นวาย]";
		mes "-";
		next;
		cutin "fenrir_a",2;
		mes "[เฟนริล]";
		mes "-";
		next;
		cutin "h_guardian1",1;
		mes "[พระศิวะ]";
		mes "หืม? ปฏิกิริยาเป็นไงบ้าง? ทุกคนก็เฉยๆ นะ... มากเกินไปรึเปล่า?";
		next;
		mes "[บาซาร่า]";
		mes "อืม ไม่หรอก ทุกคนต่างหลงใหลกับทางเข้าอันยิ่งใหญ่ของเรา";
		next;
		mes "[ลักษมี]";
		mes "ใช่เลย ใช่เลย การผลิตขนาดนี้เพื่อเปิดตัวเป็นฮีโร่มันมากเกินไปหน่อย!!";
		next;
		cutin "h_iris02",2;
		mes "[ไอริส]";
		mes "ปู๊....";
		mes "ปู่ฮ่าฮ่าฮ่าฮ่าฮ่า";
		next;
		mes "[ไอริส]";
		mes "อะไรนะ? พวกนี้เป็นใครกัน!";
		mes "แค่ดูเสื้อผ้าพวกเขาสิ!!";
		mes "นั่นชุดบนเวทีรึเปล่า เป็นดาราไม่ใช่ฮีโร่เหรอ?";
		next;
		cutin "h_guardian3",1;
		mes "[พระศิวะ]";
		mes "ฮึ่ม คุณไม่รู้ว่าคุณกำลังพูดถึงอะไร!";
		mes "รู้มั้ยว่าเสื้อผ้าพวกนี้ราคาเท่าไหร่? นั่นแหละคือความผิดของคนที่ไม่สนใจแฟชั่นล่าสุด~";
		next;
		mes "[บาซาร่า]";
		mes "พวกคุณ! นี่ไม่ใช่เวลาที่จะพูดคุยแบบนี้! เรามาขจัดคลื่นแห่งความชั่วร้ายกันเถอะ!!";
		next;
		mes "[พระศิวะ]";
		mes "โรเจอร์ บาซาร่า! เรามาจบมันด้วยการโจมตีที่รวดเร็วกันเถอะ!!";
		mes "ฮูมะ เมทัล ชูริเคน!! สวัสดี!";
		next;
		cutin "",255;
		mes "- ป๊อป! ป๊อป ป๊อป!";
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "อะไร อะไรพวกนั้น... โง่สิ้นดี...";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "โอ้? โห!";
		mes "พันธมิตรที่ไม่คาดคิด?";
		mes "นี่อาจกลายเป็นงานปาร์ตี้ที่สนุกสนานได้~";
		next;
		cutin "fenrir_a",2;
		mes "[เฟนริล]";
		mes "รอ!";
		mes "ปาร์ตี้ครั้งนี้มันควรจะจบไปเลยดีกว่า...นี่มันเรื่องอะไรกัน เฟรย่าส่งคุณมาเหรอ";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "โอ้ คุณไม่ต้องกลัวหรอก";
		mes "มนุษย์ธรรมดาๆ คนหนึ่งกล้าเอ่ยชื่อเฟรย่าได้อย่างไร";
		next;
		cutin "fenrir_a",2;
		mes "[เฟนริล]";
		mes "ตามคาด เจ้าเป็นเบี้ยของเฟรย่าใช่ไหม";
		next;
		cutin "h_arcana01",2;
		mes "[อาร์คานา]";
		mes "...ระวังหน่อย ถ้ายังทำอย่างนี้ต่อไป...";
		mes "คุณจะได้รับการลงโทษจากพระเจ้า!";
		next;
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "-";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "โอเค~ ฉันจะโชว์อะไรดีๆ ให้คุณดู อะไรสักอย่างที่จะทำให้คุณรู้สึกเย็นสบาย";
		next;
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "หรือว่าจะเป็น...ลมหายใจของไวเวิร์น!!";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "บิงโก ~";
		next;
		cutin "",255;
		mes "[ไวเวิร์น]";
		mes "วู้วววว..!!";
		next;
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "ไม่นะ...ไม่นะ!! เรือ!!";
		next;
		enablenpc instance_npcname("Loki#air2");
		specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Loki#air2");
		cutin "",255;
		mes "- บูม!!!";
		next;
		cutin "fenrir_a",2;
		mes "[เฟนริล]";
		mes "...?! โลกิ!!";
		next;
		cutin "ep14_roki02",2;
		mes "[โลกิ]";
		mes "อืม...";
		mes "เสื้อผ้าใหม่ของฉันพังแล้ว...";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "อุ๊ย...คุณขว้าง Wyvern's Breath กลับมา... ผลลัพธ์ที่คาดไม่ถึง แต่คุณจะทนกับสิ่งนี้ได้ไหม";
		next;
		cutin "",255;
		mes "[ไวเวิร์น]";
		mes "วู้วววว..!!";
		next;
		cutin "ep14_roki02",2;
		mes "[โลกิ]";
		mes "ศัตรูพืชที่น่ารำคาญ...";
		mes "คุณ! ฉันจะเผชิญหน้าอันหนึ่ง ส่วนคุณเผชิญอีกอัน...";
		close2;
		cutin "",255;
		if ('airship_status == 6)
				donpcevent instance_npcname("calla#air2") + "::OnStart";
		end;
}

1@air2,104,72,0	script	calla#air2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		'airship_status = 7;
		disablenpc instance_npcname("Fenrir#air2");
		disablenpc instance_npcname("Loki#air2");
		disablenpc instance_npcname("Iris#air2");
		disablenpc instance_npcname("Wywern#2air2");
		disablenpc instance_npcname("Wywern#3air2");
		enablenpc instance_npcname("calla#air2");
		monster 'map_air2$,235,49,"Wywern",3180,1, instance_npcname("calla#air2") + "::OnMyMobDead";// E1_G_S_NYDHOG
		end;
OnMyMobDead:
		if (mobcount( 'map_air2$, instance_npcname("calla#air2") + "::OnMyMobDead" ) < 1) {
				for ( .@i = 1; .@i <= 8; .@i++ )
						enablenpc instance_npcname("Gremlin#" + .@i + "air2");
				enablenpc instance_npcname("Iris#2air2");
				enablenpc instance_npcname("Captain Ferlock#air2");
				disablenpc instance_npcname("calla#air2");
				'airship_status = 8;
		}
		end;
}

// Step 5
1@air2,232,51,5	script	Iris#2air2	4_F_IRIS,{
		if ('airship_status != 8)
				end;
		mes "[เกร็มลิน]";
		mes "กรี๊ด...คูอาา~!!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "ว๊ายยย ยังมีอีกเพียบ!!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟลร็อค]";
		mes "เจอแล้ว อาวุธของฉัน! อิอิอิ";
		next;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "ข้าจะแสดงให้ท่านเห็นว่าจะเกิดอะไรขึ้นหากท่านโจมตีเรือของข้า!";
		next;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "ฉันจะจบเรื่องนี้ด้วยปืนไรเฟิล Doom Exodus Joker XIII ที่ฉันผลิตขึ้นเป็นพิเศษ!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "แค่...แค่นาทีเดียว...";
		mes "จะยิงมันจริงๆ เหรอ!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "แน่นอน!";
		mes "อิอิอิอิอิอิอิอิอิ!";
		next;
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion2";
		cutin "",255;
		mes "- ปัง! ปัง! ปัง!";
		next;
		mes "[เกร็มลิน]";
		mes "อ๊อกกก!!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "มุฮ่าฮ่าฮ่า!";
		mes "คุณชอบมันมั้ย!!";
		mes "มุฮ่าฮ่าฮ่าฮ่า";
		next;
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		cutin "",255;
		mes "- ปัง! ปัง! ปัง!";
		next;
		mes "[อารักขา]";
		mes "อ๊าก...เครื่องยนต์พัง!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "อะไรนะ! ไอ้เวรพวกนั้นมันทำให้เครื่องยนต์พังอีกแล้วเหรอ!! กูโกรธแล้วนะ!!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "คุณเป็นคนทำ!!!";
		next;
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		cutin "",255;
		mes "- ปัง! ปัง! ปัง!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "ไอ้เวรเอ๊ย! ตายซะ! ตายซะ!";
		mes "มุฮ่าฮ่าฮ่า!!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "สถานการณ์แบบนี้เรือจะล่มเพราะกัปตันกวางเรนเดียร์นั่น!! หยุดกัปตันคนนั้นซะ!! ฉันกลัว...";
		next;
		mes "[ไอริส]";
		mes "เราจะหยุดเขาได้อย่างไร ...บางทีเขาอาจจะฟื้นคืนสติได้หากคุณเอาชนะเขาแค่พอให้ไม่ตาย... ฉันนับถือคุณนะ!!";
		close2;
		cutin "",255;
		if ('airship_status == 8)
				donpcevent instance_npcname("callb#air2") + "::OnStart";
		end;
}

1@air2,247,51,3	duplicate(Iris#air1)	Captain Ferlock#air2	4_M_REINDEER

1@air2,104,52,0	script	callb#air2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		'airship_status = 9;
		for ( .@i = 1; .@i <= 8; .@i++ )
				disablenpc instance_npcname("Gremlin#" + .@i + "air2");
		disablenpc instance_npcname("Captain Ferlock#air2");
		disablenpc instance_npcname("Iris#2air2");
		enablenpc instance_npcname("callb#air2");
		monster 'map_air2$,247,51,"Out of control Captain",3181,1, instance_npcname("callb#air2") + "::OnMyMobDead";// E1_FELOCK
		end;
OnMyMobDead:
		if (mobcount( 'map_air2$, instance_npcname("callb#air2") + "::OnMyMobDead" ) < 1)
				initnpctimer;
		end;
OnTimer6000:
		enablenpc instance_npcname("Chaos#air2");
		enablenpc instance_npcname("Captain Ferlock#2air2");
		disablenpc instance_npcname("callb#air2");
		'airship_status = 10;
		stopnpctimer;
		end;
}

// Final
1@air2,239,58,5	script	Chaos#air2	4_M_CHAOS,{
		if ('airship_status > 10) {
				cutin "h_chaos02",2;
				mes "[ความวุ่นวาย]";
				mes "ออกไปจากที่นี่เดี๋ยวนี้!! เรือลำนี้ตกอยู่ในอันตราย!!";
				close3;
		}
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "อุ๊ย.. ฉันยิงแบบบ้าคลั่งเลย ดูเหมือนว่า Gremlins จะหายไปเยอะแล้วเหรอเนี่ย 555";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "เรือเสียหายเพราะคุณ!!";
		next;
		cutin "h_arcana02",2;
		mes "[อาร์คานา]";
		mes "ทำหน้าที่ของเราไปเถอะ หึม แค่นี้ก็พอแล้วใช่ไหม";
		next;
		mes "[อาร์คานา]";
		mes "แล้วถึงเวลาที่เราต้องหนีออกไป...";
		next;
		cutin "h_guardian1",1;
		mes "[ลักษมี]";
		mes "โอ้... นั่น บาซาร่า ดูเหมือนพวกมันกำลังล่าถอย";
		next;
		mes "[บาซาร่า]";
		mes "แน่นอนค่ะ!! เค้ารู้ทันฝีมือเรา!! ฮ่าฮ่าฮ่า~";
		next;
		mes "[บาซาร่า]";
		mes "ในบันทึกนั้น Huuma Metal Shuriken อันสุดท้าย!!";
		next;
		for ( .@i = 1; .@i < 4; .@i++ ) {
				enablenpc instance_npcname("Gremlin#" + .@i + "air2");
				hideonnpc instance_npcname("Gremlin#" + .@i + "air2");
		}
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		cutin "",255;
		mes "- บาบัง! ปัง!";
		next;
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "อี๊ย!! เหมือนจะล้มนะ!!";
		mes "...สั้นไป...เอื้อมไม่ถึง...";
		next;
		mes "[เฟนริล]";
		mes "...นี่คือจุดจบใช่ไหม?";
		mes "...ที่ที่ฉันไปไม่ถึงเหมือนแต่ก่อน...";
		mes "ความวุ่นวาย...";
		next;
		cutin "ep14_roki02",2;
		mes "[โลกิ]";
		mes "...คว้าไว้";
		next;
		cutin "fenrir_b",2;
		mes "[เฟนริล]";
		mes "โลกิ...เหรอ?";
		next;
		cutin "ep14_roki02",2;
		mes "[โลกิ]";
		mes "มองอะไรอยู่เนี่ย ขึ้นมาสิ...";
		next;
		cutin "",255;
		mes "[อารักขา]";
		mes "ยืนยันเครื่องยนต์ทั้งหมดหยุดทำงานแล้ว!!";
		mes "เราต้องรีบหนี!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "สาปแช่ง...";
		mes "ทุกคนขึ้นเรือชูชีพกันเถอะ!";
		mes "ขึ้นเรือชูชีพโดยให้ผู้โดยสารเป็นลำดับแรก! ปล่อยเรือชูชีพที่ขึ้นเรือเรียบร้อยแล้ว!";
		next;
		cutin "h_chaos02",2;
		mes "[ความวุ่นวาย]";
		mes "เดี๋ยวก่อน!! ยังมีคนเหลืออยู่!!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "ไม่มีอะไรที่เราทำได้!";
		mes "ถ้าเรายังคงชะงักต่อไป ส่วนที่เหลือจะตกอยู่ในอันตราย!! น่าเสียดายแต่เราก็ทำเต็มที่แล้ว!";
		next;
		cutin "h_chaos03",2;
		mes "[ความวุ่นวาย]";
		mes "แต่คนที่เหลือก็เป็นชีวิตอันล้ำค่าเหมือนกันนะ! จะทิ้งพวกเขาไปงั้นเหรอ?!! ห๊ะ?!!";
		next;
		cutin "fly_felrock2",2;
		mes "[กัปตันเฟอร์ล็อค]";
		mes "ใช่แล้วคุณพูดถูก...";
		mes "สำหรับเรื่องนั้น ฉันจะปล่อยให้คุณจัดการเรื่องอื่นๆ เอง ฉันไปแล้ว!!";
		disablenpc instance_npcname("Captain Ferlock#2air2");
		next;
		cutin "h_chaos03",2;
		mes "[ความวุ่นวาย]";
		mes "เฮ้ย จะไปไหนวะ อ๊าก!!";
		mes "ฉันจะจับเธอ ฉันจะฆ่าเธอ!!";
		next;
		cutin "h_guardian1",1;
		mes "[บาซาร่า]";
		mes "ไม่ใช่เวลาที่เราจะมาเผชิญหน้ากับพวก Gremlins ตัวน้อยเหล่านี้";
		next;
		mes "[บาซาร่า]";
		mes "ฉันสัมผัสได้ถึงคลื่นความชั่วร้ายที่ใหญ่ขึ้น";
		mes "ปล่อยให้พวกเขาจัดการเจ้าตัวน้อยเหล่านี้ไปเถอะ ไปกันเถอะ!";
		next;
		mes "[พระลักษมี&พระศิวะ]";
		mes "โรเจอร์!! เรนเจอร์ ฟอร์มบิน!!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "ห๋า? เฮ้!!";
		mes "คิดว่าจะไปไหนหลังจากทำลายเรือแล้ว?!";
		next;
		mes "[ไอริส]";
		mes "ถ้าคราวหน้าเจอกู กูจะตบให้ตายเลย!! ประหม่าไว้!! ทำได้แน่เหรอ?! เตรียมตัวไว้ให้ดีไอ้พวกขี้ขลาด!!";
		next;
		cutin "h_guardian3",1;
		mes "[ฮีโร่สาวผู้พิทักษ์]";
		mes "...พวกชาวบ้านธรรมดาๆ? (โกรธจัด)";
		next;
		mes "[ฮีโร่สาวผู้พิทักษ์]";
		mes "คนที่ดูหมิ่นและประณามเส้นทางแห่งความชอบธรรม... เราควรที่จะตัดสินอย่างยุติธรรมโดยไม่ปราณีใช่หรือไม่?";
		next;
		mes "[ฮีโร่สาวผู้พิทักษ์]";
		mes "ตายซะคนร้าย!! ฮูม่า เมทัล ชูริเคน!!";
		next;
		donpcevent instance_npcname("Gremlin#1air2") + "::OnExplosion1";
		cutin "",255;
		mes "- ปัง! ปัง! ปัง!";
		next;
		cutin "h_iris01",2;
		mes "[ไอริส]";
		mes "อี๊ยย!!";
		next;
		cutin "h_guardian1",1;
		mes "[ฮีโร่สาวผู้พิทักษ์]";
		mes "ฮ่าฮ่าฮ่าฮ่า!!";
		mes "สมน้ำหน้าคุณจริงๆ~!! ไปกันเถอะ!!";
		next;
		cutin "h_chaos03",2;
		mes "[ความวุ่นวาย]";
		mes "ไอริส!! มานี่สิ!!";
		mes "รีบไปหาเฟนริลกับโลกิแล้วออกไปจากที่นี่กันเถอะ!!";
		next;
		cutin "h_chaos02",2;
		mes "[ความวุ่นวาย]";
		mes "ออกไปจากที่นี่เดี๋ยวนี้!! เรือลำนี้ตกอยู่ในอันตราย!!";
		close2;
		if ('airship_status == 10){
				'airship_status = 11;
				enablenpc instance_npcname("warp5a#air2");
				cutin "",255;
				getexp 100000,65000;
		}
		end;
}

1@air2,243,58,3	duplicate(Iris#air1)	Captain Ferlock#2air2	4_M_REINDEER

// Explosions
1@air2,113,52,0	duplicate(Iris#air1)	fire1#air2	HIDDEN_WARP_NPC
1@air2,115,68,0	duplicate(Iris#air1)	fire2#air2	HIDDEN_WARP_NPC
1@air2,96,58,0	duplicate(Iris#air1)	fire3#air2	HIDDEN_WARP_NPC
1@air2,90,75,0	duplicate(Iris#air1)	fire4#air2	HIDDEN_WARP_NPC
1@air2,92,51,0	duplicate(Iris#air1)	fire5#air2	HIDDEN_WARP_NPC
1@air2,77,42,0	duplicate(Iris#air1)	fire6#air2	HIDDEN_WARP_NPC
1@air2,69,77,0	duplicate(Iris#air1)	fire7#air2	HIDDEN_WARP_NPC
1@air2,50,49,0	duplicate(Iris#air1)	fire8#air2	HIDDEN_WARP_NPC
1@air2,44,61,0	duplicate(Iris#air1)	fire9#air2	HIDDEN_WARP_NPC
1@air2,31,53,0	duplicate(Iris#air1)	fire10#air2	HIDDEN_WARP_NPC
1@air2,24,74,0	duplicate(Iris#air1)	fire11#air2	HIDDEN_WARP_NPC
1@air2,43,45,0	duplicate(Iris#air1)	fire12#air2	HIDDEN_WARP_NPC
1@air2,73,65,0	duplicate(Iris#air1)	fire13#air2	HIDDEN_WARP_NPC
1@air2,53,72,0	duplicate(Iris#air1)	fire14#air2	HIDDEN_WARP_NPC
1@air2,82,62,0	duplicate(Iris#air1)	fire15#air2	HIDDEN_WARP_NPC
1@air2,234,70,0	duplicate(Iris#air1)	fire21#air2	HIDDEN_WARP_NPC
1@air2,247,64,0	duplicate(Iris#air1)	fire22#air2	HIDDEN_WARP_NPC
1@air2,230,33,0	duplicate(Iris#air1)	fire23#air2	HIDDEN_WARP_NPC
1@air2,277,39,0	duplicate(Iris#air1)	fire25#air2	HIDDEN_WARP_NPC
1@air2,223,50,0	duplicate(Iris#air1)	fire26#air2	HIDDEN_WARP_NPC
1@air2,238,70,0	duplicate(Iris#air1)	fire29#air2	HIDDEN_WARP_NPC
1@air2,238,32,0	duplicate(Iris#air1)	fire30#air2	HIDDEN_WARP_NPC

1@air2,0,0,0	script	firecontrol#air2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("firecontrol#air2");
		for ( .@i = 1; .@i <= 15; .@i++ )
				enablenpc instance_npcname("fire" + .@i + "#air2");
		initnpctimer;
		end;
OnTimer1000:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire1#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire8#air2");
		end;
OnTimer1400:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire2#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire9#air2");
		end;
OnTimer1800:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire3#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire10#air2");
		end;
OnTimer2200:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire4#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire11#air2");
		end;
OnTimer2600:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire5#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire12#air2");
		end;
OnTimer3000:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire6#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire13#air2");
		end;
OnTimer3400:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire7#air2");
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire14#air2");
		end;
OnTimer3800:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire15#air2");
		initnpctimer;
		end;
}

1@air2,0,0,0	script	firecontrol2#air2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("firecontrol2#air2");
		enablenpc instance_npcname("fire21#air2");
		enablenpc instance_npcname("fire22#air2");
		enablenpc instance_npcname("fire23#air2");
		enablenpc instance_npcname("fire25#air2");
		enablenpc instance_npcname("fire26#air2");
		enablenpc instance_npcname("fire29#air2");
		enablenpc instance_npcname("fire30#air2");
		initnpctimer;
		end;
OnTimer1000:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire21#air2");
		end;
OnTimer1400:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire22#air2");
		end;
OnTimer1800:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire23#air2");
		end;
OnTimer2200:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire25#air2");
		end;
OnTimer2600:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire26#air2");
		end;
OnTimer3000:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire29#air2");
		end;
OnTimer3400:
		specialeffect EF_BLASTMINEBOMB,AREA, instance_npcname("fire30#air2");
		initnpctimer;
		end;

OnInstanceInit:
		'airship_status = 0;

		'map_air1$ = instance_mapname("1@air1");
		'map_air2$ = instance_mapname("1@air2");

		// Warps
		disablenpc instance_npcname("warp1#air1");
		disablenpc instance_npcname("warp2a#air1");
		disablenpc instance_npcname("warp2b#air1");
		disablenpc instance_npcname("warp3a#air1");
		disablenpc instance_npcname("warp3b#air1");
		disablenpc instance_npcname("warp4a#air2");
		disablenpc instance_npcname("warp5a#air2");

		// Entrance
		disablenpc instance_npcname("Acidus#1air1");
		disablenpc instance_npcname("Acidus#2air1");
		disablenpc instance_npcname("Rotar Zairo#1air1");
		disablenpc instance_npcname("Rotar Zairo#2air1");
		disablenpc instance_npcname("Wywern#air1");
		for ( .@i = 1; .@i <= 8; .@i++ )
				disablenpc instance_npcname("Gremlin#" + .@i + "air1");

		// Step 1
		disablenpc instance_npcname("#airshipputmob1");
		disablenpc instance_npcname("Traveling Knight#air1");
		disablenpc instance_npcname("Passed out Drunkard#air1");
		disablenpc instance_npcname("Wealthy man at a meal#a");
		disablenpc instance_npcname("Free Riding Bard#air1");
		disablenpc instance_npcname("Airship Cafeteria Manager#air1");
		disablenpc instance_npcname("Gambling Spirit#air1");

		// Step 2
		disablenpc instance_npcname("#airshipputmob2");
		disablenpc instance_npcname("Iris#2air1");
		disablenpc instance_npcname("Chaos#2air1");
		disablenpc instance_npcname("Gremlin#2aair1");
		disablenpc instance_npcname("Airship Crew#1air1");
		disablenpc instance_npcname("Airship Crew#2air1");
		disablenpc instance_npcname("King's Escort#1air1");
		disablenpc instance_npcname("King's Escort#2air1");
		disablenpc instance_npcname("Tourist#1air1");
		disablenpc instance_npcname("Tourist#2air1");
		disablenpc instance_npcname("King of a Small Country#1air1");

		// Step 3 - 1@air2
		disablenpc instance_npcname("#airshipputmob3");
		disablenpc instance_npcname("firecontrol#air2");
		disablenpc instance_npcname("Passed out Drunkard#air2");
		disablenpc instance_npcname("Wealthy man at a meal#b");
		disablenpc instance_npcname("Traveling Knight#air2");
		disablenpc instance_npcname("Free Riding Bard#air2");
		disablenpc instance_npcname("Gambling Spirit#air2");
		disablenpc instance_npcname("Airship Cafeteria Manager#air2");

		// Step 4
		disablenpc instance_npcname("calla#air2");
		disablenpc instance_npcname("firecontrol2#air2");
		disablenpc instance_npcname("Fenrir#air2");
		disablenpc instance_npcname("Loki#air2");
		disablenpc instance_npcname("Iris#air2");
		disablenpc instance_npcname("Wywern#air2");
		disablenpc instance_npcname("Wywern#2air2");
		disablenpc instance_npcname("Wywern#3air2");
		disablenpc instance_npcname("Acidus#1air2");
		disablenpc instance_npcname("Acidus#2air2");
		disablenpc instance_npcname("Rotar Zairo#1air2");
		disablenpc instance_npcname("Rotar Zairo#2air2");
		disablenpc instance_npcname("Captain Ferlock#air2");
		for ( .@i = 1; .@i <= 3; .@i++ )
				hideonnpc instance_npcname("Gremlin#" + .@i + "air2");
		for ( .@i = 4; .@i <= 8; .@i++ )
				disablenpc instance_npcname("Gremlin#" + .@i + "air2");

		// Step 5
		disablenpc instance_npcname("callb#air2");
		disablenpc instance_npcname("Iris#2air2");

		// Final
		disablenpc instance_npcname("Chaos#air2");
		disablenpc instance_npcname("Captain Ferlock#2air2");

		// Blast
		for ( .@i = 1; .@i <= 15; .@i++ )
				disablenpc instance_npcname("fire" + .@i + "#air2");
		disablenpc instance_npcname("fire21#air2");
		disablenpc instance_npcname("fire22#air2");
		disablenpc instance_npcname("fire23#air2");
		disablenpc instance_npcname("fire25#air2");
		disablenpc instance_npcname("fire26#air2");
		disablenpc instance_npcname("fire29#air2");
		disablenpc instance_npcname("fire30#air2");
		end;
}
//===== rAthena Script =======================================
//= Central Laboratory
//===== Description: =========================================
//= [Official Conversion]
//= Central Laboratory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Updated to match official script. [Capuche]
//============================================================

verus01,153,155,5	script	Temporary Dimension-mov#pa0829_01	PORTAL,{
		.@party_id = getcharid(1);
		if (.@party_id == 0) {
				mes "[ผู้ย้ายมิติชั่วคราว]";
				mes "คุณไม่มีเพื่อนร่วมเดินทาง? จัดปาร์ตี้ของคุณให้สนุกแม้จะเดินทางคนเดียว";
				close;
		}
		switch( checkquest(12347,PLAYTIME) ) {
		case -1:
				switch( instance_enter("Central Laboratory") ) {
				case IE_NOMEMBER:
				case IE_NOINSTANCE:
						mes "[ผู้ย้ายมิติชั่วคราว]";
						mes "ช่องทางสำหรับการเคลื่อนตัวมิติยังไม่ได้รับความสำคัญ";
						close;
				case IE_OTHER:
						mes "[มิติชั่วคราว-เคลื่อนไหว]";
						mes "ข้อผิดพลาดที่ไม่รู้จัก";
						close;
				case IE_OK:
						mapannounce "verus01", "" + strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering Central Laboratory.",bc_map,"0x00FF99";
						setquest 12347;// Trace of Laboratory Access
						end;
				}
				end;
		case 0:
		case 1:
				mes "[ผู้ย้ายมิติชั่วคราว]";
				mes "ร่องรอยของเหตุการณ์ครั้งสุดท้ายยังคงหลงเหลืออยู่ การเข้าถึงถูกปฏิเสธ";
				close;
		case 2:
				mes "^0000ffการใช้ Dimension-mover ดูเหมือนจะเป็นไปได้^000000";
				erasequest 12347;// Trace of Laboratory Access
				close;
		}
}

verus01,149,155,5	script	Civilization Explorer#pa0829_01	4_LGTSCIENCE,{
		if (BaseLevel < 135) {
				mes "[นักสำรวจอารยธรรม]";
				mes "เอ่อ...ผมไม่รู้ว่าจะช่วยได้หรือเปล่านะ ดูเหมือนว่ายังมีช่องว่างอยู่!";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "เมื่อคุณแข็งแกร่งขึ้นแล้ว กลับมาหาฉันเถอะ คุณน่าจะมีเลเวลเกือบ 135 แล้ว!";
				close;
		}
		mes "^ff0000การฝึกมอนสเตอร์จะขัดขวางความก้าวหน้าในดันเจี้ยนนี้ โปรดทราบสิ่งนี้^000000";
		next;
		mes "[นักสำรวจอารยธรรม]";
		switch( isbegin_quest(12346) ) {
		case 0:
				mes "มีสิ่งที่น่าสนใจมากมายเกี่ยวกับอารยธรรมโบราณในพื้นที่นี้";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "จากการสังเกตชั้นดินคาดว่าน่าจะมีอายุประมาณ 500 - 600 ปี!";
				mes "แต่สิ่งของที่ขุดพบที่นี่หลายอย่างนั้นล้ำหน้าเทคโนโลยีปัจจุบันของเราไปไกลมาก";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "บันทึกที่กู้คืนมายังพบคำศัพท์ที่ไม่ชัดเจนจำนวนมากด้วย";
				next;
				select("What kind of unknown terms?");
				mes "[นักสำรวจอารยธรรม]";
				mes "^0000ffคอมพิวเตอร์แม่^000000 หรือ ^0000ffชิป LSI^000000 หรือ และอะไรอีก ^0000ffไก่^000000?";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "อืม ใช่แล้ว มีคำว่าไก่แน่นอน ฉันไม่รู้ว่านั่นเป็นการผสมผสานเทคโนโลยีแบบใด";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "มีการอ้างอิงถึงบันทึกของผู้คนที่ดูเหมือนจะ 'หยั่งราก' ท่ามกลางร่องรอยต่างๆ ของเทคโนโลยี";
				next;
				if (select( "It is not a topic I would be interested in.", "Interesting. Have there been any results?" ) == 1) {
						mes "[นักสำรวจอารยธรรม]";
						mes "น่าเสียดาย.";
						close;
				}
				mes "[นักสำรวจอารยธรรม]";
				mes "เป้าหมายของฉันคือการวิจัยการใช้เทคโนโลยีที่เกี่ยวข้องกับยุคสมัย แม้ว่าสิ่งที่ฉันค้นพบมาจนถึงตอนนี้จะยังไม่มีอะไรสำคัญนัก";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "ถ้าเป็นฉัน ฉันจะใช้เครื่องมือเคลื่อนย้ายมิติเพื่อดูด้วยตัวเอง แต่งานมีเยอะเกินไป นอกจากนี้ ฉันไม่ใช่นักผจญภัย...";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "คุณสนใจที่จะเดินทางไปในอดีตบ้างไหม?";
				next;
				mes "[นักสำรวจอารยธรรม]";
				mes "จะดีกว่ามากหากคุณพาฉันไปดูรอบๆ หากคุณพบวัตถุที่น่าสนใจ";
				next;
				if (select( "Not interested.", "Ok." ) == 1) {
						mes "[นักสำรวจอารยธรรม]";
						mes "น่าเสียดาย.";
						close;
				}
				mes "[นักสำรวจอารยธรรม]";
				mes "คุณเป็นนักผจญภัย บอกฉันหน่อยสิว่าคุณตัดสินใจจะทำอะไร";
				if (isbegin_quest(12346) == 0)
						setquest 12346;// Strange Ancient Science
				break;
		case 1:
				mes "ฉันสามารถช่วยคุณได้อย่างไร?";
				break;
		case 2:
				mes "คราวนี้คุณอยากทำอะไร?";
				break;
		}
		next;
		if (isbegin_quest(12346) == 1)
				completequest 12346;
		.@party_id = getcharid(1);
		if (.@party_id == 0) {
				mes "[นักสำรวจอารยธรรม]";
				mes "กรุณาพูดคุยกับฉันหลังจากจัดปาร์ตี้ที่มีสมาชิก 1 คนขึ้นไปหรือหลังจากเข้าร่วมปาร์ตี้แล้ว";
				close;
		}
		switch( checkquest(12347,PLAYTIME) ) {
		case -1:
				if (is_party_leader() == false) {
						mes "[นักสำรวจอารยธรรม]";
						mes "อ๋อ คุณไม่ใช่หัวหน้าพรรค พาฉันไปหาหัวหน้าพรรคของคุณสิ หรือไม่ก็พาหัวหน้าพรรคของคุณมาหาฉัน";
						close;
				}
				if (select( "Produce a crack of time", "Cancel" ) == 2)
						close;
				if (instance_create("Central Laboratory") < 1) {
						mes "งานสังสรรค์:" + getpartyname( getcharid(1) ) + "";
						mes "ผู้นำ:" + getpartyleader( getcharid(1) ) + "";
						mes "^0000ffห้องปฏิบัติการกลาง ^000000 - ข้อผิดพลาดที่ไม่ทราบสาเหตุ";
						close;
				}
				mes "[นักสำรวจอารยธรรม]";
				mes "เมื่อช่องว่างแห่งเวลาเปิดขึ้น ให้หายใจเข้าแล้วพูดอีกครั้ง";
				close;
		case 0:
		case 1:
				mes "[นักสำรวจอารยธรรม]";
				mes "อ่า โอ้ ไม่นะ";
				mes "ยังคงมีผลกระทบจากการเดินทางในมิติต่างๆ หลงเหลืออยู่บ้าง เราไม่สามารถเดินทางในสภาพเช่นนี้ได้อีก";
				close;
		case 2:
				mes "^0000ffร่องรอยของการเข้าไปในห้องทดลองกลางถูกลบเลือนไปหมดแล้ว การพูดคุยกับนักสำรวจอารยธรรมดูเหมือนจะเป็นไปได้^000000";
				erasequest 12347;// Trace of Laboratory Access
				close;
		}
}

// Entrance
1@lab,104,34,4	script	Probationary researcher#pa0829	4_LGTSCIENCE,{
		if ('switch_enabled == 1) {
				mes "[นักวิจัยทดลองงาน]";
				mes "ประตูทางเข้าเปิดอยู่แล้ว";
				close;
		}
		if (is_party_leader() == false) {
				mes "[นักวิจัยทดลองงาน]";
				mes "กรุณารอสักครู่ ฉันกำลังคุยกับไกด์ว่าคุณได้รับอนุมัติหรือไม่";
				close;
		}
		mes "[นักวิจัยทดลองงาน]";
		mes "คุณมีส่วนร่วมเป็นผู้ช่วยในการทดลองโครงการวิจัย 32001 หรือไม่?";
		next;
		if (select( "Yes.", "No." ) == 2) {
				mes "[นักวิจัยทดลองงาน]";
				mes "ดูเหมือนว่าคุณจะอยู่ในห้องทดลองที่ผิด กรุณาออกไปและขอความช่วยเหลือ";
				close;
		}
		mes "[นักวิจัยทดลองงาน]";
		mes "เราต้องใช้เวลาราวๆ 5 วินาทีในการยืนยันรหัสเข้าใช้งาน";
		close2;
		sleep 3000;
		npctalk "นักวิจัยทดลองงาน : รหัสเข้าห้องตรงกันค่ะ ดิฉันจะเปิดประตูให้ค่ะ";
		sleep 6000;
		npctalk "นักวิจัยทดลองงาน : ขณะนี้ท่านสามารถเข้าไปได้แล้ว โปรดทราบว่าท่านไม่สามารถเปิดประตูจากด้านอื่นได้";
		if ('switch_enabled == 0) {
				'switch_enabled = 1;
				enablenpc instance_npcname("lab_warp#1");
				enablenpc instance_npcname("lab_warp#2");
				enablenpc instance_npcname("lab_warp#3");
				enablenpc instance_npcname("Main Switch#");
				enablenpc instance_npcname("Senior researcher#pa082");
				enablenpc instance_npcname("#eff_nc");
				for ( .@i = 1; .@i <= 8; ++.@i )
						enablenpc instance_npcname( "Switch" + .@i + "#" );
		}
		end;

OnInstanceInit:
		'map_name$ = instance_mapname("1@lab");
		'code_found = 'bool_event = 'switch_enabled = 0;
		for ( .@i = 1; .@i <= 8; .@i++ )
				'switch_position[.@i] = 0;

		// Warps
		disablenpc instance_npcname("lab_warp#1");
		disablenpc instance_npcname("lab_warp#2");
		disablenpc instance_npcname("lab_warp#3");
		disablenpc instance_npcname("lab_warp#4");
		disablenpc instance_npcname("lab_warp#5");

		// Switchs
		disablenpc instance_npcname("Main Switch#");
		disablenpc instance_npcname("Senior researcher#pa082");
		disablenpc instance_npcname("#eff_nc");
		for ( .@i = 1; .@i <= 8; ++.@i )
				disablenpc instance_npcname( "Switch" + .@i + "#" );

		// Experiments
		disablenpc instance_npcname("DR Silvia#pa0829");
		disablenpc instance_npcname("DR Federico#pa0829");
		disablenpc instance_npcname("DR Artemia#pa0829");
		disablenpc instance_npcname("DR Vincent#pa0829");
		disablenpc instance_npcname("#wp01");
		disablenpc instance_npcname("#npc_con01");
		disablenpc instance_npcname("#npc_con03");
		disablenpc instance_npcname("#npc_con05");
		disablenpc instance_npcname("#npc_con06");
		disablenpc instance_npcname("#npc_con07");
		disablenpc instance_npcname("#npc_con08");
		disablenpc instance_npcname("#npc_con10");
		disablenpc instance_npcname("#npc_con11");
		for ( .@i = 1; .@i <= 14; ++.@i )
				hideonnpc instance_npcname("#gen_" + (.@i < 10 ? "0" : "") + .@i);
		disablenpc instance_npcname("lab_warp#end");
		end;
}
// 1@lab,104,34,4	script	Probationary researcher#pa0829_2	4_LGTSCIENCE

// Switchs
1@lab,37,37,3	script	Switch1#	CLEAR_NPC,{
		.@num = charat( strnpcinfo(1),6 );
		setarray .@position$[0], "OFF", "ON";
		mes "[สวิตช์" + .@num + "-";
		mes "สวิตช์ปัจจุบันอยู่ใน" + .@position$[ 'switch_position[.@num] ] + "ตำแหน่งคุณจะทำอย่างไร?";
		next;
		'switch_position[.@num] = !( select( "ON","OFF" ) - 1 );
		close;
}
1@lab,39,37,3	duplicate(Switch1#)	Switch2#	CLEAR_NPC
1@lab,41,37,3	duplicate(Switch1#)	Switch3#	CLEAR_NPC
1@lab,43,37,3	duplicate(Switch1#)	Switch4#	CLEAR_NPC
1@lab,46,37,3	duplicate(Switch1#)	Switch5#	CLEAR_NPC
1@lab,48,37,3	duplicate(Switch1#)	Switch6#	CLEAR_NPC
1@lab,50,37,3	duplicate(Switch1#)	Switch7#	CLEAR_NPC
1@lab,52,37,3	duplicate(Switch1#)	Switch8#	CLEAR_NPC

1@lab,34,37,3	script	Main Switch#	CLEAR_NPC,{
		.@string$ = 'switch_position[1] + "" + 'switch_position[2] + "" + 'switch_position[3] + "" + 'switch_position[4] + "" +
				'switch_position[5] + "" + 'switch_position[6] + "" + 'switch_position[7] + "" + 'switch_position[8];
		.@current_code = strtol( .@string$,2 );
		.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
		npctalk "รหัสเข้าใช้งานวันนี้" + .@code + ", รหัสที่ป้อนในปัจจุบัน" + .@current_code + "-";
		sleep 6000;
		if ('code_found > 0)
				npctalk "ประตูเปิดอยู่แล้ว.";
		else if (.@current_code != .@code)
				npctalk "รหัสไม่ถูกต้อง กรุณาลองใหม่อีกครั้ง";
		else {
				'code_found = 1;
				npctalk "รหัสถูกต้องครับ เปิดประตูได้ครับ";
				enablenpc instance_npcname("lab_warp#4");
				enablenpc instance_npcname("lab_warp#5");
				enablenpc instance_npcname("DR Silvia#pa0829");
				enablenpc instance_npcname("DR Federico#pa0829");
				enablenpc instance_npcname("DR Artemia#pa0829");
				enablenpc instance_npcname("DR Vincent#pa0829");
				enablenpc instance_npcname("#wp01");
				donpcevent instance_npcname("#eff_nc") + "::OnStop";
				for ( .@i = 1; .@i <= 8; ++.@i )
						disablenpc instance_npcname( "Switch" + .@i + "#" );
		}
		end;
}

1@lab,45,32,3	script	Senior researcher#pa082	4_LGTSCIENCE,{
		if ('code_found > 0) {
				mes "[นักวิจัยอาวุโส]";
				mes "ประตูเปิดอยู่ คุณจึงสามารถเข้าไปได้แล้ว โปรดทราบว่าคุณไม่สามารถเปิดประตูจากด้านอื่นได้";
				close;
		}
		.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
		mes "[นักวิจัยอาวุโส]";
		mes "ดูเหมือนว่าคุณมาที่นี่เพื่อเข้าร่วมการทดลองการเร่งอนุภาค";
		next;
		mes "[นักวิจัยอาวุโส]";
		mes "รหัสเข้าใช้งานวันนี้คือ ^0000ff" + .@code + "^000000.";
		mes "ต้องป้อนรหัสเป็นเลขฐานสอง ใช้สวิตช์ด้านหลังฉันเพื่อป้อนรหัส";
		next;
		mes "[นักวิจัยอาวุโส]";
		mes "หลังจากกรอกรหัสแล้วให้กดปุ่มหลักใกล้ทางเข้า";
		for ( .@i = 1; .@i <= 8; ++.@i )
				specialeffect EF_STEAL, AREA, instance_npcname("Switch" + .@i + "#");
		close2;
		donpcevent instance_npcname("#eff_nc") + "::OnStart";
		end;
}

1@lab,1,1,0	script	#eff_nc	-1,{
		end;
OnStart:
OnTimer2000:
		specialeffect 203,AREA, instance_npcname("Main Switch#");
		for ( .@i = 1; .@i <= 8; ++.@i )
				specialeffect (203+.@i),AREA, instance_npcname("Switch" + .@i + "#");
		initnpctimer;
		end;
OnStop:
		stopnpctimer;
		end;
}


// Experiments
1@lab,81,84,7	script	DR Silvia#pa0829	4_F_SCIENCE,{
		mes "[ดร.ซิลเวีย]";
		mes "หลังจากการทดลอง ฉันอยากกลับบ้านไปดื่มเบียร์เย็นๆ และไก่ทอด";
		next;
		mes "[ดร.ซิลเวีย]";
		mes "ฉันไม่ได้กลับบ้านมาเกือบสองปีแล้ว";
		next;
		mes "^0000ffซิลเวียกำลังมองอุปกรณ์เร่งอนุภาคด้วยสายตาที่เหนื่อยล้ามาก^000000";
		close;
}

1@lab,80,87,7	script	DR Federico#pa0829	4_LGTSCIENCE,{
		mes "[ดร.เฟเดอริโก]";
		mes "ยิ่งดูก็ยิ่งน่าทึ่งขึ้นเรื่อยๆ ไม่เคยมีเครื่องเร่งอนุภาคขนาดเท่านี้มาก่อน";
		next;
		mes "[ดร.เฟเดอริโก]";
		mes "ด้วยขนาดนี้สงสัยว่าจะสร้างหลุมดำขนาดเล็กได้หรือไม่";
		next;
		mes "[ดร.เฟเดอริโก]";
		mes "อ๋อ ฉันแค่พูดเล่นๆ เหตุการณ์แบบนี้มันใกล้หายนะมากกว่าการทดลองนะ 555";
		close;
}

1@lab,80,90,6	script	DR Artemia#pa0829	4_F_SCIENCE,{
		mes "[ดร.อาร์ทีเมีย]";
		mes "ดูสิ ความเป็นจริงไม่ชอบเลยเมื่อคุณคว้าเชือกเส้นหนึ่งแล้วดึงมัน";
		next;
		mes "[ดร.อาร์ทีเมีย]";
		mes "การบิดเบือนมิติสามารถเกิดขึ้นได้ด้วยการเร่งอนุภาคที่ทรงพลังเช่นนี้";
		next;
		mes "[ดร.อาร์ทีเมีย]";
		mes "ฉันรู้สึกไม่ดีเกี่ยวกับเรื่องนี้เพราะว่า...ธรรมชาติมักจะหาวิธีของมันเอง";
		close;
}

1@lab,81,93,6	script	DR Vincent#pa0829	4_LGTSCIENCE,{
		mes "[ดร.วินเซนต์]";
		mes "เครื่องดีมากครับ ไม่มีอะไรเสียหายแน่นอน!";
		next;
		mes "[ดร.วินเซนต์]";
		mes "อาร์ทีเมียก็กังวลเกินไป ยังไงก็ตาม หลังจากทดลองเสร็จก็จะไปเที่ยวต่อ";
		close;
}

1@lab,80,89,0	script	#wp01	HIDDEN_WARP_NPC,9,9,{
		end;
OnTouch:
		donpcevent instance_npcname("#npc_con01") + "::OnStart";
		disablenpc instance_npcname("#wp01");
		end;
}

// Round 1
1@lab,4,1,3	script	#npc_con01	CLEAR_NPC,{
		end;
OnStart:
		'vincent$ = instance_npcname("DR Vincent#pa0829");
		'federico$ = instance_npcname("DR Federico#pa0829");
		'silvia$ = instance_npcname("DR Silvia#pa0829");
		'artemia$ = instance_npcname("DR Artemia#pa0829");
		'gen_01$ = instance_npcname("#gen_01");
		'gen_02$ = instance_npcname("#gen_02");
		'gen_03$ = instance_npcname("#gen_03");
		'gen_04$ = instance_npcname("#gen_04");
		'gen_05$ = instance_npcname("#gen_05");
		'gen_06$ = instance_npcname("#gen_06");
		'gen_07$ = instance_npcname("#gen_07");
		'gen_08$ = instance_npcname("#gen_08");
		'gen_09$ = instance_npcname("#gen_09");
		'gen_10$ = instance_npcname("#gen_10");
		'gen_11$ = instance_npcname("#gen_11");
		'gen_12$ = instance_npcname("#gen_12");
		'gen_13$ = instance_npcname("#gen_13");
		'gen_14$ = instance_npcname("#gen_14");

		npctalk "เฟเดอริโก้ : เริ่มกันเลยดีกว่า ฮึม... คุณพร้อมหรือยัง คุณหมอเฟรซ่า?", 'federico$;
		sleep 5000;
		npctalk "อาร์ทีเมีย : อาจกำลังทำการตรวจสอบยืนยันขั้นสุดท้าย การทดลองในระดับนี้ต้องใช้ความระมัดระวังอย่างยิ่ง", 'artemia$;
		sleep 6000;
		npctalk "ซิลเวีย : ต้องใช้ความเอาใจใส่ในการดำเนินการผลิตภัณฑ์สำเร็จรูปสำหรับการทดลองครั้งแรก", 'silvia$;
		sleep 4000;
		npctalk "วินเซนต์ : อ๋อ ฉันหวังว่าเราจะเสร็จเร็วๆ นี้ ฉันจะได้ไปกินไก่ได้", 'vincent$;
		sleep 3000;
		npctalk "ซิลเวีย : อ๋อ! ฉันก็ชอบเหมือนกัน โทรหาฉันด้วยนะถ้าไป", 'silvia$;
		sleep 6000;
		mapannounce 'map_name$, "?????: Ah, ah, one two one two, mic test.", bc_map,0x00ff77;
		sleep 2000;
		npctalk "เฟเดอริโก : เธออยู่ที่นี่", 'federico$;
		sleep 2500;
		mapannounce 'map_name$, "?????: Chief director Lona Fresa of project particle accelerator.", bc_map,0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa : Current time " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day 25 minutes after 10.", bc_map,0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa : Testing the effects of particle acceleration on its surroundings in an open space.", bc_map,0x00ff77;
		sleep 5000;
		npctalk "อาร์ทีเมีย : อา... ฉันไม่ชอบการทดลองนี้เลยจริงๆ", 'artemia$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : Starting the experiment. Chief researcher Dr. Federico, Artemia, Silvia, Vincent and assistant researchers.", bc_map,0x00ff77;
		sleep 5000;
		'bool_event = true;
		donpcevent instance_npcname("#npc_con03") + "::OnStart";
		npctalk "อาร์ทีเมีย : มาเริ่มกันเลย~", 'artemia$;
		sleep 3000;
		npctalk "วินเซนต์ : การเริ่มหมุนของเครื่องกำเนิดไฟฟ้า", 'vincent$;
		sleep 3000;
		npctalk "เฟเดอริโก : ความเร็วรอบของเครื่องกำเนิดไฟฟ้าคงที่ที่ 10,000 รอบต่อนาที", 'federico$;
		sleep 2500;
		npctalk "ซิลเวีย : แรงดันไฟฟ้ายังคงอยู่ในแถบความคลาดเคลื่อน", 'silvia$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : This experiment is for the creation of new material that hasn't existed before.", bc_map,0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa : Recorders make sure to make detailed records of the catalyst bed's reaction when it is installed to the accelerator.", bc_map,0x00ff77;
		sleep 6000;
		npctalk "อาร์ทีเมีย : ติดต่อตัวเร่งปฏิกิริยาหมายเลขเตียง 1.", 'artemia$;
		sleep 9000;
		npctalk "ซิลเวีย : แรงดันไฟของเครื่องกำเนิดไฟฟ้าไม่เสถียร ยังคงอยู่ในระดับความคลาดเคลื่อน", 'silvia$;
		sleep 3000;
		donpcevent instance_npcname("#npc_con03") + "::OnStart2";
		sleep 2000;
		npctalk "ซิลเวีย : ตรวจจับความร้อนใกล้กับแถบความคลาดเคลื่อนระหว่างชั้นตัวเร่งปฏิกิริยาและตัวเร่งความเร็ว!", 'silvia$;
		sleep 5000;
		npctalk "วินเซนต์ : คันเร่งกำลังร้อนอยู่", 'vincent$;
		sleep 5000;
		npctalk "วินเซนต์ : 320c.", 'vincent$;
		sleep 5000;
		npctalk "Vincent : 670c มันร้อนเร็วเกินไป!", 'vincent$;
		sleep 3000;
		npctalk "วินเซนต์ : ทะลุ 1000c ไปแล้ว!!!! คุณหมอเฟรซ่า! เราต้องหยุดเครื่องนี้ชั่วคราว!", 'vincent$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : It is fine.", bc_map,0x00ff77;
		sleep 5000;
		npctalk "ซิลเวีย : มันไม่ใช่ปัญหาง่ายๆ เลย อุณหภูมิได้สูงเกินกว่าที่คาดไว้มาก!", 'silvia$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : This problem was predicted. Maintain current state.", bc_map,0x00ff77;
		sleep 3000;
		npctalk "อาร์ทีเมีย : แต่ว่าอย่างน้อย.. ฉันจะกินไก่ทีหลัง", 'artemia$;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Receiving changed circumstance through main server. Detecting dimensional distortion between particle accelerator and catalyst.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		npctalk "อาร์ทีเมีย : ดร.เฟรซ่า! มันเป็นสถานการณ์อันตรายของคอมพิวเตอร์แม่ ปิดแหล่งจ่ายไฟหลัก!", 'artemia$;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		npctalk "วินเซนต์ : เกิดอะไรขึ้น ใครก็ได้ช่วยปิดคอมพิวเตอร์ที!", 'vincent$;
		sleep 3000;
		mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		donpcevent instance_npcname("#npc_con05") + "::OnStart";
		mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		'bool_event = false;
		end;
}

1@lab,4,1,3	script	#npc_con05	CLEAR_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#npc_con05");
		killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
		.@boss_call = rand(1,105);
		if (.@boss_call <= 7)			.@mob_id = 1087;	// ORK_HERO
		else if (.@boss_call <= 14)		.@mob_id = 1147;	// MAYA
		else if (.@boss_call <= 21)		.@mob_id = 1190;	// ORC_LORD
		else if (.@boss_call <= 28)		.@mob_id = 1115;	// EDDGA
		else if (.@boss_call <= 35)		.@mob_id = 1086;	// GOLDEN_BUG
		else if (.@boss_call <= 42)		.@mob_id = 1038;	// OSIRIS
		else if (.@boss_call <= 49)		.@mob_id = 1159;	// PHREEONI
		else if (.@boss_call <= 56)		.@mob_id = 1389;	// DRACULA
		else if (.@boss_call <= 63)		.@mob_id = 1046;	// DOPPELGANGER
		else if (.@boss_call <= 70)		.@mob_id = 1059;	// MISTRESS
		else if (.@boss_call <= 77)		.@mob_id = 1150;	// MOONLIGHT
		else if (.@boss_call <= 84)		.@mob_id = 1688;	// LADY_TANEE
		else if (.@boss_call <= 91)		.@mob_id = 1039;	// BAPHOMET
		else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
		else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
		else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
		else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
		else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
		else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
		else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

		monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con05") + "::OnMyMobDead";
		sleep 3000;
		npctalk "อาร์ทีเมีย : อะไรนะ...นั่นอะไรนะ!", 'artemia$;
		sleep 3000;
		npctalk "ซิลเวีย : มันไม่ตอบสนองต่อเรา บางทีมันอาจมองไม่เห็นเรา?", 'silvia$;
		sleep 5000;
		npctalk "วินเซนต์ : แต่ว่ามันไม่เป็นมิตรต่อผู้ช่วยโครงการนะ สิ่งมีชีวิตนั่นมันอะไร!?", 'vincent$;
		sleep 5000;
		npctalk "วินเซนต์ : ฉันขยับตัวไม่ได้เลย ฉันคิดว่าอวกาศถูกแช่แข็งไปแล้ว", 'vincent$;
		end;

OnMyMobDead:
		mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Restarting device.", bc_map, 0xffff00,FW_NORMAL,13,0;
		if (mobcount( 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead" ) < 1)
				initnpctimer;
		end;
OnTimer1000:
		killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
		donpcevent instance_npcname("#npc_con06") + "::OnStart";
		disablenpc instance_npcname("#npc_con05");
		stopnpctimer;
		end;
}

// Round 2
1@lab,4,1,3	script	#npc_con06	CLEAR_NPC,{
		end;
OnStart:
		sleep 9000;
		npctalk "อาร์ทีเมีย : MA-4885!! ใครสั่งให้เริ่มใหม่ หยุดเลย!!", 'artemia$;
		sleep 3000;
		mapannounce 'map_name$, "MA-4885: Unrecognized command.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 6000;
		npctalk "อาร์ทีเมีย: ดร.เฟรซ่า!! เราต้องหยุดได้แล้ว! มันอันตรายเกินไป!", 'artemia$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
		sleep 3000;
		npctalk "วินเซนต์: เฟรซ่า! เราไม่มีเวลาแล้ว! ปิดคอมพิวเตอร์ซะ!!", 'vincent$;
		sleep 3000;
		mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		'bool_event = true;
		donpcevent instance_npcname("#npc_con03") + "::OnStart";
		npctalk "ซิลเวีย : เฟรซา! ตัดสินใจ!", 'silvia$;
		sleep 3000;
		mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
		sleep 6000;
		mapannounce 'map_name$, "Lona Fresa : Current time 20 minutes past 11. Momentarily postpone experiment.", bc_map,0x00ff77;
		sleep 6000;
		mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		donpcevent instance_npcname("#npc_con03") + "::OnStart2";
		sleep 2000;
		mapannounce 'map_name$, "Lona Fresa : Mother Comp MA-4885. I am your Chief director Dr. Fresa. Cease current progress.", bc_map,0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		npctalk "เฟเดอริโก้ : บ้าเอ้ย! มีอะไรบางอย่างกำลังมาอีกแล้ว!!", 'federico$;
		sleep 2500;
		mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		npctalk "Vincent : โอ้... บ้าเอ๊ย นี่เป็นเหตุผลที่คุณไม่ควรติดตั้ง CPU ใหม่ล่าสุด", 'vincent$;
		sleep 3000;
		npctalk "เฟเดอริโก้ : อ่า... ร่างกายของฉัน... ขยับไม่ได้เลย บ้าเอ๊ย", 'federico$;
		sleep 4000;
		mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 1000;
		donpcevent instance_npcname("#npc_con07") + "::OnStart";
		mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 3000;
		'bool_event = false;
		end;
}

1@lab,4,1,3	script	#npc_con07	CLEAR_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#npc_con07");
		killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
		.@boss_call = rand(1,98);
		if (.@boss_call <= 7)		.@mob_id = 1980;	// KUBLIN
		else if (.@boss_call <= 14)	.@mob_id = 1157;	// PHARAOH
		else if (.@boss_call <= 21)	.@mob_id = 1112;	// DRAKE
		else if (.@boss_call <= 28)	.@mob_id = 1251;	// KNIGHT_OF_WINDSTORM
		else if (.@boss_call <= 35)	.@mob_id = 2068;	// BOITATA
		else if (.@boss_call <= 42)	.@mob_id = 1373;	// LORD_OF_DEATH
		else if (.@boss_call <= 49)	.@mob_id = 2156;	// LEAK
		else if (.@boss_call <= 56)	.@mob_id = 1272;	// DARK_LORD
		else if (.@boss_call <= 63)	.@mob_id = 1630;	// BACSOJIN_
		else if (.@boss_call <= 70)	.@mob_id = 1252;	// GARM
		else if (.@boss_call <= 77)	.@mob_id = 1779;	// KTULLANUX
		else if (.@boss_call <= 84)	.@mob_id = 1708;	// THANATOS
		else if (.@boss_call <= 86)	.@mob_id = 1089;	// TOAD
		else if (.@boss_call <= 88)	.@mob_id = 1092;	// VAGABOND_WOLF
		else if (.@boss_call <= 90)	.@mob_id = 1088;	// VOCAL
		else if (.@boss_call <= 92)	.@mob_id = 1096;	// ANGELING
		else if (.@boss_call <= 94)	.@mob_id = 1093;	// ECLIPSE
		else if (.@boss_call <= 96)	.@mob_id = 1120;	// GHOSTRING
		else if (.@boss_call <= 98)	.@mob_id = 1090;	// MASTERING

		monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con07") + "::OnMyMobDead";
		sleep 6000;
		npctalk "ซิลเวีย: เหมือนกับครั้งที่แล้ว ดูเหมือนมันจะไม่สนใจเราเลย", 'silvia$;
		sleep 4000;
		npctalk "วินเซนต์: มาปิดคอมพิวเตอร์แม่ให้หมดก่อนเถอะครับในขณะที่พลังงานหลักปิดอยู่ชั่วคราว!", 'vincent$;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Answering question. This space is currently stuck in a distorted phase.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Direct interaction between the demi-plane creature and this world's organisms will be possible after the phase has been balanced.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map,0xFFFF00,FW_NORMAL,13;
		sleep 5000;
		npctalk "อาร์ทีเมีย: ไอ้ก้อนโลหะบ้าๆ นั่นมันพูดอะไรน่ะ มันบอกว่าเราไม่ได้อยู่ในยุคเดียวกับพวกสัตว์ประหลาดนั่นเหรอ", 'artemia$;
		sleep 4000;
		npctalk "เฟเดอริโก: อ๋อ ฉันเดาว่าเราน่าจะหวังว่าคอมพิวเตอร์แม่จะไม่คำนวณที่ถูกต้อง", 'federico$;
		end;

OnMyMobDead:
		mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Phase adjustment failed. Restarting relevant procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
		if (mobcount( 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead" ) < 1)
				initnpctimer;
		end;
OnTimer1000:
		killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
		donpcevent instance_npcname("#npc_con08") + "::OnStart";
		disablenpc instance_npcname("#npc_con07");
		stopnpctimer;
		end;
}

// Round 3
1@lab,4,1,3	script	#npc_con08	CLEAR_NPC,{
		end;
OnStart:
		sleep 13000;
		mapannounce 'map_name$, "Lona Fresa: Mother Computer MA-4885. Looks like you are trying to synchronize the other dimension and this world.", bc_map, 0x00ff77;
		sleep 6000;
		mapannounce 'map_name$, "MA-4885: That is correct.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 6000;
		npctalk "อาร์ทีเมีย: ดร.เฟรซ่า!! เหตุการณ์ไร้สาระนี้ไม่เกี่ยวข้องกับการทดลองเดิมของเราเลย!", 'artemia$;
		sleep 6000;
		mapannounce 'map_name$, "Lona Fresa: I have fully realized that.", bc_map, 0x00ff77;
		sleep 6000;
		npctalk "ซิลเวีย: เราไม่สามารถปิดไฟหลักทั้งหมดได้หรือเปล่า?", 'silvia$;
		sleep 6000;
		mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		'bool_event = true;
		donpcevent instance_npcname("#npc_con03") + "::OnStart";
		mapannounce 'map_name$, "Lona Fresa: " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day. Current time 38 minutes past 11 temporarily terminating particle accelerator project.", bc_map, 0x00ff77;
		sleep 6000;
		mapannounce 'map_name$, "Lona Fresa: Ending record with reasons of Mother Computer malfunction and overheating of accelerator.", bc_map, 0x00ff77;
		sleep 5000;
		npctalk "ซิลเวีย : โอเค ตอนนี้คุณหมอเฟรซ่าต้องปิดไฟแล้ว", 'silvia$;
		sleep 6000;
		mapannounce 'map_name$, "Lona Fresa: Cutting off power to Mother Computer MA-4885.", bc_map, 0x00ff77;
		sleep 6000;
		mapannounce 'map_name$, "MA-4885: Suspending operation of all equipment as per Administrator request.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: .....command... ----!!!  Zizip... ---!! -- following command ----- !! Zizip.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		'bool_event = false;
		npctalk "อาร์ทีเมีย : เฮ้อ...จบแล้วเหรอ?", 'artemia$;
		sleep 6000;
		mapannounce 'map_name$, "Lona Fresa: Completed shut down of Mother computer MA-4885. Confirm equipment suspension.", bc_map, 0x00ff77;
		sleep 5000;
		npctalk "ซิลเวีย: ดูเหมือนว่ามันจะจบลงแล้ว", 'silvia$;
		sleep 4000;
		npctalk "เฟเดอริโก้: การเริ่มต้นพืชเป็นสิ่งที่ดีที่สุด", 'federico$;
		sleep 4000;
		mapannounce 'map_name$, "Lona Fresa: Recording all experiment progress and terminating at current point.", bc_map, 0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa: The final experiment failure is unfortunate. But I will promise a more perfect experiment next time.", bc_map, 0x00ff77;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa: Good work everybody.", bc_map, 0x00ff77;
		sleep 5000;
		npctalk "อาร์ทีเมีย : ดูเหมือนจะจบแล้ว", 'artemia$;
		sleep 5000;
		npctalk "เฟเดอริโก: ฉันคิดว่านั่นเป็นการทดลองตามหลักการของเฟรซา แต่เป็นปัญหาของคอมพิวเตอร์แม่", 'federico$;
		sleep 5500;
		npctalk "ซิลเวีย: เรามาสงบสติอารมณ์กันก่อน แล้วรีบออกไปจากห้องทดลองที่น่ากลัวแห่งนี้กันเถอะ", 'silvia$;
		sleep 5000;
		npctalk "Vincent : ยังไงก็ตาม... ฉันยังขยับตัวไม่ได้เลย เกิดอะไรขึ้น?", 'vincent$;
		sleep 5000;
		'bool_event = true;
		donpcevent instance_npcname("#npc_con03") + "::OnStart";
		donpcevent instance_npcname("#npc_con03") + "::OnStart3";
		sleep 2000;
		mapannounce 'map_name$, "MA-4885: CODE NAME - DESCENT", bc_map, 0xff0000,FW_NORMAL,16,0;
		sleep 5000;
		npctalk "เฟเดอริโก้ : อะไรนะ นี่ไม่ใช่ขั้นเริ่มต้นเหรอ?", 'federico$;
		sleep 2500;
		mapannounce 'map_name$, "MA-4885: Starting final phase transformation.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 3000;
		npctalk "ซิลเวีย : มันจบแล้ว", 'silvia$;
		sleep 5500;
		mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 1000;
		mapannounce 'map_name$, "MA-4885: 1 second remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 1000;
		donpcevent instance_npcname("#npc_con10") + "::OnStart";
		mapannounce 'map_name$, "MA-4885: Temporarily shutting down main power for syntonization with transformed phase.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 3000;
		'bool_event = false;
		end;
}

1@lab,4,1,3	script	#npc_con10	CLEAR_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#npc_con10");
		killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
		.@boss_call = rand(1,105);
		if (.@boss_call <= 7)			.@mob_id = 1623;	// RSX_0806
		else if (.@boss_call <= 14)		.@mob_id = 1418;	// DARK_SNAKE_LORD
		else if (.@boss_call <= 21)		.@mob_id = 1312;	// TURTLE_GENERAL
		else if (.@boss_call <= 28)		.@mob_id = 1785;	// ATROCE
		else if (.@boss_call <= 35)		.@mob_id = 1734;	// KIEL_
		else if (.@boss_call <= 42)		.@mob_id = 1719;	// DETALE
		else if (.@boss_call <= 49)		.@mob_id = 1768;	// GLOOMUNDERNIGHT
		else if (.@boss_call <= 56)		.@mob_id = 2087;	// QUEEN_SCARABA
		else if (.@boss_call <= 63)		.@mob_id = 1751;	// RANDGRIS
		else if (.@boss_call <= 70)		.@mob_id = 2253;	// DAEHYON
		else if (.@boss_call <= 77)		.@mob_id = 2255;	// KADES
		else if (.@boss_call <= 84)		.@mob_id = 1832;	// IFRIT
		else if (.@boss_call <= 91)		.@mob_id = 1874;	// BEELZEBUB_
		else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
		else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
		else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
		else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
		else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
		else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
		else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

		monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con10") + "::OnMyMobDead";
		mapannounce 'map_name$, "MA-4885: Final phase transformation process.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: This world will be summoned to the demi-plane after the phase interference phenomenon with creature fades.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		end;
OnMyMobDead:
		mapannounce 'map_name$, "MA-4885: Phase synchronization completed before creature is eliminated. Starting dimensional travel.", bc_map, 0xffff00,FW_NORMAL,13,0;
		if (mobcount( 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead" ) < 1)
				initnpctimer;
		end;
OnTimer1000:
		killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
		donpcevent instance_npcname("#npc_con11") + "::OnStart";
		disablenpc instance_npcname("#npc_con10");
		stopnpctimer;
		end;
}

1@lab,4,1,3	script	#npc_con11	CLEAR_NPC,{
		end;
OnStart:
		'bool_event = true;
		donpcevent instance_npcname("#npc_con03") + "::OnStart";
		sleep 12000;
		mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		npctalk "เฟเดอริโก : แม่ของเรากำลังทำการทดลองที่ยิ่งใหญ่เกินกว่าที่เราเคยคิด", 'federico$;
		sleep 2500;
		npctalk "วินเซนต์ : ดูเหมือนเขาจะใจกว้างกว่าพวกเรานะ เฟรซ่า!!! ฟังอยู่ไหม ส่งเสียงเตือนหน่อยสิ!", 'vincent$;
		sleep 5000;
		npctalk "วินเซนต์ : อย่างน้อยเราก็ควรอพยพผู้คนจากห้องแล็ปอื่นออกไป", 'vincent$;
		sleep 5000;
		mapannounce 'map_name$, "Lona Fresa: But ... you.", bc_map, 0x00ff77;
		sleep 5000;
		npctalk "ซิลเวีย : โอ้ ฉันไม่รู้ว่าจะเกิดอะไรขึ้น แต่ฉันอยากรู้เรื่องการเปลี่ยนมิติ อย่าสนใจฉันเลย!", 'silvia$;
		sleep 5000;
		npctalk "อาร์ทีเมีย : มิติที่เคลื่อนที่ไปพร้อมกับการทดลองผลิตวัสดุใหม่ ติดอยู่ในระดับการทดลองอื่น", 'artemia$;
		sleep 5000;
		mapannounce 'map_name$, "Alarm System : Alert! Alert! Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "Alarm System : I repeat. Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
		npctalk "เฟเดอริโก้ : นี่คือความรู้สึกเมื่อต้องขยับมิติเหรอ แปลกจริงๆ...", 'federico$;
		sleep 4000;
		specialeffect EF_SUI_EXPLOSION, AREA, 'federico$;
		disablenpc 'federico$;
		mapannounce 'map_name$, "MA-4885: Dimension move at 20% progress. There could be dimensional interference with certain creatures. Please be aware.", bc_map, 0xffff00,FW_NORMAL,13,0;
		donpcevent instance_npcname("#npc_con03") + "::OnStart2";
		npctalk "วินเซนต์ : ฉันสงสัยว่าคนอื่นอพยพออกไปแล้วหรือเปล่า?", 'vincent$;
		sleep 5000;
		specialeffect EF_SUI_EXPLOSION, AREA, 'vincent$;
		disablenpc 'vincent$;
		npctalk "ซิลเวีย : อ่า...... ฉันน่าจะเห็นสิ่งนี้ขณะกินไก่และเบียร์นะ...", 'silvia$;
		sleep 5000;
		specialeffect EF_SUI_EXPLOSION, AREA, 'silvia$;
		disablenpc 'silvia$;
		mapannounce 'map_name$, "MA-4885: Dimension move at 78% progress. Most of the dimension encroached by demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
		npctalk "อาร์ทีเมีย : ฮะ...ใครจะกินไก่ตอนนี้ล่ะ?", 'artemia$;
		sleep 5000;
		specialeffect EF_SUI_EXPLOSION, AREA, 'artemia$;
		disablenpc 'artemia$;
		donpcevent instance_npcname("#npc_con03") + "::OnStart3";
		sleep 10000;
		'bool_event = false;
		mapannounce 'map_name$, "MA-4885: Dimension move 100% completed. Welcome to the demi-plane dimension.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: A gateway possible for contact with the demi-plane open at 3 o'clock of the laboratory.", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: Cannot guarantee what kind of dimension will be met due to the instability of the gateway...", bc_map, 0xffff00,FW_NORMAL,13,0;
		sleep 5000;
		mapannounce 'map_name$, "MA-4885: If there are any surviving organisms I wish you luck. I hope to see you again.", bc_map, 0xffff00,FW_NORMAL,13,0;
		enablenpc instance_npcname("lab_warp#end");
		end;
}


// Display effects
1@lab,4,1,5	script	#npc_con03	CLEAR_NPC,{
		end;

function effect_gen {
		.@effect = getarg(0);
		.@count = getargcount();
		for ( .@i = 1; .@i < .@count; .@i++ )
				specialeffect .@effect, AREA, getd( "'gen_" + ( getarg(.@i) < 10 ? "0" : "" ) + getarg(.@i) + "$" );
		return;
}

function effect_1_10_gen { effect_gen( getarg(0), 1,2,3,4,5,6,7,8,9,10 ); }

OnStart:
		effect_gen( EF_MAGICALATTHIT, 1 );
		sleep 2000;
		effect_gen( EF_MAGICALATTHIT, 1,3 );
		sleep 1900;
		effect_gen( EF_MAGICALATTHIT, 1,3,8 );
		sleep 1700;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4 );
		sleep 1600;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6 );
		sleep 1500;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2 );
		sleep 1400;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7 );
		sleep 1300;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9 );
		sleep 1200;
		effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9,5 );
		sleep 1100;
		while( 'bool_event ) {
				effect_1_10_gen( EF_MAGICALATTHIT );
				sleep 1000;
		}
		end;
OnStart2:
		effect_gen( EF_BLASTMINEBOMB, 1 );
		sleep 2000;
		effect_gen( EF_BLASTMINEBOMB, 1,3 );
		sleep 1900;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8 );
		sleep 1800;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4 );
		sleep 1700;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6 );
		sleep 1600;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2 );
		sleep 1500;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7 );
		sleep 1400;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9 );
		sleep 1300;
		effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9,5 );
		sleep 1200;
		while( 'bool_event ) {
				effect_1_10_gen( EF_BLASTMINEBOMB );
				sleep 1100;
		}
		end;

OnStart3:
		while( 'bool_event ) {
				specialeffect EF_LORD, AREA, 'gen_11$;
				sleep 2000;
				specialeffect EF_LORD, AREA, 'gen_12$;
				sleep 2000;
				specialeffect EF_LORD, AREA, 'gen_13$;
				sleep 2000;
				specialeffect EF_LORD, AREA, 'gen_14$;
				sleep 2000;
		}
		end;
}

1@lab,91,80,5	script	#gen_01	CLEAR_NPC,{ end; }
1@lab,96,81,5	duplicate(#gen_01)	#gen_02	CLEAR_NPC
1@lab,99,85,5	duplicate(#gen_01)	#gen_03	CLEAR_NPC
1@lab,99,91,5	duplicate(#gen_01)	#gen_04	CLEAR_NPC
1@lab,96,95,5	duplicate(#gen_01)	#gen_05	CLEAR_NPC
1@lab,91,97,5	duplicate(#gen_01)	#gen_06	CLEAR_NPC
1@lab,85,96,5	duplicate(#gen_01)	#gen_07	CLEAR_NPC
1@lab,82,91,5	duplicate(#gen_01)	#gen_08	CLEAR_NPC
1@lab,82,86,5	duplicate(#gen_01)	#gen_09	CLEAR_NPC
1@lab,85,81,5	duplicate(#gen_01)	#gen_10	CLEAR_NPC
1@lab,90,97,5	duplicate(#gen_01)	#gen_11	CLEAR_NPC
1@lab,82,89,5	duplicate(#gen_01)	#gen_12	CLEAR_NPC
1@lab,91,80,5	duplicate(#gen_01)	#gen_13	CLEAR_NPC
1@lab,99,88,5	duplicate(#gen_01)	#gen_14	CLEAR_NPC


// Warps
1@lab,102,28,0	warp2	lab_warp#1	2,2,1@lab,97,28
1@lab,80,28,0	warp2	lab_warp#2	2,2,1@lab,75,28
1@lab,58,28,0	warp2	lab_warp#3	2,2,1@lab,53,28
1@lab,29,38,0	warp2	lab_warp#4	2,2,1@lab,29,43
1@lab,48,82,0	warp2	lab_warp#5	2,2,1@lab,58,82
1@lab,123,88,0	warp2	lab_warp#end	2,2,verus01,152,150


//-- GM NPCs
1@lab,1,1,3	script	#adsw_	CLEAR_NPC,{
		if (callfunc("F_GM_NPC",1854,0,0,2000) < 1)
				end;
		mes "[ผู้ดูแลเวลา]";
		mes "คุณต้องการไปเวลาไหน?";
		next;
		switch( select( "Cancel", "1 start", "2 start", "3 start", "4 start" ) ) {
		case 1:
				close;
		case 2:
				donpcevent instance_npcname("#npc_con01") + "::OnStart";
				break;
		case 3:
				donpcevent instance_npcname("#npc_con06") + "::OnStart";
				break;
		case 4:
				donpcevent instance_npcname("#npc_con08") + "::OnStart";
				break;
		case 5:
				end;
		}
		warp instance_mapname("1@lab"), 84,90;
		end;
}

verus01,1,5,5	script	Laboratory Administrator#pa0829_01	CLEAR_NPC,{
		if (callfunc("F_GM_NPC",1854,0,0,2000) < 1) {
				warp "prontera",100,100;
				end;
		}
		mes "[ผู้ดูแลห้องปฏิบัติการ]";
		mes "จัดการได้ดีทั้งหมด!";
		close2;
		if (isbegin_quest(12347) > 0)
				erasequest 12347;
		end;
}
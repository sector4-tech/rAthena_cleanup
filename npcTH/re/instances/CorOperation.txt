//===== rAthena Script =======================================
//= Cor Operation
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 Securing Elyumina
//= Episode 17.1 EL1-A17T Suppression (Cor Memorial)
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@cor,1,1,0	script	#171_cor_control	-1,{
		end;

OnInstanceInit:
		'map$ = instance_mapname("1@cor");
		'step = 0;
		setcell 'map$,159,216,159,225,CELL_WALKABLE,0;
		setcell 'map$,159,216,159,225,CELL_SHOOTABLE,0;
		setcell 'map$,98,216,98,225,CELL_WALKABLE,0;
		setcell 'map$,98,216,98,225,CELL_SHOOTABLE,0;
		setcell 'map$,132,240,141,240,CELL_WALKABLE,0;
		setcell 'map$,132,240,141,240,CELL_SHOOTABLE,0;

		//= Story
		disablenpc instance_npcname("Reinforced Energy#171_box_0");
		disablenpc instance_npcname("Biological Battery#171_box_0");
		disablenpc instance_npcname("Chemical Poison#171_box_1");
		disablenpc instance_npcname("Rebellion#171_box_4");
		disablenpc instance_npcname("Elena Volkova#171_cmd_1");
		disablenpc instance_npcname("Elena Volkova#171_cmd_2");
		disablenpc instance_npcname("Elyumina#171_cor_end");
		disablenpc instance_npcname("Rebellion#171_cmd_6");
		disablenpc instance_npcname("Rebellion#171_cmd_7");
		disablenpc instance_npcname("#171_cor_warp_0");
		disablenpc instance_npcname("#171_cor_warp_1");
		for (.@i = 0; .@i < 6; .@i++)
				disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
		for (.@i = 0; .@i < 4; .@i++) {
				disablenpc instance_npcname("Rebellion#171_box_" + .@i);
				disablenpc instance_npcname("Box#171_box_" + .@i);
		}

		//= Daily
		for (.@i = 0; .@i < 4; .@i++) {
				disablenpc instance_npcname("Elyumina#171_box_d" + .@i);
				disablenpc instance_npcname("Box#171_box_d" + .@i);
		}
		disablenpc instance_npcname("Biological Battery#171_box_trap_0");
		disablenpc instance_npcname("Chemical Poison#171_box_trap_1");
		disablenpc instance_npcname("Biological Battery#171_box_trap_2");
		disablenpc instance_npcname("Chemical Poison#171_box_trap_3");
		disablenpc instance_npcname("Reinforced Energy#171_trap_0");
		disablenpc instance_npcname("Reinforced Energy#171_trap_2");
		disablenpc instance_npcname("Elyumina#171_cmd_0");
		disablenpc instance_npcname("Elyumina#171_cmd_1");

		//= Boss Stage
		for (.@i = 0; .@i < 11; .@i++) {
				disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
				disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
		}
		disablenpc instance_npcname("Reinforced Energy#171_cor_0");
		end;

OnStory01:
		disablenpc instance_npcname("Elena Volkova#171_cmd_0");
		disablenpc instance_npcname("Elyumina#171_cmd_0");
		for (.@i = 0; .@i < 6; .@i++)
				disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
		for (.@i = 0; .@i < 4; .@i++) {
				enablenpc instance_npcname("Rebellion#171_box_" + .@i);
				enablenpc instance_npcname("Box#171_box_" + .@i);
		}
		enablenpc instance_npcname("Rebellion#171_box_4");
		end;

OnStory02:
		for (.@i = 0; .@i < 4; .@i++) {
				disablenpc instance_npcname("Rebellion#171_box_" + .@i);
				disablenpc instance_npcname("Box#171_box_" + .@i);
		}
		disablenpc instance_npcname("Rebellion#171_box_4");
		enablenpc instance_npcname("Elena Volkova#171_cmd_1");
		mapannounce 'map$,"Elena Volkova : Awesome, she came out! I've sent the signal, come to my location!",bc_map,0xFFFF99;
		viewpointmap 'map$,1,172,223,5,0xFFFFFF;
		viewpointmap 'map$,2,140,79,1,0xFFFFFF;
		viewpointmap 'map$,2,160,119,2,0xFFFFFF;
		viewpointmap 'map$,2,220,170,3,0xFFFFFF;
		viewpointmap 'map$,2,222,236,4,0xFFFFFF;
		end;

OnStory03:
		disablenpc instance_npcname("Elena Volkova#171_cmd_1");
		donpcevent instance_npcname("#171_cor_warp_0") + "::OnActive";
		end;

OnDaily01:
		disablenpc instance_npcname("Elena Volkova#171_cmd_0");
		disablenpc instance_npcname("Elyumina#171_cmd_0");
		for (.@i = 0; .@i < 4; .@i++) {
				enablenpc instance_npcname("Elyumina#171_box_d" + .@i);
				enablenpc instance_npcname("Box#171_box_d" + .@i);
		}
		end;

OnDaily02:
		viewpointmap 'map$,1,172,223,5,0xFFFFFF;
		mapannounce 'map$,"Elena Volkova : Once you've finish all of it, come to the barracks and Elyumina will guide you!",bc_map,0xFFFF99;
		enablenpc instance_npcname("Elyumina#171_cmd_1");
		end;

OnDaily03:
		disablenpc instance_npcname("Elyumina#171_cmd_1");
		donpcevent instance_npcname("#171_cor_warp_1") + "::OnActive";
		end;
}

1@cor,177,169,0	script	#171_cor_ev	HIDDEN_WARP_NPC,4,4,{
		end;

OnTouch:
		if (!is_party_leader())
				end;
		if (isbegin_quest(16360) == 2)
				'mode = 1;
		disablenpc();
		end;
}

1@cor,180,169,3	script	Elena Volkova#171_cmd_0	4_F_ELENA,5,5,{
		if (!'mode) {
				cutin "162elena_01",2;
				mes "[เอเลน่า โวลโควา]";
				mes "ผมจะอธิบายการทำงานก่อนนะครับ ก่อนอื่น...";
				specialeffect EF_TETRA_GROUND;
				next;
				for (.@i = 0; .@i < 6; .@i++)
						enablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
				next;
				mes "[เอเลน่า โวลโควา]";
				mes "ผมจะอธิบายการทำงานก่อนนะครับ ก่อนอื่น...";
				mes "...... ตอนนี้คุณสามารถวิ่งได้อย่างเสรี";
				next;
				cutin "",255;
				mes "[กบฏ]";
				mes "ดูเหมือนว่ามีอะไรบางอย่างระเบิดอยู่ตรงนั้น เปลวไฟได้พุ่งขึ้นมาจากด้านหน้า!";
				next;
				mes "[กบฏ]";
				mes "มี 4 ตำแหน่งที่ระบุได้ว่าระเบิดนี้มาจาก! ฉันได้ ^FF0000แสดงตำแหน่งเหล่านั้นบนแผนที่ย่อของคุณแล้ว^000000";
				viewpointmap 'map$,1,140,79,1,0xFFFFFF;
				viewpointmap 'map$,1,160,119,2,0xFFFFFF;
				viewpointmap 'map$,1,220,170,3,0xFFFFFF;
				viewpointmap 'map$,1,222,236,4,0xFFFFFF;
				next;
				mes "[เอเลน่า โวลโควา]";
				mes "มันกะทันหันมากแต่ผมจะเปลี่ยนการดำเนินการ";
				mes "อย่ากังวลกับคนที่อยู่ข้างหน้า พวกเขาจะปรับตัวเข้ากับสถานการณ์ใดก็ได้";
				next;
				mes "[เอเลน่า โวลโควา]";
				mes "แบ่งเป็น 4 ทีม โดยแต่ละทีมจะประจำตำแหน่งของตัวเอง แล้วเริ่มกดดันผู้นำ หากทีมใดพบเอลิวมินา โปรดแจ้งให้เราทราบทันที";
				next;
				mes "[เอเลน่า โวลโควา]";
				mes "ทุกคนไปยังตำแหน่งของตน! เริ่มปฏิบัติการได้!";
				npctalk "ใช่!",instance_npcname("Rebellion#171_cmd_0");
				npctalk "-",instance_npcname("Rebellion#171_cmd_1");
				npctalk "ใช่!",instance_npcname("Rebellion#171_cmd_2");
				npctalk "ใช่!",instance_npcname("Rebellion#171_cmd_3");
				npctalk "ใช่!",instance_npcname("Rebellion#171_cmd_4");
				npctalk "ใช่!",instance_npcname("Rebellion#171_cmd_5");
				next;
				cutin "162elena_02",2;
				mes "[เอเลน่า โวลโควา]";
				mes "ฉันขอให้คุณทำหน้าที่แยกกันในฐานะนักผจญภัย โปรดตรวจสอบ ^0000CDminimap^000000 ของคุณเพื่อจัดระเบียบสถานการณ์ให้เร็วที่สุด เข้าร่วมทีมหากต้องการการสนับสนุน!";
				close2;
				cutin "",255;
				if ('step == 0) {
						'step = 1;
						donpcevent instance_npcname("#171_cor_control") + "::OnStory01";
				}
		} else {
				enablenpc instance_npcname("Elyumina#171_cmd_0");
				.@elyumina$ = instance_npcname("Elyumina#171_cmd_0");
				cutin "162elena_01",2;
				mes "[เอเลน่า โวลโควา]";
				mes "การบรรยายสรุป...คนร้ายที่นี่จะเป็นคนทำ";
				mes "การดูแลเด็กอาชญากรเป็นเรื่องยาก";
				npctalk "เอเลน่า โวลโควา : การบรรยายสรุป... อาชญากรที่นี่จะเป็นคนทำ มันยากที่จะดูแลอาชญากร";
				next;
				cutin "ep171_elyumina04",0;
				mes "[เอลูมินา]";
				mes "ฮ่าๆ นั่นไม่ใช่หน้าที่ของคุณเหรอ? ทำมันให้ถูกต้องสิ ทำไมคุณถึงปล่อยให้ฉันเป็นคนสรุปล่ะ?";
				npctalk "เอลิวมินา : ฮ่าๆ นั่นไม่ใช่หน้าที่ของคุณเหรอ? ทำมันให้เหมาะสมสิ ทำไมคุณถึงปล่อยให้ฉันเป็นคนสรุปข้อมูลล่ะ?",.@elyumina$;
				next;
				cutin "162elena_01",2;
				mes "[เอเลน่า โวลโควา]";
				mes "เสียงดังจัง ก่อนที่เราจะเริ่มกัน คุณเป็นส่วนหนึ่งของ USU แล้วหรือยัง?";
				npctalk "เอเลน่า โวลโควา : เสียงดังจัง ก่อนที่เราจะเริ่มกัน คุณเป็นส่วนหนึ่งของ USU แล้วหรือยัง?";
				next;
				cutin "ep171_elyumina03",0;
				mes "[เอลูมินา]";
				mes "นั่นสิ เค้าพูดแบบนั้น... กลัวฉันจะกลัวรึไง!";
				mes "เหอะๆ ฉันจะอธิบายเอง เพราะอยากดูแลลูกของฉัน!";
				npctalk "เอลิวมินา: เฮ้ ฉันจะอธิบายให้ฟัง เพราะว่าฉันอยากดูแลลูกๆ ของฉัน!",.@elyumina$;
				next;
				cutin "ep171_elyumina01",0;
				mes "[เอลูมินา]";
				mes "... โอเค ใกล้ถึงเวลาแล้ว";
				mes "ลูกๆ ของฉันจะเริ่มวางกับดักใน <TIPBOX>[สี่แห่ง]<INFO>8086</INFO></TIPBOX>";
				npctalk "เอลิวมินา: ... โอเค ใกล้ถึงเวลาแล้ว ลูกๆ ของฉันจะเริ่มวางกับดักในสี่จุด",.@elyumina$;
				next;
				mes "[เอลูมินา]";
				mes "เอาแผนที่มาตรงนี้ ข้อมูลจะถูกส่งไปยังสมาชิกปาร์ตี้ด้วยเช่นกัน";
				mes "คุณไม่ได้อยู่คนเดียวใช่ไหม คุณรู้ไหมว่าลูกๆ ของฉันมีพลัง?";
				npctalk "เอลิวมินา: เอาล่ะ เอาแผนที่มาเลย ข้อมูลจะถูกส่งไปยังสมาชิกปาร์ตี้ด้วยเช่นกัน",.@elyumina$;
				viewpointmap 'map$,1,140,79,1,0xFFFFFF;
				viewpointmap 'map$,1,160,119,2,0xFFFFFF;
				viewpointmap 'map$,1,220,170,3,0xFFFFFF;
				viewpointmap 'map$,1,222,236,4,0xFFFFFF;
				next;
				cutin "ep171_elyumina04",0;
				mes "[เอลูมินา]";
				mes "ไปข้างหน้าและสัมผัสกับดัก กับดักจะทำงานและลูกๆ ที่น่ารักของฉันจะปรากฏตัว";
				mes "คุณจำได้ใช่ไหมว่ากับดักทำงานอย่างไร?";
				npctalk "เอลิวมินา: เข้าไปสัมผัสกับดักได้เลย กับดักจะทำงานและลูกๆ ที่น่ารักของฉันจะปรากฏตัว",.@elyumina$;
				next;
				cutin "ep171_elyumina03",0;
				mes "[เอลูมินา]";
				mes "หลังจากที่คุณผ่านกับดักทั้งสี่แล้ว EL1_A17T จะออกมา และหญิงสาวคนนี้จะพาคุณไปที่สถานที่เดียวกับครั้งที่แล้ว";
				npctalk "Elyumina: หลังจากที่คุณผ่านกับดักทั้งสี่แล้ว EL1_A17T จะออกมา และหญิงสาวคนนี้จะพาคุณไปที่สถานที่เดียวกับครั้งที่แล้ว",.@elyumina$;
				next;
				cutin "ep171_elyumina04",0;
				mes "[เอลูมินา]";
				mes "แค่นี้ก็อธิบายได้หมดแล้ว ลงมือทำงานกันเลย! ไอ้กล้ามโง่! ฮ่าๆๆๆ";
				npctalk "เอลิวมินา : เท่านี้ก็อธิบายได้หมดแล้ว ไปทำงานกันเถอะ พวกกล้ามโง่! ฮ่าๆๆๆ",.@elyumina$;
				emotion ET_SMILE,getnpcid(0,.@elyumina$);
				next;
				cutin "162elena_01",2;
				mes "[เอเลน่า โวลโควา]";
				mes "...ฉันจะคุยกับอาชญากรคนนี้ในอีกสักครู่... ยังไงก็ตาม ฉันหวังพึ่งพวกคุณนะ นักผจญภัย!";
				npctalk "เอเลน่า โวลโควา: ...ฉันจะคุยกับอาชญากรคนนี้ในอีกสักครู่... ยังไงก็ตาม ฉันหวังพึ่งพวกคุณนะ นักผจญภัย!";
				close2;
				cutin "",255;
				if ('step == 0) {
						'step = 1;
						donpcevent instance_npcname("#171_cor_control") + "::OnDaily01";
				}
		}
		end;

OnTouch:
		npctalk "เอเลน่า โวลโควา : ทางนี้ค่ะ เรามาสรุปกันหลังจากที่ทุกคนมารวมตัวกันครบแล้ว";
		end;
}

1@cor,178,172,3	duplicate(dummy_npc)	Elyumina#171_cmd_0	4_EP17_ELYUMINA

1@cor,172,223,3	script	Elyumina#171_cmd_1	4_EP17_ELYUMINA,{
		cutin "ep171_elyumina03",0;
		mes "[เอลูมินา]";
		mes "เร็วมากครับ...แต่คาดว่าลูกผมคงไม่ชนะแน่ครับ";
		mes "ข้างบนยังมีเรื่องวุ่นวายอีกเยอะ~";
		npctalk "เอลิวมินา : เร็วมาก... คาดว่าลูกๆ ของฉันคงไม่ชนะหรอก มีปัญหาเกิดขึ้นมากมายที่นั่น~";
		next;
		cutin "ep171_elyumina01",0;
		mes "[เอลูมินา]";
		mes "...คุณจะตรงไปที่ EL1-A17T ของฉันเลยไหม?";
		mapannounce 'map$,"Elena Volkova : Yeah, damn criminal! Why don't you open the portal right now?",bc_map,0xFFFF99;
		npctalk "Elyumina : ...คุณจะตรงไปที่ EL1_A17T ของฉันเลยใช่ไหม?";
		next;
		if (select("Go in.:Don't go in yet.") == 2) {
				cutin "ep171_elyumina02",0;
				mes "[เอลูมินา]";
				mes "เอาล่ะ ฉันกำลังจะเปิดมันแล้ว";
				close3;
		}
		cutin "ep171_elyumina04",0;
		mes "[เอลูมินา]";
		mes "ฮ่าๆๆ ใช่แล้ว มาทำงานหนักกันเถอะ รวบรวมข้อมูลการต่อสู้ให้ฉันหน่อย!";
		npctalk "เอลิวมินา : ฮ่าๆ ใช่แล้ว มาทำงานหนักกันเถอะ รวบรวมข้อมูลการต่อสู้ให้ฉันหน่อยสิ!";
		close2;
		cutin "",255;
		viewpointmap 'map$,2,172,223,5,0xFFFFFF;
		if ('step == 1) {
				'step = 2;
				donpcevent instance_npcname("#171_cor_control") + "::OnDaily03";
		}
		end;
}

1@cor,177,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_0	4_M_REBELLION3
1@cor,180,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_1	4_M_GONY
1@cor,183,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_2	4_F_REBELLION3
1@cor,177,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_3	4_F_ANYA
1@cor,180,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_4	4_M_ILYA
1@cor,183,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_5	4_F_REBELLION2

1@cor,224,238,3	script	Rebellion#171_box_0	4_M_REBELLION3,{
		if (isbegin_quest(16355) == 0) {
				mes "[กบฏ]";
				mes "มีกล่องต้องสงสัย! สัญญาณกำลังถูกส่งมาจากด้านใน กำลังถอดรหัสอยู่!";
				close;
		}
		.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
		if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
				mes "[กบฏ]";
				mes "ทำลายแบตเตอรี่ชีวภาพและสังหารสัตว์ประหลาด!";
				close;
		}
		if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
				mes "[กบฏ]";
				mes "ขออภัยที่วิเคราะห์ล่าช้า มันไม่ใช่อุปกรณ์สำหรับเรียกมอนสเตอร์";
				next;
				mes "[กบฏ]";
				mes "หลังจากเรียกมอนสเตอร์แล้ว มันจะเรียกแบตเตอรี่ชีวภาพมาเพื่อเพิ่มความแข็งแกร่งให้กับมอนสเตอร์ และระเบิด";
				next;
				mes "[กบฏ]";
				mes "ถ้าจะจัดการก็ทำลายมันซะ เป็นวิธีแก้ไขที่ง่าย";
				next;
				mes "[กบฏ]";
				mes "เอาล่ะ ดูเหมือนว่ามันจะจบไปแล้ว...";
				next;
				mes "[กบฏ]";
				mes "ดูเหมือนว่าคุณจะพบสิ่งที่น่าสนใจที่นี่ คุณควรลองดูทีมอื่นๆ บ้าง!";
				close2;
				completequest 16355;
				disablenpc();
				disablenpc instance_npcname("Box#" + strnpcinfo(2));
				viewpointmap 'map$,2,224,236,4,0xFFFFFF;
				if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
						'step = 2;
						doevent instance_npcname("#171_cor_control") + "::OnStory02";
				}
		}
		end;
}

1@cor,222,236,3	script	Box#171_box_0	4_STEELBOX,{
		if (isbegin_quest(16355) == 0) {
				mes "[กบฏ]";
				mes "สาเหตุของการระเบิดนั้นไม่ทราบแน่ชัด แต่ฉันพบกล่องที่น่าสงสัยนี้ที่บริเวณนั้น ดูเหมือนว่าจะเป็นกลไกบางอย่าง";
				next;
				mes "[กบฏ]";
				mes "มีสัญญาณบางอย่างกำลังถูกส่งมาจากด้านใน... ระวังหน่อย มีอะไรเกิดขึ้น!";
				close2;
				setquest 16355;
				specialeffect EF_BAKU;
				donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
				end;
		}
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
				mes "[กบฏ]";
				mes "ทำลายแบตเตอรี่ชีวภาพและสังหารสัตว์ประหลาด!";
				close;
		}
		if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$))
				doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
		end;

OnSpawn:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		setarray .@xy,229,229,20341,217,235,20341,222,229,20341;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
				monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
		initnpctimer;
		end;

OnMobKill:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (!mobcount('map$,.@event$)) {
				stopnpctimer;
				npctalk "ขอขอบคุณทุกท่านที่ทำงานหนัก! ก่อนที่คุณจะไป โปรดฟังสิ่งที่ฉันจะพูดสักพัก",instance_npcname("Rebellion#" + strnpcinfo(2));
				killmonster 'map$,instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
				disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
				stopnpctimer instance_npcname("Reinforced Energy#" + strnpcinfo(2));
				disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		}
		end;

OnTimer1000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (mobcount('map$,.@event$)) {
				npctalk "ผลการวิเคราะห์คือ แบตเตอรี่นั้นจ่ายพลังงานให้กับมอนสเตอร์ที่เรียกออกมา เหยียบมันแล้วหลีกเลี่ยงความเสียหายทันที!",instance_npcname("Rebellion#" + strnpcinfo(2));
				enablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		} else {
				stopnpctimer;
				disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		}
		end;

OnTimer11000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		.@event2$ = instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
		stopnpctimer;
		if (mobcount('map$,.@event$)) {
				disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
				if (unitexists(getnpcid(0,instance_npcname("Biological Battery#" + strnpcinfo(2)))))
						initnpctimer;
		}
		end;
}

1@cor,222,235,3	script	Biological Battery#171_box_0	1738,3,3,{
		end;

OnTouch:
		disablenpc();
		stopnpctimer instance_npcname("Box#171_box_0");
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
		if (!mobcount('map$,.@event$)) {
				getmapxy(.@m$,.@x,.@y,BL_NPC);
				monster 'map$,.@x,.@y,"",20345,1,.@event$;
		}
		initnpctimer;
OnBombKill:
		end;

OnTimer7000:
		.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
		if (mobcount('map$,.@event$))
				enablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		else
				stopnpctimer;
		end;

OnTimer17000:
		.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
		.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
		if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#" + strnpcinfo(2))))) {
				disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
				if (mobcount('map$,.@event2$))
						killmonster 'map$,.@event2$;
				stopnpctimer;
				if (mobcount('map$,.@event$))
						initnpctimer instance_npcname("Box#171_box_0");
		}
		end;
}

1@cor,225,232,3	script	Reinforced Energy#171_box_0	4_ENERGY_WHITE,{
		if (!getd("'rf_" + strnpcinfo(2))) {
				setd("'rf_" + strnpcinfo(2),1);
				specialeffect2 EF_ENHANCE;
				specialeffect2 EF_LIGHTSPHERE_STAR;
				sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
				npctalk "พลัง พลังล้นเหลือ!";
				unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
				sleep 1500;
				disablenpc();
				setd("'rf_" + strnpcinfo(2),0);
				stopnpctimer instance_npcname("Biological Battery#" + strnpcinfo(2));
				initnpctimer instance_npcname("Box#" + strnpcinfo(2));
		}
		end;
}

1@cor,218,172,5	script	Rebellion#171_box_1	4_F_REBELLION2,{
		if (isbegin_quest(16356) == 0) {
				mes "[กบฏ]";
				mes "มีกล่องต้องสงสัย! สัญญาณกำลังถูกส่งมาจากด้านใน กำลังถอดรหัสอยู่!";
				close;
		}
		.@event$ = instance_npcname("Box#" + strnpcinfo(2)) + "::OnMobKill";
		if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
				mes "[กบฏ]";
				mes "หลีกเลี่ยงพิษเคมีและฆ่าสัตว์ประหลาด!";
				close;
		}
		if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
				mes "[กบฏ]";
				mes "มันเป็นอุปกรณ์ที่น่ากลัวแต่ก็น่าสนใจ มันไม่ใช่อุปกรณ์ที่ใช้เรียกสัตว์ประหลาด";
				next;
				mes "[กบฏ]";
				mes "หลังจากเรียกมอนสเตอร์ออกมาแล้ว มันจะปล่อยพิษเคมีออกมาอย่างต่อเนื่อง ทำให้การกำจัดมอนสเตอร์นั้นยากยิ่งขึ้น";
				next;
				mes "[กบฏ]";
				mes "หากคุณสัมผัสพิษเคมี มันจะแพร่กระจายเหมือนระเบิด คุณไม่ควรเข้าใกล้มากกว่านี้";
				next;
				mes "[กบฏ]";
				mes "ดูเหมือนเรื่องนี้จะจบแล้ว ฉันจะไปร่วมทีมอื่น! คุณก็ควรทำแบบนั้นด้วย";
				close2;
				disablenpc();
				disablenpc instance_npcname("Box#" + strnpcinfo(2));
				viewpointmap 'map$,2,220,170,3,0xFFFFFF;
				completequest 16356;
				if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
						'step = 2;
						doevent instance_npcname("#171_cor_control") + "::OnStory02";
				}
		}
		end;
}

1@cor,220,170,3	script	Box#171_box_1	4_WOODBOX,{
		if (isbegin_quest(16356) == 0) {
				mes "[กบฏ]";
				mes "สาเหตุของการระเบิดนั้นไม่ทราบแน่ชัด แต่ฉันพบกล่องที่น่าสงสัยนี้ที่บริเวณนั้น ดูเหมือนว่าจะเป็นกลไกบางอย่าง";
				next;
				mes "[กบฏ]";
				mes "มีสัญญาณบางอย่างกำลังถูกส่งมาจากด้านใน... ระวังหน่อย มีอะไรเกิดขึ้น!";
				close2;
				setquest 16356;
				specialeffect EF_BAKU;
				donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
				end;
		}
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
		if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
				mes "[กบฏ]";
				mes "หลีกเลี่ยงพิษเคมีและฆ่าสัตว์ประหลาด!";
				close;
		}
		if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$))
				doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
		end;

OnSpawn:
		setd("'" + strnpcinfo(2),1);
		setarray .@xy,213,175,20342,216,174,20342,219,167,20342;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
				monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
		initnpctimer;
		end;

OnMobKill:
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
		if (!mobcount('map$,.@event$)) {
				stopnpctimer;
				killmonster 'map$,instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
				disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
				npctalk "ขอขอบคุณทุกท่านที่ทำงานหนัก! ก่อนที่คุณจะไป โปรดฟังสิ่งที่ฉันจะพูดสักพัก",instance_npcname("Rebellion#" + strnpcinfo(2));
		}
		end;

OnTimer1000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (mobcount('map$,.@event$)) {
				npctalk "มันพ่นสารเคมีพิษออกมา! หลีกเลี่ยงไม่ให้เหยียบมันและสู้ต่อไป!",instance_npcname("Rebellion#" + strnpcinfo(2));
				enablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
		} else
				stopnpctimer;
		end;

OnTimer11000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		.@event2$ = instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
		stopnpctimer;
		if (mobcount('map$,.@event$)) {
				disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
				if (unitexists(getnpcid(0,instance_npcname("Chemical Poison#171_box_1"))))
						initnpctimer;
		}
		end;
}

1@cor,220,169,3	script	Chemical Poison#171_box_1	2531,3,3,{
		end;

OnTouch:
		disablenpc();
		stopnpctimer instance_npcname(strnpcinfo(0));
		npctalk "เมื่อคุณสัมผัสกับพิษเคมี สารพิษจะแพร่กระจาย คุณเหยียบมันไปแล้ว รีบหลีกเลี่ยงมันซะ!",instance_npcname("Rebellion#" + strnpcinfo(2));
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
		if (!mobcount('map$,.@event$)) {
				getmapxy(.@m$,.@x,.@y,BL_NPC);
				monster 'map$,.@x,.@y,"",20344,1,.@event$;
		}
		initnpctimer;
		end;

OnPoisonKill:
		end;

OnTimer10000:
		stopnpctimer;
		initnpctimer instance_npcname("Box#" + strnpcinfo(2));
		end;
}

1@cor,162,117,1	script	Rebellion#171_box_2	4_M_GONY,{
		mes "[กบฏ]";
		mes "-";
		close;
}

1@cor,160,119,3	script	Box#171_box_2	4_STEELBOX,{
		mes "[กบฏ]";
		mes "-";
		specialeffect EF_M03;
		next;
		mes "[กบฏ]";
		mes "-";
		next;
		mes "- มีควันลอยออกมาจากกล่องที่น่าสงสัย -";
		next;
		select("Have you done this already?:Are you still silent?");
		mes "[กบฏ]";
		mes "-";
		next;
		mes "[กบฏ]";
		mes "*พยักหน้า พยักหน้า*";
		next;
		mes "- ไปให้กำลังใจทีมอื่นกันเถอะ -";
		viewpointmap 'map$,2,160,119,2,0xFFFFFF;
		close2;
		disablenpc();
		disablenpc instance_npcname("Rebellion#" + strnpcinfo(2));
		end;
}

1@cor,138,82,5	script	Rebellion#171_box_3	4_F_ANYA,{
		mes "[กบฏ]";
		mes "ฉันเคยบอกคุณไหมว่าฉันมีความสุขมากที่ได้ทำงานกับ Secret Wing? ต้องขอบคุณพวกเขาที่ทำให้ฉันถูกเลือกให้เข้าร่วมปฏิบัติการนี้";
		next;
		mes "[กบฏ]";
		mes "มาแสดงให้พวกเขารู้ว่าพวกเราเป็นยังไงและพวกเราเป็นคนดี";
		close;
}

1@cor,142,82,3	script	Rebellion#171_box_4	4_M_ILYA,{
		mes "[กบฏ]";
		mes "ฉันไม่ได้เจอคุณนานแล้วนะนักผจญภัย";
		next;
		mes "[กบฏ]";
		mes "ฉันรู้จักคุณแล้วในฐานะนักผจญภัย แต่โปรดระวังด้วย";
		close;
}

1@cor,140,79,3	script	Box#171_box_3	4_WOODBOX,{
		mes "[กบฏ]";
		mes "สวัสดีนักผจญภัย";
		specialeffect EF_M03;
		next;
		mes "[กบฏ]";
		mes "มันเป็นเรื่องดีที่ได้เป็นส่วนหนึ่งของทีมนี้ เราดูแลกล่องนี้เรียบร้อยแล้ว";
		next;
		mes "[กบฏ]";
		mes "เครื่องจักรเหล่านี้ใช้พิษเคมี ระวังด้วย";
		next;
		mes "[กบฏ]";
		mes "เราจะดำเนินการค้นหาต่อไปจนกว่าจะได้รับสัญญาณ นักผจญภัย ระวังตัวด้วย!";
		viewpointmap 'map$,2,140,79,1,0xFFFFFF;
		close2;
		disablenpc();
		disablenpc instance_npcname("Rebellion#171_box_3");
		disablenpc instance_npcname("Rebellion#171_box_4");
		end;
}

1@cor,172,223,3	script	Elena Volkova#171_cmd_1	4_F_ELENA,{
		cutin "162elena_01",2;
		mes "[เอเลน่า โวลโควา]";
		mes "ฉันรอคุณอยู่ ฉันเจอนักวิจัยบ้าแล้ว";
		mes "คนอื่นๆ กำลังล้อมรอบสถานที่อยู่";
		next;
		mes "[เอเลน่า โวลโควา]";
		mes "เพื่อที่จะควบคุมเครื่องจักรดังกล่าวได้อย่างดี จำเป็นต้องมีเอลียูมินาอยู่ใกล้ ๆ";
		next;
		mes "[เอเลน่า โวลโควา]";
		mes "ฉันสามารถจัดการเครื่องนี้ได้ แต่ฉันจะจับเอลิวมินาไม่ได้";
		next;
		mes "[เอเลน่า โวลโควา]";
		mes "หากคุณดูแลเครื่องจักร ฝ่ายค้นหาอื่นจะดูแลการค้นหา Elyumina";
		next;
		cutin "162elena_02",2;
		mes "[เอเลน่า โวลโควา]";
		mes "กองกำลังกบฏมีการประสานงานกันเป็นอย่างดี งานนี้เหมาะกับเราที่สุด ดังนั้นปล่อยให้เป็นหน้าที่ของเรา";
		next;
		cutin "162elena_01",2;
		mes "[เอเลน่า โวลโควา]";
		mes "สถานที่ที่เราจะเข้าไปคือ 9 โมง ฉันยืนยันว่าไม่มีใครซ่อนตัวอยู่ที่นั่น และเราก็ปิดทางออก";
		next;
		mes "[เอเลน่า โวลโควา]";
		mes "ให้สัญญาณฉันเมื่อคุณพร้อมที่จะเข้ามา ฉันจะบอกหน่วยให้ปล่อยคุณเข้าไป";
		next;
		if (select("Give signal.:Not yet.") == 2) {
				mes "[เอเลน่า โวลโควา]";
				mes "ใช่แล้ว มันเป็นคู่ต่อสู้ที่อันตรายมาก ฉันขอโทษที่ต้องปล่อยให้คุณสู้เพียงลำพัง บอกฉันด้วยเมื่อคุณพร้อม";
				close3;
		}
		cutin "162elena_02",2;
		mes "[เอเลน่า โวลโควา]";
		mes "โอเค ระวังไว้! ฉันเปิดประตูมิติให้คุณเข้าไปได้แล้ว มันเป็นทางเข้าทางเดียว ดังนั้นเตรียมตัวให้พร้อมนะ!";
		close2;
		cutin "",255;
		viewpointmap 'map$,2,172,223,5,0xFFFFFF;
		if ('step == 2) {
				'step = 3;
				donpcevent instance_npcname("#171_cor_control") + "::OnStory03";
		}
		end;
}

1@cor,138,221,3	script	Elena Volkova#171_cmd_2	4_F_ELENA,{
		if ('mode) {
				if (isbegin_quest(16363) == 0) {
						cutin "162elena_01",2;
						mes "[เอเลน่า โวลโควา]";
						mes "โอ้ใช่ คราวนี้คุณทำงานหนักอีกแล้ว มันยากจริงๆ! เราต้องใช้เศษโลหะนั้นนานแค่ไหนถึงจะหยุดมันได้?";
						next;
						cutin "ep171_elyumina02",0;
						mes "[เอลูมินา]";
						mes "ฉันบอกแล้วไงว่าอย่าเรียกมันว่าเศษโลหะ โง่จริง!";
						next;
						cutin "162elena_01",2;
						mes "[เอเลน่า โวลโควา]";
						mes "ใช่ ใช่ ตอนแรกมันไม่ใช่เศษโลหะ แต่ตอนนี้เราจับได้แล้ว มันคือเศษโลหะ ยังไงก็ตาม คุณทำงานหนักแล้ว คุณกลับไปพักผ่อนได้แล้ว";
						next;
						cutin "",255;
						if (select("Go back.:Stay for a moment.") == 2) {
								mes "[เอเลน่า โวลโควา]";
								mes "โอเค แจ้งให้ฉันทราบเมื่อคุณพร้อมที่จะออกไป";
								close3;
						}
						cutin "162elena_02",2;
						mes "[เอเลน่า โวลโควา]";
						mes "ทำได้ดีมาก มันตลกดีนะที่เราต้องทำแบบนี้อีกพรุ่งนี้ เจอกันพรุ่งนี้นะ";
						setquest 16363;
						getitem 25723,1;
						getitem 25669,5;
						close2;
						warp "sp_cor",111,125;
				}
				end;
		}
		if (isbegin_quest(16354) == 1 && checkquest(16376,HUNTING) == 2) {
				cutin "162elena_02",2;
				mes "[เอเลน่า โวลโควา]";
				mes "ทำได้ดี," +strcharinfo(0)+ ". การดำเนินการประสบความสำเร็จ!";
				mes "ฉันก็หนีออกมาเหมือนกัน น่าเขินจังที่คุณทำเศษเหล็กขนาดใหญ่หล่นลงมา ฉันยังจับภาพบางอย่างที่น่าหลงใหลได้อีกด้วย";
				next;
				cutin "ep171_elyumina02",0;
				mes "[เอลูมินา]";
				mes "เศษเหล็ก! สุดยอดผลงาน! ลูกน้อยของแม่!";
				mes "คุณ! คุณ...ทำร้ายลูกของฉัน! คุณทำลายลูกของฉัน!";
				next;
				cutin "162elena_02",2;
				mes "[เอเลน่า โวลโควา]";
				mes "คุณเพิ่งเรียกเศษอาหารพวกนั้นว่าเด็กเหรอ?";
				mes "ลูกของคุณคงเป็นคนดี งั้นเรามาชดใช้ความผิดของลูกคุณกันดีกว่า";
				next;
				cutin "162elena_01",2;
				mes "[เอเลน่า โวลโควา]";
				mes "แล้วคุณจะทำอย่างไรต่อไป" + strcharinfo(0) + "-";
				next;
				if (select("I'm going right back.:I'll stay here for a bit") == 2) {
						mes "[เอเลน่า โวลโควา]";
						mes "โอเค แจ้งให้ฉันทราบเมื่อคุณพร้อมที่จะออกไป";
						close3;
				}
				mes "[เอเลน่า โวลโควา]";
				mes "ทำได้ดีมาก เราจะเคลียร์กันตรงนี้ เธอพักก่อนได้ ฉันจะสอบสวนเธอ พักก่อนแล้วค่อยกลับมาหาฉัน";
				completequest 16354;
				erasequest 16376;
				setquest 16357;
				setquest 16377;
				close2;
				warp "sp_cor",176,169;
		}
		end;
}

1@cor,140,221,3	script	Elyumina#171_cor_end	4_EP17_ELYUMINA,{
		npctalk "เอลิวมินา : พวกเธอ... พวกเธอฆ่ามันเหรอ?! พวกเธอสังหารมัน! พวกเพชฌฆาต!";
		end;
}
1@cor,141,222,3	script	Rebellion#171_cmd_6	4_M_ILYA,{
		npctalk "นักวิจัยภาพลวงตาเอลียูมินาถูกจับแล้ว!";
		end;
}
1@cor,141,220,3	script	Rebellion#171_cmd_7	4_F_ANYA,{
		npctalk "เราได้จับเธอไว้ได้สำเร็จแล้ว ปฏิบัติการสิ้นสุดลงแล้ว!";
		end;
}

1@cor,1,1,0	script	#171_cor_boss	HIDDEN_WARP_NPC,{
		end;

OnSummonStory:
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
		monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
		'boss_gid = $@mobid[0];
		getunitdata 'boss_gid,.@boss_data;
		getunitdata $@mobid[0],.@boss_data;
		.@DAMAGE = (.@boss_data[UMOB_MAXHP]/10) * 9;
		.@HP = (.@boss_data[UMOB_MAXHP] - .@DAMAGE)/2;
		setunitdata $@mobid[0],UMOB_HP,.@HP;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
		initnpctimer;
		end;

OnSummonDaily:
		.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
		monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
		'boss_gid = $@mobid[0];
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
		initnpctimer;
		end;

OnBossKill:
		stopnpctimer;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnClear";
		enablenpc instance_npcname("Elena Volkova#171_cmd_2");
		enablenpc instance_npcname("Elyumina#171_cor_end");
		enablenpc instance_npcname("Rebellion#171_cmd_6");
		enablenpc instance_npcname("Rebellion#171_cmd_7");
		end;

OnClear:
		for (.@i = 0; .@i < 11; .@i++) {
				disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
				disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
		}
		killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
		killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBombKill";
		killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBossKill";
		end;

OnTimer30000:
		stopnpctimer;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
		end;

OnSummonSkill:
		if (unitexists('boss_gid)) {
				mapannounce 'map$,"Elyumina : It's starting to spread something. Prepare yourself!",bc_map,0xFFFF99;
				if (rand(2)) {
						.@npc$ = "Chemical Poison#171_cor_";
						.@type$ = "trap_0_";
				} else {
						.@npc$ = "Biological Battery#171_cor_";
						.@type$ = "trap_1_";
				}
				while(true) {
						.@id = rand(11);
						if (getd("'" + .@type$ + .@id))
								continue;
						if (unitexists('boss_gid)) {
								enablenpc instance_npcname(.@npc$ + .@id);
								setd("'" + .@type$ + .@id,1);
						}
						break;
				}
				initnpctimer;
		}
		end;
}

1@cor,151,221,3	script	Biological Battery#171_cor_0	1738,3,3,{
		end;

OnTouch:
		.@id = atoi(replacestr(strnpcinfo(2),"171_cor_",""));
		disablenpc();
		setd("'trap_1_" + .@id,0);
		getmapxy('map$,.@x,.@y,BL_NPC);
		monster 'map$,.@x,.@y,"",20345,1,instance_npcname("#171_cor_boss") + "::OnBombKill";
		initnpctimer;
		end;

OnTimer7000:
		if (!'rf_171_cor_0)
				enablenpc instance_npcname("Reinforced Energy#171_cor_0");
		end;

OnTimer12000:
		stopnpctimer;
		if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
				disablenpc instance_npcname("Reinforced Energy#171_cor_0");
		end;
}

1@cor,151,221,3	script	Chemical Poison#171_cor_0	2531,3,3,{
		end;

OnTouch:
		disablenpc();
		getmapxy('map$,.@x,.@y,BL_NPC);
		.@event$ = instance_npcname("#171_cor_boss") + "::OnPoisonKill";
		if (!mobcount('map$,.@event$))
				monster 'map$,.@x,.@y,"",20344,1,.@event$;
		end;
}

1@cor,137,220,3	script	Reinforced Energy#171_cor_0	4_ENERGY_WHITE,{
		if (!getd("'rf_" + strnpcinfo(2))) {
				setd("'rf_" + strnpcinfo(2),1);
				specialeffect2 EF_ENHANCE;
				specialeffect2 EF_LIGHTSPHERE_STAR;
				sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
				npctalk "พลัง พลังล้นเหลือ!";
				unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
				sleep 1500;
				disablenpc();
				setd("'rf_" + strnpcinfo(2),0);
		}
		end;
}

1@cor,145,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_1	1738,3,3
1@cor,129,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_2	1738,3,3
1@cor,138,206,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_3	1738,3,3
1@cor,136,213,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_4	1738,3,3
1@cor,138,228,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_5	1738,3,3
1@cor,138,235,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_6	1738,3,3
1@cor,102,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_7	1738,3,3
1@cor,108,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_8	1738,3,3
1@cor,115,219,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_9	1738,3,3
1@cor,122,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_10	1738,3,3

1@cor,145,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_1	2531,3,3
1@cor,129,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_2	2531,3,3
1@cor,138,206,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_3	2531,3,3
1@cor,136,213,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_4	2531,3,3
1@cor,138,228,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_5	2531,3,3
1@cor,138,235,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_6	2531,3,3
1@cor,102,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_7	2531,3,3
1@cor,108,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_8	2531,3,3
1@cor,115,219,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_9	2531,3,3
1@cor,122,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_10	2531,3,3

1@cor,161,221,0	script	#171_cor_warp_0	WARPNPC,2,2,{
		end;

OnActive:
		enablenpc();
		specialeffect EF_READYPORTAL2;
		end;

OnTouch:
		if (!'boss_summon) {
				'boss_summon = 1;
				donpcevent instance_npcname("#171_cor_boss") + "::OnSummonStory";
		}
		warp 'map$,151,220;
		end;
}

1@cor,161,221,0	script	#171_cor_warp_1	WARPNPC,2,2,{
		end;

OnActive:
		enablenpc();
		specialeffect EF_READYPORTAL2;
		end;

OnTouch:
		if (!'boss_summon) {
				'boss_summon = 1;
				donpcevent instance_npcname("#171_cor_boss") + "::OnSummonDaily";
		}
		warp 'map$,151,220;
		end;
}

1@cor,224,238,3	script	Elyumina#171_box_d0	4_EP17_ELYUMINA,{
		if ('cor == 4) {
				npctalk "เอลิวมินา: รู้ไหมว่าค่ายทหารอยู่ที่ไหน ฉันแสดงมันไว้บนแผนที่ย่อของคุณแล้ว ฉันจะไปที่นั่นเร็วๆ นี้";
				end;
		}
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		.@var = getd("'box_" + .@id);
		switch (.@var) {
				case 0: .@talk$ = "Here, here! Then please, touch the box and it will activate."; break;
				case 1: .@talk$ = "Chatting during a battle, quite relaxed aren't you? Hm, that's a bit annoying."; break;
				case 2: .@talk$ = "That's a very good combat data... Well, go ahead now!";
		}
		npctalk "เอลูมินา :" + .@talk$;
		end;
}

1@cor,218,172,5	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d1	4_EP17_ELYUMINA
1@cor,162,117,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d2	4_EP17_ELYUMINA
1@cor,140,82,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d3	4_EP17_ELYUMINA

1@cor,222,236,3	script	Box#171_box_d0	4_STEELBOX,{
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		if (!getd("'box_" + .@id)) {
				setd("'box_" + .@id,1);
				switch (.@id) {
						case 0: setarray .@xy,228,232,20341,216,229,20341,223,242,20341,226,239,20341,227,242,20341,229,230,20341,214,241,20343,224,235,20343,219,244,20343,227,230,20355,215,228,20355,221,231,20355,230,227,20355; break;
						case 1: setarray .@xy,224,164,20342,215,175,20342,220,174,20342,215,170,20342,216,164,20342,217,175,20342,226,164,20343,224,165,20343,215,172,20343,219,170,20356,217,176,20356,213,165,20356; break;
						case 2: setarray .@xy,158,119,20341,165,125,20341,160,119,20341,162,119,20341,158,119,20341,165,113,20341,149,117,20343,162,125,20343,149,125,20343,166,113,20355,167,115,20355,166,119,20355; break;
						case 3: setarray .@xy,143,72,20342,141,77,20342,146,84,20342,139,82,20342,138,75,20342,144,82,20342,140,70,20343,140,73,20343,142,72,20343,133,85,20356,140,82,20356,142,76,20356;
				}
				specialeffect EF_BAKU;
				for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
						monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
				initnpctimer;
		}
		end;

OnMobKill:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (!mobcount('map$,.@event$)) {
				.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
				setd("'box_" + .@id,2);
				'cor += 1;
				if (.@id == 0 || .@id == 2) {
						.@npc$ = "Biological Battery#";
						disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
				} else
						.@npc$ = "Chemical Poison#";
				switch (.@id) {
				case 0:
						viewpointmap 'map$,2,222,236,4,0xFFFFFF;
						.@npc$ = "Biological Battery#";
						break;
				case 1:
						viewpointmap 'map$,2,220,170,3,0xFFFFFF;
						.@npc$ = "Chemical Poison#";
						break;
				case 2:
						viewpointmap 'map$,2,160,119,2,0xFFFFFF;
						.@npc$ = "Biological Battery#";
						break;
				case 3:
						viewpointmap 'map$,2,140,79,1,0xFFFFFF;
						.@npc$ = "Chemical Poison#";
				}
				killmonster 'map$,instance_npcname(.@npc$ + "171_box_trap_" + .@id) + "::OnMobKill";
				disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
				if ('cor == 4)
						donpcevent instance_npcname("#171_cor_control") + "::OnDaily02";
		}
		end;

OnTimer1000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		if (.@id == 0 || .@id == 2)
				.@npc$ = "Biological Battery#";
		else
				.@npc$ = "Chemical Poison#";
		if (mobcount('map$,.@event$))
				enablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
		else
				stopnpctimer;
		end;

OnTimer11000:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		stopnpctimer;
		if (mobcount('map$,.@event$)) {
				if (.@id == 0 || .@id == 2)
						.@npc$ = "Biological Battery#";
				else
						.@npc$ = "Chemical Poison#";
				disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
				if (unitexists(getnpcid(0,instance_npcname(.@npc$ + "171_box_trap_" + .@id))))
						initnpctimer;
		}
		end;
}

1@cor,222,235,3	script	Biological Battery#171_box_trap_0	1738,3,3,{
		end;

OnTouch:
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
		if (.@id == 0 || .@id == 2)
				.@mob = 20345;
		else
				.@mob = 20344;
		stopnpctimer instance_npcname("Box#171_box_d" + .@id);
		.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
		disablenpc();
		if (mobcount('map$,.@event$)) {
				.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
				getmapxy(.@m$,.@x,.@y,BL_NPC);
				if (!mobcount('map$,.@event2$))
						monster 'map$,.@x,.@y,"--ja--",.@mob,1,.@event2$;
				initnpctimer;
		}
		end;

OnMobKill:
		end;

OnTimer7000:
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
		if (.@id == 0 || .@id == 2)
				.@mob = 20345;
		else
				.@mob = 20344;
		if (.@mob == 20345) {
				if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_trap_" + .@id)))) {
						disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
						enablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
				}
		}
		end;

OnTimer10000:
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
		if (.@id == 0 || .@id == 2)
				.@mob = 20345;
		else
				.@mob = 20344;
		if (.@mob == 20344) {
				stopnpctimer;
				initnpctimer instance_npcname("Box#171_box_d" + .@id);
		}
		end;

OnTimer17000:
		stopnpctimer;
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
		.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
		if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
				disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
		if (mobcount('map$,.@event$))
				initnpctimer instance_npcname("Box#171_box_d" + .@id);
		end;
}

1@cor,220,169,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_1	2531,3,3
1@cor,160,118,3	duplicate(Biological Battery#171_box_trap_0)	Biological Battery#171_box_trap_2	1738,3,3
1@cor,140,78,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_3	2531,3,3

1@cor,225,232,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_0	4_ENERGY_WHITE
1@cor,163,115,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_2	4_ENERGY_WHITE

1@cor,220,170,3	duplicate(Box#171_box_d0)	Box#171_box_d1	4_WOODBOX
1@cor,160,119,3	duplicate(Box#171_box_d0)	Box#171_box_d2	4_STEELBOX
1@cor,140,79,3	duplicate(Box#171_box_d0)	Box#171_box_d3	4_WOODBOX

1@cor,159,218,0	script	#cor_barricade_0	4_ROPEPILE,{
		end;

OnTouch:
		npctalk "ทางเข้าถูกปิดกั้น!";
		end;
}

1@cor,159,220,3	duplicate(#cor_barricade_0)	#cor_barricade_1	4_ROPEPILE
1@cor,159,222,3	duplicate(#cor_barricade_0)	#cor_barricade_2	4_ROPEPILE
1@cor,159,224,3	duplicate(#cor_barricade_0)	#cor_barricade_3	4_ROPEPILE,2,2
1@cor,98,218,3	duplicate(#cor_barricade_0)	#cor_barricade_4	4_ROPEPILE
1@cor,98,220,3	duplicate(#cor_barricade_0)	#cor_barricade_5	4_ROPEPILE
1@cor,98,222,3	duplicate(#cor_barricade_0)	#cor_barricade_6	4_ROPEPILE
1@cor,98,224,3	duplicate(#cor_barricade_0)	#cor_barricade_7	4_ROPEPILE,2,2
1@cor,134,240,3	duplicate(#cor_barricade_0)	#cor_barricade_8	4_ROPEPILE
1@cor,136,240,3	duplicate(#cor_barricade_0)	#cor_barricade_9	4_ROPEPILE
1@cor,138,240,3	duplicate(#cor_barricade_0)	#cor_barricade_10	4_ROPEPILE
1@cor,140,240,3	duplicate(#cor_barricade_0)	#cor_barricade_11	4_ROPEPILE,2,2
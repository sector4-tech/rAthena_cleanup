//===== rAthena Script =======================================
//= Eclage Instance Script
//===== By: ==================================================
//= Dastgir
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Eclage Interior Instance
//===== Additional Comments: =================================
//= 1.0 Converted from the official Aegis script. [Dastgir]
//= 1.1 Converted from Hercules script to rAthena script & Small bug fix. [Skorm]
//============================================================

ecl_hub01,132,12,3	script	Chief of Staff#tl02	435,{
		.@party_id = getcharid(1);
		.@md_name$ = "Eclage Interior";

		if (.@party_id == 0 || is_party_leader() == false){
				mes "-! คำเตือน !-";
				mes "ภารกิจปัจจุบันนี้จะจัดขึ้นที่ดันเจี้ยนอนุสรณ์";
				mes "เฉพาะหัวหน้าปาร์ตี้เท่านั้นที่จะเข้าได้ กรุณาสร้างปาร์ตี้ของคุณ";
				close;
		}
		if (ep14_2_mylord == 29) {
				mes "[เชนิเมะ]";
				mes "สถานที่นี้ถูกจัดไว้พร้อมแสงไฟสำหรับ Orb";
				mes "หากคุณชอบนายกเทศมนตรี คุณควรระมัดระวังในส่วนนี้";
				next;
				switch (select("Enter it.:Forget it.")) {
				case 1:
						if (instance_create(.@md_name$) < 0) {
								mes "[เชนิเมะ]";
								mes "มีทหารจำนวนหนึ่งที่ถูกนายกเทศมนตรีส่งมาเมื่อไม่นานนี้";
								mes "ฉันหวังว่าคุณคงไม่ใช่สิ่งรบกวนที่พวกเขากำลังมองหาอยู่ใช่มั้ย?";
								close;
						}
						ecl_interior_time = gettimetick(2) + 20 * 60;
						mes "[เชนิเมะ]";
						mes "เนื่องจากมีการรักษาความปลอดภัยภายในที่เข้มงวด คุณควรเตรียมตัวให้พร้อม";
						mes "รอตรงนี้สักครู่";
						close;
				case 2:
						mes "[เชนิเมะ]";
						mes "มันไม่ใช่เรื่องง่าย";
						close;
				}
		}
		mes "[ลาฟินระดับสูง]";
		mes "อืม ห้องนี้เหรอ?";
		mes "ไม่เป็นไร อย่ากังวลไปเลย";
		close;
}

ecl_hub01,130,15,0	script	It is closed shut.	844,{
		if (ep14_2_mylord == 29) {
				mes "ประตูนี้เชื่อมต่อกับเพดานของลานด้านหน้า";
				mes "นอกจากนี้ยังเป็นสถานที่จัดแสงไฟให้ Orb ดูสวยงามยิ่งขึ้น";
				next;
				if (select("Enter it.:Forget it.") == 1) {
						.@party_id = getcharid(1);
						.@md_name$ = "Eclage Interior";
						if (.@party_id == 0 || is_party_leader() == false) {
								mes "-! คำเตือน !-";
								mes "ภารกิจปัจจุบันนี้จะจัดขึ้นที่ดันเจี้ยนอนุสรณ์";
								mes "เฉพาะหัวหน้าปาร์ตี้เท่านั้นที่จะเข้าได้ กรุณาสร้างปาร์ตี้ของคุณ";
								close;
						}
						if (gettimetick(2) >= ecl_interior_time){
								mes "[เชนิเมะ]";
								mes "ดูเหมือนทหารข้างในจะตึงเครียดมากเลยนะ";
								mes "การจะแนะนำคนนอกอย่างคุณเป็นเรื่องยากมาก";
								close;
						}
						switch (instance_enter(.@md_name$)) {
						case IE_OTHER:
								mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						case IE_NOINSTANCE:
								mes "มันปิดสนิทแล้ว.";
								close;
						case IE_OK:
								mapannounce "ecl_hub01",getpartyname(.@party_id) + " Party leader " + strcharinfo(0) + " is entering " + .@md_name$,bc_map,"0x00ff99";
								end;
						default:
						}
				} else
						close;
		}
		mes "ประตูนี้เชื่อมต่อกับเพดานของลานด้านหน้า";
		mes "นอกจากนี้ยังเป็นสถานที่จัดแสงไฟให้ Orb ดูสวยงามยิ่งขึ้น";
		mes "มันปิดสนิทแล้ว.";
		close;
}

1@ecl,58,69,3	script	Shenime#ecl01	435,{
		mes "[เชนิเมะ]";
		mes "โอ้เกิดอะไรขึ้นที่นี่?";
		mes "พวกเขาได้ทำลายทหารเพิ่มเติมที่นายกเทศมนตรีส่งมา?";
		next;
		select("What...?:I fought monsters off!");
		mes "[เชนิเมะ]";
		mes "มันไม่สำคัญ";
		mes "งานของคุณสิ้นสุดที่นี่";
		mes "ตอนนี้คุณกลายเป็นคนทรยศแล้ว";
		mes "ลาก่อนนะ!";
		specialeffect EF_BEGINSPELL3;
		next;
		mes "-" + strcharinfo(0) + "-";
		mes "ไม่...ทาง!!!";
		specialeffect2 EF_LORD;
		percentheal -99,0;
		soundeffect "wander_man_move.wav",1;
		sc_start SC_BLIND,60000,0;
		close2;
		warp instance_mapname("1@ecl"),146,95;
		end;

OnInstanceInit:
		hideonnpc instance_npcname(strnpcinfo(0));
		end;

OnEnable:
		hideoffnpc instance_npcname(strnpcinfo(0));
		end;
}

1@ecl,51,64,1	script	Suspicious Creature#1	2375,{
		end;
}
1@ecl,43,67,3	duplicate(Suspicious Creature#1)	Suspicious Creature#2	2375,0,0
1@ecl,55,80,3	duplicate(Suspicious Creature#1)	Suspicious Creature#3	2375,0,0
1@ecl,60,75,3	duplicate(Suspicious Creature#1)	Suspicious Creature#4	2375,0,0

1@ecl,42,80,5	script	Suspicious Creature#5	2376,{
		end;
OnInstanceInit:
		hideonnpc instance_npcname(strnpcinfo(0));
		end;
}
1@ecl,48,86,3	duplicate(Suspicious Creature#5)	Suspicious Creature#6	2376,0,0
1@ecl,58,82,3	duplicate(Suspicious Creature#5)	Suspicious Creature#7	2376,0,0
1@ecl,56,88,3	duplicate(Suspicious Creature#5)	Suspicious Creature#8	2376,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
		end;

OnInstanceInit:
		'mob_summoned = 0;
		disablenpc();
		end;

OnEnable:
		sleep 1000;
        mapannounce instance_mapname("1@ecl"),"Warning: Abnormal occurrence in the lighting room. Abnormal occurrence in the lighting room.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		sleep 2000;
        mapannounce instance_mapname("1@ecl"),"Suspicious Creature: Kerrrrr....krwahhhh!!",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		hideoffnpc instance_npcname("Suspicious Creature#5");
		hideoffnpc instance_npcname("Suspicious Creature#6");
		hideoffnpc instance_npcname("Suspicious Creature#7");
		hideoffnpc instance_npcname("Suspicious Creature#8");
		sleep 3000;
		hideonnpc instance_npcname("#sndmaster");
		end;

OnMyMobDead:
		if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
				donpcevent instance_npcname("Shenime#ecl01")+"::OnEnable";
				mapannounce instance_mapname("1@ecl"),"Shenime: Good, I got here just in time.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		}
		end;

OnTouch:
		if ('mob_summoned > 1) end;
        mes "หลังจากกำจัดผู้บุกรุกใกล้กับ Orb เสร็จ สิ่งมีชีวิตหน้าตาประหลาดอีกตัวก็ปรากฏตัวขึ้น";
        next;
        mes "เราไม่สามารถสื่อสารกับพวกมันได้เช่นกัน และตัวหนึ่งดูโกรธมากหลังจากมองไปรอบๆ และพบว่าพวกของมันพ่ายแพ้แล้ว";
        next;
        mes "-" + strcharinfo(0) + "-";
        mes "พวกเขามีรูปร่างเหมือนกับว่าพวกเขาเป็นชาวโมร็อกโก";
        mes "King of Domons อยู่หลัง Orb หรือเปล่า?";
        mes "หรือมันเป็นแค่เรื่องบังเอิญ?";
        next;
        mes "[ผู้ใต้บังคับบัญชาของราชาอสูร]";
        mes "คร่อกกกกกก...คร่อกกกกกก!!!";
        mes "กว้ากกกก!";
        next;
        mes "การต่อสู้ไม่อาจหลีกเลี่ยงได้!";
        mes "พวกเขาจะฆ่าฉัน!";
		close2;
		hideonnpc instance_npcname("Suspicious Creature#5");
		hideonnpc instance_npcname("Suspicious Creature#6");
		hideonnpc instance_npcname("Suspicious Creature#7");
		hideonnpc instance_npcname("Suspicious Creature#8");
		disablenpc instance_npcname("Suspicious Creature#5");
		disablenpc instance_npcname("Suspicious Creature#6");
		disablenpc instance_npcname("Suspicious Creature#7");
		disablenpc instance_npcname("Suspicious Creature#8");
		.@eclmap$ = instance_mapname("1@ecl");
		.@sndmaster$ = instance_npcname("#sndmaster");
		monster .@eclmap$,42,80,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,48,86,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,59,82,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,56,88,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,39,72,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,42,69,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,47,64,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		monster .@eclmap$,50,60,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
		'mob_summoned = 2;
        end;
}

1@ecl,57,65,0	script	#event	139,5,5,{
		end;
OnTouch:
		if ('mob_summoned > 0) end;
		.@eclmap$ = instance_mapname("1@ecl");
		.@eventname$ = instance_npcname("#event");
		mes "ห้องว่างเปล่าไม่มีอะไรอยู่ข้างใน";
		mes "มีแสงสว่างออกมาจากอุปกรณ์วงกลมตรงกลางห้อง";
		next;
		mes "[สิ่งมีชีวิตที่น่าสงสัย]";
		mes "เคะๆๆๆๆๆๆๆ....";
		next;
		mes "สิ่งมีชีวิตที่ไม่สามารถระบุชนิดได้กลุ่มหนึ่งกำลังเดินเข้ามาใกล้ในลักษณะคุกคาม";
		mes "พวกเขาดูจะประหลาดใจ และถึงแม้ว่าความตั้งใจของ Bifrost จะยังคงมีอยู่ แต่มันก็ยากที่จะสื่อสาร";
		next;
		mes "[สิ่งมีชีวิตที่น่าสงสัย]";
		mes "คยาร์...เค...เคียร์ยาร์!!";
		next;
		mes "ดูเหมือนพวกเขาจะเริ่มโจมตีได้ทุกนาที";
		mes "เราไม่สามารถช่วยต่อสู้กับพวกมันได้!";
		close2;
		hideonnpc instance_npcname("Suspicious Creature#1");
		hideonnpc instance_npcname("Suspicious Creature#2");
		hideonnpc instance_npcname("Suspicious Creature#3");
		hideonnpc instance_npcname("Suspicious Creature#4");
		disablenpc instance_npcname("Suspicious Creature#1");
		disablenpc instance_npcname("Suspicious Creature#2");
		disablenpc instance_npcname("Suspicious Creature#3");
		disablenpc instance_npcname("Suspicious Creature#4");
		monster .@eclmap$,51,64,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
		monster .@eclmap$,43,67,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
		monster .@eclmap$,55,80,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
		monster .@eclmap$,60,75,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
		'mob_summoned = 1;
		end;

OnMyMobDead:
		if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
				enablenpc instance_npcname("#sndmaster");
				donpcevent instance_npcname("#sndmaster")+"::OnEnable";
		}
		end;
}

1@ecl,148,97,3	script	Avant#	618,{
		mes "[อาวองต์]";
		mes "เฮ้ ดูสิว่าใครอยู่ที่นี่!";
		mes "คุณเป็นเพื่อนของลูกชายฉันใช่ไหม?";
		mes "มันเป็นโอกาสแปลกประหลาดที่ทำให้เรามาพบกันที่นี่!";
		cutin "avant01",1;
		next;
		mes "[อาวองต์]";
		mes "มีอะไรเกิดขึ้นกับคุณหรือเปล่า?";
		mes "มีเสียงสะท้อนอันละเอียดอ่อนและมหัศจรรย์";
		mes "ทันทีหลังจากนั้นคุณก็ถูกโยนมาที่นี่";
		next;
		if (select("It's all because of you!:Shenime said...") == 1) {
				mes "[อาวองต์]";
				mes "ฉันไม่เข้าใจคำพูดของคุณเลย";
				mes "คุณจัดระเบียบเรื่องราวของคุณไม่ได้เหรอ คุณไม่รู้จักกฎ '5W และ 1H' เหรอ?";
				next;
		}
		mes "[อาวองต์]";
		mes "อ๋อ...ก็คือสิ่งที่เกิดขึ้น";
		mes "ดังนั้นคุณกำลังบอกว่า Shenime กำลังตามล่า Orb ใช่ไหม?";
		next;
		mes "[อาวองต์]";
		mes "อิอิอิ...ฮ่าฮ่าฮ่าฮ่าฮ่า";
		mes "เอาละ...เขาจับฉันได้ด้วยเหรอ?";
		mes "ฉันเองแหละ อวองต์ ของทุกคน?! ฮ่าๆๆๆ";
		cutin "avant02",1;
		next;
		select("What do you mean?");
		mes "[อาวองต์]";
		mes "ฉันกำลังค้นคว้าเกี่ยวกับพลังของออร์บด้วยตัวเอง";
		mes "แต่ Shenime ยังคงกระตุ้นฉันอย่างต่อเนื่อง";
		cutin "avant01",1;
		next;
		mes "[อาวองต์]";
		mes "ผมเริ่มจากการคิดถึงความงดงามของออร์บและคิดต่อไปว่ามีพลังชนิดใดที่สะสมอยู่ภายในออร์บ...";
		mes "และฉันเริ่มตั้งคำถามถึงหลักการของพลังของ Orb เนื่องจากเราทุกคนได้ประโยชน์จากมัน";
		next;
		mes "[อาวองต์]";
		mes "ชาวลาฟินทุกคนคิดว่าออร์บเป็นสิ่งที่เป็นธรรมชาติ เช่น อากาศและน้ำ แต่เขาเป็นคนแรกที่ทำให้ฉันมองมันแตกต่างออกไป และสืบหาต้นกำเนิดของมัน";
		mes "ใช่แล้ว...เชนิเมะ เขาทำให้ฉันอยากรู้ขึ้นมาโดยตั้งใจ";
		next;
		mes "[อาวองต์]";
		mes "กระตุ้นให้คนที่ไม่เคยรู้จักอากาศและน้ำรับรู้และสืบเสาะต่อไป!";
		mes "เอ่อ เอ่อ...น่าสนใจใช่ไหมล่ะ!";
		cutin "avant02",1;
		next;
		mes "[อาวองต์]";
		mes "ฮ่าๆๆๆ...";
		mes "...ฉันโกรธมากที่เพิ่งตระหนักว่าเขาใช้ฉันอย่างไร แต่ที่แย่กว่านั้นคือฉันทำเรื่องเลวร้ายกับคนของเราไปแล้ว";
		mes "ฉันคิดว่าตอนนี้ฉันรู้สึกหงุดหงิดมาก";
		next;
		select("You had a conscience?");
		mes "[อาวองต์]";
		mes "คุณคิดว่าฉันเป็นคนแบบไหนกันแน่!";
		mes "เพราะลูกชายของฉันเหรอ?";
		mes "เอาล่ะ ฉันก็ไม่โทษคุณมากเกินไปนัก";
		cutin "avant01",1;
		next;
		mes "[อาวองต์]";
		mes "...บ้าเอ๊ย.";
		mes "มันไม่สนุกเลย";
		mes "ไม่สนุกแล้ว ฉันไม่สนใจคุณอีกต่อไป";
		next;
		mes "[อาวองต์]";
		mes "-";
		mes "ดูเหมือนว่าจะมีผู้มาเยือน";
		next;
		cutin "minuel01",4;
		mes "[มินูเอล]";
		mes "" + strcharinfo(0) + "ท่าน,";
		mes "แม้คุณจะถูกควบคุมตัวเนื่องจากสงสัยว่ามีส่วนรับผิดชอบต่อเหตุการณ์ที่เกิดขึ้นในห้องไฟเมื่อเร็วๆ นี้";
		mes "มีพยานบางคนมาพิสูจน์ความบริสุทธิ์ของคุณ";
		next;
		mes "[มินูเอล]";
		mes "ฉันไม่ทราบรายละเอียด";
		mes "แต่ฉันก็เชื่อว่าคุณไม่ได้มีความผิด";
		mes "ฉันจะพาคุณไปที่ห้องรักษาเพราะดูเหมือนว่าแผลนี้ร้ายแรงมาก";
		next;
		mes "[มินูเอล]";
		mes "ฉันไม่คิดว่ามันเป็นเรื่องใหญ่อะไร";
		mes "...หวังว่านะ...";
		mes "ไปกันเถอะ ฉันจะนำทางเอง";
		close2;
		cutin "",255;
		warp "1@ecl",146,29;
		end;
}

1@ecl,148,29,3	script	Loki#ecl01	512,{
		mes "[โลกิ]";
		mes "ดูเหมือนว่าผู้ชายข้างฉันคนนี้จะมีเรื่องที่จะพูดมากกว่าฉันเยอะเลย";
		mes "ทำไมคุณไม่อธิบายให้เขาฟังก่อนล่ะ?";
		close;
}

1@ecl,147,30,3	script	Nydhog#ecl01	510,{
		mes "[ผู้พิทักษ์ Nydhog]";
		mes "ฉันคิดว่าคุณควรคุยกับ Hisie ก่อน";
		close;
}

1@ecl,144,27,5	script	Hisie#ecl01	623,{
		cutin "hisie01",0;
		mes "[ฮิซิ]";
		mes "คุณบอกฉันได้ไหมว่าเกิดอะไรขึ้น?";
		next;
		select("I fought with monsters.");
		cutin "hisie03",0;
		mes "[ฮิซิ]";
		mes "สัตว์ประหลาด? ลูกน้องของพวกโมร็อค?";
		mes "สิ่งที่คุณกำลังพยายามฆ่าก็คือทหารของพวกเรา พวกลาฟินส์!!";
		mes "ที่นั่นเกิดอะไรขึ้นกันแน่?";
		npcskill "AL_HEAL",9,99,80;
		next;
		cutin "",255;
		mes "[โลกิ]";
		mes "มันเพราะอย่างนี้นี่เอง.";
		mes "" + strcharinfo(0) + "มันอยู่ในร่างกายของ-";
		mes "นี่คือพลังของ Shenime";
		next;
		mes "บนฝ่ามือของโลกิ มีหนามแห้งเล็ก ๆ บาง ๆ วางไว้";
		next;
		cutin "hisie03",0;
		mes "[ฮิซิ]";
		mes "เชอนิเมะ หัวหน้าคณะเจ้าหน้าที่เหรอ?";
		mes "ทำไมเขาถึงเกี่ยวข้องกับเรื่องนี้?";
		npcskill "AL_HEAL",9,99,80;
		next;
		cutin "",255;
		mes "[โลกิ]";
		mes "เชนิเมะเป็นหนึ่งในผู้ใต้บังคับบัญชาของโมร็อกโก";
		mes "พลังของเขาทำให้เขาสามารถควบคุมบุคคลอื่นได้โดยตรง หรือส่งอิทธิพลทางจิตใจต่อบุคคลนั้นโดยทางอ้อมผ่านส่วนหนึ่งของร่างกายของเขา";
		next;
		select("Now that I think about it...");
		mes "-" + strcharinfo(0) + "-";
		mes "ก่อนจะมาที่นี่ เชอเนมก็ยื่นมือมาหาฉัน และฉันก็รู้สึกถึงประกายไฟเมื่อฉันจับมือเขา";
		mes "ฉันคิดว่านั่นเป็นแค่จินตนาการของฉันเท่านั้นและก็ไม่สนใจมัน...บางทีนั่นอาจเป็นตอนที่เขา...";
		next;
		mes "[ไนด์ฮ็อก]";
		mes "คุณอาจจะพูดถูก";
		mes "พวกเขาใช้อุบายอันชั่วร้าย";
		mes "หนามนี้คงทำให้การตัดสินของคุณขุ่นมัว";
		next;
		cutin "hisie03",0;
		mes "[ฮิซิ]";
		mes "แล้วคุณกำลังบอกว่าเขาหลอกเราทุกคนใช่ไหม?";
		mes "เสนาธิการทหารบกคือลาฟินที่อยู่กับเรามานาน! เขาไม่สามารถ...";
		npcskill "AL_HEAL",9,99,80;
		next;
		cutin "",255;
		mes "[โลกิ]";
		mes "เขาอาจจะได้รับอิทธิพลมาสักพักหนึ่งแล้ว";
		mes "อย่างไรก็ตามผู้ชายคนนี้ก็ไม่ผิด";
		mes "ฉันสามารถพิสูจน์มันได้";
		next;
		mes "[ไนด์ฮ็อก]";
		mes "ฉันขอสาบานด้วยชื่อของผู้พิทักษ์แห่ง Yggdrasil ด้วยเช่นกัน";
		mes "" + strcharinfo(0) + "- จะไม่ทำสิ่งดังกล่าวเด็ดขาด";
		next;
		cutin "hisie02",0;
		mes "[ฮิซิ]";
		mes "ฮ่าฮ่า...";
		mes "แต่ทุกอย่างกลับหลุดจากการควบคุม";
		mes "ฉันไม่ได้คาดหวังสิ่งนี้.";
		next;
		select("What happened to the Orb?");
		mes "[ฮิซิ]";
		mes "แกนในถูกขโมยไป";
		mes "ไม่มีใครรู้ว่าแกนในของออร์บหายไปแล้ว";
		mes "และนั่นคือเหตุผลว่าทำไม Karr ไม่ได้มาที่นี่";
		npcskill "AL_HEAL",9,99,80;
		next;
		mes "[ฮิซิ]";
		mes "และยิ่งไปกว่านั้นทหารหลายนายได้รับบาดเจ็บเพราะคุณ";
		mes "ไม่มีใครเสียชีวิตอย่างโชคดี แต่ส่วนใหญ่ได้รับบาดเจ็บสาหัส";
		mes "สถานการณ์ตอนนี้ไม่ค่อยดีนัก.";
		next;
		cutin "",255;
		mes "[โลกิ]";
		mes "ก็ต้องมีคนรับผิดชอบตรงนี้แล้วล่ะ";
		mes "ตามความน่าจะเป็นทั้งหมด" + strcharinfo(0) + "มันจะเป็นคุณ";
		next;
		cutin "hisie02",0;
		mes "[ฮิซิ]";
		mes "......สองสุภาพบุรุษท่านนี้ได้เคลียร์ข้อกล่าวหาทั้งหมดที่เกี่ยวข้องกับออร์บแล้ว";
		mes "แต่คุณและคาร์ก็ไม่สามารถหลีกเลี่ยงความรับผิดชอบของคุณได้";
		mes "คุณจะต้องจับ Shenime";
		npcskill "AL_HEAL",9,99,80;
		next;
		cutin "hisie01",0;
		mes "[ฮิซิ]";
		mes "ดูเหมือนว่าคุณจะได้พักผ่อนเพียงพอแล้ว ไปกันเถอะ";
		mes "คารร์กำลังรอคุณอยู่";
		mes "พวกคุณสองคนจะทำยังไง?";
		next;
		cutin "",255;
		mes "[โลกิ]";
		mes "อย่ากังวลเกี่ยวกับพวกเรา";
		mes "เป้าหมายของเราคือ โมร็อค ราชาแห่งปีศาจ";
		mes "ดีพอแล้วที่เราพบหลักฐานที่นี่";
		next;
		cutin "hisie01",0;
		mes "[ฮิซิ]";
		mes "ขอบคุณสำหรับการช่วยเหลือของคุณ.";
		mes "เราจะออกเดินทางกันแล้ว";
		mes "ไปกันเถอะ." + strcharinfo(0) + "-";
		ep14_2_mylord = 30;
		changequest 7450,7451;
		close2;
		cutin "",255;
		warp "ecl_in03",57,76;
		instance_destroy;
		end;
}
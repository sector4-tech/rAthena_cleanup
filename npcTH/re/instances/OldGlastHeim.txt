//===== rAthena Script =======================================
//= Old Glast Heim
//===== Description: =========================================
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Changelogs: =================================
//= 1.0 First version. [Euphy, Ziu, Heris]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//= 1.5 idAthena merge. Removed Hugin's Follower NPCs. [Secretdataz]
//=     Changed `set` calls to direct assignments.
//=     Cleaned up the script.
//=     TODO: Confirm Hugin NPC's code flow.
//= 1.6 Cleaned up the dialogue. [Aleos]
//= 1.7 Implemented fast mode. [Atemo]
//      Note: The treasure room is not accessible in fast mode.
//============================================================

glast_01,204,273,5	script	Hugin	4_M_SAGE_C,{
		if (BaseLevel < 130) {
				mes "[ฮูกิน]";
				mes "ทำไมคุณไม่กลับมาหลังจากแข็งแกร่งขึ้นล่ะ?";
				mes "อาจจะเป็นระดับ 130.";
				close;
		}
		if (isbegin_quest(12316) == 0) {
				mes "[ฮูกิน]";
				mes "เมื่อนานมาแล้วปราสาทแห่งนี้ไม่ได้มีลักษณะเช่นนี้";
				next;
				mes "[ฮูกิน]";
				mes "อ่า ขอโทษที ฉันบ่นพึมพำต่อหน้าคนแปลกหน้า";
				next;
				mes "[ฮูกิน]";
				mes "ฉันชื่อฮูกิน ฉันกำลังศึกษาช่องว่างมิติระหว่างเวลาและอวกาศ";
				next;
				select("There's something like that here?");
				mes "[ฮูกิน]";
				mes "คุณเคยสงสัยเกี่ยวกับประวัติศาสตร์ของ Glast Heim บ้างไหม?";
				next;
				mes "[ฮูกิน]";
				mes "มีกษัตริย์พระองค์หนึ่งพระนามว่าชูมิเช ซึ่งเป็นที่รู้จักในฐานะผู้เผด็จการในประวัติศาสตร์ของอาณาจักรรูน-มิดการ์ต";
				next;
				mes "[ฮูกิน]";
				mes "กาลเวลาจะเผยคำตอบที่แท้จริงหากเราค้นหาในสถานที่ที่ถูกต้อง";
				next;
				if (select("I don't care about history","That is interesting. Did you find anything?") == 1) {
						mes "[ฮูกิน]";
						mes "จริงเหรอ? ฮึม โปรดกลับมาใหม่เมื่อคุณสนใจนะ";
						close;
				}
				mes "[ฮูกิน]";
				mes "จริงๆแล้วการเดินทางข้ามเวลาข้ามมิติเป็นไปได้นะแต่ผมกลัวเกินกว่าจะไปที่นั่น";
				next;
				mes "[ฮูกิน]";
				mes "มันอาจจะเป็นไปได้สำหรับคุณ!";
				next;
				mes "[ฮูกิน]";
				mes "คุณอยากจะเดินทางข้ามเวลามั้ย?";
				next;
				if (select("No, thanks.","Yes, of course I do!") == 1) {
						mes "[ฮูกิน]";
						mes "จริงเหรอ? แต่การก้าวกระโดดเป็นกิกะวัตต์ ถือเป็นโอกาสที่ดีจริงๆ";
						close;
				}
				mes "[ฮูกิน]";
				mes "ฉันรู้ว่าคุณคงเข้าใจสิ่งที่ฉันพูด โปรดบอกฉันว่าคุณอยากทำอะไร";
				setquest 12316;// Meeting Hugin
				completequest 12316;
		}
		else {
				mes "[ฮูกิน]";
				mes "เอ่อ คุณเจอฉันที่อื่นไหม ฉันไม่คิดว่าจะเจอ แล้วคราวนี้ล่ะ";
				if (isbegin_quest(12322) == 1)
						erasequest 12322;
		}
		next;
		switch( checkquest(12317,PLAYTIME) ) {
		case -1:
				.@party_id = getcharid(1);
				.@p_name$ = getpartyname(.@party_id);
				.@md_name$ = "Old Glast Heim";

				if (!instance_check_party(.@party_id)) {
						mes "[ฮูกิน]";
						mes "ทำไมคุณไม่จัดปาร์ตี้กับคนมากกว่า 1 คนแล้วคุยกับฉันอีกครั้งล่ะ?";
						close;
				}
				if (is_party_leader() == true)
						.@menu$ = "Generate Time Gap";
				else {
						mes "[ฮูกิน]";
						mes "เราเคยเจอกันมาก่อนไหม? ไม่หรอก นี่เป็นครั้งแรกที่เจอเธอ เธอต้องการอะไร";
				}
				if (isbegin_quest(12318) == 0)
						setquest 12318;// Corrupted Soul Hunt
				switch( select( .@menu$, "Enter Old Glast Heim", "Cancel" ) ) {
				case 1:
						switch( instance_create(.@md_name$) ) {
						case -3:
								dispbottom "Memorial Dungeon, 'Old Glast Heim' is already in progress.",0xFFFFFF;
								close;
						case -4:
						case -2:
						case -1:
								mes "ชื่อปาร์ตี้:" + getpartyname( getcharid(1) );
								mes "หัวหน้าพรรค:" + strcharinfo(0);
								mes "^0000ff" + .@md_name$ + "^000000 - การสร้างช่องว่างเวลาล้มเหลว";
								close;
						}
						mes "[ฮูกิน]";
						mes "หลังจากเปิดช่องว่างเวลาแล้วกรุณาบอกฉันอีกครั้ง";
						close;
				case 2:
						switch( instance_enter(.@md_name$) ) {
						case IE_OTHER:
								mes "[ฮูกิน]";
								mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						case IE_NOINSTANCE:
								mes "[ฮูกิน]";
								mes "ช่องว่างเวลายังไม่เปิด";
								close;
						case IE_NOMEMBER:
								mes "[ฮูกิน]";
								mes "ร่างกายของคุณไม่พร้อมสำหรับการเข้าช่องว่างเวลา คุณจะไม่สามารถเข้าได้หากคุณไม่ได้อยู่ในปาร์ตี้";
								close;
						case IE_OK:
								mapannounce "glast_01", "" + getpartyname( getcharid(1) ) + " party member " + strcharinfo(0) + " enters the " + .@md_name$ + ".", bc_map,0x00FF99;
								setquest 12317;// Trace of Time Travel
								// warp "1@gl_k",150,20;
								end;
						}
				case 3:
						close;
				}
		case 0:
		case 1:
				mes "[ฮูกิน]";
				mes "โอ้พระเจ้า...";
				mes "คุณยังมีอาการตามมาจากการเดินทางข้ามเวลา คุณไม่สามารถเดินทางได้อีกต่อไปหากมีอาการเช่นนี้";
				next;
				mes "[ฮูกิน]";
				mes "การรักษาสุขภาพเป็นเรื่องสำคัญ ดังนั้นโปรดหยุดพักก่อนแล้วค่อยกลับมาใหม่ในภายหลัง";
				close;
		case 2:
				mes "^0000ffร่องรอยการเข้าถึง Old Glast Heim ทั้งหมดถูกลบออกแล้ว ตอนนี้คุณสามารถคุยกับ Hugin ได้อีกครั้ง^000000";
				if (isbegin_quest(12318) == 0)
						setquest 12318;// Corrupted Soul Hunt
				erasequest 12317;
				close;
		}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
// 1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Floor 1
//============================================================
1@gl_k,149,41,5	script	Varmundt#0	4_M_BARMUND,{
		if (is_party_leader() == false) {
				mes "[วาร์มุนด์]";
				mes "หัวหน้าของคุณอยู่ไหน ฉันต้องการความช่วยเหลือจากเขา";
				cutin "gl_barmund2",2;
				close3;
		}
		// If the instance has already been completed once, the player can skip sector 1,2,3 and starts from the 1st boss MG_CORRUPTION_ROOT
		if (isbegin_quest(12321) == 2) {
				mes "คุณมีประวัติที่ชัดเจนก่อนหน้านี้แล้ว คุณสามารถข้ามสถานการณ์นี้และดำเนินการต่อตามนั้นได้";
				next;
				if (select( "Omit moderately.", "Appreciate everything." ) == 1) {
						cutin "GL_BARMUND1.bmp",2;
						mes "[วาร์มุนด์]";
						mes "ฉันเห็นคุณแล้ว" + strcharinfo(0) + "-";
						mes "ฉันมั่นใจว่าฉันไว้ใจคุณได้ รีบไปกันเถอะ";
						close2;
						cutin "",255;
						if ('step == 0) {
								'step = 1;
								'mode = 2;
								disablenpc();
								enablenpc instance_npcname("Heinrich#1");
								enablenpc instance_npcname("Varmundt#1");
						}
						end;
				}
				// (original scenario)
		}
		.@account_id = getcharid(3);
		.@player_name$ = strcharinfo(0);
		mes "[วาร์มุนด์]";
		mes "คุณคือคนที่ ^0000ffHerico^000000 ถูกส่งมาช่วยฉันใช่ไหม?";
		npctalk "วาร์มุนด์: คุณคือคนที่เฮริโก้ส่งมาช่วยฉันใช่ไหม?";
		cutin "gl_barmund1",2;
		next;
		select("Oh. Well I...");
		mes "-" + .@player_name$ + "-";
		mes "ใช่แล้ว ฉันคือเฮริโก้ ฉันบอกให้คุณมาพบคุณ";
		unittalk .@account_id, .@player_name$ + " : Ah yes, I am. Herico told me to meet you.";
		next;
		mes "[วาร์มุนด์]";
		mes "เรามีเวลาไม่มาก เราต้องรายงานให้เซอร์ไฮน์ริชทราบเกี่ยวกับการรุกรานของฮิเมลเมซ";
		npctalk "วาร์มุนด์: เราไม่มีเวลาเหลือมากนัก เราต้องรายงานให้เซอร์ไฮน์ริชทราบเกี่ยวกับการรุกรานของฮิเมลเมซ";
		cutin "gl_barmund2",2;
		next;
		select("Himelmez...");
		mes "-" + .@player_name$ + "-";
		mes "ฮิเมิลเมซ คือใคร?";
		unittalk .@account_id, .@player_name$ + " : Who is Himelmez?";
		next;
		mes "[วาร์มุนด์]";
		mes "เฮริโก้ไม่ได้บอกคุณเหรอ?";
		npctalk "วาร์มุนด์: เฮริโก้ไม่ได้บอกคุณเหรอ?";
		cutin "gl_barmund3",2;
		next;
		mes "[วาร์มุนด์]";
		mes "เธอคือผู้ปกครองแห่งความตาย วาลคิรีแห่งคนตาย ฮิเมลเมซกำลังหาทางเอาหัวใจของยิร์ไปจากพวกเรา";
		npctalk "Varmundt: เธอคือผู้ปกครองแห่งความตาย วาลคิรีแห่งคนตาย ฮิเมลเมซกำลังหาทางเอาหัวใจของ Ymir จากพวกเรา";
		cutin "gl_barmund2",2;
		next;
		mes "[วาร์มุนด์]";
		mes "เธออาจทำลายปราสาททั้งหมดนี้ได้ถ้าเธอต้องการ";
		npctalk "วาร์มุนด์: เธออาจทำลายปราสาททั้งหมดนี้ได้ถ้าเธอต้องการ";
		next;
		mes "[วาร์มุนด์]";
		mes "รีบไปรายงานท่านไฮน์ริชเรื่องฮิเมลเมซเถอะ ฉันต้องตรวจดูกำแพงป้องกันที่นี่!";
		npctalk "วาร์มุนด์: รีบไปรายงานท่านไฮน์ริชเรื่องฮิเมลเมซเถอะ ฉันต้องตรวจสอบกำแพงป้องกันที่นี่!";
		close2;
		cutin "",255;
		if ('step == 0) {
				'step = 1;
				'mode = 1;
				disablenpc instance_npcname("Varmundt#0");
				enablenpc instance_npcname("Heinrich#1");
				enablenpc instance_npcname("Varmundt#1");
		}
		end;
}

1@gl_k,145,104,6	script	Khalitzburg Knightage#1	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "งานของฉันจะสิ้นสุดเร็วๆ นี้ แต่ทีมงานชุดต่อไปจะไม่มา";
		close;
}

1@gl_k,154,104,3	script	Khalitzburg Knightage#2	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "มีอะไรที่คุณอยากจะบอกฉันมั้ย?";
		close;
}

1@gl_k,145,99,6	script	White Knight#3	4_WHITEKNIGHT,{
		mes "[อัศวินสีขาว]";
		mes "คุณอยู่กับ Varmundt ไหม ผู้บัญชาการกำลังรอคุณอยู่";
		close;
}

1@gl_k,154,99,3	duplicate(Khalitzburg Knightage#1)	White Knight#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	Khalitzburg Knightage#5	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "เมื่อคืนฉันฝันประหลาดมาก แม่ของฉันอยู่ในนั้นด้วย... ไม่รู้ว่าเธอจะโอเคไหม...";
		close;
}

1@gl_k,154,94,3	duplicate(White Knight#3)	Khalitzburg Knightage#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	White Knight#7	4_WHITEKNIGHT,{
		mes "[อัศวินสีขาว]";
		mes "กรุณาอย่าสร้างความยุ่งยากที่นี่";
		close;
}
1@gl_k,154,89,3	duplicate(White Knight#7)	White Knight#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	Khalitzburg Knightage#9	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "กรุณาสั่งซื้อสินค้าค่ะ.";
		close;
}
1@gl_k,154,84,3	duplicate(Khalitzburg Knightage#9)	Khalitzburg Knightage#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	White Knight#11	4_WHITEKNIGHT,{
		mes "[อัศวินสีขาว]";
		mes "คุณรู้จักวาร์มุนด์ได้อย่างไร ฉันได้ยินมาว่าเขาไม่ใช่คนเป็นมิตรสักเท่าไหร่...";
		close;
}
1@gl_k,154,79,3	duplicate(White Knight#11)	White Knight#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	Khalitzburg Knightage#13	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "ปราสาทแห่งนี้ดูแปลกๆ ขึ้นมาอยู่เรื่อยๆ หรือเปล่านะ? มีบางอย่างไม่ค่อยจะถูกต้อง";
		close;
}
1@gl_k,154,74,3	duplicate(Khalitzburg Knightage#13)	Khalitzburg Knightage#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	White Knight#15	4_WHITEKNIGHT,{
		mes "[อัศวินสีขาว]";
		mes "ฉันไม่ชอบคุยในระหว่างทำงาน";
		close;
}
1@gl_k,154,69,3	duplicate(White Knight#15)	White Knight#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	Khalitzburg Knightage#17	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "-";
		close;
}
1@gl_k,154,64,3	duplicate(Khalitzburg Knightage#17)	Khalitzburg Knightage#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	White Knight#19	4_WHITEKNIGHT,{
		mes "[อัศวินสีขาว]";
		mes "ฉันสามารถทำอะไรให้คุณได้บ้าง?";
		close;
}
1@gl_k,154,59,3	duplicate(White Knight#19)	White Knight#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	Khalitzburg Knightage#21	4_F_KHALITZBURG,{
		mes "[ยุคอัศวินคาลิตซ์เบิร์ก]";
		mes "มีอะไรที่คุณต้องการหรือเปล่า?";
		close;
}
1@gl_k,154,54,3	duplicate(Khalitzburg Knightage#21)	Khalitzburg Knightage#22	4_F_KHALITZBURG

1@gl_k,149,100,5	script	Heinrich#1	4_M_HEINRICH,{
		if (is_party_leader() == false || 'step != 1) {
				mes "[ไฮน์ริช]";
				mes "หัวหน้าของคุณอยู่ไหน ฉันต้องคุยกับเขา";
				cutin "gl_heinrich2",2;
				close3;
		}
		if ('mode == 2) {
				cutin "GL_HEINRICH2.bmp",2;
				select( "Heinrich, you are in the castle now..." );
				mes "[ไฮน์ริช]";
				mes "โอ้" + strcharinfo(0) + "";
				mes "ฉันคิดว่าฉันไว้ใจคุณได้ รีบออกไปกันเถอะ";
				close2;
				cutin "",255;
				if ('step == 1) {
						'step = 2;
						donpcevent instance_npcname("#talkinstance2") + "::OnEnable";
				}
				end;
		}
		.@account_id = getcharid(3);
		.@player_name$ = strcharinfo(0);
		cutin "gl_heinrich2",2;
		select("Sir Heinrich. Varmundt...");
		mes "-" + .@player_name$ + "-";
		mes "เซอร์ไฮน์ริช คุณรู้ไหมว่าตอนนี้มีอะไรเกิดขึ้นในปราสาท?";
		unittalk .@account_id, .@player_name$ + " : Sir Heinrich. Do you know what is happening in the castle now?";
		next;
		mes "[ไฮน์ริช]";
		mes "คุณไม่ใช่นักผจญภัยที่มากับ Varmundt หรอกเหรอ?";
		npctalk "ไฮน์ริช: คุณไม่ใช่นักผจญภัยที่มากับวาร์มุนด์ใช่ไหม?";
		next;
		mes "[ไฮน์ริช]";
		mes "มีอะไรผิดปกติกับปราสาทเหรอ?";
		npctalk "ไฮน์ริช: มีอะไรผิดปกติกับปราสาทเหรอ?";
		next;
		select("Himelmez's invasion...");
		mes "-" + .@player_name$ + "-";
		mes "วาลคิรีแห่งคนตาย ฮิเมลเมซ กำลังจะมาเพื่อเอาชิ้นส่วนหัวใจของ Ymir ที่ซ่อนอยู่ในปราสาทแห่งนี้!";
		unittalk .@account_id, .@player_name$ + " : Dead man's Valkyrie, Himelmez is coming to take the Ymir's Heart piece hidden inside this castle!";
		next;
		mes "[ไฮน์ริช]";
		mes "ฮ่าๆ ตลกดี คิดว่าเป็นไปได้จริงเหรอ?";
		npctalk "ไฮน์ริช: ฮ่าๆ ตลกดีนะ คุณคิดว่ามันเป็นไปได้จริงๆ เหรอ?";
		cutin "gl_heinrich1",2;
		next;
		mes "[วาร์มุนด์]";
		mes "ฉันไม่ได้ล้อเล่นนะ เซอร์ไฮน์ริช ถ้าฉันจำไม่ผิด เธอคงจะมาที่นี่เร็วๆ นี้";
		npctalk "วาร์มุนด์: ฉันไม่ได้ล้อเล่นนะ เซอร์ไฮน์ริช ถ้าฉันจำไม่ผิด เธอคงจะมาถึงเร็วๆ นี้", instance_npcname("Varmundt#1");
		cutin "gl_barmund2",2;
		next;
		mes "[วาร์มุนด์]";
		mes "เราจะต้องซ่อนชิ้นส่วนหัวใจไว้ในที่ปลอดภัยก่อนที่การโจมตีของ Himelmez จะเริ่มต้น!";
		npctalk "Varmundt: เราต้องซ่อนชิ้นส่วนหัวใจไว้ในที่ปลอดภัยก่อนที่การโจมตีของ Himelmez จะเริ่มต้น!", instance_npcname("Varmundt#1");
		next;
		select("Even if you don't believe me...");
		mes "-" + .@player_name$ + "-";
		mes "ฉันไม่สามารถทำให้คุณเชื่อฉันได้ แต่ไม่มีเวลาที่จะโต้แย้ง!";
		unittalk .@account_id, .@player_name$ + " : I can't make you believe me, but there's no time to argue!";
		cutin "",255;
		next;
		mes "[ไฮน์ริช]";
		mes "ขอบคุณสำหรับความช่วยเหลือ แต่ตอนนี้เราไม่มีกษัตริย์อยู่ด้วย";
		npctalk "ไฮน์ริช: ขอบคุณสำหรับความช่วยเหลือ แต่ตอนนี้เราไม่มีกษัตริย์อยู่กับเราด้วยซ้ำ";
		cutin "gl_heinrich1",2;
		next;
		mes "[ไฮน์ริช]";
		mes "เราไม่สามารถเสี่ยงทำให้หัวใจสั่นไหวเพียงเพราะคนแปลกหน้าบอกอย่างนั้น";
		npctalk "ไฮน์ริช: เราไม่สามารถเสี่ยงขยับหัวใจเพียงเพราะคนแปลกหน้าบอกอย่างนั้น";
		close2;
		cutin "",255;
		if ('step == 1) {
				'step = 2;
				donpcevent instance_npcname("#talkinstance1") + "::OnEnable";
		}
		end;
}

1@gl_k,152,97,3	duplicate(dummy_npc)	Varmundt#1	4_M_BARMUND
1@gl_k,149,97,5	duplicate(Varmundt#1)	Heinrich#2	4_M_HEINRICH
1@gl_k,149,100,5	duplicate(Varmundt#1)	Heinrich#3	4_M_HEINRICH
1@gl_k,149,89,1	duplicate(Varmundt#1)	Himelmez#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
		end;
OnEnable:
		disablenpc instance_npcname("Heinrich#1");
		enablenpc instance_npcname("Heinrich#2");
		enablenpc instance_npcname("Himelmez#1");
		mapannounce 'map_name$[0], "????'s: Muahahahaha~!", bc_map,0xFFFF00,FW_NORMAL,18;
		initnpctimer;
		'npc_himelmez1$ = instance_npcname("Himelmez#1");
		'npc_varmundt1$ = instance_npcname("Varmundt#1");
		'npc_heinrich2$ = instance_npcname("Heinrich#2");
		'npc_heinrich3$ = instance_npcname("Heinrich#3");
		end;
OnTimer1500:
		npctalk "ไฮน์ริช : คุณเป็นใคร?", 'npc_heinrich2$;
		end;
OnTimer4500:
		npctalk "ฮิเมลเมซ: เอ่อ เอ่อ~ ฉันขัดจังหวะคุณรึเปล่า คุณไม่ได้คาดหวังให้ฉันมาเหรอ?", 'npc_himelmez1$;
		end;
OnTimer10000:
		npctalk "วาร์มุนด์: ฮิเมิลเมซ!! เร็วๆ เข้า!", 'npc_varmundt1$;
		end;
OnTimer17500:
		npctalk "ฮิเมลเมซ: ฉันชื่อลิซ่า คาน ฮิเมลเมซ อาจารย์แห่งดูลลาฮาน วาลคิรีแห่งคนตาย นั่นคือชื่อที่พวกเขาเรียกฉัน", 'npc_himelmez1$;
		end;
OnTimer22000:
		npctalk "ไฮน์ริช: คุณไม่ดูเป็นผู้หญิงเกินไปที่จะเป็นผู้ปกครองความตายหรือ? เราไม่กลัวคุณหรอก...", 'npc_heinrich2$;
		end;
OnTimer28500:
		npctalk "ฮิเมลเมซ: มาดูกันว่าคุณจะผ่อนคลายได้แบบนั้นไหมหลังจากที่คุณรู้ว่ากษัตริย์ของคุณอยู่ที่ไหน", 'npc_himelmez1$;
		end;
OnTimer36000:
		npctalk "ไฮน์ริช : อะไรนะ?", 'npc_heinrich2$;
		end;
OnTimer41000:
		npctalk "ฮิเมลเมซ: หืม ตอนนี้ฉันได้ความสนใจจากคุณแล้วไม่ใช่เหรอ?", 'npc_himelmez1$;
		end;
OnTimer47000:
		npctalk "ฮิเมลเมซ: เจ้าไม่ต้องการตำแหน่งกษัตริย์หรอก รอให้พระองค์กลับมาก่อนเถอะ ฉันชอบความบริสุทธิ์ของเจ้า", 'npc_himelmez1$;
		end;
OnTimer54000:
		npctalk "ฮิเมิลเมซ: น่าเสียดายที่ได้พบคุณในสถานการณ์แบบนี้", 'npc_himelmez1$;
		end;
OnTimer59000:
		npctalk "ไฮน์ริช: กษัตริย์ของฉันกำลังมาเยี่ยมราชวงศ์รูน-มิดการ์ต เขายังไม่กลับมา", 'npc_heinrich2$;
		end;
OnTimer66500:
		npctalk "ไฮน์ริช: แล้วตอนนี้คุณพยายามหลอกฉัน เกิดอะไรขึ้นกับเขา!", 'npc_heinrich2$;
		end;
OnTimer71500:
		npctalk "ฮิเมลเมซ: เอาล่ะ~ ฉันอยากจะนั่งลงแล้วอธิบายให้คุณฟังนะ แต่ว่าวันนี้ฉันยุ่งนิดหน่อย~", 'npc_himelmez1$;
		end;
OnTimer78000:
		npctalk "ฮิเมลเมซ: ฉันมีธุระต้องจัดการ ลูกน้องของฉันจะดูแลคุณอย่างดีสำหรับฉัน~", 'npc_himelmez1$;
		end;
OnTimer84500:
		npctalk "ไฮน์ริช: เธอน่าจะรู้แล้วว่าชิ้นส่วนหัวใจของ Ymir อยู่ที่ไหน", 'npc_heinrich2$;
		disablenpc 'npc_himelmez1$;
		end;
OnTimer90500:
		npctalk "ไฮน์ริช: คาลิตซ์เบิร์กและอัศวินสีขาวทั้งหมดควรตามฉันมาเดี๋ยวนี้...", 'npc_heinrich2$;
		end;
OnTimer92000:
		for (.@i = 1; .@i <= 20; .@i += 4) {
				hideonnpc instance_npcname("Khalitzburg Knightage#" + .@i);
				hideonnpc instance_npcname("Khalitzburg Knightage#" + (.@i+1));
				hideonnpc instance_npcname("White Knight#" + (.@i+2));
				hideonnpc instance_npcname("White Knight#" + (.@i+3));
		}
		hideonnpc instance_npcname("Khalitzburg Knightage#21");
		hideonnpc instance_npcname("Khalitzburg Knightage#22");

		.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
		monster 'map_name$[0],145,59,"Abysmal Knight",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
		monster 'map_name$[0],154,59,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,69,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,69,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,79,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,79,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,89,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,89,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,99,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,99,"Abysmal Knight",2470,1,.@label$;

		monster 'map_name$[0],145,54,"Khalitzburg",2471,1,.@label$;// G_MG_KHALITZBURG
		unittalk $@mobid[0],"Water... Someone give me water...";
		monster 'map_name$[0],154,54,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Kkkrrrruughgh...";
		monster 'map_name$[0],145,64,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Sir Heinrich. Save me...";
		monster 'map_name$[0],154,64,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I miss my sister...";
		monster 'map_name$[0],145,74,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Don't leave me alone, help me.";
		monster 'map_name$[0],154,74,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Aaarrrrrhhhh";
		monster 'map_name$[0],145,84,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I am so thirsty.";
		monster 'map_name$[0],154,84,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Oh...No...I can't die yet...";
		monster 'map_name$[0],145,94,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I feel sick to my stomach urrgg";
		monster 'map_name$[0],154,94,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Can someone...";
		monster 'map_name$[0],145,104,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Arrgg... My body";
		monster 'map_name$[0],154,104,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"My throat is burning!";
		end;
OnTimer94000:
		npctalk "ไฮน์ริช: ลูกน้องของฉัน... แบบนี้มันไม่น่าจะเกิดขึ้นได้!", 'npc_heinrich2$;
		end;
OnTimer96500:
		npctalk "วาร์มุนด์: เซอร์ไฮน์ริช พวกมันเป็นสัตว์ประหลาดทั้งนั้น คุณต้องให้พวกมันพักผ่อนบ้าง!", 'npc_varmundt1$;
		end;
OnTimer100000:
		npctalk "ไฮน์ริช: ฉันขอโทษมาก...", 'npc_heinrich2$;
		end;
OnTimer103000:
		npctalk "ไฮน์ริช: ขออภัย อัศวินของฉัน! ยกโทษให้ฉันด้วย!", 'npc_heinrich2$;
		end;
OnTimer106000:
		mapannounce 'map_name$[0], "Sir Heinrich: Death to all!", bc_map,0xFFFF33,FW_NORMAL,15;
		end;
OnTimer109000:
		mapannounce 'map_name$[0], "Sir Heinrich: Go back to the darkness!", bc_map,0xFFFF33,FW_NORMAL,15;
		end;
OnTimer109500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#1");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#2");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#1");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#2");
		end;
OnTimer110000:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#3");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#4");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#5");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#6");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#3");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#4");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#5");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#6");
		end;
OnTimer110500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#7");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#8");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#9");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#10");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#7");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#8");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#9");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#10");
		end;
OnTimer111000:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#11");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#12");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#13");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#14");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#11");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#12");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#13");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#14");
		end;
OnTimer111500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#15");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#16");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#17");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#18");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#15");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#16");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#17");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#18");
		end;
OnTimer112000:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#19");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#20");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#21");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#22");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#19");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#20");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#21");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#22");
		disablenpc 'npc_heinrich2$;
		enablenpc 'npc_heinrich3$;
		killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
		end;
OnTimer112500:
		npctalk "ไฮน์ริช: ฉันฆ่าลูกน้องของฉันเอง...", 'npc_heinrich3$;
		end;
OnTimer115500:
		npctalk "วาร์มุนด์: ท่านเซอร์ไฮน์ริช! เรามีเวลาไม่มาก!", 'npc_varmundt1$;
		end;
OnTimer118500:
		npctalk "วาร์มุนด์: ถ้าคุณรีบตอนนี้ มีโอกาสนะ!", 'npc_varmundt1$;
		end;
OnTimer121500:
		npctalk "ไฮน์ริช: วาร์มุนด์พูดถูก ตอนนี้ไม่ใช่เวลาที่จะไว้อาลัย", 'npc_heinrich3$;
		end;
OnTimer124500:
		npctalk "ไฮน์ริช: ฉันมีคำขอถึงผู้ติดตามทุกท่าน", 'npc_heinrich3$;
		end;
OnTimer127500:
		npctalk "ไฮน์ริช: ฮิเมิลเมซสามารถเปลี่ยนสิ่งมีชีวิตให้กลายเป็นสัตว์ประหลาดได้", 'npc_heinrich3$;
		end;
OnTimer130500:
		npctalk "ไฮน์ริช: แต่ก็ยังอาจมีผู้รอดชีวิตอยู่ที่นี่", 'npc_heinrich3$;
		end;
OnTimer134500:
		npctalk "ไฮน์ริช: ทำลายสัตว์ประหลาดและค้นหาผู้รอดชีวิต", 'npc_heinrich3$;
		end;
OnTimer138500:
		npctalk "ไฮน์ริช: วาร์มุนด์และฉันจะไล่ตามฮิเมิลเมซ", 'npc_heinrich3$;
		end;
OnTimer143500:
		npctalk "ไฮน์ริช: ดีมาก วาร์มุนด์ ไปหาฮิเมลเมซกันเถอะ", 'npc_heinrich3$;
		end;
OnTimer147500:
		disablenpc 'npc_heinrich3$;
		end;
OnTimer148500:
		stopnpctimer;
		donpcevent instance_npcname("#ghmemorialmob01") + "::OnStart";

		disablenpc 'npc_varmundt1$;
		disablenpc instance_npcname("#talkinstance1");
		'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
		end;
OnMyMobDead:
		end;
}

// Fast mode
1@gl_k,1,1,0	script	#talkinstance2	HIDDEN_WARP_NPC,{
		end;
OnEnable:
		disablenpc instance_npcname("Heinrich#1");
		enablenpc instance_npcname("Heinrich#2");
		enablenpc instance_npcname("Himelmez#1");
		mapannounce 'map_name$[0], "????'s: Muahahahaha~!", bc_map,0xFFFF00,FW_NORMAL,18;
		initnpctimer;
		'npc_himelmez1$ = instance_npcname("Himelmez#1");
		'npc_varmundt1$ = instance_npcname("Varmundt#1");
		'npc_heinrich2$ = instance_npcname("Heinrich#2");
		'npc_heinrich3$ = instance_npcname("Heinrich#3");
		end;
OnTimer1500:
		npctalk "ไฮน์ริช : คุณเป็นใคร?", 'npc_heinrich2$;
		end;
OnTimer3500:
		npctalk "ฮิเมลเมซ: เอ่อ เอ่อ~ ฉันขัดจังหวะคุณรึเปล่า คุณไม่ได้คาดหวังให้ฉันมาเหรอ?", 'npc_himelmez1$;
		end;
OnTimer7500:
		npctalk "ฮิเมลเมซ: ฉันมีธุระต้องจัดการ ลูกน้องของฉันจะดูแลคุณอย่างดีสำหรับฉัน~", 'npc_himelmez1$;
		end;
OnTimer11500:
		disablenpc 'npc_himelmez1$;
		end;
OnTimer12500:
		npctalk "ไฮน์ริช: เธอน่าจะรู้แล้วว่าชิ้นส่วนหัวใจของ Ymir อยู่ที่ไหน", 'npc_heinrich2$;
		end;
OnTimer16500:
		for (.@i = 1; .@i <= 20; .@i += 4) {
				hideonnpc instance_npcname("Khalitzburg Knightage#" + .@i);
				hideonnpc instance_npcname("Khalitzburg Knightage#" + (.@i+1));
				hideonnpc instance_npcname("White Knight#" + (.@i+2));
				hideonnpc instance_npcname("White Knight#" + (.@i+3));
		}
		hideonnpc instance_npcname("Khalitzburg Knightage#21");
		hideonnpc instance_npcname("Khalitzburg Knightage#22");

		.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
		monster 'map_name$[0],145,59,"Abysmal Knight",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
		monster 'map_name$[0],154,59,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,69,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,69,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,79,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,79,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,89,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,89,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],145,99,"Abysmal Knight",2470,1,.@label$;
		monster 'map_name$[0],154,99,"Abysmal Knight",2470,1,.@label$;

		monster 'map_name$[0],145,54,"Khalitzburg",2471,1,.@label$;// G_MG_KHALITZBURG
		unittalk $@mobid[0],"Water... Someone give me water...";
		monster 'map_name$[0],154,54,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Kkkrrrruughgh...";
		monster 'map_name$[0],145,64,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Sir Heinrich. Save me...";
		monster 'map_name$[0],154,64,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I miss my sister...";
		monster 'map_name$[0],145,74,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Don't leave me alone, help me.";
		monster 'map_name$[0],154,74,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Aaarrrrrhhhh";
		monster 'map_name$[0],145,84,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I am so thirsty.";
		monster 'map_name$[0],154,84,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Oh...No...I can't die yet...";
		monster 'map_name$[0],145,94,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"I feel sick to my stomach urrgg";
		monster 'map_name$[0],154,94,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Can someone...";
		monster 'map_name$[0],145,104,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"Arrgg... My body";
		monster 'map_name$[0],154,104,"Khalitzburg",2471,1,.@label$;
		unittalk $@mobid[0],"My throat is burning!";
		end;
OnTimer17500:
		npctalk "คนตายควรไปที่ที่คนตายควรอยู่! ทุกคนควรกลับไป!", 'npc_heinrich2$;
		end;
OnTimer18500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#1");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#2");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#1");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#2");
		end;
OnTimer19000:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#3");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#4");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#5");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#6");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#3");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#4");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#5");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#6");
		end;
OnTimer19500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#7");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#8");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#9");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#10");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#7");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#8");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#9");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#10");
		end;
OnTimer20000:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#11");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#12");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#13");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#14");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#11");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#12");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#13");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#14");
		end;
OnTimer20500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#15");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#16");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#17");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#18");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#15");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#16");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#17");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#18");
		end;
OnTimer21500:
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#19");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#20");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#21");
		specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#22");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#19");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#20");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#21");
		specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#22");
		disablenpc 'npc_heinrich2$;
		enablenpc 'npc_heinrich3$;
		killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
		npctalk "ไฮน์ริช: ดีมาก วาร์มุนด์ ไปหาฮิเมลเมซกันเถอะ", 'npc_heinrich3$;
		end;
OnTimer25500:
		npctalk "วาร์มุนด์: เดี๋ยวก่อน ฉันไม่มีเวลาเดินไปรอบๆ ปราสาทและเปิดทาง ฉันจะเปิดประตูมิติที่สามารถติดตามได้โดยเร็วที่สุด", 'npc_varmundt1$;
		end;
OnTimer27000:
		specialeffect EF_BEGINSPELL_N5, AREA, 'npc_varmundt1$;
		end;
OnTimer28500:
		stopnpctimer;
		disablenpc 'npc_heinrich3$;
		disablenpc 'npc_varmundt1$;
		enablenpc instance_npcname("#cut01");
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("Himelmez#2");
		enablenpc instance_npcname("Varmundt#2");
		enablenpc instance_npcname("Heinrich#4");
		'step = 4;

		'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
		end;
OnMyMobDead:
		end;
}

1@gl_k,149,96,1	script	#cut01	GATE_SKYBLUE,{
		warp 'map_name$[0],151,232;
		end;
}


// Sector 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob01");
		enablenpc instance_npcname("#2Control");
		enablenpc instance_npcname("#2Control2");
		mapannounce 'map_name$[0], "A portal has opened to the west.", bc_map,0xFFFF00;

		// note: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
		.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
		areamonster 'map_name$[0],76,99,87,10,"Grand Chamberlain in pain",2466,14,.@label$;
		areamonster 'map_name$[0],67,39,12, 6,"Corrupted Monk",2465,17,.@label$;
		areamonster 'map_name$[0],67,39,12, 6,"Grand Chamberlain in pain",2466,3,.@label$;
		areamonster 'map_name$[0],32,75,51,58,"Corrupted Steward",2464,5,.@label$;
		areamonster 'map_name$[0],45,84,6,137,"Corrupted Steward",2464,12,.@label$;
		end;
OnMyMobDead:
		.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
		.@mob_dead_num = 51 - mobcount('map_name$[0],.@label$);
		if (.@mob_dead_num > 35) {
				mapannounce 'map_name$[0], "Himelmez's curse is getting weaker. Find any survivors!", bc_map,0xFFFFFF;
				killmonster 'map_name$[0], .@label$;
				enablenpc instance_npcname("Altar boy Domun#1");
				disablenpc instance_npcname("#ghmemorialmob01");
		}
		end;
}

1@gl_k,17,51,3	script	Altar boy Domun#1	4_M_KID1,{
		if (is_party_leader() == false || 'step != 2) {
				mes "[เด็กวัดดอมุน]";
				mes "ช่วยฉันด้วย ช่วยฉันด้วย โปรด...";
				close;
		}
		.@account_id = getcharid(3);
		.@player_name$ = strcharinfo(0);
		mes "[เด็กวัดดอมุน]";
		mes "ช่วยฉันด้วย! ช่วยฉันด้วย!!!";
		npctalk "ช่วยฉันด้วย! ช่วยฉันด้วย!!!";
		next;
		select("Hold on! Are you the only survivor?");
		mes "-" + .@player_name$ + "-";
		mes "รอก่อนนะ! คุณเป็นผู้รอดชีวิตเพียงคนเดียวใช่ไหม?";
		unittalk .@account_id, .@player_name$ + " : Hold on! Are you the only survivor?";
		next;
		mes "[เด็กวัดดอมุน]";
		mes "พวกห้องปกครอง... พวกนักบวช... พวกเขาทั้งหมดกลายเป็นสัตว์ประหลาด ฉันทำอะไรไม่ได้เลย";
		npctalk "เด็กวัดโดมุน: เหล่าข้าหลวง... พระสงฆ์... พวกเขาทั้งหมดกลายเป็นสัตว์ประหลาด ฉันทำอะไรไม่ได้เลย";
		next;
		mes "[เด็กวัดดอมุน]";
		mes "สิ่งเดียวที่ฉันทำได้คือ... ซ่อนตัวอยู่ที่นี่... ไม่มีอะไร ฉันทำอะไรไม่ได้เลย...";
		npctalk "เจ้าหนูช่วยพิธี Domun: สิ่งเดียวที่ฉันทำได้คือ... ซ่อนตัวอยู่ที่นี่... ไม่มีอะไร ฉันทำอะไรไม่ได้เลย...";
		next;
		select("Pull it together!");
		mes "-" + .@player_name$ + "-";
		mes "ตื่นได้แล้วหนูน้อย ไปทางทิศตะวันออกแล้วหาทางตรงกลางไปด้านนอก ที่นั่นปลอดภัยแน่นอน!";
		unittalk .@account_id, .@player_name$ + " : Wake up kid! Go east and find the middle passage to the outside! It is safe there!";
		next;
		mes "[เด็กวัดดอมุน]";
		mes "ไปทางตะวันออกเหรอ? คนเดียวเหรอ? ยังไง?";
		npctalk "เด็กวัดโดมุน: ไปทางตะวันออกเหรอ? คนเดียวเหรอ? ยังไง?";
		next;
		select("I will give you a weapon.");
		mes "-" + .@player_name$ + "-";
		mes "นี่คืออาวุธ เพียงแค่หลับตาแล้วฟันเข้าหาสัตว์ประหลาดพวกนั้น";
		unittalk .@account_id, .@player_name$ + " : Here's a weapon. Just close your eye and swing for those monsters.";
		next;
		mes "[เด็กวัดดอมุน]";
		mes "โอเค ฉัน... ฉันจะพยายาม";
		npctalk "เจ้าหนูช่วยพิธี Domun: โอเค ฉัน... ฉันจะลองดู";
		if ('step == 2) {
				'step = 3;
				disablenpc instance_npcname("Altar boy Domun#1");
				donpcevent instance_npcname("#ghmemorialmob02") + "::OnStart";
		}
		close;
}

// Sector 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob02");
		enablenpc instance_npcname("#3Control");
		enablenpc instance_npcname("#3Control2");
		for ( .@i = 1; .@i <= 26; .@i++ )
				enablenpc instance_npcname("A dead man#" + .@i);
		mapannounce 'map_name$[0], "A portal has opened to the east.", bc_map,0xFFFF00;

		// note: x34 MG_ZOMBIE / x35 MG_GHOUL
		.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
		areamonster 'map_name$[0],236,12,288,52,"Outraged Refiner",2466,18,.@label$;// MG_GHOUL
		areamonster 'map_name$[0],236,12,288,52,"Decomposed Blacksmith",2464,17,.@label$;// MG_ZOMBIE
		areamonster 'map_name$[0],242,71,286,145,"Outraged Refiner",2466,17,.@label$;
		areamonster 'map_name$[0],242,71,286,145,"Decomposed Blacksmith",2464,17,.@label$;
		end;
OnMyMobDead:
		.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
		.@mob_dead_num = 69 - mobcount('map_name$[0],.@label$);
		if (.@mob_dead_num > 56) {
				mapannounce 'map_name$[0], "Himelmez's curse is getting weaker. Find any survivors!", bc_map,0xFFFFFF;
				killmonster 'map_name$[0],.@label$;
				enablenpc instance_npcname("Holgren the Destroyer#1");
				disablenpc instance_npcname("#ghmemorialmob02");
		}
		end;
}

1@gl_k,291,145,3	script	Holgren the Destroyer#1	4_F_JOB_BLACKSMITH,{
		if (is_party_leader() == false || 'step != 3) {
				mes "[โฮลเกรนผู้ทำลายล้าง]";
				mes "คุณเป็นมนุษย์เหรอ?";
				close;
		}
		.@account_id = getcharid(3);
		.@player_name$ = strcharinfo(0);
		mes "[โฮลเกรนผู้ทำลายล้าง]";
		mes "ตายซะ!! เจ้าจะต้องตาย!!!";
		npctalk "โฮลเกรนผู้ทำลายล้าง: ตายซะ!! เจ้าจะต้องตาย!!!";
		specialeffect EF_CRASHEARTH;
		next;
		select("Relax! I'm not a monster!");
		mes "-" + .@player_name$ + "-";
		mes "ผ่อนคลายสิ ฉันเป็นมนุษย์ คุณอยู่คนเดียวไหม มีใครอยู่ที่นี่บ้างไหม";
		unittalk .@account_id, .@player_name$ + " : Relax! I am Human. Are you alone? Is anyone else here?";
		next;
		mes "[โฮลเกรนผู้ทำลายล้าง]";
		mes "ก็มีแค่ฉันคนเดียว";
		npctalk "โฮลเกรนผู้ทำลายล้าง: มีเพียงฉันเท่านั้น";
		next;
		select("It's dangerous here...");
		mes "-" + .@player_name$ + "-";
		mes "ที่นี่มันอันตรายนะ รู้มั้ยทางไปทางเดินกลางเป็นยังไงบ้าง ขยับได้ไหม";
		unittalk .@account_id, .@player_name$ + " : It's dangerous here. You know the way to the middle passage? Can you move?";
		next;
		mes "[โฮลเกรนผู้ทำลายล้าง]";
		mes "ใช่ ฉันสามารถย้ายออกไปได้ ฉันต้องออกไปจากที่นี่";
		npctalk "โฮลเกรนผู้ทำลายล้าง: ใช่ ฉันเคลื่อนไหวได้แล้ว ฉันต้องออกไปจากที่นี่";
		next;
		select("Be safe...");
		mes "-" + .@player_name$ + "-";
		mes "ขออภัย ฉันไปกับคุณไม่ได้";
		unittalk .@account_id, .@player_name$ + " : Sorry, I can't go with you.";
		next;
		mes "[โฮลเกรนผู้ทำลายล้าง]";
		mes "ไม่เป็นไร คุณทำมาพอแล้ว ฉันช่วยตัวเองได้ ขอให้คุณโชคดีเช่นกัน";
		npctalk "โฮลเกรนผู้ทำลายล้าง: ไม่เป็นไร คุณทำมาพอแล้ว ฉันช่วยตัวเองได้ ขอให้คุณโชคดีเช่นกัน";
		if ('step == 3) {
				'step = 4;
				disablenpc instance_npcname("Holgren the Destroyer#1");
				donpcevent instance_npcname("#ghmemorialmob03") + "::OnEnable";
		}
		close;
}

// Tramp Mobs
//============================================================
1@gl_k,221,82,3	script	A dead man#1	4_M_DIEMAN,5,5,{
		end;
OnTouch_:
		.@i = rand(1,10);
		if (.@i == 1) .@mobs = 3;
		else if (.@i == 2) .@mobs = 4;
		else if (.@i == 3) .@mobs = 5;
		else if (.@i < 7) .@mobs = 6;
		else .@mobs = 7;
		getmapxy .@map$,.@x,.@y, BL_NPC;
		specialeffect EF_VENOMDUST;
		monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
		disablenpc();
		end;
}
1@gl_k,233,123,4	duplicate(A dead man#1)	A dead man#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(A dead man#1)	A dead man#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(A dead man#1)	A dead man#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(A dead man#1)	A dead man#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(A dead man#1)	A dead man#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(A dead man#1)	A dead man#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(A dead man#1)	A dead man#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(A dead man#1)	A dead man#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(A dead man#1)	A dead man#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(A dead man#1)	A dead man#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(A dead man#1)	A dead man#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(A dead man#1)	A dead man#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(A dead man#1)	A dead man#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(A dead man#1)	A dead man#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(A dead man#1)	A dead man#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(A dead man#1)	A dead man#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(A dead man#1)	A dead man#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(A dead man#1)	A dead man#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(A dead man#1)	A dead man#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(A dead man#1)	A dead man#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(A dead man#1)	A dead man#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(A dead man#1)	A dead man#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(A dead man#1)	A dead man#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(A dead man#1)	A dead man#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(A dead man#1)	A dead man#26	4_M_DIEMAN,5,5

// Sector 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
		end;
OnEnable:
		enablenpc instance_npcname("#ghmemorialmob03");
		enablenpc instance_npcname("#4Control");
		enablenpc instance_npcname("#4Control2");
		mapannounce 'map_name$[0], "A portal has appeared to the northwest.", bc_map,0xFFFF00;

		// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
		.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
		areamonster 'map_name$[0],16,186,43,280,"Outraged Archer",2469,25,.@label$;
		areamonster 'map_name$[0],44,191,89,225,"Hungry Palace Guard",2468,25,.@label$;
		areamonster 'map_name$[0],115,215,188,273,"Corrupted Palace Guard",2468,20,.@label$;
		areamonster 'map_name$[0],108,232,193,281,"Corrupted Palace Guard",2468,5,.@label$;
		areamonster 'map_name$[0],158,236,230,250,"Wandering Archer",2469,12,.@label$;
		areamonster 'map_name$[0],65,231,155,256,"Wandering Archer",2469,13,.@label$;
		end;
OnMyMobDead:
		.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
		.@mob_dead_num = 100 - mobcount('map_name$[0],.@label$);
		if (.@mob_dead_num > 85) {
				killmonster 'map_name$[0],.@label$;
				enablenpc instance_npcname("#Mimelon");
				enablenpc instance_npcname("Himelmez#2");
				enablenpc instance_npcname("Varmundt#2");
				enablenpc instance_npcname("Heinrich#4");
				disablenpc instance_npcname("#ghmemorialmob03");
				mapannounce 'map_name$[0], "Himelmez: Not bad. Thought you would be dead by now by my creatures~", bc_map,0xFFFFFF;
				initnpctimer;
		}
		end;
OnTimer5000:
		mapannounce 'map_name$[0], "Himelmez: I'll wait for you at the north of the castle. I want to see how lucky you are.", bc_map,0xFFFFFF;
		stopnpctimer;
		end;
}

// 1st MVP
//============================================================
1@gl_k,144,258,6	script	Heinrich#4	4_M_HEINRICH,{
		mes "[ไฮน์ริช]";
		mes "ฮิเมิลเมซ... ข้าพเจ้าจะไม่มีวันลืมสิ่งที่ท่านทำกับพวกของข้าพเจ้า";
		cutin "gl_heinrich1",2;
		close3;
}

1@gl_k,156,258,3	script	Varmundt#2	4_M_BARMUND,{
		if ('step < 6)
				end;
		if (checkweight(6607,1, 6608,1) == 0) {
				mes "^ff0000ดูเหมือนว่าคุณมีสินค้าประเภทหรือน้ำหนักมากเกินไป โปรดจัดระเบียบสินค้าคงคลังของคุณ^000000";
				close;
		}
		cutin "gl_barmund1",2;
		mes "[วาร์มุนด์]";
		if (checkquest(12318,HUNTING) != 2) {
				mes "ฉันคิดว่านี่แหละคือมัน มันเป็นไปไม่ได้เลยที่จะหยุดเจตจำนงของนักเดินทางข้ามเวลา?!";
				mes "ดูเหมือนจะมีประตูมิติปรากฏทางทิศเหนือ";
				close3;
		}
		erasequest 12318;
		if (isbegin_quest(12319) == 0)
				setquest 12319;// Amdarais Hunt
		if (isbegin_quest(12320) == 0) {
				setquest 12320;// Time Traveler
				completequest 12320;
				getexp 250000,250000;
		}
		mes "ไอเทมชิ้นนี้มีความพิเศษในตัว สามารถนำไปใช้ได้กับคนพิเศษ";
		getitem 6607,1;// Temporal_Crystal
		getitem 6608,1;// Coagulated_Spell
		getitem 30001,100;
		close3;
}

1@gl_k,150,257,3	script	Himelmez#2	4_F_HIMEL,{
		cutin "gl_himel2",2;
		mes "[ฮิเมลเมซ]";
		mes "อย่ามองฉันด้วยความกังวลแบบนั้น อีกไม่นานก็จะจบแล้ว...";
		close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch_:
		if ('step != 4)
				end;
		'step = 5;
		disablenpc();
		if ('mode == 1)
				donpcevent instance_npcname("#talkinstance3") + "::OnStart";
		else if ('mode == 2)
				donpcevent instance_npcname("#talkinstance4") + "::OnStart";
		end;
}

1@gl_k,1,1,0	script	#talkinstance3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		'npc_himelmez2$ = instance_npcname("Himelmez#2");
		'npc_heinrich4$ = instance_npcname("Heinrich#4");
		end;
OnTimer3000:
		npctalk "ฮิเมลเมซ: เดาว่าคุณคงโชคดีหรือมีพลังมากที่สามารถมาถึงจุดนี้ได้", 'npc_himelmez2$;
		end;
OnTimer6000:
		npctalk "ฮิเมลเมซ: แต่มันไม่สำคัญ", 'npc_himelmez2$;
		end;
OnTimer9000:
		npctalk "ฮิเมลเมซ: พวกคุณทุกคนจะต้องตายที่นี่", 'npc_himelmez2$;
		end;
OnTimer12000:
		npctalk "ไฮน์ริช: ฮิเมลเมซ! เราจะไม่ปล่อยให้คุณหนีจากเราไปได้!", 'npc_heinrich4$;
		end;
OnTimer15000:
		npctalk "ฮิเมลเมซ: ฮ่าๆๆ ห่วงตัวเองบ้างเถอะ คุณคิดว่าฉันอยู่คนเดียวที่นี่เหรอ", 'npc_himelmez2$;
		end;
OnTimer18000:
		npctalk "วาร์มุนด์: เซอร์ไฮน์ริช! ฉันรู้สึกว่ามีบางอย่างแปลก ๆ และรุนแรงกำลังเข้ามา!", instance_npcname("Varmundt#2");
		end;
OnTimer21000:
		npctalk "ไฮน์ริช : อะไรนะ... นี่มัน!", 'npc_heinrich4$;
		end;
OnTimer24000:
		npctalk "ฮิเมลเมซ: ฉันจะไปต่อจนกว่าของเล่นใหม่ของฉันจะทำให้คุณเพลิดเพลิน", 'npc_himelmez2$;
		end;
OnTimer27000:
		npctalk "ฮิเมลเมซ: ลาก่อนนะ~ หวังว่าจะได้พบกันอีกครั้งนะ ไฮน์ริช", 'npc_himelmez2$;
		end;
OnTimer28000:
		disablenpc 'npc_himelmez2$;
		end;
OnTimer31000:
		npctalk "ไฮน์ริช: เซอร์วาร์มุนด์ท! ช่วยนักผจญภัยพวกนั้นด้วย ฉันจะตามล่าฮิเมลเมซ!", 'npc_heinrich4$;
		end;
OnTimer32000:
		disablenpc 'npc_heinrich4$;
		end;
OnTimer35000:
		mapannounce 'map_name$[0], "เสียงสะท้อนดังมาจากส่วนลึกภายในศาลเจ้าอัศวิน", bc_map,0xFFFFFF;
		end;
OnTimer38000:
		donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
		stopnpctimer;
		'npc_himelmez2$ = 'npc_heinrich4$ = "";
		end;
}

1@gl_k,1,1,0	script	#talkinstance4	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		'npc_himelmez2$ = instance_npcname("Himelmez#2");
		'npc_heinrich4$ = instance_npcname("Heinrich#4");
		npctalk "ฮิเมลเมซ: เดาว่าคุณคงโชคดีหรือมีพลังมากที่สามารถมาถึงจุดนี้ได้", 'npc_himelmez2$;
		end;
OnTimer3500:
		disablenpc 'npc_himelmez2$;
		end;
OnTimer4500:
		npctalk "ไฮน์ริช: เซอร์วาร์มุนด์ท! ช่วยนักผจญภัยพวกนั้นด้วย ฉันจะตามล่าฮิเมลเมซ!", 'npc_heinrich4$;
		end;
OnTimer6500:
		disablenpc 'npc_heinrich4$;
		end;
OnTimer9500:
		mapannounce 'map_name$[0], "เสียงสะท้อนดังมาจากส่วนลึกภายในศาลเจ้าอัศวิน", bc_map,0xFFFFFF;
		end;
OnTimer14500:
		stopnpctimer;
		donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
		'npc_himelmez2$ = 'npc_heinrich4$ = "";
		end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob04");
		monster 'map_name$[0],150,259,"Corrupted Soul",2475,1, instance_npcname("#ghmemorialmob04") + "::OnMyMobDead";// MG_CORRUPTION_ROOT
		unittalk $@mobid[0],"Grrrrrrhh~~~";
		end;
OnMyMobDead:
		if (mobcount('map_name$[0], instance_npcname("#ghmemorialmob04") + "::OnMyMobDead") < 1) {
				mapannounce 'map_name$[0], "Opening 2nd floor entrance towards 12 O'clock direction.", bc_map,0xFFFF00;
				npctalk "วาร์มุนด์: ข้าเก็บไอเทมบางส่วนที่หล่นจากร่างของมัน เจ้าเอาไปจากข้าได้", instance_npcname("Varmundt#2");

				enablenpc instance_npcname("#2F Entrance");
				enablenpc instance_npcname("#1 Control");
				enablenpc instance_npcname("#Servanton");
				enablenpc instance_npcname("Heinrich#21");
				enablenpc instance_npcname("Varmundt#21");
				donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
				disablenpc instance_npcname("#ghmemorialmob04");
				'step = 6;
		}
		end;
}

1@gl_k,150,284,0	script	#2F Entrance	WARPNPC,2,2,{
		end;
OnTouch:
		if (checkquest(12318,HUNTING) == 2) {
				mes "^0000ffดูเหมือนว่าคุณจะไม่ได้รับรางวัลจากการเคลียร์ชั้นแรก คุยกับวาร์มุนด์แล้วรับรางวัลของคุณ^000000";
				close2;
				warp 'map_name$[0],156,256;
				end;
		}
		warp 'map_name$[1],150,46;
		end;
}

// Floor 2
//============================================================
2@gl_k,148,67,1	script	Heinrich#21	4_M_HEINRICH,{
		mes "[ไฮน์ริช]";
		mes "พื้นที่ปิดของฮิเมิลเมซครอบคลุมทุกที่...";
		cutin "gl_heinrich1",2;
		close3;
}

2@gl_k,151,71,7	script	Varmundt#21	4_M_BARMUND,{
		mes "[วาร์มุนด์]";
		mes "งานนี้ฉันทำมากี่ครั้งแล้ว ในฝันฉันทำซ้ำแล้วซ้ำเล่า...";
		cutin "gl_barmund2",2;
		close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
		end;
OnEffect:
		specialeffect EF_LORD;
		end;
}

2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch_:
		if ('step != 6)
				end;
		'step = 7;
		disablenpc();
		if ('mode == 1)
				donpcevent instance_npcname("#talkinstance5") + "::OnStart";
		else if ('mode == 2)
				donpcevent instance_npcname("#talkinstance6") + "::OnStart";
		end;
}

2@gl_k,1,1,0	script	#talkinstance5	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		'npc_heinrich$ = instance_npcname("Heinrich#21");
		'npc_varmundt$ = instance_npcname("Varmundt#21");
		end;
OnTimer3000:
		npctalk "ไฮน์ริช: โครงสร้างแบบนี้ไม่เคยมีอยู่ในปราสาทมาก่อน!", 'npc_heinrich$;
		end;
OnTimer6000:
		npctalk "วาร์มุนด์: นี่คือพื้นที่ปิดของฮิเมลเมซ เราปิดกั้นพื้นที่และทางเดินทุกแห่ง", 'npc_varmundt$;
		end;
OnTimer9000:
		npctalk "ไฮน์ริช: เราพยายามที่จะทำลายมันลงแต่ว่ามันแข็งแกร่งมาก", 'npc_heinrich$;
		end;
OnTimer12000:
		npctalk "วาร์มุนด์: ถอยกลับไปหน่อย ให้ฉันลองสลายเวทมนตร์ดู", 'npc_varmundt$;
		end;
OnTimer15000:
		specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
		disablenpc instance_npcname("#Servanton_effect");
		end;
OnTimer18000:
		npctalk "วาร์มุนด์: ฉันคิดว่าส่วนหนึ่งของพื้นที่ปิดนั้นถูกทำลายลงแล้ว", 'npc_varmundt$;
		end;
OnTimer21000:
		npctalk "วาร์มุนด์: ฉันไม่เคยเห็นพื้นที่ปิดแบบนี้มาก่อน", 'npc_varmundt$;
		end;
OnTimer24000:
		npctalk "Varmundt: ฮิเมลเมซซ่อนโซ่หินไว้ในร่างของผู้คนที่ไม่ตาย", 'npc_varmundt$;
		end;
OnTimer27000:
		npctalk "วาร์มุนด์: บางทีพื้นที่ปิดนี้อาจได้รับการบำรุงรักษาด้วยโซ่หินเหล่านี้", 'npc_varmundt$;
		end;
OnTimer30000:
		npctalk "วาร์มุนด์: ถ้าหากคุณต้องการทำลายมัน คุณต้องฆ่าหนึ่งในผู้ที่มีโซ่หิน", 'npc_varmundt$;
		end;
OnTimer33000:
		npctalk "วาร์มุนด์: แต่เราไม่สามารถรู้ได้ว่าใครมีโซ่หิน ดังนั้นเราจึงต้องชำระล้างทุกสิ่ง", 'npc_varmundt$;
		end;
OnTimer36000:
		npctalk "ไฮน์ริช: มันเป็นพฤติกรรมที่ไม่สามารถให้อภัยได้", 'npc_heinrich$;
		end;
OnTimer39000:
		npctalk "ไฮน์ริช: ฉันกำจัดทหารของฉันทั้งหมดไปแล้ว และบางทีอาจจะมากกว่านั้นด้วย...", 'npc_heinrich$;
		end;
OnTimer42000:
		npctalk "วาร์มุนด์: ผู้บัญชาการ...", 'npc_varmundt$;
		end;
OnTimer45000:
		npctalk "วาร์มุนด์: ผู้บัญชาการ! คุณต้องเข้มแข็งนะ", 'npc_varmundt$;
		end;
OnTimer48000:
		npctalk "วาร์มุนด์: ศัตรูของเราไม่ใช่มนุษย์", 'npc_varmundt$;
		end;
OnTimer51000:
		npctalk "วาร์มุนด์: ถึงแม้คุณจะปฏิเสธ พวกมันก็จะไม่มีวันหันกลับมาเป็นมนุษย์อีก", 'npc_varmundt$;
		end;
OnTimer54000:
		npctalk "ไฮน์ริช: ...", 'npc_heinrich$;
		end;
OnTimer57000:
		npctalk "วาร์มุนด์: เอาล่ะ ไปกันเถอะ", 'npc_varmundt$;
		end;
OnTimer60000:
		npctalk "Varmundt: ส่วนพวกคุณ ลองพักสักนิดแล้วติดตามพวกเราสิ", 'npc_varmundt$;
		end;
OnTimer63000:
		npctalk "วาร์มุนด์: การต่อสู้อาจจะยาก ดังนั้นจงเข้มแข็งไว้", 'npc_varmundt$;
		end;
OnTimer66000:
		npctalk "วาร์มุนด์: โอเค ถึงเวลาเริ่มแล้ว ไฮน์ริช", 'npc_varmundt$;
		end;
OnTimer69000:
		mapannounce 'map_name$[1], "A portal to the west has opened in the central hallway.", bc_map,0xFFFF00;
		disablenpc 'npc_varmundt$;
		disablenpc 'npc_heinrich$;
		stopnpctimer;
		'npc_varmundt$ = 'npc_heinrich$ = "";
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		end;
}

2@gl_k,1,1,0	script	#talkinstance6	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		'npc_heinrich$ = instance_npcname("Heinrich#21");
		'npc_varmundt$ = instance_npcname("Varmundt#21");
		npctalk "ไฮน์ริช: โครงสร้างแบบนี้ไม่เคยมีอยู่ในปราสาทมาก่อน!", 'npc_heinrich$;
		end;
OnTimer2000:
		npctalk "วาร์มุนด์: โอเค ถึงเวลาเริ่มแล้ว ไฮน์ริช", 'npc_varmundt$;
		end;
OnTimer3500:
		stopnpctimer;
		mapannounce 'map_name$[1], "A portal to the west has opened in the central hallway.", bc_map,0xFFFF00;
		disablenpc 'npc_varmundt$;
		disablenpc 'npc_heinrich$;
		'npc_varmundt$ = 'npc_heinrich$ = "";
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		end;
}

// Spots
// Note: timer is the only condition for them to respawn
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
		end;
OnTouch_:
		disablenpc();
		.@hidden_name$ = strnpcinfo(2);
		.@event_type = atoi( charat(.@hidden_name$,4) );
		.@event_number = atoi( charat(.@hidden_name$,6) );
		if (.@event_type == 2)
				.@event$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead" + .@event_number;	// left side
		else
				.@event$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead" + .@event_number;	// right side
		killmonster 'map_name$[1], .@event$;
		getmapxy .@map$,.@x,.@y, BL_NPC;
		monster .@map$,.@x,.@y, "Corrupted Palace Guard", 2468,1, .@event$;
		monster .@map$,.@x,.@y, "Archer of Death", 2469,1, .@event$;
		monster .@map$,.@x,.@y, "Corrupted Abysmal Knight", 2470,1, .@event$;
		monster .@map$,.@x,.@y, "Suffered Khalitzburg", 2471,1, .@event$;
		monster .@map$,.@x,.@y, "Bloody Knight", 2472,1, .@event$;
		initnpctimer;
		end;
OnTimer30000:
		enablenpc();
		stopnpctimer;
		end;
OnStop:
		disablenpc();
		stopnpctimer;
		end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Commander 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob05");
		for ( .@i = 1; .@i <= 7; .@i++ )
				enablenpc instance_npcname("#ogh_2-" + .@i);
		end;

OnMyMobDead1:
OnMyMobDead2:
OnMyMobDead3:
OnMyMobDead4:
OnMyMobDead5:
OnMyMobDead6:
OnMyMobDead7:
		if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead") > 0)	// Prevent boss re-spawn
				end;
		if ('mode == 2)	// Higher (but unknown) spawn rate in fast mode
				.@r = 10;
		else
				.@r = 50;
		if (rand(.@r) == 0) {
				mapannounce 'map_name$[1], "Evil Forces are appearing in this area.", bc_map,0xFFFF44,FW_NORMAL,15;
				killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

				.@r = rand(4) * 2;
				setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
				monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "1st Commander of Destruction",2473,1, instance_npcname("#ghmemorialmob05") + "::OnBossDead";
		}
		end;

OnBossDead:
		disablenpc();
		mapannounce 'map_name$[1], "A portal has opened to the east in the central hallway.", bc_map,0xFFFF00;
		enablenpc instance_npcname("#23 Control");
		enablenpc instance_npcname("#23 Control2");
		donpcevent instance_npcname("#ghmemorialmob06") + "::OnStart";

		// Event stopped and monsters in this sector killed
		for ( .@i = 1; .@i <= 7; .@i++ )
				donpcevent instance_npcname("#ogh_2-" + .@i) + "::OnStop";

		.@npc_name$ = instance_npcname("#ghmemorialmob05");
		for ( .@i = 1; .@i <= 7; .@i++ )
				killmonster 'map_name$[1], .@npc_name$ + "::OnMyMobDead" + .@i;
		end;
}

// Commander 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob06");
		for ( .@i = 1; .@i <= 7; .@i++ )
				enablenpc instance_npcname("#ogh_3-" + .@i);

		// Fix spawn
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
		monster 'map_name$[1],252, 75, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],253, 76, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],247, 77, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],248, 80, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],236,100, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],240,100, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],242,100, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],244,100, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],199,111, "Corrupted Palace Guard",2468,1, .@label$;
		monster 'map_name$[1],181,107, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],177,110, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],236, 27, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],242, 27, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],252, 26, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],172,130, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],171,127, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],173,127, "Wandering Archer",2469,1, .@label$;
		monster 'map_name$[1],177, 69, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],186, 66, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],189, 67, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],190, 68, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],257,157, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],246,159, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],237,158, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],206,159, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],172,120, "Corrupted Abysmal Knight",2470,1, .@label$;
		monster 'map_name$[1],226, 30, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],226, 38, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],228, 29, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],226, 63, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],268,137, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],263,138, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],187,121, "Suffered Khalitzburg",2471,1, .@label$;
		monster 'map_name$[1],265, 52, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],265, 55, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],264, 79, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],225, 92, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],237, 92, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],249, 92, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],256, 93, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],264,100, "Bloody Knight",2472,1, .@label$;
		monster 'map_name$[1],181,120, "Bloody Knight",2472,1, .@label$;
		end;

OnMyMobDead:
OnMyMobDead1:
OnMyMobDead2:
OnMyMobDead3:
OnMyMobDead4:
OnMyMobDead5:
OnMyMobDead6:
OnMyMobDead7:
		if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead") > 0)	// Prevent boss re-spawn
				end;
		if ('mode == 2)	// Higher (but unknown) spawn rate in fast mode
				.@r = 10;
		else
				.@r = 50;
		if (rand(.@r) == 0) {
				mapannounce 'map_name$[1], "An evil presence has teleported into this area.", bc_map,0xFFFF44,FW_NORMAL,15;
				killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead";

				.@r = rand(4) * 2;
				setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
				monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "2nd Commander of Destruction",2474,1, instance_npcname("#ghmemorialmob06") + "::OnBossDead";
		}
		end;

OnBossDead:
		disablenpc();
		mapannounce 'map_name$[1], "A new portal has appeared at the end of the central corridor.", bc_map,0xFFFF00;
		enablenpc instance_npcname("#24 Control");
		enablenpc instance_npcname("#24 Control2");
		enablenpc instance_npcname("#Geron");
		for ( .@i = 1; .@i <= 35; .@i++ )
				enablenpc instance_npcname("Decomposed body#" + .@i);

		// Event stopped and monsters in this sector killed
		for ( .@i = 1; .@i <= 7; .@i++ )
				donpcevent instance_npcname("#ogh_3-" + .@i) + "::OnStop";

		.@npc_name$ = instance_npcname("#ghmemorialmob06");
		killmonster 'map_name$[1], .@npc_name$ + "::OnMyMobDead";
		for ( .@i = 1; .@i <= 7; .@i++ )
				killmonster 'map_name$[1], .@npc_name$ + "::OnMyMobDead" + .@i;

		// Hidden mobs
		setarray .@coord[0],
		 		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
				monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Flame of destruction",2337,1;// HIDDEN_MOB
		end;
}

// Amdarais Room Entrance
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch_:
		if ('step != 7)
				end;
		'step = 8;
		disablenpc instance_npcname("#Geron");
		mapannounce 'map_name$[1], "???: Do not come here! It's a trap... Kkkkah!!!", bc_map,0xFF7777,FW_NORMAL,15;
		enablenpc instance_npcname("Himelmez#22");
		enablenpc instance_npcname("Varmundt#23");
		enablenpc instance_npcname("Heinrich#23");
		enablenpc instance_npcname("Gerhalt#23");
		end;
}

2@gl_k,143,260,4	script	Decomposed body#1	4_M_DIEMAN,5,5,{
		end;
OnTouch_:
		disablenpc();
		.@i = rand(1,10);
		if (.@i == 1) .@mobs = 3;
		else if (.@i == 2) .@mobs = 4;
		else if (.@i == 3) .@mobs = 5;
		else if (.@i < 7) .@mobs = 6;
		else .@mobs = 7;
		.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMobDead";
		killmonster 'map_name$[1], .@event$;
		getmapxy .@map$,.@x,.@y, BL_NPC;
		specialeffect EF_VENOMDUST;
		monster .@map$,.@x,.@y,"Maggot",2467,.@mobs, .@event$;
		initnpctimer;
		end;
OnTimer45000:
		enablenpc();
		stopnpctimer;
		end;
OnMobDead:
		end;
}
2@gl_k,145,236,4	duplicate(Decomposed body#1)	Decomposed body#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(Decomposed body#1)	Decomposed body#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(Decomposed body#1)	Decomposed body#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(Decomposed body#1)	Decomposed body#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(Decomposed body#1)	Decomposed body#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(Decomposed body#1)	Decomposed body#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(Decomposed body#1)	Decomposed body#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(Decomposed body#1)	Decomposed body#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(Decomposed body#1)	Decomposed body#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(Decomposed body#1)	Decomposed body#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(Decomposed body#1)	Decomposed body#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(Decomposed body#1)	Decomposed body#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(Decomposed body#1)	Decomposed body#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(Decomposed body#1)	Decomposed body#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(Decomposed body#1)	Decomposed body#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(Decomposed body#1)	Decomposed body#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(Decomposed body#1)	Decomposed body#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(Decomposed body#1)	Decomposed body#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(Decomposed body#1)	Decomposed body#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(Decomposed body#1)	Decomposed body#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(Decomposed body#1)	Decomposed body#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(Decomposed body#1)	Decomposed body#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(Decomposed body#1)	Decomposed body#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(Decomposed body#1)	Decomposed body#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(Decomposed body#1)	Decomposed body#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(Decomposed body#1)	Decomposed body#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(Decomposed body#1)	Decomposed body#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(Decomposed body#1)	Decomposed body#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(Decomposed body#1)	Decomposed body#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(Decomposed body#1)	Decomposed body#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(Decomposed body#1)	Decomposed body#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(Decomposed body#1)	Decomposed body#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(Decomposed body#1)	Decomposed body#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(Decomposed body#1)	Decomposed body#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	Heinrich#23	4_M_HEINRICH,{
		cutin "gl_heinrich1",2;
		mes "[ไฮน์ริช]";
		mes "ใส่อะไรไว้บนฮิเมิลเมซ!";
		close3;
}

2@gl_k,162,250,1	script	Varmundt#23	4_M_BARMUND,{
		mes "[วาร์มุนด์]";
		mes "ถอดบังเหียนอันนี้ไม่ได้ครับ...";
		cutin "gl_barmund2",2;
		close3;
}

2@gl_k,158,255,1	script	Gerhalt#23	4_LEVITATEMAN,{
		mes "[เกอร์ฮัลท์]";
		mes "โอ๊ยยย... หนีไปกับผู้บัญชาการดีกว่า";
		close;
}

2@gl_k,158,252,1	script	Himelmez#22	4_F_HIMEL,3,3,{
		cutin "gl_himel2",2;
		mes "[ฮิเมลเมซ]";
		mes "น่าทึ่งมาก ~ คุณมาถึงที่นี่ได้สำเร็จ ฉันขอกล่าวขอบคุณได้ไหม?";
		close3;
OnTouch_:
		if ('step != 8)
				end;
		'step = 9;
		disablenpc();
		if ('mode == 1)
				donpcevent instance_npcname("#talkinstance7") + "::OnStart";
		else if ('mode == 2)
				donpcevent instance_npcname("#talkinstance8") + "::OnStart";
		end;
}

2@gl_k,1,1,0	script	#talkinstance7	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("Himelmez#23");
		initnpctimer;
		'npc_himelmez$ = instance_npcname("Himelmez#23");
		'npc_gerhalt$ = instance_npcname("Gerhalt#23");
		'npc_heinrich$ = instance_npcname("Heinrich#23");
		'npc_varmundt$ = instance_npcname("Varmundt#23");
		end;
OnTimer3000:
		npctalk "ฮิเมลเมซ: สุดยอดไปเลย~ ฉันคิดว่าคุณคงไม่ใกล้จะมาถึงที่นี่ซะด้วยซ้ำ...", 'npc_himelmez$;
		end;
OnTimer6000:
		npctalk "เกอร์ฮัลท์: คิ๊กก้าห์! หนีไปซะ! ฉันทนไม่ไหวแล้ว!", 'npc_gerhalt$;
		end;
OnTimer9000:
		npctalk "ไฮน์ริช: เกอร์ฮัลท์!", 'npc_heinrich$;
		end;
OnTimer15000:
		npctalk "ไฮน์ริช: คุณกำลังทำอะไรกับฮิเมิลเมซทหารของฉัน!", 'npc_heinrich$;
		end;
OnTimer18000:
		npctalk "ฮิเมลเมซ: ฮู้ฮู้ ฉันเจอชิ้นส่วนหัวใจของยูมีร์แล้วนะ ไฮน์ริช", 'npc_himelmez$;
		end;
OnTimer21000:
		npctalk "ฮิเมลเมซ: ถ้าเขาไม่รบกวนฉัน ฉันคงทำได้เร็วขึ้น", 'npc_himelmez$;
		end;
OnTimer24000:
		npctalk "เกอร์ฮัลท์: ผู้บัญชาการ... เอาล่ะ คุณต้องหนีจากที่นี่นะ... อึ๋ย", 'npc_gerhalt$;
		end;
OnTimer27000:
		npctalk "ฮิเมลเมซ: จริงเหรอ? อะไรทำให้คุณคิดแบบนั้น?", 'npc_himelmez$;
		end;
OnTimer30000:
		npctalk "ไฮน์ริช: ฮิเมลเมซ! คุณได้ทำในสิ่งที่คุณต้องการแล้ว ดังนั้นไม่จำเป็นต้องเสียสละอะไรอีกแล้ว!", 'npc_heinrich$;
		end;
OnTimer33000:
		npctalk "ไฮน์ริช: ปล่อยเขาไปเถอะ ฉันไม่ต้องการเห็นใครต้องทนทุกข์อีกต่อไป!", 'npc_heinrich$;
		end;
OnTimer36000:
		npctalk "ฮิเมลเมซ: ปล่อยเขาไปเหรอ? ฉันคิดว่า... ไม่...", 'npc_himelmez$;
		end;
OnTimer39000:
		npctalk "ฮิเมลเมซ: นอกจากนี้ นี่คือทหารของคุณคนสุดท้ายที่รอดชีวิต ไฮน์ริช", 'npc_himelmez$;
		end;
OnTimer42000:
		npctalk "ฮิเมลเมซ: เขาทำให้ชีวิตของฉันยากลำบาก", 'npc_himelmez$;
		end;
OnTimer45000:
		npctalk "ฮิเมลเมซ: เขาเหมาะสมกับผลงานสร้างสรรค์ใหม่ของฉัน อัมดาราอิส เป็นอย่างดี", 'npc_himelmez$;
		end;
OnTimer48000:
		npctalk "ไฮน์ริช: ฉันจะไม่มีวันให้อภัยคุณ", 'npc_heinrich$;
		end;
OnTimer51000:
		npctalk "ฮิเมลเมซ: อืมๆ~ ฉันกลัวมากเลย", 'npc_himelmez$;
		end;
OnTimer54000:
		npctalk "ฮิเมลเมซ: ยังไงก็ตามเราคงมีโอกาสได้เห็นเพราะเรายังต้องคุยกันอีก", 'npc_himelmez$;
		end;
OnTimer57000:
		npctalk "ฮิเมลเมซ: สวัสดีเพื่อนๆ", 'npc_himelmez$;
		end;
OnTimer60000:
		specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
		end;
OnTimer63000:
		disablenpc 'npc_himelmez$;
		end;
OnTimer65000:
		npctalk "เกอร์ฮัลท์: เจ้าสร้างร่างกายข้าได้ แต่เจ้าพรากวิญญาณข้าไปไม่ได้ ฮิเมลเมซ!", 'npc_gerhalt$;
		end;
OnTimer66000:
		specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
		end;
OnTimer67000:
		npctalk "ไฮน์ริช: ฉันจะไม่มีวันปล่อยให้คุณหนีไปได้ ฮิเมิลเมซ!!", 'npc_heinrich$;
		specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
		end;
OnTimer70000:
		specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
		specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
		disablenpc 'npc_heinrich$;
		npctalk "วาร์มุนด์: เราไม่มีทางเลือกอื่น เราต้องต่อสู้กับอมดาไรส์!", 'npc_varmundt$;
		end;
OnTimer73000:
		mapannounce 'map_name$[1], "Gerhalt's body is changing.", bc_map,0xFFFFFF;
		end;
OnTimer76000:
		specialeffect EF_LORD, AREA, 'npc_gerhalt$;
		disablenpc 'npc_varmundt$;
		mapannounce 'map_name$[1], "Varmundt: Let me help this battle with my illusion. Just follow the illusion's orders.", bc_map,0xFFFF00;
		end;
OnTimer80000:
		disablenpc 'npc_gerhalt$;
		donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
		stopnpctimer;
		'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
		end;
}

2@gl_k,1,1,0	script	#talkinstance8	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("Himelmez#23");
		initnpctimer;
		'npc_himelmez$ = instance_npcname("Himelmez#23");
		'npc_gerhalt$ = instance_npcname("Gerhalt#23");
		'npc_heinrich$ = instance_npcname("Heinrich#23");
		'npc_varmundt$ = instance_npcname("Varmundt#23");

		npctalk "ฮิเมลเมซ: สุดยอดไปเลย~ ฉันคิดว่าคุณคงไม่ใกล้จะมาถึงที่นี่ซะด้วยซ้ำ...", 'npc_himelmez$;
		end;
OnTimer1500:
		specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
		end;
OnTimer3500:
		specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
		end;
OnTimer5000:
		specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
		end;
OnTimer5500:
		disablenpc 'npc_himelmez$;
		end;
OnTimer7000:
		specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
		end;
OnTimer10000:
		npctalk "ไฮน์ริช: ฉันจะไม่มีวันปล่อยให้คุณหนีไปได้ ฮิเมิลเมซ!!", 'npc_heinrich$;
		end;
OnTimer11000:
		disablenpc 'npc_heinrich$;
		npctalk "วาร์มุนด์: เราไม่มีทางเลือกอื่น เราต้องต่อสู้กับอมดาไรส์!", 'npc_varmundt$;
		end;
OnTimer14000:
		specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
		specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
		end;
OnTimer14500:
		mapannounce 'map_name$[1], "Gerhalt's body is changing.", bc_map,0xFFFFFF;
		end;
OnTimer15500:
		disablenpc 'npc_varmundt$;
		mapannounce 'map_name$[1], "Varmundt: Let me help this battle with my illusion. Just follow the illusion's orders.", bc_map,0xFFFF00;
		end;
OnTimer17500:
		specialeffect EF_LORD, AREA, 'npc_gerhalt$;
		end;
OnTimer20500:
		stopnpctimer;
		disablenpc 'npc_gerhalt$;
		donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
		'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
		end;
}

2@gl_k,158,252,4	script	Himelmez#23	4_F_HIMEL,{
		cutin "gl_himel2",2;
		mes "[ฮิเมลเมซ]";
		mes "น่าทึ่งมาก ~ คุณมาถึงที่นี่ได้สำเร็จ ฉันขอกล่าวขอบคุณได้ไหม?";
		close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob07");
		monster 'map_name$[1],158,255,"Amdarais",2476,1,instance_npcname("#ghmemorialmob07") + "::OnMyMobDead";// MG_AMDARAIS
		'boss_id = $@mobid[0];
		unittalk 'boss_id, "Run away... run away from me...";
		initnpctimer;
		end;
OnTimer5000:
		unittalk 'boss_id, "I don't want... I don't want to kill anyone. Uhuuuuuh";
		end;
OnTimer10000:
		unittalk 'boss_id, "Please kill me! Please!";
		end;
OnTimer16000:
		unittalk 'boss_id, "Eeeeee...eee...die... die...";
		end;
OnTimer22000:
		unittalk 'boss_id, "Demolition... Death!...";
		end;
OnTimer55000:
		donpcevent instance_npcname("#ghmemorialmob08") + "::OnStart";// Varmundt buffs and additionnal monsters
		stopnpctimer;
		end;

OnMyMobDead:
		if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob07") + "::OnMyMobDead") < 1) {
				stopnpctimer;
				enablenpc instance_npcname("Hugin#21");
				enablenpc instance_npcname("#Secret Room Exit");

				stopnpctimer;
				disablenpc instance_npcname("#ghmemorialmob07");
				// note: monsters from #ghmemorialmob08 still alive on Amdarais's dead
		}
		end;
}

// Varmundt Buffs
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#ghmemorialmob08");
		initnpctimer;
		end;
OnTimer5000:
		if (unitexists('boss_id) == 0) {
				disablenpc instance_npcname("#ghmemorialmob08");
				stopnpctimer;
				end;
		}
		getunitdata 'boss_id, .@data;
		.@percent_hp = (.@data[UMOB_HP] * 100) / .@data[UMOB_MAXHP];
		mapannounce 'map_name$[1], "Amdarais HP " + .@percent_hp + "% reach!", bc_map,0x70DBDB;

		// event type every 10%
		switch( .@percent_hp / 10 ) {
		case 10:
				break;
		case 9:
				donpcevent instance_npcname("Varmundt's Ghost#Buff2") + "::OnEvent";
				break;
		case 8:
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Suffered Khalitzburg",2471,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 7:
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Abysmal Knight",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 6:
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Bloody Knight",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 5:
				donpcevent instance_npcname("Varmundt's Ghost#Buff2") + "::OnEvent";
				break;
		case 4:
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Wandering Archer",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 3:
				donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Bloody Knight",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 2:
				donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
				donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Abysmal Knight",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		case 1:
		case 0:
				donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
				donpcevent instance_npcname("Varmundt's Ghost#Buff4") + "::OnEvent";
				areamonster 'map_name$[1],140,220,175,255,"Wandering Archer",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
				break;
		}
		end;
OnTimer30000:
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		initnpctimer;
		end;
OnMobDead:
		end;
}

2@gl_k,150,247,5	script	Varmundt's Ghost#Buff1	4_M_BARMUND,2,2,{
		end;
OnTouch:
		specialeffect2 EF_ENHANCE;
		.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
		if (.@num == 1)
				sc_start SC_GLASTHEIM_HIDDEN,30000,90,10000,SCSTART_NOTICKDEF;
		else if (.@num == 2)
				sc_start SC_GLASTHEIM_STATE,30000,20,10000,SCSTART_NOTICKDEF;
		else if (.@num == 3) {
				sc_start2 SC_GLASTHEIM_HEAL,30000,100,50,10000,SCSTART_NOTICKDEF;
				sc_start2 SC_GLASTHEIM_HPSP,30000,10000,1000,10000,SCSTART_NOTICKDEF;
		}
		else {
				sc_start SC_GLASTHEIM_DEF,30000,100,10000,SCSTART_NOTICKDEF;
				sc_start2 SC_GLASTHEIM_ITEMDEF,30000,200,50,10000,SCSTART_NOTICKDEF;
		}
		end;
OnEvent:
		initnpctimer;
		enablenpc();
		.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
		if (.@num == 1)
				npctalk "ผีแห่งวาร์มุนด์: อัมดาไรส์อาจใช้โล่เวทมนตร์อันแข็งแกร่งได้! อยู่ใกล้ๆ ฉันแล้วฉันจะปกป้องคุณเอง!";
		else if (.@num == 2)
				npctalk "ผีแห่งวาร์มุนด์: ตอนนี้เป็นโอกาสที่จะโจมตีแล้ว! เข้ามาใกล้ฉันแล้วโจมตี!";
		else if (.@num == 3)
				npctalk "ผีแห่งวาร์มุนด์: หากเจ้าไม่อยากถูกซอมบี้ของอัมดาราอิสโจมตี มาหาข้าแล้วรับพลังเพิ่มสิ!";
		else
				npctalk "ผีแห่งวาร์มุนด์: หากเจ้าไม่อยากถูกโจมตีด้วยพลังของอมดาราอิส ก็มาหาข้าแล้วรับพลังเพิ่มสิ!";
		end;
OnTimer10000:
		stopnpctimer;
		disablenpc();
		end;
}
2@gl_k,165,247,3	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	Hugin#21	4_M_SAGE_C,{
		if ('step != 9)
				end;
		if (checkquest(12319,HUNTING) == 2) {// note: complete hunting 'Corrupted Soul Hunt' prerequis
				if (checkweight(6607,5, 6608,5) == 0) {
						mes "^ff0000ดูเหมือนว่าคุณมีสินค้าประเภทหรือน้ำหนักมากเกินไป โปรดจัดระเบียบสินค้าคงคลังของคุณ^000000";
						close;
				}
				mes "[ฮูกิน]";
				mes "อืม คุณสบายดี ก่อนอื่น ฉันขอของจากอัมดาราอิสให้คุณก่อน";
				erasequest 12319;
				setquest 12322;// Space Distortion
				if (isbegin_quest(12321) == 0) {
						setquest 12321;// Time Conqueror
						completequest 12321;
						getitem 6607,5;// Temporal_Crystal
						getitem 6608,5;// Coagulated_Spell
						getexp 350000,350000;
				}
				else {
						getitem 6607,1;// Temporal_Crystal
						getitem 6608,1;// Coagulated_Spell
				}
				next;
				mes "[ฮูกิน]";
				mes "เวลาของวาร์มุนด์ถูกหยุดลงโดยฉัน";
				mes "และเวลาของคุณก็จะผิดเพี้ยนไปในเร็วๆ นี้";
				specialeffect2 EF_BLIND;
				soundeffect "_blind.wav",0;
				next;
				mes "[ฮูกิน]";
				mes "บางทีนักเดินทางข้ามเวลาผู้โชคร้ายคนนี้อาจจะพยายามหยุดยั้งโศกนาฏกรรมที่ Glast Heim ไม่ให้เกิดขึ้น";
				next;
				mes "[ฮูกิน]";
				mes "อย่างไรก็ตามเราจะไม่มองข้ามพฤติกรรมของเขาอีกต่อไปและตลอดไป...";
				next;
				mes "[ฮูกิน]";
				mes "ตอนนี้ให้ฉันลบความทรงจำของคุณออกไป ถ้าคุณเห็นฉันอีกครั้ง แสดงว่าเป็นคนใหม่";
				specialeffect2 EF_FREEZE;
				close2;
				warp 'map_name$[1],158,244;
				end;
		}
		mes "[ฮูกิน]";
		mes "อุ๊ย เกือบทำเวลาคลาดเคลื่อนซะแล้ว มานี่สิ เราต้องออกไปแล้ว";
		next;
		select("You were just with me...");
		mes "[ฮูกิน]";
		mes "ก่อนหน้านี้คุณพูดอะไรไป?";
		mes "อย่างไรก็ตาม นั่นไม่สำคัญ ช่องว่างเวลาจะหมดลง ดังนั้นเราต้องออกไปจากที่นี่";
		next;
		if (select("Let me look around more:Please let me out") == 1) {
				mes "[ฮูกิน]";
				mes "จริงเหรอ? ที่นี่จะพังในไม่ช้านี้ โปรดมองหารอบๆ อย่างรวดเร็ว";
				close;
		}
		close2;
		warp "glast_01",204,270;
		end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	Strange crack#2	CLEAR_NPC,{
		if ('step != 9)
				end;
		if (isbegin_quest(12322) == 0) {// Space Distortion
				mes "รอยร้าวนั้นดูน่าสงสัยแต่ก็ไม่มีอะไรต้องตรวจสอบเพิ่มเติม";
				close;
		}
		specialeffect EF_SPELLBREAKER;

		.@random = rand(1,4);
		switch( atoi(strnpcinfo(2)) ) {
		case 2:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 719,1,"this",165,138;
				makeitem 6608,1,"this",165,138;
				break;
		case 3:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 720,1,"this",159,138;
				if (rand(1,4) == 4)
						makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
				makeitem 6608,1,"this",159,138;
				makeitem 7229,1,"this",159,138;
				break;
		case 4:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 721,1,"this",153,138;
				if (rand(1,4) == 4)
						makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
				makeitem 6608,1,"this",153,138;
				makeitem 7230,1,"this",153,138;
				break;
		case 5:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 722,1,"this",147,138;
				if (rand(1,4) == 4)
						makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
				makeitem 6612,1,"this",147,138;
				makeitem 6613,1,"this",147,138;
				makeitem 6608,1,"this",147,138;
				break;
		case 6:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 725,1,"this",141,138;
				makeitem 7228,1,"this",141,138;
				if (rand(1,4) == 4)
						makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
				makeitem 6608,1,"this",141,138;
				break;
		case 7:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 726,1,"this",135,138;
				if (rand(1,4) == 4)
						makeitem2 2022,1,"this",135,138,0,0,0,0,0,0,0;
				makeitem 6608,1,"this",135,138;
				break;
		case 8:
				for ( .@i = 1; .@i <= .@random; ++.@i )
						makeitem 727,1,"this",129,138;
				if (rand(1,4) == 4)
						makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
				makeitem 6608,1,"this",129,138;
				break;
		}
		disablenpc();
		end;
}
1@gl_k,159,136,3	duplicate(Strange crack#2)	Strange crack#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(Strange crack#2)	Strange crack#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(Strange crack#2)	Strange crack#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(Strange crack#2)	Strange crack#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(Strange crack#2)	Strange crack#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(Strange crack#2)	Strange crack#8	CLEAR_NPC

1@gl_k,269,267,3	script	Strange crack#1	CLEAR_NPC,{
		if (isbegin_quest(12322) == 0) {// Space Distortion
				mes "รอยร้าวนั้นดูน่าสงสัยแต่ก็ไม่มีอะไรต้องตรวจสอบเพิ่มเติม";
				close;
		}
		warp 'map_name$[0],149,198;
		end;

OnInstanceInit:
		'step = 0;
		'mode = 0;
		'map_name$[0] = instance_mapname("1@gl_k");
		'map_name$[1] = instance_mapname("2@gl_k");

		// Entrance
		disablenpc instance_npcname("Himelmez#1");
		disablenpc instance_npcname("Varmundt#1");
		disablenpc instance_npcname("Heinrich#1");
		disablenpc instance_npcname("Heinrich#2");
		disablenpc instance_npcname("Heinrich#3");

		// Rescue 1
		disablenpc instance_npcname("#ghmemorialmob01");
		disablenpc instance_npcname("Altar boy Domun#1");

		// Rescue 2
		disablenpc instance_npcname("#ghmemorialmob02");
		disablenpc instance_npcname("Holgren the Destroyer#1");
		for ( .@i = 1; .@i <= 26; .@i++ )
				disablenpc instance_npcname("A dead man#" + .@i);

		// Sector 3
		disablenpc instance_npcname("#ghmemorialmob03");

		// Root of Corruption
		disablenpc instance_npcname("Himelmez#2");
		disablenpc instance_npcname("Varmundt#2");
		disablenpc instance_npcname("Heinrich#4");
		disablenpc instance_npcname("#Mimelon");
		disablenpc instance_npcname("#ghmemorialmob04");

		// Entrance 2nd map
		disablenpc instance_npcname("Varmundt#21");
		disablenpc instance_npcname("Heinrich#21");
		disablenpc instance_npcname("#Servanton");
		hideonnpc instance_npcname("#Servanton_effect");

		// Commanders
		disablenpc instance_npcname("#ghmemorialmob05");
		disablenpc instance_npcname("#ghmemorialmob06");
		for ( .@i = 1; .@i <= 7; .@i++ ) {
				disablenpc instance_npcname("#ogh_2-" + .@i);
				disablenpc instance_npcname("#ogh_3-" + .@i);
		}

		// Amdarais Room Entrance
		disablenpc instance_npcname("#Geron");
		for ( .@i = 1; .@i <= 35; .@i++ )
				disablenpc instance_npcname("Decomposed body#" + .@i);

		// Amdarais Spawn
		disablenpc instance_npcname("Gerhalt#23");
		disablenpc instance_npcname("Himelmez#22");
		disablenpc instance_npcname("Himelmez#23");
		disablenpc instance_npcname("Varmundt#23");
		disablenpc instance_npcname("Heinrich#23");
		disablenpc instance_npcname("#ghmemorialmob07");

		// Varmundt Buffs
		disablenpc instance_npcname("#ghmemorialmob08");
		disablenpc instance_npcname("Varmundt's Ghost#Buff1");
		disablenpc instance_npcname("Varmundt's Ghost#Buff2");
		disablenpc instance_npcname("Varmundt's Ghost#Buff3");
		disablenpc instance_npcname("Varmundt's Ghost#Buff4");
		disablenpc instance_npcname("Hugin#21");

		// Warps
		disablenpc instance_npcname("#2Control");
		disablenpc instance_npcname("#2Control2");
		disablenpc instance_npcname("#3Control");
		disablenpc instance_npcname("#3Control2");
		disablenpc instance_npcname("#4Control");
		disablenpc instance_npcname("#4Control2");
		disablenpc instance_npcname("#2F Entrance");
		disablenpc instance_npcname("#1 Control");
		disablenpc instance_npcname("#22 Control");
		disablenpc instance_npcname("#22 Control2");
		disablenpc instance_npcname("#23 Control");
		disablenpc instance_npcname("#23 Control2");
		disablenpc instance_npcname("#24 Control");
		disablenpc instance_npcname("#24 Control2");
		disablenpc instance_npcname("#Secret Room Exit");

		// Fast mode
		disablenpc instance_npcname("#cut01");
		end;
}
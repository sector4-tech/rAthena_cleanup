//===== rAthena Script =======================================
//= Instances Werner Laboratory central room.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Require Terra Gloria main quest.
//- Note: For now there are 2 instances (officially one) to
//  avoid exploit, the warp entry being the same. The first
//  (main quest) is a solo instance and the 2nd (daily) a
//  a party instance. The instance is a basic version,
//  KRO added Seyren boss fight in the next updates.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

slabw01,232,91,5	duplicate(dummy_cloaked_npc)	Skia#ep162_06	4_EP16_SKIA
slabw01,233,92,3	duplicate(dummy_cloaked_npc)	Scared researcher#ep162	4_M_ALCHE_A

// Werner Laboratory central room (solo instance)
slabw01,236,91,3	script	Rookie#ep162_04	4_M_ROOKIE,{
		if (terra_gloria_main == 20) {
				mes "[มือใหม่]";
				mes "ลองดูสิ่งนี้ดู";
				mes "ตามแบบออกแบบส่วนนี้จะเชื่อมต่อกับห้องกลาง...แต่ผมเห็นเพียงประตูที่น่าสงสัยเท่านั้น";
				next;
				mes "[มือใหม่]";
				mes "ไม่แปลกใจที่ดูเหมือนว่าพวกเขามีอะไรบางอย่างมาสนับสนุนพวกเขา";
				mes "จะใช้วัตถุระเบิดแล้วระเบิดเลยไหม?";
				next;
				cloakoffnpc( "Skia#ep162_06", getcharid(0) );
				cloakoffnpc( "Scared researcher#ep162", getcharid(0) );
				npctalk "รอ!", "Skia#ep162_06", bc_self;
				mes "[สกีอา]";
				mes "ไม่ใช่แบบหยาบคายขนาดนั้น!";
				mes "ฉันได้ตัดสินใจที่ดีเมื่อฉันมาที่นี่";
				mes "บอกเขาให้เปิดอันนี้";
				next;
				npctalk "ว้าว! ผู้หญิงคนนี้ฉลาดจริงๆ!", "", bc_self;
				npctalk "ทำไม...ทำไมคุณถึงทำแบบนี้กับฉัน!", "Scared researcher#ep162", bc_self;
				mes "[สกีอา]";
				mes "ฉันถามเขาว่าเขารู้จักเส้นทางที่นี่หรือไม่ เขาตอบว่าใช่ ฉันจึงพาเขามาที่นี่";
				mes "เขาจะเปิดประตูสู่ห้องกลาง";
				next;
				npctalk "นี่คือ...วิธีที่จะทำมัน", "Scared researcher#ep162", bc_self;
				mes "[มือใหม่]";
				mes "อืม เข้าใจแล้ว นั่นคือวิธีการใช้งาน";
				mes "เยี่ยมเลย ตอนนี้ฉันเข้าใจแล้ว";
				mes "และเราไม่ต้องการเขาอีกต่อไป";
				next;
				npctalk "ห...อะไรนะ?! อย่าฆ่าฉันนะ!", "Scared researcher#ep162", bc_self;
				mes "[สกีอา]";
				mes "จริงเหรอ? งั้นฉันจะพานักวิจัยคนนี้ไปที่ฝูงบินด้านนอก";
				mes "เกี่ยวกับห้องกลาง" + strcharinfo(0) + "ฉันกำลังนับถือคุณอยู่!";
				next;
				cloakonnpc( "Skia#ep162_06", getcharid(0) );
				cloakonnpc( "Scared researcher#ep162", getcharid(0) );
				mes "[มือใหม่]";
				mes "ฮ่าๆ เค้าให้เราเปิดประตูห้องกลางได้นะ!";
				mes "กรุณาพูดคุยกับฉันอีกครั้งเมื่อคุณพร้อมที่จะเข้ามาที่นี่";
				next;
				mes "[มือใหม่]";
				mes "หลังจากเปิดใช้งานประตูแล้ว เราจะต้องป้องกันมันจากภายนอก";
				mes "ฉันจึงไม่สามารถเข้าไปที่นั่นกับคุณได้";
				terra_gloria_main = 21;
				close;
		}
		if (terra_gloria_main == 21) {
				if (is_party_leader() == false) {
						mes "^4d4dffงานนี้จัดขึ้นที่อนุสรณ์สถาน";
						mes "กรุณาลองใหม่อีกครั้งหลังจากจัดปาร์ตี้^000000";
						close;
				}
				mes "[มือใหม่]";
				mes "คุณพร้อมแล้วหรือยัง?";
				mes "หากคุณเข้าตอนนี้ ฉันจะเปิดใช้งานประตู";
				next;
				if (select( "Enter now.", "Do not enter." ) == 2) {
						mes "[มือใหม่]";
						mes "เราทั้งสองรู้ดีว่าเราไม่มีเวลาสำหรับเรื่องนี้ใช่ไหม?";
						close;
				}
				mes "[มือใหม่]";
				mes "ว้าว! นี่มันใช่เลย! ฉันเรียนรู้อะไรๆ ได้เร็วเสมอ!";
				mes "เอาล่ะ เข้าไปได้เลย!";
				cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
				instance_create("Werner Laboratory central room#1");	// officially there is only one 'Werner Laboratory central room' instance
				close;
		}
		if (terra_gloria_main == 22) {
				mes "[มือใหม่]";
				mes "คุณได้เอาของคืนมาแล้วใช่ไหม?";
				close;
		}
		switch( checkquest(7742,HUNTING) ) {
		case -1:
				mes "[มือใหม่]";
				mes "เราพบปรากฏการณ์ประหลาดในห้องกลาง เราไม่ได้สืบสวนมัน";
				mes "บางทีอาจมีวัตถุทดลองอันแข็งแกร่งเหลืออยู่ข้างใน";
				next;
				mes "[มือใหม่]";
				mes "นี่เป็นสถานที่ประเภทไหน?";
				mes "ฮึๆๆ.";
				mes "เมื่อระบุได้ถูกต้องแล้ว ฉันอาจขอให้คุณตกลง ...";
				mes "ฉันจะต้องเฝ้าประตูห้องกลางอีกแล้ว";
				close;
		case 0:
		case 1:
				if (getcharid(1) < 1) {
						mes "^4d4dffงานนี้จัดขึ้นที่อนุสรณ์สถาน";
						mes "กรุณาลองใหม่อีกครั้งหลังจากจัดปาร์ตี้^000000";
						close;
				}
				if (is_party_leader() == false) {
						mes "[มือใหม่]";
						mes "หากมีประตูที่ถูกเปิดใช้งานแล้วก็จะมองเห็นได้";
						next;
						mes "[มือใหม่]";
						mes "หากไม่เช่นนั้น กรุณาขอทางเข้าจากหัวหน้ากลุ่มของคุณ";
						close;
				}
				mes "[มือใหม่]";
				mes "คุณพร้อมแล้วหรือยัง?";
				mes "หากคุณเข้าตอนนี้ ฉันจะเปิดใช้งานประตู";
				next;
				if (select( "Enter now.", "Do not enter." ) == 2) {
						mes "[มือใหม่]";
						mes "เราทั้งสองรู้ดีว่าเราไม่มีเวลาสำหรับเรื่องนี้ใช่ไหม?";
						close;
				}
				mes "[มือใหม่]";
				mes "ว้าว! นี่มันใช่เลย! ฉันเรียนรู้อะไรๆ ได้เร็วเสมอ!";
				mes "เอาล่ะ เข้าไปได้เลย!";
				cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
				instance_create("Werner Laboratory central room#2");
				close;
		case 2:
				mes "[มือใหม่]";
				mes "เราไม่แน่ใจว่ามีวัตถุทดลองอีกกี่ชิ้นที่ถูกสร้างและละทิ้งโดยไอเซน แวร์เนอร์";
				next;
				mes "[มือใหม่]";
				mes "แต่ขอเรียกมันว่าวันนี้ดีกว่า";
				close;
		}
		end;

OnInit:
		questinfo( QTYPE_QUEST, QMARK_YELLOW, " checkquest(7742,HUNTING) == 0 || checkquest(7742,HUNTING) == 1 " );
		end;
}

slabw01,246,88,3	script	Central_room#evt_gate01	PORTAL,{
		if (terra_gloria_main != 21 && terra_gloria_main < 23)
				end;
		if (getcharid(1) < 1 || (terra_gloria_main == 21 && is_party_leader() == false)) {
				mes "^4d4dffMD ตอนนี้มีเพียงหัวหน้าปาร์ตี้เท่านั้นที่สามารถเข้าสู่ [ห้องกลาง] ได้^000000";
				close;
		}
		mes "สามารถย้ายไปห้องกลางได้";
		next;
		if (select( "Go in.", "Do not go in." ) == 2) {
				mes "[มือใหม่]";
				mes "โอ้ย...ต้องเปิดทุกครั้งเลย ไม่สะดวกเลย";
				close;
		}
		if (terra_gloria_main == 21)
				.@md_name$ = "Werner Laboratory central room#1";
		else
				.@md_name$ = "Werner Laboratory central room#2";	// officially player without the daily hunting quest can enter in the instance, allowing to spam the instance without delay
		switch( instance_enter(.@md_name$) ) {
		case IE_NOMEMBER:
				end;
		case IE_NOINSTANCE:
				mes "[มือใหม่]";
				mes "ตอนนี้ประตูมีปัญหานะครับ แบบนี้มันอันตราย...";
				mes "กรุณารอสักครู่.";
				close;
		case IE_OTHER:
				mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
				close;
		case IE_OK:
				mapannounce "slabw01", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
				// warp "1@slw",187,24;
				end;
		}
		end;

OnInit:
		cloakonnpc( strnpcinfo(0) );
		end;
}

// warps
1@slw,187,170,0	warp	#slwwarp01	1,1,1@slw,71,32
1@slw,71,77,0	warp	#slwwarp02	1,1,1@slw,54,134
1@slw,55,150,0	warp	Emergency exit#exit_war	1,1,que_swat,155,58

// Entrance - event type
1@slw,187,25,0	script	#sl_evt01	HIDDEN_WARP_NPC,2,2,{
		end;
OnTouch:
		if (is_party_leader() == false)
				end;
		disablenpc instance_npcname("#sl_evt01");
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
				enablenpc instance_npcname("Eisen Werner#werner01");
		else {
				enablenpc instance_npcname("Guard#gard01");
				enablenpc instance_npcname("Guard#gard02");
				enablenpc instance_npcname("Guard#gard03");
				enablenpc instance_npcname("Guard#gard04");
		}
		end;

OnInstanceInit:
		'device_state[0] = 'device_state[1] = 0;
		'event_final = 0;

		'map_slw$ = instance_mapname("1@slw");

		disablenpc instance_npcname("#slwwarp01");
		disablenpc instance_npcname("#slwwarp02");
		disablenpc instance_npcname("Central Entrance#door");
		disablenpc instance_npcname("Emergency exit#exit_war");

		disablenpc instance_npcname("Eisen Werner#werner01");
		disablenpc instance_npcname("Eisen Werner#werner02");
		disablenpc instance_npcname("Eisen Werner#werner03");
		disablenpc instance_npcname("Eisen Werner#werner04");
		disablenpc instance_npcname("Eisen Werner#werner05");

		disablenpc instance_npcname("Security device#switch01");
		disablenpc instance_npcname("Security device#switch02");
		disablenpc instance_npcname("Security device#switch11");
		disablenpc instance_npcname("Security device#switch12");

		disablenpc instance_npcname("Guard#gard01");
		disablenpc instance_npcname("Guard#gard02");
		disablenpc instance_npcname("Guard#gard03");
		disablenpc instance_npcname("Guard#gard04");
		disablenpc instance_npcname("Guard#gard05");
		disablenpc instance_npcname("Guard#gard06");
		disablenpc instance_npcname("Guard#gard07");

		disablenpc instance_npcname("Seyren Windsor#seiren");
		disablenpc instance_npcname("Pet child#boss_master");
		disablenpc instance_npcname("#boss_master_dummy");
		end;
}

// Room 1
// * Samples and devices
1@slw,174,145,3	script	Displayed sample#01	CLEAR_NPC,{
		setarray .@sample$[0],
				"^ff0000Purity^000000", "MT-Wxx0s57b",
				"^4d4dffRose^000000", "MT-Wxx00b21",
				"^4d4dffContradiction^000000", "MT-Wxx023-f1",
				"^4d4dffJoy^000000", "OT-Wxx01-c",
				"^4d4dffSea^000000", "MT-Wxc1c1b",
				"^ff0000Eternity^000000", "MT-Wx267b",
				"^4d4dffWay back home^000000", "OTM-Wxff01",
				"^4d4dffLoneliness^000000", "MT-Wxx00c46",
				"^4d4dffGlow^000000", "OT-Wxx4d4dff",
				"^ff0000Dawn^000000", "MT-Wff_01v",
				"^4d4dffTwilight^000000", "OT-Wxx00c3b",
				"^4d4dffGirl^000000's ^ff0000dream^000000", "MT-Wxx0ax2-1";
		.@num = (atoi(strnpcinfo(2)) - 1) * 2;
		mes "-" + .@sample$[.@num] + "-";
		mes "หมายเลขการทดลอง:" + .@sample$[.@num+1] + "";
		close;
}
1@slw,201,145,3	duplicate(Displayed sample#01)	Displayed sample#02	CLEAR_NPC
1@slw,174,132,3	duplicate(Displayed sample#01)	Displayed sample#03	CLEAR_NPC
1@slw,201,132,3	duplicate(Displayed sample#01)	Displayed sample#04	CLEAR_NPC
1@slw,174,118,3	duplicate(Displayed sample#01)	Displayed sample#05	CLEAR_NPC
1@slw,201,118,3	duplicate(Displayed sample#01)	Displayed sample#06	CLEAR_NPC
1@slw,174,104,3	duplicate(Displayed sample#01)	Displayed sample#07	CLEAR_NPC
1@slw,201,104,3	duplicate(Displayed sample#01)	Displayed sample#08	CLEAR_NPC
1@slw,174,90,3	duplicate(Displayed sample#01)	Displayed sample#09	CLEAR_NPC
1@slw,201,90,3	duplicate(Displayed sample#01)	Displayed sample#10	CLEAR_NPC
1@slw,174,76,3	duplicate(Displayed sample#01)	Displayed sample#11	CLEAR_NPC
1@slw,201,76,3	duplicate(Displayed sample#01)	Displayed sample#12	CLEAR_NPC

// * Events - First entrance
1@slw,188,42,3	script	Eisen Werner#werner01	4_M_EISEN,{
		if (is_party_leader() == false)
				end;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "นี่คือตัวละครหลักมาแล้ว";
		mes "ฉันประทับใจมากจริงๆ ไม่คิดว่าคุณจะเดินทางมาไกลถึงที่นี่";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เอาเถอะ";
		mes "ยินดีต้อนรับ.";
		mes "ยินดีต้อนรับสู่โลกของไอเซน แวร์เนอร์";
		next;
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันไม่เคยคิดว่าจะเกิดเหตุการณ์แบบนี้ขึ้นทันที";
		mes "อะไรก็ได้ ใครสนใจ?";
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันยอมแพ้กับการช่วยเหลือห้องปฏิบัติการแห่งนี้อยู่แล้ว";
		mes "อยากดูมั้ยจ๊ะ?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "มีสิ่งหลายอย่างเกิดขึ้น เช่น รอยแตกร้าวในมิติ เป็นต้น";
		mes "ในบางโลกการไหลของเวลาจะบิดเบือน";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "มันคือโลกคู่ขนานใช่ไหม?";
		mes "หรือมีใครเคยไปถึงจุดใดจุดหนึ่งในอดีต?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เวลาหมายถึงอะไร คุณเคยคิดเกี่ยวกับมันบ้างไหม?";
		mes "การไหลของมันเป็นแบบสัมพันธ์กัน";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คนนั้นหยุดแล้วตอนนี้";
		mes "หากกาลเวลาผ่านไปในทางชีววิทยา แสดงว่ามีคนคนหนึ่งแก่ลง";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "นั่นก็ถูกหยุดไว้ด้วยกำลัง";
		mes "ในกรณีนั้นเราใช้สำนวนเช่นนี้";
		next;
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เวลาได้หยุดลงแล้ว";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "-";
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อยากดูภายในมั้ย?";
		mes "ใช้เวลาของคุณและมองผ่านทางเดิน";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันจะนำทางคุณ.";
		close2;
		cutin "",255;
		if (is_party_leader() == true) {
				disablenpc instance_npcname("Eisen Werner#werner01");
				enablenpc instance_npcname("Eisen Werner#werner02");
		}
		end;
}

1@slw,189,164,3	script	Eisen Werner#werner02	4_M_EISEN,{
		if (is_party_leader() == false)
				end;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณชอบงานของฉันมั้ย?";
		mes "ฉันส่งคนโง่ๆเหล่านั้นกลับไปเริ่มต้นใหม่";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "มันก็ไม่ง่ายเลย";
		mes "-";
		next;
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ถ้าเด็กคนนั้นกลับไปเขาคงไม่จบชีวิตแบบนี้...";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันหมายถึงว่าถ้าเขาหยุดเท่านั้น...";
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ประธาน... คือว่าคนนั้นเป็นคนที่หลงใหลในอดีตน่ะ";
		mes "ต้องขอบคุณประธานทำให้ผมสามารถค้นคว้าได้ตามที่ต้องการ";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณไม่อยากรู้เรื่องนี้บ้างเหรอ?";
		mes "ทำไมเขาถึงหลงใหลกับอดีตมากขนาดนั้น?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อีกอย่างนึงคือถ้าอยากให้เกินกว่านี้ จะต้องบังคับเครื่องมือทั้งฝั่งขวาและซ้ายด้วยนะ....ทำได้มั้ย?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เนื่องจากมันเป็นอุปกรณ์ที่เรียบง่าย ฉันเดาว่าคุณทำได้";
		mes "เปลี่ยน ^ff0000red^000000 เป็น ^4d4dffblue^000000";
		mes "แล้วเปลี่ยน ^4d4dffblue^000000 เป็น ^ff0000red^000000 เท่านี้ก็เรียบร้อย";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "หากคุณดูงานของฉันอย่างถูกต้อง คุณจะเข้าใจสิ่งที่ฉันกำลังพูด";
		mes "แล้วพบกันใหม่ในโซนถัดไปครับ...";
		close2;
		cutin "",255;
		if (is_party_leader() == true) {
				disablenpc instance_npcname("Eisen Werner#werner02");
				enablenpc instance_npcname("Security device#switch01");
				enablenpc instance_npcname("Security device#switch02");
		}
		end;
}

1@slw,155,191,3	script	Security device#switch01	CLEAR_NPC,{
		if (is_party_leader() == false)
				end;
		.@number = atoi( replacestr( strnpcinfo(2), "switch", "" ) );
		.@type = (.@number < 10) ? 0 : 1;
		.@npc_num = (.@number < 10) ? .@number : (.@number-10);
		if ('device_state[.@type] & .@npc_num) {
				mes "ได้รับการเปิดใช้งานเรียบร้อยแล้ว.";
				close;
		}
		callsub S_Code, .@string$, .@answer;
		mes "มีรหัสความปลอดภัยเขียนเอาไว้";
		mes "-" + .@string$ + "-";
		mes "คุณจะใส่ข้อมูลอะไร?";
		next;
		if (select( "^ff0000" + .@string$ + "^000000", "^4d4dff" + .@string$ + "^000000" ) != .@answer) {
				'device_state[.@type] = 0;
				mes "อุปกรณ์ได้รับการเริ่มต้นใช้งานแล้ว";
				close;
		}
		if (is_party_leader() == false)
				end;
		'device_state[.@type] = 'device_state[.@type] | .@npc_num;
		if ('device_state[.@type] != 3) {
				mes "ดำเนินการอุปกรณ์รักษาความปลอดภัย";
				mes "ดูเหมือนจะมีอุปกรณ์รักษาความปลอดภัยเพิ่มอีกอันหนึ่ง";
				close;
		}
		mes "ดำเนินการอุปกรณ์รักษาความปลอดภัย";
		mes "ตอนนี้ประตูได้เปิดใช้งานแล้ว";
		switch( .@type ) {
		case 0:
				enablenpc instance_npcname("#slwwarp01");
				if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
						enablenpc instance_npcname("Eisen Werner#werner03");
				else {
						enablenpc instance_npcname("Guard#gard05");
						enablenpc instance_npcname("Guard#gard06");
						enablenpc instance_npcname("Guard#gard07");
				}
				break;
		case 1:
				if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")
						enablenpc instance_npcname("Eisen Werner#werner04");
				else
						enablenpc instance_npcname("Central Entrance#door");
				enablenpc instance_npcname("#slwwarp02");
				break;
		}
		close;

S_Code:
		setarray .@sample$[0],
				"Purity", 2,
				"Eternity", 2,
				"Dawn", 2,
				"Rose", 1,
				"Contradiction", 1,
				"Joy", 1,
				"Sea", 1,
				"Way back home", 1,
				"Loneliness", 1,
				"Glow", 1,
				"Twilight", 1;
		.@size = getarraysize(.@sample$) / 2;
		.@r = rand(.@size) * 2;
		set getarg(0), .@sample$[.@r];
		set getarg(1), .@sample$[.@r+1];
		return;
}
1@slw,220,191,3	duplicate(Security device#switch01)	Security device#switch02	CLEAR_NPC
1@slw,22,61,3	duplicate(Security device#switch01)	Security device#switch11	CLEAR_NPC
1@slw,122,61,3	duplicate(Security device#switch01)	Security device#switch12	CLEAR_NPC


// * Events - Daily
1@slw,188,58,3	script	Guard#gard01	3622,4,4,{
		end;
OnTouch:
		.@num = atoi( replacestr( strnpcinfo(2), "gard0", "" ) );
		getmapxy .@map$,.@x,.@y, BL_NPC;
		switch( .@num ) {
		case 1:	// 2 spawn area
				npctalk "..โห่ เข้ามาที่นี่ได้ยังไงเนี่ย?";
				areamonster .@map$, 186,92,190,94, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
				.@mob_count = 3;
				break;
		case 2:	// 3 spawn area
				npctalk "..โห่ เข้ามาที่นี่ได้ยังไงเนี่ย?";
				areamonster .@map$, 186,142,190,143, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
				areamonster .@map$, 186,160,190,162, "Special Guard", 3622,3;
				.@mob_count = 3;
				break;
		case 5:
				npctalk "..โห่ เข้ามาที่นี่ได้ยังไงเนี่ย?";
				.@mob_count = 3;
				break;
		case 3:
				npctalk "..โห่ เข้ามาที่นี่ได้ยังไงเนี่ย?";
				.@mob_count = 1;
				.@label$ = instance_npcname("#security_device_lab") + "::OnLeft1";
				break;
		case 4:
				npctalk "..โห่ เข้ามาที่นี่ได้ยังไงเนี่ย?";
				.@mob_count = 3;
				.@label$ = instance_npcname("#security_device_lab") + "::OnRight1";
				break;
		case 6:
				npctalk "มีใครอยู่มั้ย?";
				.@mob_count = 3;
				.@label$ = instance_npcname("#security_device_lab") + "::OnLeft2";
				break;
		case 7:
				npctalk "ให้ฉันจัดการคุณให้เสร็จเถอะ!";
				.@mob_count = 3;
				.@label$ = instance_npcname("#security_device_lab") + "::OnRight2";
				break;
		}
		disablenpc();
		areamonster .@map$, (.@x-2), (.@y-2), (.@x+2), (.@y+2), "Special Guard", 3622, .@mob_count, .@label$;	// EP16_2_MM_S_GUARDS
		end;
}
1@slw,189,117,3	duplicate(Guard#gard01)	Guard#gard02	3622,4,4
1@slw,171,167,3	duplicate(Guard#gard01)	Guard#gard03	3622,4,4
1@slw,206,167,3	duplicate(Guard#gard01)	Guard#gard04	3622,4,4
1@slw,70,38,5	duplicate(Guard#gard01)	Guard#gard05	3622,4,4
1@slw,33,52,3	duplicate(Guard#gard01)	Guard#gard06	3622,4,4
1@slw,111,52,3	duplicate(Guard#gard01)	Guard#gard07	3622,4,4

1@slw,1,1,3	script	#security_device_lab	HIDDEN_WARP_NPC,{
		end;
OnLeft1:
		if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnLeft1" ) < 1) {
				mapannounce 'map_slw$, "Security device L-01 has been activated.", bc_map,0xFF99;
				enablenpc instance_npcname("Security device#switch01");
		}
		end;
OnRight1:
		if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnRight1" ) < 1) {
				mapannounce 'map_slw$, "Security device R-01 has been activated.", bc_map,0xFF99;
				enablenpc instance_npcname("Security device#switch02");
		}
		end;
OnLeft2:
		if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnLeft2" ) < 1) {
				mapannounce 'map_slw$, "Security device L-02 has been activated.", bc_map,0xFF99;
				enablenpc instance_npcname("Security device#switch11");
		}
		end;
OnRight2:
		if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnRight2" ) < 1) {
				mapannounce 'map_slw$, "Security device R-02 has been activated.", bc_map,0xFF99;
				enablenpc instance_npcname("Security device#switch12");
		}
		end;
}


// Room 2
// * Events - First entrance
1@slw,72,70,3	script	Eisen Werner#werner03	4_M_EISEN,{
		if (is_party_leader() == false)
				end;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ง่ายกว่าที่คิดใช่ไหม?";
		mes "อุปกรณ์รักษาความปลอดภัยตัวต่อไปก็จะคล้ายๆ กันนะครับ คงไม่ต้องอธิบายซ้ำอีกครับ";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "หลังจากมองดูรอบๆ แล้วคุณคิดอย่างไรบ้าง?";
		mes "มันน่าทึ่งใช่ไหม?";
		mes "คุณเคยไปที่ ^4d4dffVerus^000000 แล้วหรือยัง?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "นั่นคือห้องทดลองที่ผมสร้างขึ้นโดยอ้างอิงถึงมัน";
		mes "มันเป็นประเภทของฉัน";
		mes "-";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เทคโนโลยีนั้นแตกต่างอย่างสิ้นเชิงจากสิ่งที่เราเคยมีมาจนถึงตอนนั้น";
		mes "เราได้ใช้ทุกสิ่งที่มีอยู่แล้ว";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณว่ายังไงบ้าง?";
		mes "ดูดีมั้ย?";
		next;
		select("Do you wonder why I'm saying these things?");
		mes "[ไอเซน แวร์เนอร์]";
		mes "อืม เพราะยังไงคุณก็ต้องตายที่นี่อยู่ดี";
		mes "ฉันจึงอยากที่จะส่งเสริมความสำเร็จของฉัน";
		next;
		select("Die here? Me?");
		mes "[ไอเซน แวร์เนอร์]";
		mes "แน่นอน คุณจะต้องตายที่นี่";
		mes "คุณคิดว่าฉันโง่พอที่จะถูกทิ้งไว้ที่นี่คนเดียวเหรอ?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันรู้ว่าคุณมาที่นี่เพื่อกิจการของอาณาจักรรูน-มิดการ์ต";
		mes "ฉันก็เลยเตรียมอะไรบางอย่างมา";
		next;
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ด้วยความรู้สึกอันมหาศาล";
		mes "คุณจะต้องตายที่นี่";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เพราะฉะนั้นก็เป็นเพียงแขกของฉัน";
		mes "มาต่อโซนถัดไปกันเลย";
		close2;
		cutin "",255;
		if (is_party_leader() == true) {
				disablenpc instance_npcname("Eisen Werner#werner03");
				enablenpc instance_npcname("Security device#switch11");
				enablenpc instance_npcname("Security device#switch12");
		}
		end;
}


// Room 3
// * Events - First entrance
1@slw,57,141,3	script	Eisen Werner#werner04	4_M_EISEN,{
		if (is_party_leader() == false)
				end;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เมื่อพ้นกำแพงนี้ไปแล้วจะมีอะไรบางอย่างมาให้รับไป";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ใช่แล้ว สิ่งที่คุณแกะสลักลงในอัญมณีและนำมาใช้เป็นเครื่องประดับบนมงกุฎ";
		mes "คุณรู้ไหมว่ามันคืออะไร?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "แม้ว่ามันจะเป็นเศษโลหะ แต่มันก็เป็นชิ้นส่วนหัวใจของ Ymir";
		mes "มันเทียบไม่ได้กับการเลียนแบบ";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "... ส่วนคนโง่ในอาณาจักรก็ไม่สามารถใช้พลังที่ตนมีอยู่ได้";
		mes "น่าเสียดายจริงๆ.";
		mes "ฉันก็เลยตัดสินใจใช้มัน...";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อยากจะเอาคืนไหมล่ะ? มันขึ้นอยู่กับความสามารถของคุณ";
		mes "ฉันไม่ได้รอด้วยมือเปล่า";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ขอพูดอีกครั้งหนึ่งว่า อย่าแปลกใจ";
		mes "ไม่เป็นไร ฉันจะดีใจถ้าคุณจะแปลกใจ";
		mes "หุหุหุหุหุ....";
		next;
		cutin "ep16_eisen03.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ตอนนี้มาดูของขวัญที่ฉันเตรียมไว้กันดีกว่า";
		close2;
		cutin "",255;
		if (is_party_leader() == true) {
				disablenpc instance_npcname("Eisen Werner#werner04");
				enablenpc instance_npcname("Central Entrance#door");
		}
		end;
}

1@slw,54,146,0	script	Central Entrance#door	WARPNPC,1,1,{
		end;
OnTouch:
		if (is_party_leader() == false) {
				warp 'map_slw$,55,153;
				end;
		}
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
				if ('event_final == 0) {
						'event_final = 1;
						enablenpc instance_npcname("Eisen Werner#werner05");
				}
				warp 'map_slw$,55,153;
				end;
		}
		switch( 'event_final ) {
		case 1:
				mes "การเล่นซ้ำเรื่องราว";
				close2;
				warp 'map_slw$,55,153;
				end;
		case 2:
				warp 'map_slw$,55,153;
				end;
		}
		mes "คุณมีสองทางเลือกในโซนถัดไป";
		mes "หากคุณต้องการดูเรื่องราวของ Seyren Windsor อีกครั้ง โปรดเลือก ^4d4dffดำเนินการต่อด้วยเรื่องราว^000000 หากคุณต้องการชมผลงานชิ้นเอกของ Eisen Werner โปรดเลือก ^4d4dffดำเนินการต่อด้วยการต่อสู้^000000";
		next;
		if (select( "Proceed with the story.", "Proceed with the battle." ) == 1) {
				mes "คุณได้เลือกเรื่องราวแล้ว";
				close2;
				if ('event_final == 0) {
						'event_final = 1;
						enablenpc instance_npcname("Eisen Werner#werner05");
				}
				warp 'map_slw$,55,153;
				end;
		}
		mes "คุณได้เลือกการต่อสู้แล้ว";
		close2;
		if ('event_final == 0) {
				'event_final = 2;
				enablenpc instance_npcname("Pet child#boss_master");
				enablenpc instance_npcname("#boss_master_dummy");
		}
		warp 'map_slw$,55,153;
		end;
}


// Room Boss
// * Story
1@slw,56,171,3	duplicate(dummy_npc)	Seyren Windsor#seiren	4_M_SEIREN_UC

1@slw,53,171,5	script	Eisen Werner#werner05	4_M_EISEN,{
		if (is_party_leader() == false) {
				mes "^4d4dffเฉพาะหัวหน้าปาร์ตี้เท่านั้นที่สามารถดำเนินการกับกิจกรรมนี้ได้^000000";
				close;
		}
		if (checkweight(501,1) == 0) {
				mes "- ไม่สามารถดำเนินการภารกิจต่อได้ เนื่องจากคุณมีไอเทมอยู่ในครอบครองมากเกินไป -";
				close;
		}
		.@seyren$ = instance_npcname("Seyren Windsor#seiren");
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "สถานที่แห่งนี้คือหัวใจหลักของห้องปฏิบัติการและเป็นที่เก็บข้อมูลการวิจัยของฉันทั้งหมด";
		npctalk "สถานที่แห่งนี้คือหัวใจหลักของห้องปฏิบัติการและเป็นที่เก็บข้อมูลการวิจัยของฉันทั้งหมด";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "มันคือชิ้นส่วนหัวใจของ Ymir ที่มีพลังพื้นฐานในการสร้างโลก และยังเป็นต้นกำเนิดของชีวิตอีกด้วย";
		npctalk "มันคือชิ้นส่วนหัวใจของ Ymir ที่มีพลังพื้นฐานในการสร้างโลก และยังเป็นต้นกำเนิดของชีวิตอีกด้วย";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ลองมองดูสิ่งนี้ดีๆ";
		mes "สิ่งที่คุณมาเพื่อเรียกร้องกลับคืนกำลังทำสิ่งมหัศจรรย์!";
		npctalk "ลองพิจารณาดูดีๆ สิ่งที่คุณได้มาเพื่อเอากลับคืนมานั้นเป็นสิ่งที่น่าอัศจรรย์!";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "พลังงานที่สกัดมาจากสิ่งนี้ไหลเข้าสู่วงเวทย์มนตร์บนพื้น... ฮ่าฮ่าฮ่า แน่นอนว่าวงเวทย์มนตร์นี้ถูกตีความใหม่ด้วยเวทย์มนตร์";
		npctalk "พลังงานที่สกัดมาจากสิ่งนี้ไหลเข้าสู่วงเวทย์มนตร์บนพื้น... ฮ่าฮ่าฮ่า แน่นอนว่าวงเวทย์มนตร์นี้ถูกตีความใหม่ด้วยเวทย์มนตร์";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ไม่ว่าจะอย่างไรก็ตาม พลังงานจะเข้าสู่ตัวทดลอง... โดยการใช้พลังนั้น ฉันได้ปรับเวลาของพวกเขาแล้ว";
		next;
		cutin "ep16_eisen03.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "...แน่นอนว่าบางคนตายเพราะเซลล์แตก ฮ่าๆ!";
		next;
		select("Is that all you want to say?");
		unittalk getcharid(3), "" + strcharinfo(0) + " : Is that all you want to say?";
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ไม่! ไม่เคย!";
		mes "คุณลืมสิ่งที่ฉันบอกคุณก่อนหน้านี้ไปแล้วหรือ?";
		mes "ฉันบอกคุณแล้วว่าฉันเตรียมของขวัญไว้แล้ว";
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "บางสิ่งบางอย่าง...";
		mes "นั่นจะทำให้คุณประหลาดใจ";
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		sleep2 3000;
		enablenpc .@seyren$;
		npctalk "-", .@seyren$;
		sleep2 1000;
		setpcblock PCBLOCK_NPC, false;
		unittalk getcharid(3), "" + strcharinfo(0) + " : ... Seyren?!";
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณชอบของขวัญที่ฉันเตรียมไว้ไหม?";
		mes "คุณพอใจหรือยัง?";
		cutin "ep16_eisen01.bmp",1;
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คนคนนี้เป็นหนึ่งในคณะผู้แทนราชวงศ์ที่โง่เขลานั้น";
		mes "ด้วยการทดลองของผู้กล้าบางคน ฉันจึงได้สิ่งที่ยอดเยี่ยมนี้มา";
		next;
		select("Are you talking about the In Vivo Experiment?");
		unittalk getcharid(3), "" + strcharinfo(0) + " : Are you talking about the In Vivo Experiment?";
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ใช่แล้ว แน่นอน";
		mes "มีคนมากมายที่ทำการทดลอง In Vivo นอกเหนือจากเพื่อนคนนั้น แต่เขาเป็นคนแรกที่กล้าพอที่จะปรับปรุงสิ่งมีชีวิต";
		next;
		cutin "ep16_eisen01.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "โดยใช้ชิ้นส่วนหัวใจเลียนแบบของ Ymir";
		mes "ถือเป็นจุดเปลี่ยนครั้งใหญ่สำหรับ In Vivo Experiments";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "ฉันพยายามจะนำไปใช้กับหลายๆ ชนิด";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อนึ่ง....";
		mes "คุณควรทักทายมันใช่ไหม?";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อ่า~. ถูกต้องแล้ว.";
		mes "ไม่ต้องทักทายกันแล้วนะครับ เพราะทั้งคู่จะต้องตายกันหมด!";
		cutin "ep16_eisen03.bmp",1;
		next;
		cutin "ep16_eisen02.bmp",1;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เซย์เรน พาแขกพวกนี้ไปนรกซะ";
		mes "คุณเข้าใจสิ่งที่ฉันพูดไหม?";
		next;
		cutin "ep16_seiren02.bmp",2;
		mes "[เซย์เรน]";
		mes "-";
		npctalk "-", .@seyren$;
		next;
		cutin "ep16_evt_ws.bmp",4;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อืม?";
		mes "นี่มันอะไร เซย์เรน ตอนนี้มันเป็นช่วงลบรึเปล่า";
		next;
		mes "[เซย์เรน]";
		mes "ฉันรู้สึกเหมือนตื่นจากการนอนหลับอันยาวนาน";
		mes "บางทีมันอาจจะเป็นสำหรับวันนี้";
		npctalk "ฉันรู้สึกเหมือนตื่นจากหลับใหลมายาวนาน บางทีอาจจะเพื่อวันนี้", .@seyren$;
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อย่าลืมหน้าที่ของคุณนะ เซย์เรน";
		mes "คุณคืออาวุธ คุณเป็นเพียงหุ่นเชิดที่สวมชุดอัศวินชื่อเซย์เรน วินด์เซอร์";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณต้องการให้ฉันอธิบายขั้นตอนต่อขั้นตอนไหม?";
		mes "หลังจากย้ายหุ่นจำลองหัวใจของ Ymir ร่างกายไม่สามารถทนต่อพลังนั้นได้และในที่สุดก็พังทลาย";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "จากนั้นวิญญาณของมันก็จำลองร่างหลักและแบ่งแยกออกไปอีกเรื่อยๆ";
		mes "ในระหว่างนั้น ความทรงจำ สติ วิญญาณ จิต และอัตตาของมันก็กระจัดกระจายไปเหมือนทราย กลายเป็นสิ่งที่ไร้ความหมาย";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เมื่อเราปลูกถ่ายหุ่นจำลองหัวใจของ Ymir ให้กับใครสักคนที่มีจิตใจและร่างกายที่แข็งแกร่ง เราก็คาดหวังว่าคนๆ นั้นจะกลายเป็นอาวุธชีวภาพที่แข็งแกร่งยิ่งขึ้น";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เราไม่เคยคาดคิดว่ามันจะขยายพันธุ์และแบ่งตัวออกไปอย่างไม่มีที่สิ้นสุด ดังนั้น การทดลองดังกล่าวจึงถูกมองว่าล้มเหลว";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "แย่ไปกว่านั้น มันเป็นสิ่งมีชีวิตที่ควบคุมไม่ได้และเต็มไปด้วยความโกรธเท่านั้น";
		next;
		mes "[เซย์เรน]";
		mes "ฉันอยู่ที่นี่.";
		npctalk "ฉันอยู่ที่นี่.", .@seyren$;
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณรู้ไหม? ตอนนี้พวกเขายังคงแบ่งแยกกันอยู่";
		mes "แค่เลียนแบบส่วนหลักก็เจริญเติบโตอยู่ภายในแล้ว";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เนื่องจากพวกเขายังไม่สมบูรณ์แบบในปัจจุบัน พวกเขาจึงต้องการที่จะสมบูรณ์แบบ";
		mes "ขณะนี้พวกเขาเป็นสิ่งมีชีวิตที่ไม่สมบูรณ์แบบ ดังนั้นพวกเขาจึงต้องการที่จะสมบูรณ์แบบ มากกว่าการเลียนแบบร่างกายหลักและมีชีวิตอยู่เป็นภาพเสมือน";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "พวกเขาต้องการดำรงอยู่เป็นหนึ่งเดียว";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "และการทดลองที่ประสบความสำเร็จเพียงอย่างเดียวคือคุณ เราเรียกคุณว่าเซย์เรนตอนนี้";
		next;
		cutin "ep16_seiren02.bmp",2;
		mes "[เซย์เรน]";
		mes "...... เป็นการสิ้นสุดความตั้งใจของคุณแล้วหรือ?";
		npctalk "...... เป็นการสิ้นสุดความตั้งใจของคุณแล้วหรือ?", .@seyren$;
		next;
		cutin "ep16_eisen03.bmp",0;
		mes "[ไอเซน แวร์เนอร์]";
		mes "เฮ้ ที่รัก คุณทำแบบนี้กับฉันไม่ได้นะ";
		mes "คุณจะต้องเชื่อฟังฉัน!";
		next;
		cutin "ep16_eisen02.bmp",0;
		mes "[ไอเซน แวร์เนอร์]";
		mes "คุณจำไม่ได้เหรอว่าฉันดิ้นรนขนาดไหนเพื่อพาคุณออกมาจากนรกนั่น?";
		mes "อย่าให้จิตสำนึกของคนที่ตายไปแล้วมาโน้มน้าวเราได้";
		next;
		cutin "ep16_seiren02.bmp",2;
		mes "[เซย์เรน]";
		mes "... ไอเซน แวร์เนอร์";
		mes "ขอถามอีกครั้งว่านี่คือจุดสิ้นสุดของพินัยกรรมของคุณแล้วใช่หรือไม่";
		npctalk "...ไอเซน แวร์เนอร์ ขอถามอีกครั้ง นี่คือจุดสิ้นสุดของพินัยกรรมของคุณใช่ไหม", .@seyren$;
		next;
		cutin "ep16_eisen01.bmp",0;
		mes "[ไอเซน แวร์เนอร์]";
		mes "สาปแช่ง!";
		mes "ฉันไม่เคยคาดหวังสิ่งนี้!";
		mes "โลกนี้มันน่าตลกไม่ใช่เหรอ ฮ่าฮ่าฮ่าฮ่า";
		next;
		mes "[ไอเซน แวร์เนอร์]";
		mes "อย่าคิดว่าฉันจบที่นี่แล้ว";
		mes "ฉันไม่ต้องการผู้แพ้เหมือนกัน";
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		specialeffect EF_BEGINSPELL3;
		sleep2 1000;
		specialeffect EF_SCREEN_QUAKE;
		sleep2 1000;
		specialeffect EF_LORD;
		disablenpc instance_npcname("Eisen Werner#werner05");
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		npctalk "โอ้....", .@seyren$;
		mes "[เซย์เรน]";
		mes "ไอเซ็น เจ้าละทิ้งทุกสิ่งทุกอย่างเช่นนี้";
		npctalk "ไอเซ็น เจ้าละทิ้งทุกสิ่งทุกอย่างเช่นนี้", .@seyren$;
		specialeffect2 EF_DEVIL3;
		cutin "ep16_seiren01.bmp",2;
		next;
		mes "[เซย์เรน]";
		mes ".. อ่า...";
		npctalk ".. อ่า...", .@seyren$;
		cutin "ep16_seiren02.bmp",2;
		next;
		mes "[เซย์เรน]";
		mes "ความแข็งแกร่งเริ่มอ่อนลงอีกแล้ว";
		mes "ตอนนี้ ฉันมีข้อความเร่งด่วนสำหรับคุณมากกว่าสำหรับไอเซ็น";
		npctalk "ความแข็งแกร่งเริ่มอ่อนลงอีกแล้ว ตอนนี้ ฉันมีข้อความเร่งด่วนสำหรับคุณมากกว่าสำหรับไอเซ็น", .@seyren$;
		next;
		mes "[เซย์เรน]";
		mes "ฉันรอคุณมานานแล้ว";
		mes "อย่างที่ไอเซ็นพูดไว้ ไม่สำคัญอีกต่อไปว่าฉันจะเป็นเซย์เรนหรือไม่";
		npctalk "ข้ารอเจ้ามานานแล้ว อย่างที่ไอเซ็นบอก ไม่สำคัญอีกต่อไปแล้วว่าฉันจะเป็นเซย์เรนหรือไม่", .@seyren$;
		next;
		mes "[เซย์เรน]";
		mes "เนื่องจากฉันมีความสามารถในการคิด ฉันจึงไม่ทราบว่าฉันเป็นใคร หรือว่าฉันมีอยู่เพื่ออะไร";
		next;
		specialeffect2 EF_DEVIL4;
		mes "[เซย์เรน]";
		mes "การแบ่งแยกอย่างต่อเนื่องทำให้เกิดสำเนาของฉันเองจำนวนมากมาย";
		next;
		mes "[เซย์เรน]";
		mes "พลังที่ผูกพันกับหุ่นจำลองของหัวใจแห่ง Ymir ซึ่งตอบสนองต่อความต้องการของฉันและของพวกพ้องของฉัน พวกเขาต้องการที่จะรวมกันเป็นหนึ่งแต่ก็แบ่งแยกกันซ้ำแล้วซ้ำเล่า";
		next;
		specialeffect2 EF_DEVIL5;
		cutin "ep16_seiren01.bmp",2;
		mes "[เซย์เรน]";
		mes "มีการสร้างสำเนาของฉันไปแล้วกี่ฉบับ?";
		next;
		cutin "ep16_seiren02.bmp",2;
		mes "[เซย์เรน]";
		mes "ฉันสูญเสียอัตตาไปหลังจากการแบ่งแยกอย่างต่อเนื่อง แต่กลับฟื้นคืนมาในรูปแบบเดิม ฉันไม่คิดว่ามันเป็นเรื่องบังเอิญ";
		next;
		mes "[เซย์เรน]";
		mes "ฉันคิดว่าต้องมีเหตุผลบางอย่าง";
		next;
		mes "[เซย์เรน]";
		mes "แล้วฉันก็ตระหนักถึงมันหลังจากดู ^4d4dffTerra Gloria^000000";
		mes "เหตุผลที่พระเจ้ามอบโอกาสนี้ให้กับฉัน";
		next;
		specialeffect2 EF_DEVIL6;
		cutin "ep16_seiren01.bmp",2;
		mes "[เซย์เรน]";
		mes "คุณมาเอาคืนมันเหรอ?";
		mes "... แผ่นดินสูญสิ้น เหลือเพียงดวงดาวเท่านั้น.";
		next;
		specialeffect2 EF_DEVIL7;
		cutin "ep16_seiren02.bmp",2;
		mes "[เซย์เรน]";
		mes "ไอเซ็นพูดถูก ^4d4dffเซย์เรน วินด์เซอร์ไม่อยู่ในโลกนี้อีกต่อไปแล้ว^000000";
		mes "ตอนนี้สิ่งเดียวที่ฉันมีคือความมุ่งมั่นอันแรงกล้าที่จะถ่ายทอดบางสิ่งบางอย่างต่อไป";
		next;
		specialeffect2 EF_SCREEN_QUAKE;
		specialeffect2 EF_DEVIL8;
		cutin "ep16_seiren01.bmp",2;
		mes "[เซย์เรน]";
		mes "บางทีความปรารถนาเล็กๆ น้อยๆ นั้นอาจทำให้ฉันมีตัวตนอยู่";
		mes "แต่ตอนนี้มันจบไปแล้ว.";
		cutin "ep16_seiren02.bmp",2;
		next;
		specialeffect2 EF_DEVIL9;
		cutin "ep16_seiren01.bmp",2;
		mes "[เซย์เรน]";
		mes "-";
		next;
		mes "[เซย์เรน]";
		mes "ฉันไม่มีเวลาอีกแล้วจริงๆ";
		mes "หลังจากวินาทีนี้ ^4d4dffI จะถูกแบ่งแยกและกระจัดกระจายอีกครั้ง^000000";
		next;
		mes "[เซย์เรน]";
		mes "ขณะนี้ฉันยังคงมีพินัยกรรมอยู่ ฉันมีข้อความหนึ่งที่จะบอกคุณ";
		next;
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
				mes "[เซย์เรน]";
				mes "โปรดดำเนินการต่อไป.";
				mes "เซย์เรน วินด์เซอร์ อัศวินแห่งพรอนเทรา ล้มเหลวในภารกิจนี้";
				next;
				mes "[เซย์เรน]";
				mes "^4d4dffฉันและสหายของฉันทุกคนเสียชีวิตไปนานแล้ว";
				mes "โปรดอย่าเสียความรักไปกับร่างกายที่ตายไปแล้วเลย^000000";
				next;
				mes "[เซย์เรน]";
				mes "พวกเขาเป็นเหมือนผีที่ถูกปกคลุมด้วยเปลือก";
				mes "ฉันโชคดีที่มีโอกาสแสดงพินัยกรรมฉบับสุดท้ายของฉันต่อหน้าคุณ";
				next;
				mes "[เซย์เรน]";
				mes "พวกเขาคือ ^4d4dffrelics^000000 ของฉันและสหายของฉัน";
				mes "โปรดมอบให้กับครอบครัวของเราด้วย";
				if (is_party_leader() == true) {
						getitem 25179,1;	// Blessing_Star
						getitem 23087,1;	// Small_Leather_Bag
						erasequest 7739;// Find Star of Blessing!
						setquest 7740;// Star of Blessing obtained
						terra_gloria_main = 22;
				}
				close2;
				setpcblock PCBLOCK_NPC, true;
				cutin "",255;
				specialeffect EF_ICECRASH, AREA, .@seyren$;
				disablenpc .@seyren$;
				// monster 'map_slw$,56,171, "Seyren", SEYREN, 1;
				sleep2 1000;
				specialeffect2 EF_SCREEN_QUAKE;
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				specialeffect2 EF_SCREEN_QUAKE;
				specialeffect2 EF_RAIN_PARTICLE2;
				warp "que_swat",155,50;
				end;
		}
		// daily
		mes "[เซย์เรน]";
		mes "ดีใจที่ได้พบคุณตอนท้าย";
		mes "ขอบคุณนะคะ... บายค่ะ.";
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		specialeffect EF_ICECRASH, AREA, .@seyren$;
		cloakonnpc .@seyren$;
		// monster 'map_slw$,56,171, "Seyren", SEYREN, 1;
		sleep2 1000;
		specialeffect2 EF_SCREEN_QUAKE;
		sleep2 1000;
		specialeffect2 EF_SCREEN_QUAKE;
		specialeffect2 EF_RAIN_PARTICLE2;
		setpcblock PCBLOCK_NPC, false;
		if (is_party_leader() == true)
				'event_final = 0;
		mapannounce 'map_slw$, "The setting of the central room has been initialized.", bc_map,0xFF99;
		warpparty 'map_slw$,54,134,getcharid(1),'map_slw$,2,2;
		disablenpc .@seyren$;
		end;
}

// * Battle
1@slw,55,170,3	duplicate(dummy_npc)	Pet child#boss_master	3621

1@slw,55,170,3	script	#boss_master_dummy	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch:
		if (is_party_leader() == false)	// officially player can spawn multiple boss changing party leader
				end;
		setpcblock PCBLOCK_NPC, true;
		.@npc_name$ = instance_npcname("Pet child#boss_master");
		npctalk "เข้ามาได้ยังไงเนี่ย กล้ามากนะ!", .@npc_name$;
		sleep2 3000;
		npctalk "คุณต้องเป็นเกียรติที่ได้ต่อสู้กับฉัน!", .@npc_name$;
		sleep2 3000;
		npctalk "-", .@npc_name$;
		sleep2 3000;
		npctalk "..ไอ้เวอร์เนอร์บ้าเอ๊ย ทำไมแกเอาฉันมาผสมกับหมาป่าวะ", .@npc_name$;
		sleep2 3000;
		npctalk "เอ...ยังไงซะ คุณก็ตายห่าไปแล้ว", .@npc_name$;
		sleep2 3000;
		setpcblock PCBLOCK_NPC, false;
		disablenpc instance_npcname("#boss_master_dummy");
		npctalk "เตรียมตัวเลย!!!", .@npc_name$;
		specialeffect EF_DANCE_BLADE_ATK, AREA, instance_npcname("Pet child#boss_master");
		specialeffect EF_ALL_FULL_THROTTLE, AREA, instance_npcname("Pet child#boss_master");
		donpcevent instance_npcname("#boss_master_lab") + "::OnStart";
		mapannounce 'map_slw$, "The Magic field has been activated.", bc_map,0xFF99;
		cloakonnpc instance_npcname("Pet child#boss_master");
		end;
}

1@slw,1,1,3	script	#boss_master_lab	HIDDEN_WARP_NPC,5,5,{
		end;
OnStart:
		enablenpc instance_npcname("#boss_master_lab");
		if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + "::OnMobDead" ) < 1)
				monster 'map_slw$,55,170, "Pet child", 3621, 1, instance_npcname("#boss_master_lab") + "::OnMobDead";	// EP16_2_MM_CUTIE
		end;
OnMobDead:
		if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + "::OnMobDead" ) < 1) {
				mapannounce 'map_slw$, "The Magic field has been eliminated. An entrance has been created.", bc_map,0xFF99;
				enablenpc instance_npcname("Emergency exit#exit_war");
				disablenpc instance_npcname("#boss_master_lab");
		}
		end;
}
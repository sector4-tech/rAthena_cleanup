//===== rAthena Script =======================================
//= Instance Ritual of Blessing.
//===== Description: =========================================
//- [Walkthrough conversion].
//- Required in Banquet main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

2@mir,100,17,5	script	Skia#skia01	4_EP16_SKIA,{
		mes "[สกีอา]";
		mes "ยินดีต้อนรับ!";
		mes "ฉันสงสัยว่าคุณจะมาถึงเมื่อไหร่";
		next;
		mes "[ไนฮิลล์]";
		mes "คุณอยู่ที่นี่!";
		mes "ส่วนคนอื่นๆจากครอบครัวอื่นก็ขึ้นไปนั่งกันหมดแล้ว";
		next;
		mes "[ไนฮิลล์]";
		mes "อนึ่ง.........";
		mes "หลังจากที่ฉันออกจากห้องจัดเลี้ยง";
		mes "....ไม่เป็นไร";
		npctalk "-", instance_npcname("Skia#skia01");
		next;
		mes "[ไนฮิลล์]";
		mes "ถ้าปล่อยให้พวกเขารอต่อไปก็คงไม่มีอะไรดีขึ้นมา เรารีบไปกันเถอะ";
		next;
		mes "[สกีอา]";
		mes "^4d4dffมีที่นั่งเหลืออยู่ถัดจากลอร์ดริชาร์ด ต้องเป็นที่นั่งที่สงวนไว้สำหรับผู้สังเกตการณ์^000000";
		next;
		mes "[สกีอา]";
		mes "เอาล่ะ ไปกันเถอะ.";
		close2;
		enablenpc instance_npcname("Skia Nerius#skia02");
		enablenpc instance_npcname("Nyhill M. Heine#nihil02");
		disablenpc instance_npcname("Skia#skia01");
		disablenpc instance_npcname("Nyhill#nihil01");
		end;
}
2@mir,103,17,3	duplicate(Skia#skia01)	Nyhill#nihil01	4_EP16_NIHIL

2@mir,107,77,0	script	#royal_evt02	HIDDEN_WARP_NPC,2,2,{
		end;
OnTouch_:
		disablenpc instance_npcname("#royal_evt02");
		donpcevent instance_npcname("#royal_evt02") + "::OnEvent";// player can move
		end;
OnEvent:
		disablenpc instance_npcname("Skia#skia01");	// extra
		disablenpc instance_npcname("Nyhill#nihil01");
		enablenpc instance_npcname("Skia Nerius#skia02");
		enablenpc instance_npcname("Nyhill M. Heine#nihil02");

		mapannounce 'map_2mir$, "Kronecker: Although there has been a slight delay, let me proceed.", bc_map,0xFF99;
		sleep 3000;
		mapannounce 'map_2mir$, "Kronecker: According to the Royal Assembly which has passed down in the palace,", bc_map,0xFF99;
		sleep 3000;
		npctalk "ไฮน์", instance_npcname("Friedrich S. Heine#stol_1");
		npctalk "ไฮน์", instance_npcname("Nyhill M. Heine#nihil02");
		sleep 3000;
		npctalk "เนริอุส", instance_npcname("Skia Nerius#skia02");
		npctalk "เนริอุส", instance_npcname("Spica Nerius#spica01");
		sleep 3000;
		npctalk "วอลเธอร์", instance_npcname("Isenhonor Walther#eisen");
		npctalk "วอลเธอร์", instance_npcname("Ian Walther#ian01");
		sleep 3000;
		npctalk "วิกเนอร์", instance_npcname("Levuiere Wigner#louvier1");
		npctalk "วิกเนอร์", instance_npcname("Jurgen Wigner#jurgen01");
		npctalk "วิกเนอร์", instance_npcname("Isaac Wigner#isaac01");
		npctalk "วิกเนอร์", instance_npcname("Katrin Wigner#kath01");
		sleep 3000;
		npctalk "จีโอบอร์ก", instance_npcname("Catherine D. Geoborg#ca1");
		npctalk "จีโอบอร์ก", instance_npcname("Ernst Geoborg#ernst01");
		sleep 3000;
		npctalk "ริชาร์ด", instance_npcname("Po Richard#po01");
		sleep 3000;
		npctalk "โรเกนเบิร์ก", instance_npcname("Maximilian Roegenburg#m1");
		npctalk "โรเกนเบิร์ก", instance_npcname("Wolf Roegenburg#wolf01");
		npctalk "โรเกนเบิร์ก", instance_npcname("Helmut Roegenburg#helmu");
		npctalk "โรเกนเบิร์ก", instance_npcname("Agnes Roegenburg#agnes0_1");
		sleep 3000;
		mapannounce 'map_2mir$, "Kronecker: On the behalf of the seven royal families supporting Rune-Midgarts,", bc_map,0xFF99;
		sleep 3000;
		mapannounce 'map_2mir$, "Kronecker: Here is the new representative appointed", bc_map,0xFF99;
		sleep 3000;
		mapannounce 'map_2mir$, "Kronecker: The blessing star light may reach the glory of the earth...", bc_map,0xFF99;
		sleep 3000;
		npctalk "ขอโปรดประทานพลังจากแหล่งให้สรรพสัตว์ทั้งหลาย", instance_npcname("Friedrich S. Heine#stol_1");
		npctalk "ขอโปรดประทานพลังจากแหล่งให้สรรพสัตว์ทั้งหลาย", instance_npcname("Nyhill M. Heine#nihil02");
		sleep 3000;
		npctalk "ความกล้าที่จะก้าวไปข้างหน้าโดยไม่หันหลังกลับและแทงศัตรูด้วยมีดสั้น", instance_npcname("Catherine D. Geoborg#ca1");
		npctalk "ความกล้าที่จะก้าวไปข้างหน้าโดยไม่หันหลังกลับและแทงศัตรูด้วยมีดสั้น", instance_npcname("Ernst Geoborg#ernst01");
		sleep 3000;
		npctalk "หัวใจที่จะโอบรับทุกสิ่งในโลกนี้", instance_npcname("Skia Nerius#skia02");
		npctalk "หัวใจที่จะโอบรับทุกสิ่งในโลกนี้", instance_npcname("Spica Nerius#spica01");
		sleep 3000;
		npctalk "พลังใจอันแข็งแกร่งที่จะปกป้องทุกสิ่งในอ้อมแขน", instance_npcname("Po Richard#po01");
		sleep 3000;
		npctalk "ความเย่อหยิ่งที่ไม่เคยอ่อนน้อมต่อความยุติธรรมและศรัทธาไม่ว่าอะไรจะเกิดขึ้น", instance_npcname("Isenhonor Walther#eisen");
		npctalk "ความเย่อหยิ่งที่ไม่เคยอ่อนน้อมต่อความยุติธรรมและศรัทธาไม่ว่าอะไรจะเกิดขึ้น", instance_npcname("Ian Walther#ian01");
		sleep 3000;
		npctalk "คุณธรรมของการมองผู้อื่นด้วยใจที่ถ่อมตน", instance_npcname("Maximilian Roegenburg#m1");
		npctalk "คุณธรรมของการมองผู้อื่นด้วยใจที่ถ่อมตน", instance_npcname("Wolf Roegenburg#wolf01");
		npctalk "คุณธรรมของการมองผู้อื่นด้วยใจที่ถ่อมตน", instance_npcname("Helmut Roegenburg#helmu");
		npctalk "คุณธรรมของการมองผู้อื่นด้วยใจที่ถ่อมตน", instance_npcname("Agnes Roegenburg#agnes0_1");
		sleep 3000;
		npctalk "และปัญญาที่จะมองเห็นความจริง", instance_npcname("Levuiere Wigner#louvier1");
		npctalk "และปัญญาที่จะมองเห็นความจริง", instance_npcname("Jurgen Wigner#jurgen01");
		npctalk "และปัญญาที่จะมองเห็นความจริง", instance_npcname("Isaac Wigner#isaac01");
		npctalk "และปัญญาที่จะมองเห็นความจริง", instance_npcname("Katrin Wigner#kath01");
		sleep 3000;
		mapannounce 'map_2mir$, "Kronecker: To our new king...", bc_map,0xFF99;
		sleep 1500;
		mapannounce 'map_2mir$, "I am sorry to interrupt the fun!", bc_map,0xDDDDDD;
		sleep 500;
		specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Peter Heine#peter01");
		disablenpc instance_npcname("Peter Heine#peter01");
		enablenpc instance_npcname("Fallen Peter#peter02");
		sleep 500;
		mapannounce 'map_2mir$, "Kronecker: Peter!!!", bc_map,0xFF99;
		sleep 500;
		disablenpc instance_npcname("Kronecker G. Heine#gran1");
		.@kronecker_2$ = instance_npcname("Kronecker G. Heine#gran2");
		enablenpc .@kronecker_2$;
		sleep 500;
		.@intruder$ = instance_npcname("Intruder#hunter01");
		enablenpc .@intruder$;
		npctalk "โอ้ สวัสดี! คุณคงเป็นอาร์ชดยุคแกรนท์ซผู้ชาญฉลาดผู้โด่งดังสินะ!", .@intruder$;
		sleep 3000;
		npctalk "คุณกำลังทำอะไร!!!", .@kronecker_2$;
		sleep 3000;
		npctalk "ไม่มีอะไรมาก ไม่มีอะไรน่าทำที่นี่อยู่แล้ว", .@intruder$;
		sleep 3000;
		npctalk "ฉันได้ยินมาว่ามีอะไรบางอย่างที่ดีมากๆ ซ่อนอยู่ที่นี่...", .@intruder$;
		sleep 3000;
		npctalk "แต่สิ่งนี้มันคืออะไร มันเป็นของปลอม!", .@intruder$;
		specialeffect EF_BEGINSPELL,AREA,.@kronecker_2$;
		sleep 500;
		specialeffect EF_BEGINSPELL6,AREA, .@intruder$;
		specialeffect EF_LEVEL99,AREA, .@intruder$;
		sleep 2000;
		npctalk "ท่านผู้เฒ่าและบรรดาผู้มีเกียรติที่อยู่เบื้องหลังท่าน...", .@intruder$;
		sleep 3000;
		npctalk "หยุดไว้ตรงนั้น มันไม่สนุกเลย ฉันอาจอยากทำลายอะไรบางอย่าง", .@intruder$;
		sleep 3000;
		npctalk "ตัวอย่างเช่น...", .@intruder$;
		sleep 3000;
		npctalk "ของปลอมนี่มันเหมือนหัวใจของ Ymir นะ!", .@intruder$;
		sleep 3000;
		npctalk "...คุณต้องการอะไร?", .@kronecker_2$;
		sleep 3000;
		npctalk "ชิ้นส่วนของหัวใจของ Ymir ฉันได้ยินมาว่ามันอยู่ที่นี่ แต่ฉันไม่เห็นเลยใช่ไหม?", .@intruder$;
		sleep 3000;
		npctalk "...คุณกำลังพูดเรื่องอะไร?", .@kronecker_2$;
		sleep 3000;
		npctalk "โอ้ คุณไม่รู้เหรอ?", .@intruder$;
		sleep 3000;
		npctalk "เข้าใจแล้ว ถ้าคุณรู้แบบนั้น คุณคงไม่ฝังมันไว้ในดิน เสียเวลาไปกับพิธีกรรมนี้หรอก", .@intruder$;
		sleep 3000;
		npctalk "แต่ผมช่วยไม่ได้นะคุณลุง นี่มันของปลอม", .@intruder$;
		sleep 2000;
		npctalk "...คุณกล้าดียังไง...", .@kronecker_2$;
		sleep 2000;
		npctalk "อย่ามองฉันแบบนั้นสิ ฉันก็เสียเวลาที่นี่เหมือนกันนะรู้ไหม", .@intruder$;
		sleep 3000;
		npctalk "เอาล่ะ คราวนี้ฉันจะไปแบบไม่เปิดฉากอะไรก่อน", .@intruder$;
		sleep 3000;
		npctalk "ฮ่าๆ พลังทั้งหมดจะมีประโยชน์อะไรเมื่อคุณไม่รู้ว่าจะใช้มันอย่างไร ฉันเดาว่าคุณคงไม่จำเป็นต้องใช้มันอีกต่อไป", .@intruder$;
		sleep 3000;
		npctalk "ถึงเวลาที่หัวใจปลอมของ Ymir จะต้องจากไป!", .@intruder$;
		sleep 500;
		specialeffect EF_BEGINSPELL3,AREA, .@intruder$;
		sleep 500;
		specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Peter Heine#peter01");
		specialeffect EF_LORD,AREA, .@kronecker_2$;
		specialeffect EF_LORD,AREA, instance_npcname("Levuiere Wigner#louvier1");
		specialeffect EF_LORD,AREA, instance_npcname("Catherine D. Geoborg#ca1");

		disablenpc instance_npcname("Ian Walther#ian01");
		disablenpc instance_npcname("Levuiere Wigner#louvier1");
		disablenpc instance_npcname("Jurgen Wigner#jurgen01");
		disablenpc instance_npcname("Catherine D. Geoborg#ca1");
		disablenpc instance_npcname("Katrin Wigner#kath01");
		disablenpc instance_npcname("Isaac Wigner#isaac01");

		enablenpc instance_npcname("Ian Walther#ian02");
		enablenpc instance_npcname("Levuiere Wigner#louvier2");
		enablenpc instance_npcname("Jurgen Wigner#jurgen02");
		enablenpc instance_npcname("Catherine D. Geoborg#ca2");
		enablenpc instance_npcname("Katrin Wigner#kath02");
		enablenpc instance_npcname("Isaac Wigner#isaac02");
		sleep 500;
		specialeffect EF_LORD,AREA, instance_npcname("Maximilian Roegenburg#m1");
		disablenpc instance_npcname("Ernst Geoborg#ernst01");
		disablenpc instance_npcname("Maximilian Roegenburg#m1");
		disablenpc instance_npcname("Wolf Roegenburg#wolf01");
		disablenpc instance_npcname("Agnes Roegenburg#agnes0_1");

		enablenpc instance_npcname("Maximilian Roegenburg#m2");
		enablenpc instance_npcname("Ernst Geoborg#ernst02");
		enablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
		enablenpc instance_npcname("Wolf Roegenburg#wolf02");

		specialeffect EF_LORD,AREA, instance_npcname("Agnes Roegenburg#agnes0_2");

		mapannounce 'map_2mir$, "Intruder: Then ladies and gentlemen, have a good time!", bc_map,0xDDDDDD;
		disablenpc .@intruder$;
		enablenpc instance_npcname("Intruder#hunter02");
		enablenpc instance_npcname("Intruder#hunter03");
		enablenpc instance_npcname("Intruder#hunter04");
		enablenpc instance_npcname("Intruder#hunter05");
		enablenpc instance_npcname("Intruder#hunter06");

		sleep 500;
		donpcevent instance_npcname("Intruder_main") + "::OnEvent";

		disablenpc instance_npcname("Po Richard#po01");
		disablenpc instance_npcname("Spica Nerius#spica01");
		disablenpc instance_npcname("Isenhonor Walther#eisen");
		disablenpc instance_npcname("Friedrich S. Heine#stol_1");
		disablenpc instance_npcname("Helmut Roegenburg#helmu");
		disablenpc instance_npcname("Skia Nerius#skia02");
		disablenpc instance_npcname("Nyhill M. Heine#nihil02");

		enablenpc instance_npcname("Skia Nerius#skia03");
		enablenpc instance_npcname("Nyhill M. Heine#nihil03");
		enablenpc instance_npcname("Spica Nerius#spica02");
		enablenpc instance_npcname("Po Richard#po02");
		enablenpc instance_npcname("Isenhonor Walther#eisen2");
		enablenpc instance_npcname("Friedrich S. Heine#stol_2");
		enablenpc instance_npcname("Helmut Roegenburg#helmu2");
		end;
}

2@mir,1,1,0	script	Intruder_main	-1,{
		end;
OnEvent:
		enablenpc instance_npcname("Intruder_main");
		.@label$ = instance_npcname("Intruder_main") + "::OnMobDead";
		monster 'map_2mir$,107,73,"Suspicious Intruder",3454,1, .@label$;	// HEART_HUNTER_N
		setunitdata $@mobid[0], UMOB_HP, 10000;
		monster 'map_2mir$, 98,73,"Suspicious Intruder",3454,1, .@label$;
		setunitdata $@mobid[0], UMOB_HP, 10000;
		monster 'map_2mir$,101,83,"Suspicious Intruder",3454,1, .@label$;
		setunitdata $@mobid[0], UMOB_HP, 10000;
		'intruder_count = 0;

		donpcevent instance_npcname("Intruder#hunter02") + "::OnStart";
		donpcevent instance_npcname("Intruder#hunter03") + "::OnStart";
		donpcevent instance_npcname("Intruder#hunter04") + "::OnStart";
		donpcevent instance_npcname("Intruder#hunter05") + "::OnStart";
		donpcevent instance_npcname("Intruder#hunter06") + "::OnStart";
		end;
OnMobDead:
		'intruder_count++;
		switch( 'intruder_count ) {
		case 1:
				donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
				break;
		case 2:
				donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";
				break;
		case 3:
				donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
				break;
		}
		sleep 100;	// it seems there is a slight delay

		switch( 'intruder_count ) {
		case 3:
				mapannounce 'map_2mir$, "Fellows! Let's leave this place!", bc_map,0xDDDDDD;
				// display the effect a second time
				donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
				donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";

				disablenpc instance_npcname("Intruder#hunter02");
				disablenpc instance_npcname("Intruder#hunter03");
				disablenpc instance_npcname("Intruder#hunter04");
				disablenpc instance_npcname("Intruder#hunter05");
				disablenpc instance_npcname("Intruder#hunter06");
				break;
		default:
				break;
		}
		end;
}

2@mir,87,79,5	script	Intruder#hunter02	3454,{
		end;
OnStart:
		initnpctimer;
		end;
OnLeave:
		stopnpctimer;
		specialeffect EF_TEIHIT3;
		hideonnpc instance_npcname("Intruder#hunter02");	// officially disabled, changed to hideonnpc to display the effect a second time
		end;
OnTimer1000:
		specialeffect EF_PIERCE;
		end;
OnTimer2000:
		specialeffect EF_CONCENTRATION, AREA, instance_npcname("Isenhonor Walther#eisen2");
		end;
OnTimer4000:
		specialeffect EF_HOLYCROSS;
		end;
OnTimer5000:
		npctalk "มันไม่มีประโยชน์!";
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
		end;
OnTimer7000:
		specialeffect EF_SHIELDCHARGE;
		end;
OnTimer8000:
		specialeffect EF_CHAINCOMBO, AREA, instance_npcname("Isenhonor Walther#eisen2");
		end;
OnTimer10000:
		specialeffect EF_PIERCE;
		end;
OnTimer11000:
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
		npctalk "เราไม่มีเวลาคุยกัน!", instance_npcname("Isenhonor Walther#eisen2");
		initnpctimer;
		end;
}

2@mir,91,84,5	script	Intruder#hunter03	3454,{
		end;
OnStart:
		initnpctimer;
		end;
OnLeave:
		stopnpctimer;
		specialeffect EF_TEIHIT3;
		hideonnpc instance_npcname("Intruder#hunter03");
		end;
OnTimer1500:
		specialeffect EF_MAGICALATTHIT;
		end;
OnTimer2500:
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Skia Nerius#skia03");
		end;
OnTimer4500:
		specialeffect EF_HIT5;
		end;
OnTimer5500:
		specialeffect EF_HIT6, AREA, instance_npcname("Skia Nerius#skia03");
		end;
OnTimer7500:
		specialeffect EF_HIT2;
		end;
OnTimer8500:
		npctalk "มันไม่มีประโยชน์!";
		specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
		end;
OnTimer11100:
		specialeffect EF_HOLYCROSS;
		end;
OnTimer12000:
		specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
		initnpctimer;
		end;
}

2@mir,94,86,5	script	Intruder#hunter04	3454,{
		end;
OnStart:
		initnpctimer;
		end;
OnLeave:
		stopnpctimer;
		specialeffect EF_TEIHIT3;
		hideonnpc instance_npcname("Intruder#hunter04");
		end;
OnTimer1200:	// weird timer but ~correct
		specialeffect EF_MAGICALATTHIT;
		end;
OnTimer2200:
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Friedrich S. Heine#stol_2");
		end;
OnTimer3800:
		specialeffect EF_HIT5;
		end;
OnTimer4800:
		npctalk "เฮอะ!";
		specialeffect EF_HIT6, AREA, instance_npcname("Friedrich S. Heine#stol_2");
		end;
OnTimer8200:
		specialeffect EF_HIT2;
		end;
OnTimer9200:
		specialeffect EF_HIT1, AREA, instance_npcname("Friedrich S. Heine#stol_2");
		end;
OnTimer11500:
		specialeffect EF_HOLYCROSS;
		end;
OnTimer12500:
		specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Friedrich S. Heine#stol_2");
		npctalk "...ไอ้เวรพวกนี้มันมาจากไหนวะ", instance_npcname("Friedrich S. Heine#stol_2");
		initnpctimer;
		end;
}

2@mir,107,86,5	script	Intruder#hunter05	3454,{
		end;
OnStart:
		initnpctimer;
		end;
OnLeave:
		stopnpctimer;
		specialeffect EF_TEIHIT3;
		hideonnpc instance_npcname("Intruder#hunter05");
		end;
OnTimer1000:
		specialeffect EF_MAGICALATTHIT;
		end;
OnTimer2000:
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Helmut Roegenburg#helmu2");
		end;
OnTimer4000:
		npctalk "ตาย!!!";
		specialeffect EF_HIT5;
		end;
OnTimer4500:
		npctalk "ดูสิว่าใครกำลังพูด!!!", instance_npcname("Helmut Roegenburg#helmu2");
		specialeffect EF_HIT6, AREA, instance_npcname("Helmut Roegenburg#helmu2");
		end;
OnTimer7000:
		specialeffect EF_HIT2;
		end;
OnTimer8000:
		specialeffect EF_HIT1, AREA, instance_npcname("Helmut Roegenburg#helmu2");
		end;
OnTimer10000:
		specialeffect EF_HOLYCROSS;
		end;
OnTimer11000:
		specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Helmut Roegenburg#helmu2");
		npctalk "อ๊าก!!!", instance_npcname("Helmut Roegenburg#helmu2");
		initnpctimer;
		end;
}

2@mir,110,85,5	script	Intruder#hunter06	3454,{
		end;
OnStart:
		initnpctimer;
		end;
OnLeave:
		stopnpctimer;
		specialeffect EF_TEIHIT3;
		hideonnpc instance_npcname("Intruder#hunter06");
		end;
OnTimer1000:
		specialeffect EF_HITDARK;
		end;
OnTimer2000:
		specialeffect EF_TEIHIT1X, AREA, instance_npcname("Po Richard#po02");
		end;
OnTimer4000:
		npctalk "ตาย!!!";
		specialeffect EF_LIGHTBOLT;
		end;
OnTimer5000:
		specialeffect EF_HIT6, AREA, instance_npcname("Po Richard#po02");
		end;
OnTimer7000:
		specialeffect EF_COMBOATTACK1;
		end;
OnTimer8000:
		specialeffect EF_HIT1, AREA, instance_npcname("Po Richard#po02");
		end;
OnTimer10000:
		specialeffect EF_NAPALMBEAT;
		end;
OnTimer12000:
		npctalk "โอ้ มันดูไม่ดีเลย", instance_npcname("Po Richard#po02");
		specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Po Richard#po02");
		initnpctimer;
		end;
}

2@mir,97,80,7	script	Friedrich S. Heine#stol_1	4_EP16_STOLZ,{
		npctalk "... เงียบ.";
		end;
}

2@mir,88,78,2	script	Isenhonor Walther#eisen2	4_M_1STPRIN1,{
		if ('intruder_count < 3) {
				npctalk "เราไม่มีเวลาคุยกัน!";
				end;
		}
		mes "[อิเซนฮอเนอร์]";
		mes "เกิดอะไรขึ้นนะ...";
		mes "เราจะต้องออกจากที่นี่ก่อน";
		next;
		mes "[อิเซนฮอเนอร์]";
		mes "ทุกคนสบายดีกันมั้ย...";
		close2;
		disablenpc instance_npcname("Isenhonor Walther#eisen2");
		disablenpc instance_npcname("Ian Walther#ian02");
		end;
}

2@mir,106,85,7	script	Helmut Roegenburg#helmu2	4_EP16_HELMUT,{
		if ('intruder_count < 3) {
				npctalk "หืม!";
				end;
		}
		mes "[เฮลมุท]";
		mes "ดูเหมือนทุกอย่างจะดีไปหมด";
		mes "...ฉันจะไปแล้วล่ะ พี่น้องดูไม่ค่อยดีเลย";
		close2;
		disablenpc instance_npcname("Helmut Roegenburg#helmu2");
		disablenpc instance_npcname("Wolf Roegenburg#wolf02");
		disablenpc instance_npcname("Maximilian Roegenburg#m2");
		disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
		end;
}

2@mir,92,83,2	script	Skia Nerius#skia03	4_EP16_SKIA,{
		if ('intruder_count < 3) {
				npctalk "...เอ๊ะ...มันมาจากไหนเนี่ย?";
				end;
		}
		mes "[สกีอา]";
		mes "โอ้โห ออกไปจากที่นี่กันเถอะ";
		mes "พี่สาว ออกไปกันเถอะ!";
		mes "ไนฮิลล์ คุณควรมาพร้อมกับพวกเรา!";
		next;
		mes "[สกีอา]";
		mes "คนอื่นจะออกไปเอง!";
		close2;
		disablenpc instance_npcname("Skia Nerius#skia03");
		disablenpc instance_npcname("Spica Nerius#spica02");
		end;
}

2@mir,109,84,7	script	Po Richard#po02	4_M_6THPRIN1,{
		if ('intruder_count < 3) {
				npctalk "...ฮ่าๆ ไม่คิดว่าจะได้สู้ที่นี่นะเนี่ย!";
				end;
		}
		npctalk "ดูเหมือนว่าห้องโถงจะโล่งแล้ว ฉันเลยต้องออกไป คราวนี้วุ่นวายจัง!";
		sleep 2000;
		disablenpc instance_npcname("Po Richard#po02");
		end;
}

2@mir,95,85,1	script	Friedrich S. Heine#stol_2	4_EP16_STOLZ,{
		if ('intruder_count < 3) {
				npctalk "โปรดดูแลคนอื่นๆด้วย";
				end;
		}
		mes "[ฟรีดริช]";
		mes "...ดูเหมือนว่าจะไม่มีใครตายเลย";
		npctalk "รออะไรอยู่ล่ะ รีบออกไปจากที่นี่พร้อมกับผู้บาดเจ็บซะ!";
		close2;
OnDisable:
		disablenpc instance_npcname("Skia Nerius#skia03");
		disablenpc instance_npcname("Spica Nerius#spica02");
		disablenpc instance_npcname("Isenhonor Walther#eisen2");
		disablenpc instance_npcname("Ian Walther#ian02");
		disablenpc instance_npcname("Levuiere Wigner#louvier2");
		disablenpc instance_npcname("Jurgen Wigner#jurgen02");
		disablenpc instance_npcname("Katrin Wigner#kath02");
		disablenpc instance_npcname("Ernst Geoborg#ernst02");
		disablenpc instance_npcname("Catherine D. Geoborg#ca2");
		disablenpc instance_npcname("Po Richard#po02");
		disablenpc instance_npcname("Maximilian Roegenburg#m2");
		disablenpc instance_npcname("Wolf Roegenburg#wolf02");
		disablenpc instance_npcname("Helmut Roegenburg#helmu2");
		disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
		disablenpc instance_npcname("Isaac Wigner#isaac02");
		end;
}

2@mir,101,93,7	script	Kronecker G. Heine#gran2	4_EP16_GRANZ,{
		if ('intruder_count < 3) {
				mes "[โครเนกเกอร์]";
				mes "ปีเตอร์อยู่ในอาการวิกฤต";
				mes "...คุณช่วยต่อสู้กับผู้บุกรุกแทนเราได้ไหม?";
				close;
		}
		if (isbegin_quest(7692) == 1) {
				mes "[โครเนกเกอร์]";
				mes "-";
				mes ".... ^4d4dffTerra Gloria^000000 หายไป...";
				next;
				select("Terra Gloria?");
				mes "[โครเนกเกอร์]";
				mes "เป็นมงกุฎที่สืบทอดมาจากพระราชวังของรูน-มิดการ์ต";
				mes "มันมีความหมายมากกว่ามงกุฎธรรมดา";
				next;
				mes "[โครเนกเกอร์]";
				mes "โดนเขาเอาไปแล้วเหรอ?";
				mes "อะไรบนโลกนี้...!";
				next;
				mes "[โครเนกเกอร์]";
				mes "สิ่งที่ซ่อนอยู่ที่นี่ไม่มีใครทราบ...ยกเว้นราชวงศ์ทั้งเจ็ด...!";
				next;
				mes "[โครเนกเกอร์]";
				mes "มันหมายความว่า...";
				mes "...อันดับแรกสิ่งสำคัญคือมีเรื่องที่ต้องค้นหา";
				next;
				mes "[โครเนกเกอร์]";
				mes "ออกไปจากที่นี่กันเถอะ";
				mes "มีสิ่งเหลือให้ค้นหาอีกมากมาย";
				erasequest 7692;
				setquest 7693;// Surprise Attack
				ep16_royal = 9;
				next;
				mes "[โครเนกเกอร์]";
				mes "ใครออกจากที่นี่ไป ขอให้เคลียร์คนบาดเจ็บที่อยู่ใกล้ๆ คุณออกไปด้วย!";
				mes "ห้องแห่งจิตสำนึกจะปิดชั่วคราว!";
				donpcevent instance_npcname("Friedrich S. Heine#stol_2") + "::OnDisable";
				disablenpc instance_npcname("Nyhill M. Heine#nihil03");
				close;
		}
		mes "[โครเนกเกอร์]";
		mes "ปีเตอร์ได้รับบาดเจ็บ";
		mes "เราจะต้องย้ายเขาอย่างรวดเร็ว";
		next;
		mes "[โครเนกเกอร์]";
		mes "ฉันจะพบคุณที่นั่น";
		mes "คุณสามารถออกไปได้ทางประตูที่คุณเข้ามา";
		mes "มาพบกันใหม่ในความทรงจำในอดีตของราชวงศ์ครับ";
		close;
}

2@mir,93,80,2	script	Nyhill M. Heine#nihil03	4_EP16_NIHIL,4,4,{
		if ('intruder_count < 3) {
				npctalk "...โอ้...พระเจ้า...";
				end;
		}
		mes "[ไนฮิลล์]";
		mes "...นี่... ทั้งหมดก็เป็น... ความผิดของฉัน...";
		close;
OnTouch_:
		npctalk "...ผมจะช่วยได้ไหม?";
		npcskill "AL_HEAL",11,99,60;
		npcskill "PR_KYRIE",10,99,60;
		end;
}

2@mir,97,65,5	script	Jurgen Wigner#jurgen02	4_M_4THPRIN1,{
		npctalk "ฉันกำลังนับถือคุณอยู่!";
		npcskill "AL_INCAGI",10,99,60;
		end;
}

2@mir,111,66,4	script	Catherine D. Geoborg#ca2	4_EP16_EGEO,{
		npctalk "...ฉันหวังว่ามันจะช่วยคุณได้";
		npcskill "AL_HEAL",10,130,115;
		end;
}

2@mir,107,61,6	script	Isaac Wigner#isaac02	4_EP16_ISAAC,{
		npctalk "ชื่นใจกันหน่อย!";
		npcskill "AL_BLESSING",10,99,60;
		end;
}

2@mir,112,71,1	script	Agnes Roegenburg#agnes0_2	4_EP16_AGNES,{
		npctalk "อ๊าก ระวัง!";
		end;
}

2@mir,96,83,2	script	Spica Nerius#spica02	4_EP16_SPICA,{
		if ('intruder_count < 3)
				npctalk "...ส...ข...อา...";
		else
				npctalk "สกาย่า ทำไมผู้ชายคนนี้ถึงช่วยลอร์ดไนฮิลล์ ไม่ใช่ฉัน?";
		end;
}


2@mir,102,97,3	script	Intruder#hunter01	3454,{
		end;
OnInstanceInit:
		'map_2mir$ = instance_mapname("2@mir");
		'intruder_count = 0;

		disablenpc instance_npcname("Spica Nerius#spica02");
		disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
		disablenpc instance_npcname("Isaac Wigner#isaac02");
		disablenpc instance_npcname("Catherine D. Geoborg#ca2");
		disablenpc instance_npcname("Jurgen Wigner#jurgen02");
		disablenpc instance_npcname("Kronecker G. Heine#gran2");
		disablenpc instance_npcname("Isenhonor Walther#eisen2");
		disablenpc instance_npcname("Helmut Roegenburg#helmu2");
		disablenpc instance_npcname("Po Richard#po02");
		disablenpc instance_npcname("Friedrich S. Heine#stol_2");

		disablenpc instance_npcname("Intruder#hunter01");
		disablenpc instance_npcname("Intruder#hunter02");
		disablenpc instance_npcname("Intruder#hunter03");
		disablenpc instance_npcname("Intruder#hunter04");
		disablenpc instance_npcname("Intruder#hunter05");
		disablenpc instance_npcname("Intruder#hunter06");

		disablenpc instance_npcname("Skia Nerius#skia03");
		disablenpc instance_npcname("Nyhill M. Heine#nihil03");
		end;
}

2@mir,96,83,0	script	dummy_npc_rituel_disabled	-1,{
		end;
OnInstanceInit:
		disablenpc();
		end;
}
2@mir,88,74,3	duplicate(dummy_npc_rituel_disabled)	Ian Walther#ian02	1_M_JOBTESTER
2@mir,86,70,4	duplicate(dummy_npc_rituel_disabled)	Levuiere Wigner#louvier2	4_EP16_LOUVIERE
2@mir,94,72,3	duplicate(dummy_npc_rituel_disabled)	Katrin Wigner#kath02	4_EP16_POE
2@mir,112,66,3	duplicate(dummy_npc_rituel_disabled)	Ernst Geoborg#ernst02	4_M_2NDPRIN1
2@mir,91,69,7	duplicate(dummy_npc_rituel_disabled)	Maximilian Roegenburg#m2	4_EP16_MAX
2@mir,94,66,3	duplicate(dummy_npc_rituel_disabled)	Wolf Roegenburg#wolf02	4_EP16_WOLF
2@mir,100,80,7	duplicate(dummy_npc_rituel_disabled)	Nyhill M. Heine#nihil02	4_EP16_NIHIL
2@mir,101,95,3	duplicate(dummy_npc_rituel_disabled)	Fallen Peter#peter02	4_EP16_PETER
2@mir,97,77,7	duplicate(dummy_npc_rituel_disabled)	Skia Nerius#skia02	4_EP16_SKIA


2@mir,101,88,4	duplicate(dummy_npc)	Kronecker G. Heine#gran1	4_EP16_GRANZ
2@mir,97,71,7	duplicate(dummy_npc)	Levuiere Wigner#louvier1	4_EP16_LOUVIERE
2@mir,100,71,7	duplicate(dummy_npc)	Jurgen Wigner#jurgen01	4_M_4THPRIN1
2@mir,97,68,7	duplicate(dummy_npc)	Isaac Wigner#isaac01	4_EP16_ISAAC
2@mir,100,68,7	duplicate(dummy_npc)	Katrin Wigner#kath01	4_EP16_POE
2@mir,103,80,2	duplicate(dummy_npc)	Catherine D. Geoborg#ca1	4_EP16_EGEO
2@mir,106,80,2	duplicate(dummy_npc)	Ernst Geoborg#ernst01	4_M_2NDPRIN1
2@mir,103,77,2	duplicate(dummy_npc)	Po Richard#po01	4_M_6THPRIN1
2@mir,103,74,2	duplicate(dummy_npc)	Maximilian Roegenburg#m1	4_EP16_MAX
2@mir,106,74,2	duplicate(dummy_npc)	Wolf Roegenburg#wolf01	4_EP16_WOLF
2@mir,101,94,0	duplicate(dummy_npc)	Peter Heine#peter01	4_M_3RDPRIN1
2@mir,100,77,7	duplicate(dummy_npc)	Spica Nerius#spica01	4_EP16_SPICA
2@mir,106,71,2	duplicate(dummy_npc)	Agnes Roegenburg#agnes0_1	4_EP16_AGNES
2@mir,100,74,7	duplicate(dummy_npc)	Ian Walther#ian01	1_M_JOBTESTER
2@mir,97,74,7	duplicate(dummy_npc)	Isenhonor Walther#eisen	4_M_1STPRIN1
2@mir,103,71,2	duplicate(dummy_npc)	Helmut Roegenburg#helmu	4_EP16_HELMUT

2@mir,101,10,0	script	to_prt_lib	WARPNPC,1,1,{
		if (isbegin_quest(7693) == 1) {
				warp "prt_lib",88,80;
				end;
		}
		mes "-" + strcharinfo(0) + "-";
		mes "เราควรดูพิธีการอวยพร";
		mes "อย่าเพิ่งออกไปตอนนี้";
		close;
}
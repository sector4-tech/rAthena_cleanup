//===== rAthena Script =======================================
//= Isle Of Bios
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Isle Of Bios Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

moro_cav,50,64,5	script	Yellow Seed#bios	CLEAR_NPC,{
		if (BaseLevel < 160) {
				mes "^ff0000ตัวละครที่มีระดับต่ำกว่า 160 ไม่สามารถเข้าดันเจี้ยนนี้ได้^000000";
				close;
		}
		if (isbegin_quest(15006) == 0) {
				mes "^ff0000คุณต้องพบกับชายชราเร่ร่อนก่อนจึงจะเข้าไปในคุกใต้ดินนี้ได้^000000";
				close;
		}
		.@party_id = getcharid(1);
		if (.@party_id < 1) {
				mes "^ff0000คุณต้องจัดตั้ง/เข้าร่วมปาร์ตี้ที่มีสมาชิกอย่างน้อย 1 คนก่อนจึงจะเข้าดันเจี้ยนนี้ได้^000000";
				close;
		}
		switch( checkquest(15005,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				if (checkquest(15007,PLAYTIME) == 2) {// 5 mins to re-enter
						mes "^ff0000ไม่สามารถเข้าถึงดันเจี้ยนแห่งความทรงจำนี้ได้ภายใน 23 ชั่วโมงหลังจากที่คุณเยี่ยมชมครั้งสุดท้าย^000000";
						close;
				}
				switch( checkquest(15008,PLAYTIME) ) {
				case -1:
						break;
				case 0:
				case 1:
						mes "^0000ffไม่สามารถเข้าไปยัง Memorial Dungeon ได้ภายใน 23 ชั่วโมงหลังจากที่คุณเยี่ยมชมครั้งสุดท้าย^000000";
						close;
				case 2:
						break;
				}
				break;
		case 2:
				erasequest 15005;// Memorial Dungeon: Isle of Bios
				erasequest 15007;// Memorial Dungeon: Isle of Bios
				if (isbegin_quest(15008) > 0)
						erasequest 15008;// Memorial Dungeon: Isle of Bios
				break;
		}
		if (is_party_leader() == true)
				.@create$ = "Create Memorial dungeon.";
		switch( select( .@create$, "Enter Isle of Bios.", "Cancel." ) ) {
		case 1:
				instance_create("Isle of Bios");
				mes "^ff0000สร้างดันเจี้ยนอนุสรณ์^000000";
				mes "^ff0000กรุณาคลิกและกด Enter อีกครั้ง^000000";
				close;
		case 2:
				switch( instance_enter("Isle of Bios") ) {
				case IE_NOMEMBER:
				case IE_NOINSTANCE:
				case IE_OTHER:
						if (is_party_leader(.@party_id) == false) {
								mes "^ff0000โปรดทราบว่าไม่สามารถเข้าไปยัง Memorial Dungeon ได้ภายใน 23 ชั่วโมงหลังจากการเยี่ยมชมครั้งสุดท้ายของคุณ^000000";
								next;
						}
						mes "^ff0000หัวหน้าปาร์ตี้ของคุณยังไม่ได้สร้าง Crack of Time ขึ้นมา^000000";
						close;
				case IE_OK:
						mapannounce "moro_cav", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering Isle of Bios.",bc_map,"0x00FF99";
						if (isbegin_quest(15005) == 0) {
								setquest 15005;// Memorial Dungeon: Isle of Bios
								setquest 15007;// Memorial Dungeon: Isle of Bios
						}
						end;
				}
		case 3:
				end;
		}
		end;

OnInit:
		while(true) {
				sleep 500;
				specialeffect EF_ENHANCE;
		}
		end;
}

moro_cav,45,60,5	script	Wandering Old Man#sara	4_M_EINOLD,{
		if (BaseLevel < 160) {
				mes "[ชายชราเร่ร่อน]";
				mes "-";
				mes "ฉันกลัวว่าคุณจะยังไม่พร้อมสำหรับสิ่งนี้";
				mes "คุณต้องมีระดับอย่างน้อย 160";
				close;
		}
		if (isbegin_quest(15006) == 0) {
				mes "[ชายชราเร่ร่อน]";
				mes "...ฉันอยู่ที่ไหน?";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "ฉันไม่รู้ว่ามันเริ่มเมื่อใด...";
				mes "ฝันร้ายแบบเดียวกันนี้คอยรบกวนฉันมาเป็นเวลานานแล้ว";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "ในฝันร้ายของฉัน ราชาปีศาจโมร็อคตื่นขึ้นจากการหลับใหลยาวนานนับศตวรรษ และทำให้โลกเต็มไปด้วยความมืดมิดและความสยองขวัญ";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "ฉันไม่อาจสลัดภาพอันชัดเจนที่ฉันเห็นในฝันร้ายนั้นออกไปได้ ฉันเดินเตร่ไปมาเหมือนคนบ้าและในที่สุดก็มาถึงที่นี่";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "ฉันเห็นสถานที่แห่งนี้ในความฝัน เป็นสถานที่ที่เทพปีศาจโมร็อคตื่นจากการหลับใหล ความฝันของฉันชี้ให้ฉันเห็นอนาคตที่เป็นไปได้";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "หากความฝันของฉันเป็นลางสังหรณ์จริง กองกำลังของเทพปีศาจโมร็อคก็อยู่ในเมล็ดสีเหลืองนั้น";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "ฉันพยายามจะเข้าไปในเมล็ดพันธุ์ แต่ไม่เป็นผล ฉันแก่เกินไปและอ่อนแอเกินไป แต่บางทีคุณอาจจะทำได้";
				next;
				mes "[ชายชราเร่ร่อน]";
				mes "กรุณาตรวจสอบเมล็ดสีเหลืองนี้";
				mes "การไปคนเดียวมันอันตรายเกินไป คุณควรพาเพื่อนไปด้วยอย่างน้อยหนึ่งคน";
				setquest 15006;// Isle of Bios Exploration
				completequest 15006;// Isle of Bios Exploration
				close;
		}
		mes "[ชายชราเร่ร่อน]";
		mes "กรุณาตรวจสอบเมล็ดสีเหลืองนี้";
		mes "การไปคนเดียวมันอันตรายเกินไป คุณควรพาเพื่อนไปด้วยอย่างน้อยหนึ่งคน";
		close;
}

// warps
1@dth1,291,102,0	warp2	warp1#bios1	2,2,1@dth2,17,93
1@dth2,291,102,0	warp2	warp1#bios2	2,2,1@dth3,45,68

// zone 1
1@dth1,74,94,3	script	Grim Reaper Ankou#bios1	3029,{ end; }
1@dth1,71,97,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios1	4_M_GUILLOTINE
1@dth1,67,97,5	duplicate(Grim Reaper Ankou#bios1)	Vrid#bios1	4_M_KNIGHT_BLACK
// 1@dth1,67,97,5	script	Vrid#bios1	4_M_KNIGHT_BLACK,5,7,{// official

// 1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,{// official
1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,5,7,{
		end;
OnTouch_:
		disablenpc instance_npcname("start1#bios1");
		'zeith$ = instance_npcname("Zeith#bios1");
		'vrid$ = instance_npcname("Vrid#bios1");
		'reaper$ = instance_npcname("Grim Reaper Ankou#bios1");
		initnpctimer;
		npctalk "วริด: อ้าว มีคนมาร่วมกับเราด้วยแล้ว", 'vrid$;
		end;
OnTimer2500:
		npctalk "วริด: คุณมาเพื่อหยุดยั้งการฟื้นคืนชีพของโมร็อคด้วยเหรอ?", 'vrid$;
		end;
OnTimer5000:
		npctalk "วริด: ฉันชื่อวริด และนี่คือไซธ์ จากพรอนเทรา พวกเราเดินทางไปทั่วโลกเพื่อต่อสู้เพื่อความยุติธรรม", 'vrid$;
		end;
OnTimer7500:
		npctalk "วริด: เฮ้ ไซต์ พูดอะไรหน่อยสิ", 'vrid$;
		end;
OnTimer10000:
		npctalk "ไซธ์: ...", 'zeith$;
		end;
OnTimer12500:
		npctalk "ไซธ์: ฉันชื่อไซธ์", 'zeith$;
		end;
OnTimer15000:
		npctalk "ไซธ์: ...", 'zeith$;
		end;
OnTimer17500:
		npctalk "วริด: ไม่ต้องสนใจเขาหรอก เขาไม่ค่อยเงียบขนาดนั้นเมื่อเราอยู่กันแค่สองคน", 'vrid$;
		end;
OnTimer19500:
		npctalk "ไซธ์: ... คุณพูดมากพอสำหรับเราทั้งสองคนแล้ว", 'zeith$;
		end;
OnTimer21000:
		npctalk "วริด: พวกเรามาที่นี่เพราะว่าผู้เฒ่าแก่ข้างล่างบ่นเรื่องการฟื้นคืนชีพของชาวโมร็อคอยู่ตลอดเวลา", 'vrid$;
		end;
OnTimer23500:
		npctalk "วริด: เอ่อ ที่นี่ที่ไหนนะ ฉันแค่แตะเมล็ดพันธุ์เท่านั้น ทิวทัศน์ก็เปลี่ยนไปทันที", 'vrid$;
		end;
OnTimer26000:
		npctalk "วริด: โอ้โห เราบินอยู่บนท้องฟ้าเหรอ?!", 'vrid$;
		end;
OnTimer28500:
		npctalk "???: *หัวเราะเบาๆ* อยากรู้มั้ยว่าคุณอยู่ที่ไหน?", 'reaper$;
		end;
OnTimer31000:
		npctalk "ไซธ์: ใครอยู่นั่น!", 'zeith$;
		end;
OnTimer33500:
		enablenpc 'reaper$;
		npctalk "ยมทูตอังโค: *หัวเราะเบาๆ* นี่คือเกาะแห่งไบโอส สถานที่ที่ข้าเสกขึ้นมาในใจ", 'reaper$;
		end;
OnTimer36000:
		npctalk "ยมทูตอันโค: เจ้าเดินเข้าไปในกับดักโดยเต็มใจที่จะเสียสละเพื่อลอร์ดโมร็อคของข้า เจ้าไม่กลัวตายหรือไง?", 'reaper$;
		end;
OnTimer38500:
		npctalk "วริด: หึ! ถ้าเป็นฉัน ฉันคงไม่มาด้วยความเต็มใจหรอกใช่ไหม", 'vrid$;
		end;
OnTimer41000:
		npctalk "วริด: ไม่ว่าคุณจะเป็นใคร เราจะฆ่าคุณก่อนที่จะถึง!", 'vrid$;
		end;
OnTimer43500:
		npctalk "ยมทูตอันโค: อย่าล่อลวงฉันนะมนุษย์", 'reaper$;
		end;
OnTimer46000:
		npctalk "ยมทูตอันโค: ฉันอยากเห็นเธอตายอย่างช้าๆ และทรมาน *หัวเราะคิกคัก*", 'reaper$;
		end;
OnTimer48500:
		disablenpc 'reaper$;
		end;
OnTimer51000:
		npctalk "วริด: เขาเป็นใคร เขากำลังพยายามคืนชีพชาวโมร็อคอยู่หรือเปล่า", 'vrid$;
		end;
OnTimer53500:
		npctalk "วริด: ไล่ตามมันมาเถอะ ไซธ์ รีบๆ เข้า!", 'vrid$;
		end;
OnTimer54000:
		disablenpc 'vrid$;
		end;
OnTimer56000:
		npctalk "ไซธ์: ... แอคครับ", 'zeith$;
		end;
OnTimer58500:
		npctalk "ไซธ์: ฉันเกลียดการวิ่ง", 'zeith$;
		end;
OnTimer61000:
		disablenpc 'zeith$;
		end;
OnTimer66000:
		stopnpctimer;
		mapannounce 'map_dth1$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00";
		'zeith$ = 'vrid$ = 'reaper$ = "";
		end;
}

1@dth1,1,1,0	script	mobs#bios1	HIDDEN_WARP_NPC,{
		end;
OnStart:
		.@label$ = instance_npcname("mobs#bios1") + "::OnMyMobDead";
		for ( .@mob_id = 3010; .@mob_id < 3013; .@mob_id++ ) {
				switch(.@mob_id) {
				case 3010:		// Corrupt Orc Baby
						setarray .@coord[0],
								90,94, 153,76, 121,53, 135,50, 145,107, 132,126, 101,125, 119,144, 124,164, 192,145, 194,116,
								203,131, 214,142, 196,156, 217,129, 227,150, 215,109, 240,130, 252,105, 249,144, 249,155, 232,87,
								316,103, 245,71, 246,71, 210,62, 214,53, 211,88;
						break;
				case 3011:		// Corrupt Baby Desert Wolf
						setarray .@coord[0],
								90,94, 121,69, 153,76, 166,80, 131,101, 145,107, 108,156, 141,143, 165,130, 203,151,
								203,162, 219,151, 203,162, 206,169, 219,171, 227,150, 238,117, 240,140, 274,104, 306,103,
								237,80, 224,62, 233,57;
						break;
				case 3012:		// Corrupt Familiar
						setarray .@coord[0],
								143,49, 156,46, 170,67, 170,67, 131,101, 144,125, 101,125, 108,156, 135,152, 144,164,
								184,130, 204,112, 227,150, 205,98, 225,103, 228,124, 243,106, 225,89, 297,103, 223,72,
								232,69, 225,54, 211,73;
						break;
				}
				.@size = getarraysize(.@coord);
				for ( .@i = 0; .@i < .@size; .@i += 2 )
						monster 'map_dth1$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
		}
		end;

OnMyMobDead:
		.@count = mobcount( 'map_dth1$,instance_npcname("mobs#bios1") + "::OnMyMobDead" );
		if (.@count == 10 || .@count == 5)
				mapannounce 'map_dth1$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
		else if (.@count == 1)
				mapannounce 'map_dth1$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00";
		else if (.@count == 0) {
				mapannounce 'map_dth1$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00";
				disablenpc instance_npcname("mobs#bios1");
				enablenpc instance_npcname("warp1#bios1");
				enablenpc instance_npcname("start1#bios2");
				donpcevent instance_npcname("mobs#bios2") + "::OnStart";	// spawn at entrance
		}
		end;
}

// zone 2
1@dth2,67,97,0	script	start1#bios2	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc instance_npcname("start1#bios2");
		mapannounce 'map_dth2$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00";
		end;
}

1@dth2,1,1,0	script	mobs#bios2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("mobs#bios2");
		.@label$ = instance_npcname("mobs#bios2") + "::OnMyMobDead";
		for ( .@mob_id = 3013; .@mob_id < 3016; ++.@mob_id ) {
				switch(.@mob_id) {
				case 3013:		// Corrupt Orc Warrior
						setarray .@coord[0],
								90,94, 153,76, 116,66, 133,47, 145,107, 137,134, 101,125, 119,144, 124,164, 194,116,
								203,131, 211,88, 215,109, 214,142, 192,145, 196,156, 217,129, 227,150, 230,89, 210,62,
								214,53, 246,71, 248,81, 252,105, 246,126, 249,144, 249,155, 316,103;
						break;
				case 3014:		// Corrupt Desert Wolf
						setarray .@coord[0],
								90,94, 121,69, 153,76, 165,84, 145,107, 131,101, 108,156, 138,140, 165,130, 203,151,
								203,162, 219,151, 206,169, 203,162, 227,150, 222,164, 238,117, 237,80, 224,62, 233,57,
								236,138, 274,104, 306,103;
						break;
				case 3015:		// Corrupt Phen
						setarray .@coord[0],
								142,54, 156,46, 170,67, 170,67, 131,101, 101,125, 108,156, 135,152, 144,164, 135,131,
								184,130, 204,112, 205,98, 225,103, 225,89, 227,150, 232,69, 223,72, 212,75, 225,54,
								247,110, 232,126, 297,103;
						break;
				}
				.@size = getarraysize(.@coord);
				for ( .@i = 0; .@i < .@size; .@i += 2 )
						monster 'map_dth2$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
		}
		end;

OnMyMobDead:
		.@count = mobcount( 'map_dth2$,instance_npcname("mobs#bios2") + "::OnMyMobDead" );
		if (.@count == 10 || .@count == 5)
				mapannounce 'map_dth2$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
		else if (.@count == 1)
				mapannounce 'map_dth2$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00";
		else if (.@count == 0) {
				mapannounce 'map_dth2$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00";
				disablenpc instance_npcname("mobs#bios2");
				enablenpc instance_npcname("warp1#bios2");
				enablenpc instance_npcname("Zeith#bios3");
				enablenpc instance_npcname("Vrid#bios3");
				enablenpc instance_npcname("event_boss#bios3");
		}
		end;
}

// zone 3
// 1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,5,7,{// official
1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,{
		if ('reward == 1) {
				mes "[วริด]";
				mes "ฉันมา";
				mes "ฮีโร่โทเค็นนี้เกิดขึ้นโดยไม่ได้ตั้งใจ";
				mes "ฉันอยากจะมอบให้เธอ เธอสมควรได้รับมัน";
				next;
				mes "[วริด]";
				mes "ออกไปจากสถานที่น่าขนลุกแห่งนี้กันเถอะ!";
				close2;
				getitem 6684,1;// Token Of Hero
				if (ep14_3_isle == 0)
						ep14_3_isle = 1;
				if (checkquest(15007,PLAYTIME) == 0 || checkquest(15007,PLAYTIME) == 1)
						setquest 15008;// Memorial Dungeon: Isle of Bios
				warp "moro_cav",45,63;
		}
		end;
}

1@dth3,64,74,5	script	event_boss#bios3	HIDDEN_WARP_NPC,5,7,{
		end;
OnTouch_:
		disablenpc instance_npcname("event_boss#bios3");
		'party_id = getcharid(1);
		donpcevent instance_npcname("freeze#bios3") + "::OnStart";
		end;
}

// 1@dth3,69,67,0	script	freeze#bios3	HIDDEN_WARP_NPC,30,30,{// official
1@dth3,1,1,0	script	freeze#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("freeze#bios3");
		'reaper3$ = instance_npcname("Grim Reaper Ankou#bios3");
		'zeith3$ = instance_npcname("Zeith#bios3");
		'vrid3$ = instance_npcname("Vrid#bios3");
		initnpctimer;
		npctalk "วริด: คุณโชคดีมากที่ได้มาที่นี่ ฉันหลงทางตั้งแต่ก้าวเข้าไปในเขาวงกตนั้นแล้ว", 'vrid3$;
		end;
OnTimer2500:
		npctalk "วริด: ถ้าไม่มีไซธ์ ฉันคงยังอยู่ที่นั่น ล่องลอยไป ขอบคุณ ไซธ์", 'vrid3$;
		end;
OnTimer5000:
		npctalk "ไซธ์: ... มีบางอย่างที่คอยรบกวนใจฉัน", 'zeith3$;
		end;
OnTimer7500:
		npctalk "วริด : เป็นไงบ้าง?", 'vrid3$;
		end;
OnTimer10000:
		npctalk "ไซธ์: ฉันรู้สึกว่าเหล่าสัตว์ประหลาดจะแข็งแกร่งขึ้นตามกาลเวลา", 'zeith3$;
		end;
OnTimer12500:
		npctalk "ไซธ์: เหมือนเด็กๆที่กำลังเติบโตเป็นผู้ใหญ่", 'zeith3$;
		end;
OnTimer15000:
		npctalk "วริด: อืม ฉันคิดว่าคุณพูดถูก", 'vrid3$;
		end;
OnTimer17500:
		npctalk "วริด: ตอนแรกพวกมันก็เล็กหมด แต่เมื่อเวลาผ่านไป เราก็เจอตัวที่ใหญ่และแข็งแกร่งกว่า", 'vrid3$;
		end;
OnTimer20000:
		npctalk "วริด: สำหรับฉันแล้ว มันฟังดูเป็นธรรมชาติ คุณกังวลเรื่องอะไรอยู่", 'vrid3$;
		end;
OnTimer22500:
		npctalk "ไซธ์: ฉันอยากรู้เกี่ยวกับสัตว์ประหลาดที่เราจะต้องเผชิญตอนนี้จัง", 'zeith3$;
		end;
OnTimer25000:
		npctalk "Zeith: ไม่ว่าพวกมันจะเป็นเวอร์ชันที่แข็งแกร่งกว่าของตัวเองดั้งเดิมหรือเป็นอะไรบางอย่างที่แตกต่างไปจากเดิมอย่างสิ้นเชิงก็ตาม", 'zeith3$;
		end;
OnTimer27000:
		enablenpc 'reaper3$;
		end;
OnTimer27500:
		npctalk "ยมทูตอังโกะ: *หัวเราะเบาๆ* อย่างน้อยหนึ่งในพวกคุณก็มีหัวคิดที่ดีนะ", 'reaper3$;
		end;
OnTimer30000:
		npctalk "วริด: คุณอีกแล้วเหรอ?", 'vrid3$;
		end;
OnTimer32500:
		npctalk "ยมทูตอังโกะ: คุณอยากรู้เกี่ยวกับศัตรูตัวต่อไปของคุณมั้ย?", 'reaper3$;
		end;
OnTimer35000:
		npctalk "ยมทูตอังโกะ: ข้างหน้าคุณคือกองทัพอมตะ", 'reaper3$;
		end;
OnTimer37500:
		npctalk "ยมทูตอังโค: *หัวเราะคิกคัก* ออกมา ทหารของฉัน!", 'reaper3$;
		end;
OnTimer37700:
		specialeffect EF_READYPORTAL,AREA,'reaper3$;
		end;
OnTimer40000:
		for ( .@i = 1; .@i <= 3; ++.@i ) {
				for ( .@j = 1; .@j <= 8; ++.@j )
						enablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
		}
		end;
OnTimer42000:
		npctalk "วริด: หุบปากไปซะไอ้ปีศาจ! เราไม่มีเวลาให้พวกลูกน้องของแกหรอก!", 'vrid3$;
		end;
OnTimer45000:
		npctalk "วริด: ฉันจะกำจัดคุณด้วยตัวเอง!", 'vrid3$;
		end;
OnTimer47500:
		npctalk "ยมทูตอังโกะ: กลับไปหาคุณซะไอ้ปากใหญ่", 'reaper3$;
		end;
OnTimer50000:
		npctalk "ยมทูตอันโค: ฉันจะสอนคุณว่าการไม่มีหนทางช่วยเหลือตัวเองเมื่อเผชิญกับความสยองขวัญมันน่ากลัวขนาดไหน!", 'reaper3$;
		end;
OnTimer52500:
		specialeffect EF_STORMGUST,AREA,'reaper3$;
		end;
OnTimer52700:
		specialeffect EF_FREEZED,AREA,'zeith3$;
		specialeffect EF_FREEZED,AREA,'vrid3$;
		specialeffect EF_FIREHIT,AREA,'zeith3$;
		specialeffect EF_FIREHIT,AREA,'vrid3$;
		setnpcdisplay( 'vrid3$, 4_M_FROZEN_KN );
		setnpcdisplay( 'zeith3$, 4_M_FROZEN_GC );

		getpartymember 'party_id, 0, .@player_name$;
		getpartymember 'party_id, 1, .@char_id;
		getpartymember 'party_id, 2, .@account_id;
		for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
				if (isloggedin(.@account_id[.@i],.@char_id[.@i]) == true) {
						getmapxy .@map$,.@x,.@y, BL_PC, .@player_name$[.@i];
						if (.@map$ == 'map_dth3$ && distance(.@x,.@y,69,67) < 30)
								sc_start SC_FREEZE,17500,0,10000, (SCSTART_NOAVOID|SCSTART_NOTICKDEF|SCSTART_NORATEDEF), .@account_id[.@i];
				}
		}
		end;
OnTimer55000:
		npctalk "วริด: อ๊าก! เกิดอะไรขึ้นเนี่ย! ผะ...ผมขยับตัวไม่ได้!", 'vrid3$;
		end;
OnTimer57500:
		npctalk "ไซธ์: มะ-ฉันก็ทำไม่ได้เหมือนกัน! ฉันขยับเท้าไม่ได้!", 'zeith3$;
		end;
OnTimer60000:
		npctalk "ยมทูตอังโกะ: บนเกาะไบโอส แต่ละวันคือชีวิตของเขา", 'reaper3$;
		end;
OnTimer62500:
		npctalk "ยมทูตอังโค: คุณคงสังเกตเห็นว่าทหารของฉันเติบโตขึ้นก่อนพระอาทิตย์ตกดิน", 'reaper3$;
		end;
OnTimer65000:
		npctalk "ยมทูตอันโค: หลังจากที่พลบค่ำ พวกเขาก็เข้าสู่ขั้นตอนสุดท้ายของการพัฒนา: ชีวิตหลังความตาย", 'reaper3$;
		end;
OnTimer67500:
		npctalk "ยมทูตอันโค: ถูกต้องแล้ว ถึงเวลาที่คุณจะต้องสัมผัสกับความตายแล้ว", 'reaper3$;
		end;
OnTimer70000:
		npctalk "ยมทูตอันโค: เตรียมพร้อมที่จะสัมผัสกับความกลัวแห่งความตายได้เลย! *หัวเราะคิกคัก*", 'reaper3$;
		// note: unfreeze player
		end;
OnTimer72000:
		disablenpc 'reaper3$;
		end;
OnTimer72500:
		npctalk "วริด: บ้าเอ้ย พวกอันเดดกำลังเข้ามา แต่ฉันต่อสู้ไม่ได้เพราะขยับเท้าไม่ได้!", 'vrid3$;
		end;
OnTimer75000:
		npctalk "วริด: อ๊าก ฉันไม่อยากตาย!", 'vrid3$;
		end;
OnTimer77500:
		npctalk "ไซธ์: ...อึ๋ย...", 'zeith3$;
		end;
OnTimer80000:
		stopnpctimer;
		for ( .@i = 1; .@i <= 3; ++.@i ) {
				for ( .@j = 1; .@j <= 8; ++.@j )
						disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
		}
		.@label$ = instance_npcname("freeze#bios3") + "::OnMyMobDead";
		for ( .@y = 80; .@y <= 92; .@y += 6 )
				for ( .@x = 62; .@x <= 78; .@x += 2 )
						monster 'map_dth3$,.@x,.@y,"Corrupt Orc Zombie",3016,1, .@label$;// EP14_3_DEATH_C_MOB1
		end;

OnMyMobDead:
		if (mobcount( 'map_dth3$,instance_npcname("freeze#bios3") + "::OnMyMobDead" ) == 0) {
				disablenpc instance_npcname("freeze#bios3");
				donpcevent instance_npcname("verit#bios3") + "::OnStart";
		}
		end;
}

1@dth3,1,1,0	script	verit#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("verit#bios3");
		initnpctimer;
		npctalk "วริด: โอ้ คุณเก่งมาก คุณฆ่าพวกมันหมดเลยเหรอ?", 'vrid3$;
		end;
OnTimer2500:
		npctalk "ไซธ์: ยังไม่มีครับ จะมีมาอีกเรื่อยๆ ครับ!", 'zeith3$;
		end;
OnTimer5000:
		stopnpctimer;
		.@label$ = instance_npcname("verit#bios3") + "::OnMyMobDead";
		for ( .@y = 80; .@y <= 92; .@y += 6 )
				for ( .@x = 62; .@x <= 78; .@x += 2 )
						monster 'map_dth3$,.@x,.@y,"Corrupt Verit",3017,1, .@label$;// EP14_3_DEATH_C_MOB2
		end;

OnMyMobDead:
		if (mobcount( 'map_dth3$,instance_npcname("verit#bios3") + "::OnMyMobDead" ) == 0) {
				disablenpc instance_npcname("verit#bios3");
				donpcevent instance_npcname("megalodon#bios3") + "::OnStart";
		}
		end;
}

1@dth3,1,1,0	script	megalodon#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("megalodon#bios3");
		initnpctimer;
		npctalk "วริด: ฉันหวังว่านั่นจะเป็นครั้งสุดท้ายจริงๆ", 'vrid3$;
		end;
OnTimer2500:
		npctalk "Zeith: ไม่หรอก เราไม่โชคดีขนาดนั้น--ยังมีอะไรอีกมากมายตามมา!", 'zeith3$;
		end;
OnTimer5000:
		stopnpctimer;
		.@label$ = instance_npcname("megalodon#bios3") + "::OnMyMobDead";
		for ( .@y = 80; .@y <= 92; .@y += 6 )
				for ( .@x = 62; .@x <= 78; .@x += 2 )
						monster 'map_dth3$,.@x,.@y,"Corrupt Megalodon",3018,1, .@label$;// EP14_3_DEATH_C_MOB3
		end;

OnMyMobDead:
		if (mobcount( 'map_dth3$,instance_npcname("megalodon#bios3") + "::OnMyMobDead" ) == 0) {
				disablenpc instance_npcname("megalodon#bios3");
				donpcevent instance_npcname("boss_prelude#bios3") + "::OnStart";
		}
		end;
}

1@dth3,1,1,0	script	boss_prelude#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc 'reaper3$;
		initnpctimer;
		end;
OnTimer500:
		npctalk "ยมทูตอังโค: ฉันประเมินคุณต่ำไป หรือไม่ก็ทหารของฉันไม่แข็งแกร่งอย่างที่คิด", 'reaper3$;
		end;
OnTimer3000:
		npctalk "วริด: แกมันโกง! ปล่อยพวกเราไปและสู้กันอย่างยุติธรรม!", 'vrid3$;
		end;
OnTimer5500:
		npctalk "ยมทูตอันโค: แกยังพูดอยู่อีกรึไง ฉันจะฆ่าแกเองแล้วจบๆ ไปซะ!", 'reaper3$;
		end;
OnTimer7500:
		disablenpc 'reaper3$;
		end;
OnTimer8000:
		stopnpctimer;
		donpcevent instance_npcname("boss#bios3") + "::OnStart";
		end;
}

1@dth3,1,1,0	script	boss#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("boss#bios3");
		monster 'map_dth3$,70,67,"Grim Reaper Ankou",3029,1, instance_npcname("boss#bios3") + "::OnMyBossDead";
		'boss_id = $@mobid[0];
		donpcevent instance_npcname("talk_friends#bios3") + "::OnStart";
		donpcevent instance_npcname("talk_boss#bios3") + "::OnStart";
		end;
OnMyBossDead:
		'boss_id = 0;
		donpcevent instance_npcname("talk_friends#bios3") + "::OnStop";
		donpcevent instance_npcname("talk_boss#bios3") + "::OnStop";
		enablenpc 'reaper3$;
		enablenpc 'zeith3$;
		enablenpc 'vrid3$;
		initnpctimer;
		end;
OnTimer3000:
		npctalk "ยมทูตอันโค: คุณแข็งแกร่งนะ...", 'reaper3$;
		end;
OnTimer6500:
		npctalk "ยมทูตอันโค: แต่เป้าหมายของฉันสำเร็จแล้ว... อย่าคิดว่าเรื่องมันจะจบแค่นี้นะ", 'reaper3$;
		end;
OnTimer8000:
		npctalk "ยมทูตอันโค: อีกไม่นานลอร์ดโมร็อคก็จะกลับมา *หัวเราะคิกคัก* ความกลัวความตายจะติดตามคุณไปทุกที่", 'reaper3$;
		end;
OnTimer10500:
		disablenpc 'reaper3$;
		specialeffect EF_ICECRASH,AREA,'vrid3$;
		specialeffect EF_ICECRASH,AREA,'zeith3$;
		setnpcdisplay( 'vrid3$, 4_M_KNIGHT_BLACK );
		setnpcdisplay( 'zeith3$, 4_M_GUILLOTINE );
		end;
OnTimer13000:
		npctalk "วริด: โอ้ ในที่สุดฉันก็เคลื่อนไหวได้แล้ว!", 'vrid3$;
		end;
OnTimer16500:
		npctalk "ไซธ์: ฮึ่ม ฉันรู้สึกละอายใจตัวเองที่อ่อนแอขนาดนี้...", 'zeith3$;
		end;
OnTimer18000:
		npctalk "วริด: โอ้โห ผู้เก็บเกี่ยวนั่นแข็งแกร่งกว่าฉันและไซธ์รวมกันเยอะเลย", 'vrid3$;
		end;
OnTimer20500:
		npctalk "วริด: ถ้าไม่มีคุณ เราอาจจะตายได้", 'vrid3$;
		end;
OnTimer23000:
		stopnpctimer;
		npctalk "วริด: ฉันอยากจะมอบบางอย่างให้กับคุณเพื่อช่วยชีวิตเราไว้ คุยกับฉันอีกครั้งเมื่อคุณพร้อมที่จะรับมัน", 'vrid3$;
		'reward = 1;
		disablenpc instance_npcname("boss#bios3");
		end;
}

1@dth3,1,1,0	script	talk_friends#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer15000:
		.@r = rand(7);
		if (.@r == 0)
				npctalk "ไซธ์: อืม... ฉันรู้สึกไร้เรี่ยวแรงเหลือเกิน...", 'zeith3$;
		else if (.@r == 1)
				npctalk "ไซธ์: ไอ้อันโคนั่นมันกลิ่นอันตรายนะ คุณควรจะเคลื่อนไหวทันทีที่มันเริ่มใช้ทักษะ", 'zeith3$;
		else if (.@r == 2)
				npctalk "วริด: อืม ขอโทษที่เราช่วยไม่ได้", 'vrid3$;
		else if (.@r == 3)
				npctalk "วริด: เจ้าคนโกง สู้ให้ยุติธรรมสิ!", 'vrid3$;
		else if (.@r == 4)
				npctalk "วริด: บ้าเอ้ย ฉันขยับตัวไม่ได้เลย...", 'vrid3$;
		initnpctimer;
		end;
OnStop:
		stopnpctimer;
		end;
}

1@dth3,1,1,0	script	talk_boss#bios3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer13000:
		.@r = rand(6);
		if (.@r == 0)
				unittalk 'boss_id, "Grim Reaper Ankou: *Chuckle*";
		else if (.@r == 1)
				unittalk 'boss_id, "Grim Reaper Ankou: Feel the fear of death!";
		else if (.@r == 2)
				unittalk 'boss_id, "Grim Reaper Ankou: Lord Morocc will return soon.";
		else if (.@r == 3)
				unittalk 'boss_id, "Grim Reaper Ankou: Is this the best you can do? Such weak creatures, you humans.";
		initnpctimer;
		end;;
OnStop:
		stopnpctimer;
		end;

OnInstanceInit:
		'reward = 'boss_id = 0;
		'map_dth1$ = instance_mapname("1@dth1");
		'map_dth2$ = instance_mapname("1@dth2");
		'map_dth3$ = instance_mapname("1@dth3");

		// warps
		disablenpc instance_npcname("warp1#bios1");
		disablenpc instance_npcname("warp1#bios2");

		// zone 1
		disablenpc instance_npcname("Grim Reaper Ankou#bios1");
		donpcevent instance_npcname("mobs#bios1") + "::OnStart";	// spawn at entrance

		// zone 2
		disablenpc instance_npcname("start1#bios2");
		disablenpc instance_npcname("mobs#bios2");

		// zone 3
		disablenpc instance_npcname("event_boss#bios3");
		disablenpc instance_npcname("freeze#bios3");
		disablenpc instance_npcname("verit#bios3");
		disablenpc instance_npcname("megalodon#bios3");
		disablenpc instance_npcname("boss_prelude#bios3");
		disablenpc instance_npcname("boss#bios3");
		disablenpc instance_npcname("talk_friends#bios3");
		disablenpc instance_npcname("talk_boss#bios3");
		disablenpc instance_npcname("Vrid#bios3");
		disablenpc instance_npcname("Zeith#bios3");
		disablenpc instance_npcname("Grim Reaper Ankou#bios3");
		for ( .@i = 1; .@i <= 3; ++.@i ) {
				for ( .@j = 1; .@j <= 8; ++.@j )
						disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
		}
		end;
}

1@dth3,72,74,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios3	4_M_GUILLOTINE
1@dth3,70,67,3	duplicate(Grim Reaper Ankou#bios1)	Grim Reaper Ankou#bios3	3029

1@dth3,62,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio1	3016
1@dth3,64,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio1	3016
1@dth3,66,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio1	3016
1@dth3,68,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio1	3016
1@dth3,72,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio1	3016
1@dth3,74,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio1	3016
1@dth3,76,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio1	3016
1@dth3,78,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio1	3016
1@dth3,62,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio2	3016
1@dth3,64,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio2	3016
1@dth3,66,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio2	3016
1@dth3,68,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio2	3016
1@dth3,72,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio2	3016
1@dth3,74,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio2	3016
1@dth3,76,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio2	3016
1@dth3,78,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio2	3016
1@dth3,62,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio3	3016
1@dth3,64,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio3	3016
1@dth3,66,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio3	3016
1@dth3,68,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio3	3016
1@dth3,72,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio3	3016
1@dth3,74,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio3	3016
1@dth3,76,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio3	3016
1@dth3,78,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio3	3016
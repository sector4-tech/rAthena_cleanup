//===== rAthena Script =======================================
//= Lost Farm in Time
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Lost Farm in Time
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//============================================================

ba_maison,311,206,3	script	Luina#greeen	4_EP17_BASIC_B,{
		if (checkweight(1000103,20) == 0) {
				mes "- คุณไม่สามารถดำเนินการภารกิจต่อได้เนื่องจากคุณมีไอเทมมากเกินไป -";
				close;
		}
		if (BaseLevel < 150) {
				mes "[ฝ้าย]";
				mes "คุณดูอ่อนแอเกินกว่าจะทำงานได้ ฉันไม่คิดว่าคุณจะทำอย่างนั้นได้ตอนนี้";
				next;
				mes "- ภารกิจนี้สำหรับเลเวล 150 ขึ้นไป -";
				close;
		}
		if (ep17_2_main < 9) {
				mes "- ฉันได้พบเห็นตุ๊กตาอัตโนมัติหน้าโรงงานทำฟาร์มแล้ว -";
				next;
				mes "ตุ๊กตาจ้องมองฉันตั้งแต่หัวจรดเท้า และหันหัวไป";
				close;
		}
		if (isbegin_quest(8586) == 0) {
				mes "- ฉันได้พบเห็นตุ๊กตาอัตโนมัติหน้าโรงงานทำฟาร์มแล้ว -";
				next;
				mes "[ตุ๊กตาอัตโนมัติ]";
				mes "อัตราการเต้นของหัวใจ ความดันโลหิต และอุณหภูมิร่างกายอยู่ในภาวะปกติ";
				mes "การหายใจเป็นปกติ สายตาคงที่เป็นปกติ";
				mes "กำลังตรวจสอบข้อมูลการลงทะเบียน...";
				next;
				mes "ตุ๊กตาจ้องมองฉันตั้งแต่หัวจรดเท้า ราวกับว่าเธอพึงพอใจ";
				next;
				mes "[ตุ๊กตาอัตโนมัติ]";
				mes "สวัสดี," + strcharinfo(0) + "-";
				mes "มาเยี่ยมชมสถานที่ทำการเกษตรกันไหมคะ?";
				npctalk "เหมียว.", "Kitty#greeen", bc_self;
				next;
				if (select( "Huh.", "No." ) == 2) {
						mes "[ตุ๊กตาอัตโนมัติ]";
						mes "ที่นี่คือโรงงานทำฟาร์มที่คิตตี้และคนอื่นๆ อาศัยอยู่";
						mes "แค่คุยกับฉันถ้าคุณสนใจ";
						mes "ฉันจะอธิบายทุกอย่างให้คุณฟัง.";
						close;
				}
				mes "[ตุ๊กตาอัตโนมัติ]";
				mes "คิตตี้ ใจเย็นๆ สักครู่นะ เด็กดี~";
				mes "ฉันเป็นคนรับผิดชอบดูแลสิ่งอำนวยความสะดวกของฟาร์มที่นี่";
				mes "ฉันปลูกต้นไม้ ใส่ปุ๋ย และให้อาหารแมวที่นี่และเพื่อนๆ ของมัน";
				npctalk "เสียงคราง", "Kitty#greeen", bc_self;
				next;
				mes "[ตุ๊กตาอัตโนมัติ]";
				mes "วัตถุดิบในการทำของหวานที่ออกมาจากโรงอาหารก็เก็บเกี่ยวกันที่นี่";
				mes "เอ่อ...";
				next;
				mes "[ตุ๊กตาอัตโนมัติ]";
				mes "ในช่วงนี้มีแขกมากันเยอะ จึงทำให้มีออเดอร์มาเก็บเกี่ยวเพิ่มมากขึ้น";
				mes "เพื่อนร่วมงานของฉันจากงานอื่นเพิ่งได้รับมอบหมายให้ทำงานที่นี่ แต่เธอค่อนข้างซุ่มซ่ามเล็กน้อย";
				mes "การเก็บเกี่ยวที่ดีเป็นเรื่องง่ายเมื่อทุกคนรับฟัง";
				next;
				select( "You're talkative." );
				mes "[ลูอิน่า]";
				mes "โอ้ ฉันลืมไปว่าฉันควรแนะนำตัวก่อนถ้าฉันอยากคุยกับแขกนานๆ";
				mes "ฉันชื่อลูอิน่า";
				next;
				mes "[ลูอิน่า]";
				mes "คิตตี้ นี่คือพืชชนิดหนึ่งที่เรียกว่า พิทายา เป็นพืชโบราณสายพันธุ์ที่ได้รับการปรับปรุงพันธุ์และเคยอาศัยอยู่บนเกาะแห่งนี้";
				mes "ผมไม่รู้ว่ามันแปลกหรือเปล่า แต่ผมไม่ใช่พิทายา แต่ผมฟังเก่งนะ";
				next;
				mes "- ลูอิน่าลูบพิทายา -";
				npctalk "เสียงคราง", "Kitty#greeen", bc_self;
				next;
				mes "[ลูอิน่า]";
				mes "นายของฉันบอกฉันว่าฉันควรตั้งชื่อสิ่งเล็กๆ ที่น่ารัก";
				mes "คิตตี้, ผีเสื้อ, แฮปปี้ ฯลฯ.";
				mes "พิทายาเป็นสิ่งมีชีวิตตัวเล็กที่น่ารัก ดังนั้นฉันจึงตั้งชื่อให้มัน";
				next;
				mes "[ลูอิน่า]";
				mes "คิตตี้น่ารักที่สุด พิทาย่าที่นี่คือคิตตี้";
				next;
				mes "[ลูอิน่า]";
				mes "ดังนั้น...";
				mes "คุณมาที่นี่เพื่อตัดหางหรือเปล่า?";
				next;
				select( "Tail?", "I don't have a tail." );
				mes "[ลูอิน่า]";
				mes "คุณมาที่นี่เพื่อตัดหางบางส่วนใช่ไหม?";
				mes "โอ้ ฉันทำผิดพลาดแล้ว";
				mes "รอสักครู่นะครับ เดี๋ยวจะอธิบายให้ฟังครับ";
				setquest 8586;
				close;
		}
		if (isbegin_quest(8586) == 1) {
				mes "[ลูอิน่า]";
				mes "ถ้าดูแต่คิตตี้ตอนนี้ เค้าไม่มีรากใช่มั้ยคะ";
				mes "มันดูดซับความชื้นจากอากาศและสังเคราะห์แสงแม้จะไม่มีรากก็ตาม";
				mes "แค่ปุ๋ยบ้าง...และอาหารบ้าง";
				next;
				mes "[ลูอิน่า]";
				mes "เมื่อคุณปลูกเมล็ดพืช มันจะงอกออกมาจากพื้นดิน แต่ฉันไม่เคยเห็นต้นที่งอกหลายต้นมาก่อน";
				next;
				mes "[ลูอิน่า]";
				mes "ส่วนหางก็แค่จับพิทายาแล้วลูบแบบนี้แล้วปล่อยไป";
				next;
				mes "- ลูอิน่าถือพิทายา สาธิตการดึงหางและวางลง";
				next;
				mes "[ลูอิน่า]";
				mes "เปล่าครับ ผมไม่ได้ดึงครับ";
				mes "หางที่เก็บมาคงไม่พอกิน";
				mes "คุณจะต้องคำนึงถึงความสูญเสียด้วย";
				npctalk "กั๊ก...!", "Kitty#greeen", bc_self;
				next;
				mes "[ลูอิน่า]";
				mes "คุณต้องเอาเมล็ดและเปลือกออก แต่ก็ไม่ใช่เรื่องยาก";
				mes "เมื่อแปรรูปแล้ว หางพิทายาที่เก็บได้จะถูกนำขึ้นมาวางบนโต๊ะ";
				next;
				mes "[ลูอิน่า]";
				mes "จริงๆ แล้วการเก็บเกี่ยวเป็นเรื่องยากสักหน่อย ดังนั้นเพื่อนร่วมงานของฉัน 'Silk' จะแนะนำคุณ";
				mes "คุณสามารถเข้าไปได้โดยไม่ต้องเก็บเกี่ยวผลผลิต ฉันจะให้คุณเข้าไปโดยไม่ต้องมีไกด์";
				next;
				mes "[ลูอิน่า]";
				mes "หากคุณว่าง คุยกับ 'คอตตัน' ที่นี่ เธอเป็นผู้จัดการการเล่น และเธอจะแจ้งให้คุณทราบว่าต้องทำอย่างไร";
				mes "ฉันรู้ว่ามันยากสำหรับคุณนิดหน่อย แต่... ถ้าคุณช่วยเหลือเรา เราก็จะชดเชยให้อย่างเหมาะสม";
				npctalk "คอตตอน : อยากให้สูงกว่านี้อีกนิดนึง...", "Cotton#172_lost_0", bc_self;
				next;
				mes "[ลูอิน่า]";
				mes "เมื่อคุณได้ยินคำแนะนำแล้ว คุณสามารถเข้าสู่ระบบได้แล้ว";
				mes "ถ้าอยากดูรอบๆ ก็สามารถเข้าไปได้เลย";
				next;
				mes "[ลูอิน่า]";
				mes "เราคุยเรื่องงานของ Cotton ก่อน แต่จริงๆ แล้ว มันเป็นเรื่องเร่งด่วนกว่า";
				mes "หากคุณมั่นใจในทักษะการต่อสู้ของคุณ ฉันอยากให้คุณช่วยเรื่องการเก็บเกี่ยว";
				mes "ฉันหวังว่าคุณจะช่วยฉันเก็บเกี่ยวได้...";
				mes "เราต้องให้อาหารบอสคิตตี้ด้วย";
				next;
				select( "Boss Kitty?" );
				mes "[ลูอิน่า]";
				mes "ผมก็เรียกเขาแบบนั้น เขาดูแตกต่างจากคนอื่นนิดหน่อย ซ่อนตัวอยู่ในที่ที่ยากจะมองเห็น";
				mes "หากคุณต้องการเห็นเขาอยู่แถวๆ นี้ คุณจะต้องโรยอาหารและทำเสียงดังเล็กน้อยรอบๆ ฟาร์ม";
				next;
				mes "[ลูอิน่า]";
				mes "ฉันคือผู้รับผิดชอบการเก็บเกี่ยว ดังนั้นโปรดคุยกับฉันได้ตลอดเวลาที่คุณต้องการ!";
				completequest 8586;
				next;
				mes "[ลูอิน่า]";
				mes "มาพูดคุยรายละเอียดกันเมื่อคุณต้องการความช่วยเหลือ";
				close;
		}
		if (isbegin_quest(8589) == 1) {
				mes "[ลูอิน่า]";
				mes "น้องๆมาเล่นกับคุณมั้ย?";
				mes "นั่นมันแปลก... ฉันได้ยินคุณชัดเจนมาก";
				mes "...อะไรนะ? แม้แต่ซิลค์เหรอ?";
				mes "เกิดอะไรขึ้น";
				next;
				select( "She is pretending to be a cat." );
				emotion ET_FRET, getnpcid(0,"Kitty#greeen");
				mes "[คิตตี้]";
				mes "กั๊ก";
				next;
				mes "-- แมวซ่อนอยู่ข้างหลังลูอิน่าก่อนที่จะไปถึงมัน --";
				next;
				mes "[ลูอิน่า]";
				mes "อะไร...";
				next;
				mes "[ลูอิน่า]";
				mes "บางที...บางทีอาจจะใช่?";
				mes "มันฟังฉันอยู่เหรอ?";
				next;
				select( "I guess." );
				mes "[ลูอิน่า]";
				mes "คิตตี้.";
				npctalk "*คำราม*", "Kitty#greeen", bc_self;
				next;
				mes "[ลูอิน่า]";
				mes "-";
				mes "นี่คือสิ่งที่แขกจะพูดว่า 'อาย' ใช่ไหม?";
				npctalk "*อ๊าก*", "Kitty#greeen", bc_self;
				next;
				mes "- เมื่อลูอิน่ายื่นมือไปหาแมว มันก็เอาหัวขึ้นมาไว้บนมือของเธอ -";
				next;
				mes "ใบหน้าของลูอิน่าเริ่มซับซ้อนขึ้นด้วยท่าทางพึงพอใจขณะที่ลูบแมว";
				next;
				mes "[ลูอิน่า]";
				mes "อืม...";
				mes "สักวันแขกจะได้เป็นเพื่อนกับเหมียวบ้างมั้ยนะ?";
				next;
				mes "[ลูอิน่า]";
				mes "จะดีแค่ไหนถ้าเราเล่นกับพวกเขาแบบนี้ต่อไป?";
				npctalk "*อ๊าก*", "Kitty#greeen", bc_self;
				next;
				mes "[ลูอิน่า]";
				mes "ใช่ค่ะ! โปรดช่วยซิลค์อีกครั้งพรุ่งนี้เช่นกัน";
				mes "แล้ววันหนึ่ง... คุณจะสามารถผูกมิตรกับพิทยาทั้งหมดได้!";
				mes "นี่...มันเป็นสัญลักษณ์แห่งคำสัญญา";
				completequest 8589;
				getitem 1000103,20;
				getexp 3094009,0;
				next;
				mes "[ลูอิน่า]";
				mes "อาจจะ.";
				npctalk "แคว๊ก แคว๊ก", "Kitty#greeen", bc_self;
				next;
				mes "- แมวส่งเสียงร้องแหลมสูงเหมือนกำลังตื่นเต้น -";
				next;
		}
		switch( checkquest(8587,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[ลูอิน่า]";
				mes "เราเก็บเกี่ยวสิ่งที่จำเป็นสำหรับวันนี้เสร็จสิ้นแล้ว";
				mes "เราจะเริ่มเก็บเกี่ยวผลผลิตอีกครั้งในรุ่งเช้า กลับมาอีกครั้งในตอนนั้น";
				close;
		case 2:
				mes "[ลูอิน่า]";
				mes "^0000FFการต่อสู้ของเรากับพิทายาดูเหมือนจะผ่านมานานมากแล้ว^000000";
				mes "ตอนนี้FFI มีความกล้าที่จะกลับเข้าสู่ฟาร์มอีกครั้งแล้ว^000000";
				erasequest 8587;
				close;
		}
		.@party_id = getcharid(1);
		if (.@party_id < 1) {
				mes "^FF0000คุณต้องจัดให้มีคนอย่างน้อยหนึ่งคนหรือเข้าร่วมกลุ่มเพื่อเข้าร่วม^000000";
				close;
		}
		.@md_name$ = "Farm Lost in Time";

		setarray .@item[0],1000093,1000094,1000095,1000096,1000097,1000098;
		for ( .@i = 0; .@i < 6; ++.@i ) {
				if (countitem(.@item[.@i]) > 0) {
						.@count = 1;
						delitem .@item[.@i], countitem(.@item[.@i]);
				}
		}
		if (.@count > 0) {
				mes "[ลูอิน่า]";
				mes "!? เกิดอะไรขึ้น? หางคุณหัก";
				mes "ฉันจัดการเรื่องนั้นให้แล้วนะ";
				mes "ขอบคุณค่ะ....หลังเก็บเกี่ยวครั้งต่อไปอย่าลืมส่งมอบให้ร้าน Silk แล้วนำออกมาค่ะ";
				next;
		}
		mes "[ลูอิน่า]";
		mes "สวัสดี," + strcharinfo(0) + "-";
		mes "วันนี้จะเข้าสถานเกษตรไหมคะ?";
		next;
		switch( select( (is_party_leader() ? "Create " + .@md_name$ : ""), "Enter the dungeon.", "Cancel" ) ) {
		case 1:
				if (instance_create(.@md_name$) == -3)
						dispbottom "The reservation of the instance '" + .@md_name$ + "' has failed due to an active instance.";
				mes "[ลูอิน่า]";
				mes "กรุณารอสักครู่~";
				mes "ฉันจะเตรียมอาหารสำหรับวันนี้";
				close;
		case 2:
				if (instance_enter(.@md_name$) != IE_OK) {
						mes "^FF0000คุกใต้ดินแห่งความทรงจำยังไม่ได้ถูกสร้างขึ้น^000000";
						close;
				}
				setquest 8587;
				if (isbegin_quest(8588) > 0)
						erasequest 8588;
				setquest 8588;
				mapannounce "ba_maison", "" + strcharinfo(0) + " of the party, " + .@party_name$ + ", is entering the " + .@md_name$ + ".", bc_map, "0x00FF99";
				end;
		case 3:
				end;
		}
		end;

OnInit:
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8587,PLAYTIME) == -1" );	// condition on ep17_2_main not required
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8587,PLAYTIME) == 2" );
		end;
}


1@lost,1,1,0	script	#lost_farm_control	-1,{
		end;
OnInstanceInit:
		'lost_farm = 0;
		deletearray 'banghae;
		'map_lost$ = instance_mapname("1@lost");
		end;
}

1@lost,161,40,0	script	#startline	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch:
		if (!is_party_leader())
				end;
		disablenpc();
		'lost_farm = 0;
		enablenpc instance_npcname("Wooden Box#01");
		npctalk "อ๋อ แขก อยู่ตรงนี้!!", instance_npcname("Wooden Box#01");
		end;
}

1@lost,163,47,3	script(DISABLED)	Wooden Box#01	4_WOODBOX,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 0) {
				mes "[ผ้าไหม]";
				mes "ยินดีต้อนรับสู่แหล่งเพาะพันธุ์! ฉันคือซิลค์ ผู้ที่รับหน้าที่เก็บรวบรวม!";
				npctalk "ซิลค์ : ยินดีต้อนรับสู่แหล่งเพาะพันธุ์! ฉันคือซิลค์ ผู้ที่รับหน้าที่รวบรวม!";
				next;
				mes "[ผ้าไหม]";
				mes "อี๊ย...ช่วยฉันด้วย!";
				npctalk "ซิลค์ : อี๊ย... ช่วยฉันด้วย!";
				close2;
				if ('lost_farm == 0) {
						'lost_farm = 1;
						donpcevent instance_npcname("Wooden Box#01") + "::OnStart";
				}
				end;
		}
		npctalk "ซิลค์ : สงบสติอารมณ์ลงหน่อยเถอะ!";
		end;

OnStart:
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#01") + "::OnMobDead";
		monster 'map_lost$,155,43,"King Eyes",20660,1, .@event$;
		monster 'map_lost$,154,50,"Happy",20664,1, .@event$;
		monster 'map_lost$,158,49,"Toto",20661,1, .@event$;
		monster 'map_lost$,162,48,"Lightning",20662,1, .@event$;
		monster 'map_lost$,168,49,"Cutie",20660,1, .@event$;
		monster 'map_lost$,170,45,"Woody",20663,1, .@event$;
		monster 'map_lost$,165,42,"Rusk",20664,1, .@event$;
		end;
OnMobDead:
		if (mobcount('map_lost$, instance_npcname("Wooden Box#01") + "::OnMobDead") == 0) {
				'lost_farm = 2;
				disablenpc();
				enablenpc instance_npcname("Silk#01");
				sleep 1000;
				npctalk "ผ้าไหม : !!", instance_npcname("Silk#01");
		}
		end;
}


1@lost,163,47,3	script(DISABLED)	Silk#01	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm != 2)
				end;
		mes "[ผ้าไหม]";
		mes "ขอบคุณที่ช่วยชีวิตฉันไว้! ตามคำบอกเล่าของลูอิน่า พิทายาเป็นสัตว์ที่อ่อนโยน!";
		npctalk "ซิลค์ : ขอบคุณที่ช่วยชีวิตฉันไว้! ตามคำบอกเล่าของลูอิน่า พิทาย่าเป็นสัตว์ที่อ่อนโยน!";
		next;
		mes "[ผ้าไหม]";
		mes "คุณก็เคยเห็นเหมือนกัน! ฉันกลัว!";
		npctalk "ซิลค์ : คุณก็เห็นเหมือนกัน! ฉันกลัว!";
		next;
		mes "[ผ้าไหม]";
		mes "ให้อาหารและกอดกันคนละ 5 นาที ฉันจะลูบมันยังไงในเมื่อมันชอบโชว์ฟัน";
		npctalk "ซิลค์ : ป้อนอาหารและกอดกันคนละ 5 นาที ฉันจะลูบมันยังไง ในเมื่อพวกมันชอบโชว์ฟัน";
		next;
		mes "[ผ้าไหม]";
		mes "5 นาทีจะทำอะไรได้อีก!";
		npctalk "ซิลค์ : 5 นาที จะทำอะไรได้อีก!";
		next;
		mes "[ผ้าไหม]";
		mes "-";
		npctalk "ผ้าไหม : ...";
		next;
		mes "[ผ้าไหม]";
		mes "เรียนแขกผู้มีเกียรติทุกท่าน...";
		npctalk "ซิลค์ : สวัสดีแขกผู้มีเกียรติทุกท่าน...";
		next;
		mes "[ผ้าไหม]";
		mes "ฉันเข้าใจว่าทำไมลูอิน่าถึงขอคนงานเพิ่ม เพราะเธอเองก็เก็บเกี่ยวได้ไม่เร็วนัก";
		npctalk "ซิลค์ : ฉันเข้าใจว่าทำไมลูอิน่าถึงขอคนงานเพิ่ม เพราะเธอเองก็เก็บเกี่ยวได้ไม่นาน";
		next;
		mes "[ผ้าไหม]";
		mes "แต่จะเร็วกว่าถ้าคุณถือมันไว้และเล่นกับมันเหมือนที่คุณทำมากกว่าลูบมันเป็นเวลา 5 นาที";
		npctalk "ซิลค์ : แต่จะเร็วกว่าถ้าคุณถือมันและเล่นกับมันเหมือนที่คุณทำ มากกว่าจะลูบมันเป็นเวลา 5 นาที";
		next;
		mes "[ผ้าไหม]";
		mes "... แม้ว่าฉันจะไม่สามารถยึดมันไว้ได้ก็ตาม";
		npctalk "ซิลค์ : ... แม้ว่าฉันจะไม่สามารถยึดมันไว้ได้";
		next;
		mes "[ผ้าไหม]";
		mes "มาดูที่อยู่อาศัยถัดไปกันบ้าง ใกล้ๆ นี้ พร้อมหรือยัง ไปกันเลย!";
		npctalk "ซิลค์ : มาดูที่อยู่อาศัยถัดไปกันบ้าง ใกล้ๆ นี้ พร้อมหรือยัง ไปกันเลย!";
		close2;
		if ('lost_farm == 2) {
				'lost_farm = 3;
				disablenpc();
				enablenpc instance_npcname("Silk#02");
				instance_warpall 'map_lost$,218,149,instance_id();
		}
		end;
}


1@lost,214,146,3	script(DISABLED)	Silk#02	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 3) {
				mes "[ผ้าไหม]";
				mes "สิ่งอำนวยความสะดวกนี้... อย่างที่คุณเห็น นี่เป็นแหล่งเพาะพันธุ์ที่เหมาะสมสำหรับเพื่อนระดับยักษ์ของเรา";
				npctalk "ซิลค์ : สถานที่นี้... อย่างที่คุณเห็น ที่นี่คือแหล่งเพาะพันธุ์ที่เหมาะสมสำหรับเพื่อนระดับยักษ์ของเรา";
				next;
				mes "[ผ้าไหม]";
				mes "ไม่ว่าจะเป็นพืชหรือสัตว์ อุณหภูมิและความชื้นจะคงที่เสมอ";
				npctalk "ผ้าไหม : ไม่ว่าจะเป็นพืชหรือสัตว์ อุณหภูมิและความชื้นจะคงที่เสมอ";
				next;
				mes "[ผ้าไหม]";
				mes "ฉันได้ยินมาว่ามันไม่เป็นผลดีต่อคน แล้วการหายใจล่ะเป็นอย่างไรบ้าง?";
				npctalk "ซิลค์ : ฉันได้ยินมาว่ามันไม่เป็นผลดีต่อคน แล้วการหายใจล่ะคะ?";
				next;
				mes "[ผ้าไหม]";
				mes "เดี๋ยวจะโรยอาหารตรงนี้ด้วย เอาล่ะ จัดการกันให้เสร็จๆ ไป ฉันจะไปซ่อนตัว...";
				npctalk "ซิลค์ : งั้นฉันจะโรยอาหารไว้ตรงนี้ด้วย เอาล่ะ จัดการกันเสร็จ... ฉันจะไปซ่อนตัว";
				next;
				mes "[ผ้าไหม]";
				mes "เฮ้...เพื่อนๆ ถึงเวลาสำหรับบาร์และข้าวแล้ว~";
				npctalk "ซิลค์ : เฮ้... เพื่อนๆ ถึงเวลาสำหรับบาร์และข้าวแล้ว~";
				close2;
				if ('lost_farm == 3) {
						'lost_farm = 4;
						npctalk "ซิลค์ : ฉันโรยอาหารไว้แล้ว พวกมันจะมาเร็วๆ นี้!";
						disablenpc();
						donpcevent instance_npcname("Wooden Box#02") + "::OnStart";
				}
				end;
		}
		if ('lost_farm == 4) {
				mes "[ผ้าไหม]";
				mes "ฮ่าฮ่าฮ่า...กลัวจังเลย...";
				npctalk "ซิลค์ : ฮ่าฮ่า... ฉันกลัวมากเลย...";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "คุณแน่ใจว่าไม่ได้เล่นอยู่ตอนนี้ใช่ไหม?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Are you sure that you're not playing right now?";
				next;
				mes "[ผ้าไหม]";
				mes "ฉันคิดว่ามันเหมาะกับพวกมัน ฉันบอกคุณไปแล้วว่าพวกมันจะสร้างหางได้ก็ต่อเมื่อพวกมันอารมณ์ดีเท่านั้น";
				npctalk "ซิลค์ : ฉันคิดว่ามันเหมาะกับพวกมัน ฉันบอกคุณไปแล้วว่าพวกมันจะสร้างหางได้ก็ต่อเมื่อพวกมันอารมณ์ดีเท่านั้น";
				next;
				mes "[ผ้าไหม]";
				mes "เอาล่ะ เราจะไปต่อกันที่จุดต่อไปเลยดีไหม...ยังไม่จบแค่นี้หรอก...";
				npctalk "ซิลค์ : เอาล่ะ เราจะไปที่ต่อไปกันเลยดีกว่า... ยังไม่เสร็จซะทีเดียวนะ...";
				next;
				mes "[ผ้าไหม]";
				mes "ถัดไปเป็นสนามเด็กเล่นที่มีหินและทราย เมื่อเก็บเกี่ยว ฉันต้องเขย่าทรายออกก่อน...";
				npctalk "ผ้าไหม : ต่อไปเป็นสนามเด็กเล่นที่มีหินและทราย เมื่อเก็บเกี่ยว ฉันต้องสะบัดทรายออกก่อน...";
				close2;
				if ('lost_farm == 4) {
						'lost_farm = 5;
						disablenpc();
						enablenpc instance_npcname("Silk#03");
						instance_warpall 'map_lost$,86,101,instance_id();
				}
				end;
		}
		end;
}

1@lost,214,146,3	script(DISABLED)	Wooden Box#02	4_WOODBOX,{
		npctalk "ซิลค์ : ช่วยฉันด้วย! ฉันจะโดนพิทายากิน!";
		end;
OnStart:
		enablenpc();
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#02") + "::OnMobDead";
		monster 'map_lost$,210,148,"Lucy",20662,1, .@event$;
		monster 'map_lost$,208,153,"Elizabeth",20660,1, .@event$;
		monster 'map_lost$,215,151,"Dorothy",20664,1, .@event$;
		monster 'map_lost$,216,151,"Clara",20661,1, .@event$;
		monster 'map_lost$,211,143,"Coco",20662,1, .@event$;
		monster 'map_lost$,211,144,"Cloud",20663,1, .@event$;
		monster 'map_lost$,216,151,"Harmony",20660,1, .@event$;
		sleep 3000;
		npctalk "ซิลค์ : เขา~ ช่วยฉันด้วย~~~~";
		sleep 3000;
		npctalk "ซิลค์ : ช่วยตุ๊กตาอัตโนมัติหน่อย~~~~";
		end;
OnMobDead:
		.@count = mobcount('map_lost$, instance_npcname("Wooden Box#02") + "::OnMobDead");
		if (.@count == 0) {
				'lost_farm = 4;
				disablenpc();
				enablenpc instance_npcname("Silk#02");
				npctalk "ซิลค์ : ...จบแล้วเหรอ?", instance_npcname("Silk#02");
				end;
		}
		npctalk "ผ้าไหม : มี" + (.@count == 1 ? "เป็น" : "เป็น") + " " + .@count + "ซ้าย!";
		end;
}


1@lost,91,92,3	script(DISABLED)	Silk#03	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 5) {
				mes "[ผ้าไหม]";
				mes "ไม่ต้องสนใจที่อยู่ของฉัน...";
				npctalk "ซิลค์ : ไม่ต้องสนใจที่อยู่ของฉัน...";
				next;
				mes "[ผ้าไหม]";
				mes "ถึงจะเหมาะที่จะมาเล่นก็ตามแต่อย่ามาเล่นกับผมนะ";
				npctalk "ซิลค์ : ถึงแม้ว่าการมาเล่นจะเป็นเรื่องถูกต้องก็ตาม อย่างน้อยก็ไม่ใช่กับฉัน";
				next;
				mes "[ผ้าไหม]";
				mes "แต่ฉันคิดว่าพวกเขาน่าจะเล่นกับคุณนะ! ถูกต้องแล้ว!";
				npctalk "ซิลค์ : แต่ฉันคิดว่าพวกเขาน่าจะเล่นกับคุณนะ! ถูกต้องแล้ว!";
				next;
				mes "[ผ้าไหม]";
				mes "ยังไงก็ช่วยเก็บเกี่ยวผลผลิตครั้งหน้าด้วยนะทุกคน~ ถึงเวลากินข้าวแล้ว~~";
				npctalk "ซิลค์ : ยังไงก็ช่วยฉันเก็บเกี่ยวผลผลิตครั้งหน้าด้วยนะ! เฮ้~ ได้เวลากินข้าวแล้ว~~";
				close2;
				if ('lost_farm == 5) {
						'lost_farm = 6;
						npctalk "ซิลค์ : ฉันจัดอาหารไว้แล้ว เดี๋ยวพวกมันจะมาเร็วๆ นี้!";
						disablenpc();
						donpcevent instance_npcname("Wooden Box#03") + "::OnStart";
				}
				end;
		}
		if ('lost_farm == 7) {
				mes "[ผ้าไหม]";
				mes "แอ่ว...";
				npctalk "ซิลค์ : ว้าววว...";
				next;
				mes "[ผ้าไหม]";
				mes "เธอและฉัน...ถูกปกคลุมไปด้วยทราย...";
				npctalk "ผ้าไหม : เธอและฉัน...ถูกปกคลุมไปด้วยทราย...";
				next;
				mes "[ผ้าไหม]";
				mes "โอ้ ฉันกันน้ำและกันฝุ่นได้ ส่วนแกนฉันก็สบายดี";
				npctalk "ซิลค์ : โอ้ ฉันกันน้ำและกันฝุ่นได้ ส่วนแกนฉันก็สบายดี";
				next;
				mes "[ผ้าไหม]";
				mes "ฉันโกรธแขกเหรอ? ไม่นะ...";
				npctalk "ซิลค์ : ฉันโกรธแขกเหรอ? ไม่นะ...";
				next;
				mes "[ผ้าไหม]";
				mes "ตุ๊กตาอัตโนมัติ...โกรธได้มั้ย...อืม...";
				npctalk "ซิลค์ : ตุ๊กตาอัตโนมัติ...โกรธได้มั้ย...อืม...";
				next;
				mes "[ผ้าไหม]";
				mes "...ไม่ใช่ตอนนี้ แต่ฉันรู้สึกว่าฉันทำได้... หื้ม";
				npctalk "ซิลค์ : ...ไม่ใช่ตอนนี้ แต่ฉันรู้สึกว่าฉันทำได้... หื้ม";
				next;
				mes "[ผ้าไหม]";
				mes "ต่อไปเป็นสนามเด็กเล่นพร้อมสระว่ายน้ำแบบโอเอซิส ลุยกันเลย...";
				npctalk "ซิลค์ : ต่อไปเป็นสนามเด็กเล่นพร้อมสระว่ายน้ำแบบโอเอซิส ลุยกันเลย...";
				close2;
				if ('lost_farm == 7) {
						'lost_farm = 8;
						disablenpc();
						enablenpc instance_npcname("Silk#04");
						instance_warpall 'map_lost$,226,256,instance_id();
				}
				end;
		}
		end;
}

1@lost,91,92,3	script(DISABLED)	Wooden Box#03	4_WOODBOX,{
		npctalk "ซิลค์ : จ้ะเอ๋!";
		end;
OnStart:
		enablenpc();
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#03") + "::OnMobDead";
		monster 'map_lost$,81,103,"Lotti",20661,1, .@event$;
		monster 'map_lost$,81,99,"Sally",20660,1, .@event$;
		monster 'map_lost$,82,95,"Glory",20662,1, .@event$;
		monster 'map_lost$,91,108,"Connie",20664,1, .@event$;
		monster 'map_lost$,92,105,"Boris",20661,1, .@event$;
		monster 'map_lost$,92,99,"Rony",20664,1, .@event$;
		monster 'map_lost$,89,94,"Mikey",20663,1, .@event$;
		sleep 3000;
		npctalk "ซิลค์ : โอ้ อย่ามาที่นี่สิ~~";
		sleep 3000;
		npctalk "ซิลค์ : ไม่ใช่ฉัน ไม่ใช่ฉัน~~~";
		end;
OnMobDead:
		.@count = mobcount('map_lost$, instance_npcname("Wooden Box#03") + "::OnMobDead");
		if (.@count == 0) {
				'lost_farm = 7;
				disablenpc();
				enablenpc instance_npcname("Silk#03");
				end;
		}
		npctalk "ผ้าไหม : มี" + (.@count == 1 ? "เป็น" : "เป็น") + " " + .@count + "ซ้าย!";
		end;
}






1@lost,232,257,3	script(DISABLED)	Silk#04	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 8) {
				mes "[ผ้าไหม]";
				mes "อีกครั้ง ไม่ต้องกังวลเกี่ยวกับตำแหน่งของฉัน...";
				npctalk "ซิลค์ : อีกครั้ง อย่ากังวลเกี่ยวกับตำแหน่งของฉัน...";
				next;
				mes "[ผ้าไหม]";
				mes "คุณคิดยังไงบ้าง ฉันคิดว่าคุณทำได้ดีทีเดียวสำหรับมือใหม่";
				npctalk "ซิลค์ : คุณคิดยังไงบ้าง ฉันคิดว่าคุณทำได้ดีทีเดียวสำหรับมือใหม่";
				next;
				mes "[ผ้าไหม]";
				mes "อย่างที่คาดไว้ คุณมีความสามารถด้านนี้! ฉันพูดถูก!";
				npctalk "ซิลค์ : อย่างที่คิดไว้ คุณมีความสามารถด้านนี้นะ ฉันคิดถูกแล้ว!";
				next;
				mes "[ผ้าไหม]";
				mes "คิดว่าตุ๊กตาอัตโนมัติจะเอาใจคนได้หรอ? ฉันบอกความจริงเท่านั้น";
				npctalk "ซิลค์ : คุณคิดว่าตุ๊กตาอัตโนมัติสามารถประจบสอพลอคนได้เหรอ ฉันแค่พูดความจริงเท่านั้น";
				next;
				mes "[ผ้าไหม]";
				mes "มาดูความสามารถและแจกอาหารกัน! สาวๆ~~ ได้เวลาทานอาหารแล้ว~~";
				npctalk "ซิลค์ : มาดูความสามารถของคุณแล้วแบ่งอาหารกันทานเถอะ! สาวๆ~~ ได้เวลาทานอาหารแล้ว~~";
				close2;
				if ('lost_farm == 8) {
						'lost_farm = 9;
						disablenpc();
						donpcevent instance_npcname("Wooden Box#04") + "::OnStart";
				}
				end;
		}
		if ('lost_farm == 10) {
				mes "[ผ้าไหม]";
				mes "-";
				npctalk "ผ้าไหม : ...";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "คุณโอเคมั้ย?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Are you okay?";
				next;
				mes "[ผ้าไหม]";
				mes "ผมก็ว่ามันต่างจากการเคยชินกับแขกกับการงานแบบนี้นิดหน่อย";
				npctalk "ซิลค์ : ผมว่ามันต่างจากการเคยชินกับแขกกับการเคยชินกับงานแบบนี้นิดหน่อยครับ";
				next;
				mes "[ผ้าไหม]";
				mes "ไปต่อกันที่จุดต่อไปเลยค่ะ!";
				npctalk "ซิลค์ : ไปต่อกันที่จุดต่อไปเลยค่ะ!";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "มีอีกอันมั้ย?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Is there another one?";
				next;
				mes "[ผ้าไหม]";
				mes "ข้างนอกอาจเป็นเรือนกระจกเล็กๆ แต่เป็นเพียงภาพลวงตา ดูเพดานสิ... สูงมากเลยใช่ไหมล่ะ";
				npctalk "ซิลค์ : ข้างนอกอาจจะเป็นเรือนกระจกเล็กๆ แต่มันเป็นภาพลวงตา ดูเพดานสิ... มันสูงมากเลยใช่ไหมล่ะ";
				next;
				mes "[ผ้าไหม]";
				mes "มีเหตุผลว่าทำไมฉันถึงคอยแนะนำแขกเสมอ";
				npctalk "ซิลค์ : มีเหตุผลว่าทำไมฉันถึงคอยแนะนำแขกอยู่เสมอ";
				next;
				mes "[ผ้าไหม]";
				mes "ถัดไปอยู่ใกล้ทางเข้า แต่ตามข้อมูลของลูอิน่า มันเป็นพื้นที่เงียบสงบ คุณคิดว่ามันกำลังจะตื่นแล้วเหรอ";
				npctalk "ซิลค์ : ถัดไปอยู่ใกล้ทางเข้า แต่ตามข้อมูลของลูอิน่า มันเป็นพื้นที่สำหรับนอนหลับ คุณคิดว่าเขาจะตื่นแล้วเหรอ?";
				close2;
				if ('lost_farm == 10) {
						'lost_farm = 11;
						disablenpc();
						enablenpc instance_npcname("Silk#05");
						instance_warpall 'map_lost$,242,117,instance_id();
				}
				end;
		}
		end;
}

1@lost,232,257,3	script(DISABLED)	Wooden Box#04	4_WOODBOX,{
		npctalk "ซิลค์ : อย่ามา อย่ามา!";
		end;
OnStart:
		enablenpc();
		npctalk "ซิลค์ : เฮ้ พวกเธอ~ ไม่ใช่ฉันนะ~";
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#04") + "::OnMobDead";
		monster 'map_lost$,227,261,"Rudolf",20664,1, .@event$;
		monster 'map_lost$,223,262,"Daisy",20663,1, .@event$;
		monster 'map_lost$,217,260,"Adonis",20660,1, .@event$;
		monster 'map_lost$,216,252,"Bingo",20662,1, .@event$;
		monster 'map_lost$,223,253,"Molly",20663,1, .@event$;
		monster 'map_lost$,228,247,"Sammy",20661,1, .@event$;
		monster 'map_lost$,234,253,"Joy",20662,1, .@event$;
		sleep 3000;
		npctalk "ซิลค์ : อย่ามา อย่ามานะ";
		sleep 3000;
		npctalk "ซิลค์ : อย่ามาทางนี้นะ!";
		end;
OnMobDead:
		.@count = mobcount('map_lost$, instance_npcname("Wooden Box#04") + "::OnMobDead");
		if (.@count == 0) {
				'lost_farm = 10;
				disablenpc();
				enablenpc instance_npcname("Silk#04");
				npctalk "ซิลค์ : ...โอ้ พระเจ้า...", instance_npcname("Silk#04");
				end;
		}
		npctalk "ผ้าไหม : มี" + (.@count == 1 ? "เป็น" : "เป็น") + " " + .@count + "ซ้าย!";
		end;
}


1@lost,247,119,3	script(DISABLED)	Silk#05	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 11) {
				mes "[ผ้าไหม]";
				mes "หายใจเข้าลึกๆ... หายใจเข้าลึกๆ...";
				npctalk "ผ้าไหม : หายใจเข้าลึกๆ... หายใจเข้าลึกๆ...";
				next;
				mes "[ผ้าไหม]";
				mes "อะไรนะ ตุ๊กตาอัตโนมัติหายใจลึกๆ เหรอ ถ้าคุณควบคุมจิตใจตัวเองไม่ได้ คุณคงอยากจะหายใจเข้าลึกๆ ใช่มั้ยล่ะ?";
				npctalk "ซิลค์ : อะไรนะ ตุ๊กตาอัตโนมัติหายใจลึกๆ เหรอ ถ้าไม่สามารถควบคุมจิตใจตัวเองได้ จะอยากหายใจเข้าลึกๆ ไหมล่ะ";
				next;
				mes "[ผ้าไหม]";
				mes "ไม่ได้ใช้ปอดนะคะ แต่... ลองคิดดูสิ พิทายาส่งเสียงได้ยังไง?";
				npctalk "ไหม : ไม่ได้ด้วยปอดนะคะ แต่ว่า... ลองคิดดูสิว่า พิทายาส่งเสียงออกมาได้อย่างไร ?";
				next;
				mes "[ผ้าไหม]";
				mes "...ฉันไม่อยากจะต้องกลัว.";
				npctalk "ซิลค์ : ...ฉันไม่อยากจะต้องกลัว";
				next;
				mes "[ผ้าไหม]";
				mes "งั้นเรามาต่อกันเลยดีกว่า... พิทายาที่นี่เป็นพวกขี้เซา ดังนั้นคำตอบอาจจะแตกต่างไปเล็กน้อย เอาล่ะ...มานี่เลย...";
				npctalk "ซิลค์ : งั้นเรามาต่อกันเลยดีกว่า... พิทายาที่นี่เป็นพวกขี้เซา ดังนั้นการตอบรับอาจจะแตกต่างไปเล็กน้อย เอาล่ะ...มานี่สิ...";
				close2;
				if ('lost_farm == 11) {
						'lost_farm = 12;
						disablenpc();
						donpcevent instance_npcname("Wooden Box#05") + "::OnStart";
				}
				end;
		}
		if ('lost_farm == 13) {
				mes "[ผ้าไหม]";
				mes "วุ้ย...";
				npctalk "ซิลค์ : เฮ้อ...";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "คุณโอเคมั้ย?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Are you okay?";
				next;
				mes "[ผ้าไหม]";
				mes "แมวกัดคุณหรือเปล่า?";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "ฉันคิดอย่างนั้น.";
				unittalk getcharid(3), "" + strcharinfo(0) + " : I think so.";
				next;
				mes "[ผ้าไหม]";
				mes "มีเงื่อนไขอะไรที่ต้องกัดคนถึงจะน่ารักมั้ย?";
				npctalk "ซิลค์ : มีเงื่อนไขอะไรที่ต้องกัดคนถึงจะน่ารักมั้ย?";
				next;
				mes "[ผ้าไหม]";
				mes "ตามที่คาดไว้...ความสุขต้องแลกมาด้วยราคาที่ต้องจ่าย";
				npctalk "ซิลค์ : ตามที่คาดไว้... ความสุขต้องแลกมาด้วยราคาที่ต้องจ่าย";
				next;
				mes "[ผ้าไหม]";
				mes "เพื่อดื่มด่ำความน่ารักนี้ ฉันก็พร้อมที่จะยอมแขนยอมขาแล้ว...!";
				npctalk "ซิลค์ : เพื่อดื่มด่ำกับความน่ารัก ฉันก็พร้อมที่จะมอบแขนและขาของฉันให้แล้ว...!";
				next;
				mes "[ผ้าไหม]";
				mes "ฉันได้เจอแขกอีกครั้ง ฉันเคารพคุณ แน่นอน มันน่าชื่นชมตั้งแต่แรกแล้ว";
				npctalk "ซิลค์ : ฉันเจอแขกอีกแล้ว ฉันเคารพคุณนะ แน่นอน มันน่าชื่นชมตั้งแต่แรกแล้ว";
				next;
				mes "[ผ้าไหม]";
				mes "ตุ๊กตาอัตโนมัติมีมุกตลกไหม? โอ้ คุณเล่นตลกเก่งทีเดียว ฉันหมายความอย่างนั้นเสมอ";
				npctalk "ซิลค์ : ตุ๊กตาอัตโนมัติมันตลกเหรอ? โอ้ คุณเล่นตลกเก่งนะ ฉันหมายความอย่างนั้นเสมอ";
				next;
				mes "[ผ้าไหม]";
				mes "ยังมีอะไรให้เก็บเกี่ยวอีกไหม? ขออภัยด้วย เรามาต่อกันที่หัวข้อถัดไป";
				npctalk "ซิลค์ : ยังมีอะไรให้เก็บเกี่ยวอีกไหม? ขออภัยด้วย เรามาต่อกันที่หัวข้อถัดไป";
				close2;
				if ('lost_farm == 13) {
						'lost_farm = 14;
						disablenpc();
						enablenpc instance_npcname("Silk#06");
						instance_warpall 'map_lost$,116,165,instance_id();
				}
				end;
		}
		end;
}

1@lost,247,119,3	script(DISABLED)	Wooden Box#05	4_WOODBOX,{
		npctalk "ซิลค์ : ช่วยตุ๊กตาอัตโนมัติ!";
		end;
OnStart:
		enablenpc();
		npctalk "ผ้าไหม : ...";
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#05") + "::OnMobDead";
		monster 'map_lost$,248,115,"Dough",20664,1, .@event$;
		monster 'map_lost$,247,110,"Roro",20663,1, .@event$;
		monster 'map_lost$,246,120,"Jasmine",20661,1, .@event$;
		monster 'map_lost$,243,124,"Melody",20660,1, .@event$;
		monster 'map_lost$,247,120,"Misty",20664,1, .@event$;
		monster 'map_lost$,244,115,"Sandy",20662,1, .@event$;
		monster 'map_lost$,243,116,"Bonnie",20661,1, .@event$;
		sleep 3000;
		npctalk "ผ้าไหม : .....";
		sleep 3000;
		npctalk "ผ้าไหม : ...........มีอะไรต่างกัน !";
		end;
OnMobDead:
		.@count = mobcount('map_lost$, instance_npcname("Wooden Box#05") + "::OnMobDead");
		if (.@count == 0) {
				'lost_farm = 13;
				disablenpc();
				enablenpc instance_npcname("Silk#05");
				npctalk "ซิลค์ : จบแล้วเหรอ?!", instance_npcname("Silk#05");
				end;
		}
		npctalk "ผ้าไหม : มี" + (.@count == 1 ? "เป็น" : "เป็น") + " " + .@count + "ซ้าย!";
		end;
}


1@lost,106,160,3	script(DISABLED)	Silk#06	4_EP17_BASKET,{
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 14) {
				mes "[ผ้าไหม]";
				mes "ฉันเป็นห่วงนะ...";
				npctalk "ซิลค์ : ฉันเป็นห่วงนะ...";
				next;
				mes "[ผ้าไหม]";
				mes "เราไม่มีการแบ่งแยก แต่พวกเขาจะแยกแยะเราออกจากกันได้อย่างไร?";
				npctalk "ซิลค์ : พวกเราไม่มีความแตกต่างกัน แต่พวกเขาจะแยกแยะเราออกจากกันได้ยังไง?";
				next;
				mes "[ผ้าไหม]";
				mes "ฉันได้กลิ่น... สูด สูด";
				npctalk "ซิลค์ : ฉันได้กลิ่น... สูด สูด";
				next;
				mes "[ผ้าไหม]";
				mes "เอาละ...โควต้าวันนี้ในที่สุดก็เกินครึ่งแล้ว";
				npctalk "ซิลค์ : เอาล่ะ...วันนี้โควต้าในที่สุดก็เกินครึ่งแล้ว";
				next;
				mes "[ผ้าไหม]";
				mes "ทั้งหมดนี้ต้องขอบคุณความช่วยเหลือของคุณ... ขอบคุณมาก";
				npctalk "ซิลค์ : ทั้งหมดนี้ต้องขอบคุณความช่วยเหลือของคุณมาก... ขอบคุณมาก";
				next;
				mes "[ผ้าไหม]";
				mes "งั้นฉันขอเรียกพิทายาแถวนี้ว่า...";
				npctalk "ซิลค์ : งั้นฉันขอเรียกพิทายาแถวนี้ว่า...";
				close2;
				if ('lost_farm == 14) {
						'lost_farm = 15;
						disablenpc();
						donpcevent instance_npcname("Wooden Box#06") + "::OnStart";
				}
				end;
		}
		if ('lost_farm == 16) {
				mes "[ผ้าไหม]";
				mes "เฮ้อ...";
				npctalk "ซิลค์ : เฮ้อ...";
				next;
				mes "[ผ้าไหม]";
				mes "ตามคำแนะนำของลูอิน่า สิ่งที่เหลืออยู่ตอนนี้คือ...";
				npctalk "ซิลค์ : ตามคำแนะนำของลูอิน่า สิ่งที่เหลืออยู่ตอนนี้คือ...";
				next;
				mes "[ผ้าไหม]";
				mes "บอสเหมียวเท่านั้น...";
				npctalk "ซิลค์ : บอสเหมียวเท่านั้น...";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "บอสเหมียว?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Boss Meow?";
				next;
				mes "[ผ้าไหม]";
				mes "ถ้าเงื่อนไขความน่ารักคือการกัด บอสเหมียวก็คงจะน่ารักและกัดเก่งพอ ๆ กับตัวอื่น ๆ แหละ...";
				npctalk "ซิลค์ : หากเงื่อนไขความน่ารักคือการกัด บอสเหมียวก็คงจะน่ารักและกัดเก่งพอ ๆ กับตัวอื่น ๆ นั่นแหละ..";
				next;
				mes "[ผ้าไหม]";
				mes "...จะกัดดีมั้ย...?";
				npctalk "ซิลค์ : ...มันจะกัดดีมั้ยนะ...?";
				next;
				mes "[ผ้าไหม]";
				mes "เพื่อดื่มด่ำกับความน่ารักของมัน ผมก็พร้อมที่จะให้มันไม่เพียงแต่แขนและขาเท่านั้น แต่รวมถึงเพื่อนร่วมงานของผมด้วย...!";
				npctalk "ซิลค์ : เพื่อดื่มด่ำกับความน่ารักของมัน ฉันก็พร้อมที่จะมอบให้ไม่เพียงแค่แขนและขาของมันเท่านั้น แต่รวมถึงเพื่อนร่วมงานของฉันด้วย...!";
				next;
				mes "[ผ้าไหม]";
				mes "เป็นยังไงบ้างคะแขกผู้มีเกียรติ?";
				npctalk "ซิลค์ : เป็นยังไงบ้างคะคุณแขกที่รัก?";
				next;
				mes "[ผ้าไหม]";
				mes "เราจะไปกันไหม?";
				npctalk "ซิลค์ : ไปกันมั้ย?";
				close2;
				if ('lost_farm == 16) {
						'lost_farm = 17;
						sleep 3000;
						npctalk "ซิลค์ : สวนกุหลาบที่บอสเหมียวนอนเป็นประจำอยู่บนชั้นสอง";
						sleep 3000;
						npctalk "ซิลค์ : มาดูสถิติของลูอิน่ากันเถอะ... ฉันต้องปลุกเจ้าเหมียวตัวใหญ่ให้ได้ ถึงจะไปถึงที่นั่นได้...";
						sleep 3000;
						npctalk "ซิลค์ : ... เอาล่ะ เจ้าเหมียวตัวโตสองตัว... บอสเหมียวและเหมียวตัวโตมีลำดับชั้นกัน ดังนั้นจึงมีสถานที่เฉพาะสำหรับงีบหลับก่อนเวลาอาหาร...";
						sleep 3000;
						npctalk "ซิลค์: ฉันเดาว่าเราแค่ต้องปลอบใจใครสักคนเพื่อให้เขาเปิดทางได้ ถ้าเธอเลือกข้างหนึ่ง ฉันจะไป... ฉันจะเลี้ยงอีกข้างหนึ่ง...";
						sleep 3000;
						npctalk "ซิลค์ : ไม่เป็นไร ฉันมีสายตาที่ดี ดังนั้นถ้าคุณไปถึงที่นั่นแล้วส่งสัญญาณมา ฉันจะจัดการเอง! ทีนี้ ฉันจะแสดงพิกัดบนแผนที่ให้คุณดูใกล้ๆ ที่สุด...";
						enablenpc instance_npcname("#compass01");
						viewpointmap 'map_lost$,1,38,253,1,0xFFFF00;
						viewpointmap 'map_lost$,1,287,253,2,0xFFFF00;
				}
				end;
		}
		if ('lost_farm == 17) {
				mes "[ผ้าไหม]";
				mes "คุณสามารถเลือกตำแหน่งใดตำแหน่งหนึ่งบนแผนที่และไปได้เลย";
				viewpointmap 'map_lost$,1,38,253,1,0xFFFF00;
				viewpointmap 'map_lost$,1,287,253,2,0xFFFF00;
				close;
		}
		end;
}

1@lost,113,168,0	script(DISABLED)	#compass01	HIDDEN_WARP_NPC,30,30,{
		end;
OnTouch:
		disablenpc();
		viewpointmap 'map_lost$,1,38,253,1,0xFFFF00;
		viewpointmap 'map_lost$,1,287,253,2,0xFFFF00;
		end;
}

1@lost,106,160,3	script(DISABLED)	Wooden Box#06	4_WOODBOX,{
		npctalk "ซิลค์ : ช่วยฉันด้วย!";
		end;
OnStart:
		enablenpc();
		sleep 1000;
		.@event$ = instance_npcname("Wooden Box#06") + "::OnMobDead";
		monster 'map_lost$,108,164,"Berry",20661,1, .@event$;
		monster 'map_lost$,107,169,"Rain",20660,1, .@event$;
		monster 'map_lost$,111,173,"Windy",20663,1, .@event$;
		monster 'map_lost$,115,173,"Poppie",20661,1, .@event$;
		monster 'map_lost$,119,169,"Lala",20662,1, .@event$;
		monster 'map_lost$,111,160,"Sasha",20664,1, .@event$;
		monster 'map_lost$,115,161,"Emerald",20660,1, .@event$;
		npctalk "ซิลค์ : ฉัน~~~";
		sleep 1000;
		npctalk "ผ้าไหม : ...ดี~~~~";
		sleep 1000;
		npctalk "ซิลค์ : เอาล่ะ ฉันบอกคุณแล้วว่า~~~~~~~~ โอ๊ย!";
		end;
OnMobDead:
		.@count = mobcount('map_lost$, instance_npcname("Wooden Box#06") + "::OnMobDead");
		if (.@count == 0) {
				'lost_farm = 16;
				disablenpc();
				enablenpc instance_npcname("Silk#06");
				npctalk "ซิลค์ : เฮ้อ...", instance_npcname("Silk#06");
				end;
		}
		npctalk "ผ้าไหม : มี" + (.@count == 1 ? "เป็น" : "เป็น") + " " + .@count + "ซ้าย!";
		end;
}


// traps left side
1@lost,38,257,3	script	Big Meow#01	MD_PITAYA_B,{
		mes "[เมี๊ยวใหญ่]";
		mes "เสียงคราง";
		next;
		mes "--เจ้าเหมียวตัวใหญ่กำลังงีบหลับ --";
		close;
}

1@lost,38,256,0	script	#cat01warp	HIDDEN_WARP_NPC,4,4,{
		end;
OnTouch:
		if ('lost_farm == 17) {
				'lost_farm = 18;
				npctalk "แมวตัวโต : กึ๋ยยยยย", instance_npcname("Big Meow#01");
				disablenpc instance_npcname("Big Meow#01");
				disablenpc instance_npcname("#cat01warp");
				disablenpc instance_npcname("#compass01");
				disablenpc instance_npcname("Silk#06");

				enablenpc instance_npcname("banghae#101");
				enablenpc instance_npcname("banghae#102");
				enablenpc instance_npcname("banghae#103");
				enablenpc instance_npcname("banghae#104");
				enablenpc instance_npcname("banghae#105");
				donpcevent instance_npcname("Wooden Box#10") + "::OnStart";

				monster 'map_lost$,38,257,"Big Meow",20662,1, instance_npcname("Wooden Box#10") + "::OnMobDead";
				end;
		}
		npctalk "บิ๊กเหมียว : *จาม*";
		warp 'map_lost$,38,226;
		end;
}

1@lost,38,265,0	script(DISABLED)	banghae#101	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[0] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,39,269,"Pearl",20663,1, .@event$;
		monster 'map_lost$,37,269,"Sapphire",20662,1, .@event$;
		monster 'map_lost$,36,264,"Ruby",20660,1, .@event$;
		monster 'map_lost$,38,268,"Diamond",20661,1, .@event$;
		monster 'map_lost$,36,269,"Emerald",20664,1, .@event$;
		end;
}

1@lost,38,296,0	script(DISABLED)	banghae#102	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[1] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,35,296,"Sardonyx",20660,1, .@event$;
		monster 'map_lost$,40,296,"Opal",20664,1, .@event$;
		monster 'map_lost$,36,294,"Topaz",20662,1, .@event$;
		monster 'map_lost$,38,299,"Zircon",20661,1, .@event$;
		monster 'map_lost$,34,298,"Amethyst",20663,1, .@event$;
		end;
}

1@lost,51,317,0	script(DISABLED)	banghae#103	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[2] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,48,319,"Aquamarine",20662,1, .@event$;
		monster 'map_lost$,51,319,"Garnet",20660,1, .@event$;
		monster 'map_lost$,45,317,"Iolite",20663,1, .@event$;
		monster 'map_lost$,49,314,"Citrine",20661,1, .@event$;
		monster 'map_lost$,49,320,"Peridot",20664,1, .@event$;
		monster 'map_lost$,39,300,"Sardonyx",20660,1, .@event$;
		monster 'map_lost$,39,300,"Opal",20664,1, .@event$;
		monster 'map_lost$,39,300,"Topaz",20662,1, .@event$;
		monster 'map_lost$,39,300,"Zircon",20661,1, .@event$;
		monster 'map_lost$,39,300,"Amethyst",20663,1, .@event$;
		end;
}

1@lost,92,317,0	script(DISABLED)	banghae#104	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[3] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,92,318,"Rutile",20660,1, .@event$;
		monster 'map_lost$,95,317,"Aragonite",20662,1, .@event$;
		monster 'map_lost$,91,316,"Amber",20661,1, .@event$;
		monster 'map_lost$,90,315,"Tanzanite",20662,1, .@event$;
		monster 'map_lost$,89,316,"Obsidian",20662,1, .@event$;
		end;
}

1@lost,135,317,0	script(DISABLED)	banghae#105	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[4] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,139,317,"Onyx",20660,1, .@event$;
		monster 'map_lost$,139,318,"Tanzite",20663,1, .@event$;
		monster 'map_lost$,132,314,"Jasper",20660,1, .@event$;
		monster 'map_lost$,137,313,"Rodonite",20660,1, .@event$;
		monster 'map_lost$,133,319,"Jet",20662,1, .@event$;
		end;
}


// traps right side
1@lost,286,253,3	duplicate(Big Meow#01)	Big Meow#02	MD_PITAYA_Y

1@lost,287,253,0	script	#cat02warp	HIDDEN_WARP_NPC,4,4,{
		end;
OnTouch:
		if ('lost_farm == 17) {
				'lost_farm = 18;
				npctalk "แมวตัวโต : กึ๋ยยยยย", instance_npcname("Big Meow#02");
				disablenpc instance_npcname("Big Meow#02");
				disablenpc instance_npcname("#cat02warp");
				disablenpc instance_npcname("#compass01");
				disablenpc instance_npcname("Silk#06");

				enablenpc instance_npcname("banghae#201");
				enablenpc instance_npcname("banghae#202");
				enablenpc instance_npcname("banghae#203");
				enablenpc instance_npcname("banghae#204");
				enablenpc instance_npcname("banghae#205");
				donpcevent instance_npcname("Wooden Box#10") + "::OnStart";

				monster 'map_lost$,286,253,"Big Meow",20661,1, instance_npcname("Wooden Box#10") + "::OnMobDead";
				end;
		}
		npctalk "บิ๊กเหมียว : *จาม*";
		warp 'map_lost$,287,226;
		end;
}

1@lost,286,267,0	script(DISABLED)	banghae#201	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[0] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,283,265,"Pearl",20663,1, .@event$;
		monster 'map_lost$,287,267,"Sapphire",20662,1, .@event$;
		monster 'map_lost$,288,268,"Ruby",20660,1, .@event$;
		monster 'map_lost$,290,265,"Diamond",20661,1, .@event$;
		monster 'map_lost$,288,262,"Emerald",20664,1, .@event$;
		end;
}

1@lost,287,296,0	script(DISABLED)	banghae#202	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[1] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,287,299,"Sardonyx",20660,1, .@event$;
		monster 'map_lost$,289,298,"Opal",20664,1, .@event$;
		monster 'map_lost$,284,295,"Topaz",20662,1, .@event$;
		monster 'map_lost$,290,291,"Zircon",20661,1, .@event$;
		monster 'map_lost$,287,293,"Amethyst",20663,1, .@event$;
		end;
}

1@lost,270,317,0	script(DISABLED)	banghae#203	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[2] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,274,318,"Aquamarine",20662,1, .@event$;
		monster 'map_lost$,272,316,"Garnet",20660,1, .@event$;
		monster 'map_lost$,279,318,"Iolite",20663,1, .@event$;
		monster 'map_lost$,278,316,"Citrine",20661,1, .@event$;
		monster 'map_lost$,275,315,"Peridot",20664,1, .@event$;
		end;
}

1@lost,232,317,0	script(DISABLED)	banghae#204	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[3] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,236,318,"Rutile",20660,1, .@event$;
		monster 'map_lost$,234,318,"Aragonite",20662,1, .@event$;
		monster 'map_lost$,241,316,"Amber",20661,1, .@event$;
		monster 'map_lost$,241,315,"Tanzanite",20662,1, .@event$;
		monster 'map_lost$,239,315,"Obsidian",20662,1, .@event$;
		end;
}

1@lost,195,317,0	script(DISABLED)	banghae#205	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		disablenpc();
		'banghae[4] = 1;
		.@event$ = instance_npcname("Wooden Box#10") + "::OnMobDead";
		monster 'map_lost$,201,317,"Onyx",20662,1, .@event$;
		monster 'map_lost$,199,315,"Tanzite",20663,1, .@event$;
		monster 'map_lost$,201,314,"Jasper",20660,1, .@event$;
		monster 'map_lost$,202,317,"Rodonite",20660,1, .@event$;
		monster 'map_lost$,202,318,"Jet",20662,1, .@event$;
		end;
}


// boss
1@lost,161,315,3	script(DISABLED)	Wooden Box#10	4_WOODBOX,{
		if ('lost_farm < 19) {
				npctalk "ซิลค์ : คุณจะต้องเล่นกับพิทายาให้หมดในระหว่างทางมาที่นี่";
				end;
		}
		if (!is_party_leader()) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 19) {
				mes "[ผ้าไหม]";
				mes "....มาทางนี้สิ.";
				npctalk "ซิลค์ : ....มาทางนี้สิ.";
				next;
				mes "[ผ้าไหม]";
				mes "บอสเหมียว...!";
				npctalk "ซิลค์ : บอสเหมียว...!";
				close2;
				disablenpc();
				enablenpc instance_npcname("Wooden Box#10-1");
				sleep 2000;
				if ('lost_farm == 19) {
						'lost_farm = 20;
						donpcevent instance_npcname("#1_lost_boss") + "::OnBossSummon";
				}
				end;
		}
		if ('lost_farm == 21) {
				mes "[ผ้าไหม]";
				mes "แขก... ห๊ะ...";
				npctalk "ซิลค์ : แขก... ห๊ะ...";
				next;
				mes "[ผ้าไหม]";
				mes "นี่มัน...ไม่ใช่ความกลัว แต่มันเหมือนความเคียดแค้นมากกว่า";
				npctalk "ซิลค์ : นี่มันไม่ใช่ความกลัว แต่มันเหมือนความเคียดแค้นมากกว่า...";
				next;
				mes "[ผ้าไหม]";
				mes "พวกเราทุกคนเหมือนกันหมด แต่ฉันไม่สามารถทำหน้าที่เดียวกับลูอิน่าได้...";
				npctalk "ซิลค์ : พวกเราทุกคนเหมือนกันหมด แต่ฉันไม่สามารถทำหน้าที่เดียวกับลูอิน่าได้...";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "ออกมาก่อนนะ";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Come out for now.";
				close2;
				disablenpc();
				enablenpc instance_npcname("Silk#10");
				npctalk "ซิลค์ : เย้!", instance_npcname("Silk#10");
				end;
		}
		end;

OnStart:
		enablenpc();
		sleep 3000;
		mapannounce 'map_lost$, "Silk : Ahhhhhhhhhhhh! Save me!!",bc_yellow;
		sleep 3000;
		mapannounce 'map_lost$, "Silk : I was successful!!!!!!",bc_yellow;
		sleep 3000;
		mapannounce 'map_lost$, "Silk : I'll wait in front of the Boss Meow!",bc_yellow;
		end;

OnMobDead:
		if ('lost_farm != 18 || 'banghae[0] == 0 || 'banghae[1] == 0 || 'banghae[2] == 0 || 'banghae[3] == 0 || 'banghae[4] == 0)
				end;
		if (mobcount('map_lost$, instance_npcname("Wooden Box#10") + "::OnMobDead") == 0) {
				'lost_farm = 19;
				npctalk "ไหม : ชู่ว์";
		}
		end;
}

1@lost,161,315,3	script(DISABLED)	Wooden Box#10-1	4_WOODBOX,{
		switch (rand(1,3)) {
		case 1:
				npctalk "ซิลค์ : เขาช่วยฉันด้วย~~~~!";
				break;
		case 2:
				npctalk "ซิลค์ : คา-คาล บอส เหมียว ดาวน์~~~";
				break;
		case 3:
				npctalk "ซิลค์ : นี่~~ คุณสามารถเอาแขนของฉันไปได้เลย~~";
				break;
		}
		end;
}

1@lost,161,315,3	script(DISABLED)	Silk#10	4_EP17_BASKET,{
		if (!is_party_leader() && 'lost_farm < 22) {
				mes "[ผ้าไหม]";
				mes "ฉันจะคุยกับหัวหน้าแขก!";
				mes "กรุณาพาหัวหน้ามาที่นี่!";
				close;
		}
		if ('lost_farm == 21) {
				mes "[ผ้าไหม]";
				mes "แต่ละชิ้นมีส่วนประกอบที่แตกต่างกันออกไป แล้วจะนำมาใช้ประโยชน์อะไรได้บ้าง...?";
				next;
				mes "[ผ้าไหม]";
				mes "แล้วนี่คือวิธีเดียวที่จะเก็บเกี่ยวได้ในอนาคตใช่ไหม...?";
				next;
				mes "[ผ้าไหม]";
				mes "ก็อดไม่ได้จริงๆ แขกสนุกกันมากไหม";
				next;
				mes "-" + strcharinfo(0) + "-";
				mes "สนุกกันมั้ย?";
				unittalk getcharid(3), "" + strcharinfo(0) + " : Did you have fun?";
				next;
				mes "[ผ้าไหม]";
				mes "ใช่แล้ว! มันจบแล้ว! งานควรจะสนุกเมื่อมันจบลงไม่ใช่หรือ?";
				next;
				mes "[ผ้าไหม]";
				mes "หากคุณมอบหางพิทายาที่คุณเก็บมาให้ฉัน ประสบการณ์การทำฟาร์มในวันนี้ก็จบลงแล้ว ออกไปนอกบ้านกันเถอะ!";
				if ('lost_farm == 21)
						'lost_farm = 22;
				close;
		}
		if (isbegin_quest(8588) == 1) {
				if (checkweight(1000103,8) == 0) {	// (custom)
						mes "^008800รอก่อนนะ!!";
						mes "คุณไม่สามารถรับสินค้าเพิ่มเติมได้เนื่องจากคุณมีสินค้ามากเกินไป โปรดลองอีกครั้งหลังจากคุณผ่อนปรนลง^000000";
						close;
				}
				mes "[ผ้าไหม]";
				mes "มาดูกันดีกว่าว่าสะสมได้กี่ชิ้น?";
				next;
				setarray .@item[0],1000093,1000094,1000095,1000096,1000097;
				for ( .@i = 0; .@i < 5; ++.@i ) {
						if (countitem(.@item[.@i]) > 0) {
								.@count += countitem(.@item[.@i]);
								delitem .@item[.@i], countitem(.@item[.@i]);
						}
				}
				if (.@count >= 20)
						.@reward = 4;
				else
						.@reward = 2;
				if (countitem(1000098) > 0) {
						delitem 1000098, countitem(1000098);
						.@reward += 2;
				}
				mes "[ผ้าไหม]";
				mes "รวมเป็น" + .@count + "-";
				mes "ขอบคุณสำหรับการทำงานหนักของคุณ!";
				erasequest 8588;
				getitem 1000103, .@reward;
				next;
		}
		mes "[ผ้าไหม]";
		mes "แล้ว...คุณอยากออกไปข้างนอกมั้ย?";
		next;
		if (select( "Go out.", "Don't go out." ) == 2) {
				mes "[ผ้าไหม]";
				mes "คุณกำลังรอเพื่อนร่วมงานของคุณอยู่หรือเปล่า?";
				mes "ฉันจะรอคุณ ดังนั้นโปรดบอกฉันเมื่อคุณอยากออกไป~";
				close;
		}
		if (isbegin_quest(8589) == 0) {	// first time entering the instance
				mes "[ผ้าไหม]";
				mes "ลูอิน่า... ฉันทำรายงานส่วนขยายเสร็จแล้ว กรุณาพูดอะไรกับแขกหลังจากนี้หน่อย!";
				mes "ฉันทำได้ดีมาก... *สะอื้น*";
				setquest 8589;
		}
		mes "เจอกันใหม่ครั้งหน้า!";
		close2;
		warp "ba_maison",308,202;
		end;
}


1@lost,1,1,0	script	#1_lost_boss	HIDDEN_WARP_NPC,{
		end;
OnBossSummon:
		monster 'map_lost$,167,318,"Boss Meow",20659,1, instance_npcname("#1_lost_boss") + "::OnBossKill";
		donpcevent instance_npcname("#1_lost_boss") + "::OnSlaveSummon";
		enablenpc instance_npcname("bossbang#301");
		enablenpc instance_npcname("bossbang#302");
		enablenpc instance_npcname("bossbang#401");
		enablenpc instance_npcname("bossbang#402");
		end;
OnBossKill:
		killmonster 'map_lost$, instance_npcname("#1_lost_boss") + "::OnBossKill";
		if ('lost_farm == 20)
				'lost_farm = 21;
		disablenpc instance_npcname("Wooden Box#10-1");
		disablenpc instance_npcname("bossbang#301");
		disablenpc instance_npcname("bossbang#302");
		disablenpc instance_npcname("bossbang#401");
		disablenpc instance_npcname("bossbang#402");
		enablenpc instance_npcname("Wooden Box#10");
		end;
OnSlaveSummon:
		// note: mobs not killed when the boss dies
		monster 'map_lost$,157,316,"Mora",20661,1;
		monster 'map_lost$,167,314,"Max",20664,1;
		monster 'map_lost$,166,319,"Cache",20664,1;
		sleep 3000;
		monster 'map_lost$,172,318,"Sammy",20655,1;
		monster 'map_lost$,172,325,"Lightning",20656,1;
		monster 'map_lost$,169,329,"Daisy",20657,1;
		monster 'map_lost$,156,329,"Rudolf",20658,1;
		monster 'map_lost$,155,318,"Happy",20654,1;
		sleep 3000;
		monster 'map_lost$,156,329,"Joy",20658,1;
		monster 'map_lost$,169,329,"Glory",20657,1;
		monster 'map_lost$,150,323,"King Eyes",20664,1;
		monster 'map_lost$,161,332,"Elizabeth",20656,1;
		sleep 6000;
		monster 'map_lost$,150,320,"Roro",20660,1;
		monster 'map_lost$,152,314,"Toto",20661,1;
		monster 'map_lost$,166,327,"Dough",20664,1;
		monster 'map_lost$,174,318,"Adonis",20660,1;
		monster 'map_lost$,172,313,"Coco",20662,1;
		end;
}

1@lost,135,317,0	script(DISABLED)	bossbang#301	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:	// note: mobs not killed when the boss dies
		monster 'map_lost$,135,319,"Onyx",20656,1;
		monster 'map_lost$,135,316,"Tanzanite","G_PITAYA_V",1;
		monster 'map_lost$,131,313,"Jasper","G_PITAYA_R",1;
		monster 'map_lost$,137,315,"Rhodonite","G_PITAYA_R",1;
		monster 'map_lost$,133,319,"Jet",20656,1;
		end;
}

1@lost,129,317,0	script(DISABLED)	bossbang#302	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		monster 'map_lost$,129,316,"Rutile","G_PITAYA_R",1;
		monster 'map_lost$,132,317,"Aragonite",20656,1;
		monster 'map_lost$,134,316,"Amber","G_PITAYA_Y",1;
		monster 'map_lost$,130,316,"Joysite",20656,1;
		monster 'map_lost$,131,316,"Obsidian",20656,1;
		end;
}

1@lost,195,317,0	script(DISABLED)	bossbang#401	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		monster 'map_lost$,197,313,"Onyx",20656,1;
		monster 'map_lost$,194,315,"Tanzanite","G_PITAYA_V",1;
		monster 'map_lost$,201,313,"Jasper","G_PITAYA_R",1;
		monster 'map_lost$,200,315,"Rhodonite","G_PITAYA_R",1;
		monster 'map_lost$,202,318,"Jet",20656,1;
		end;
}

1@lost,201,317,0	script(DISABLED)	bossbang#402	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch_:
		monster 'map_lost$,199,316,"Rutile","G_PITAYA_R",1;
		monster 'map_lost$,203,317,"Aragonite",20656,1;
		monster 'map_lost$,204,316,"Amber","G_PITAYA_Y",1;
		monster 'map_lost$,199,315,"Joysite",20656,1;
		monster 'map_lost$,193,315,"Obsidian",20656,1;
		end;
}
//===== rAthena	script	=======================================
//= Instance: The Maze of Oz.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Part of the episode 18.1 main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Main Quest: Step 28.
ra_fild10,179,176,5	script	Merchant#ep18_gorge	4_EP18_MERCHANT,{
		.@md_name$ = "The Maze of Oz";

		if (ep18_1_main == 29) {
				mes "[มิเรียมปลอมตัว]";
				mes "จากนี้ไป <FONT SIZE = 14><B>^e5555e" + .@md_name$ + "^000000</FONT></B>";
				mes "^e5555eเตรียมตัวให้พร้อมแล้วคุยกับฉันอีกครั้ง^000000";
				cutin "ep18_merchant.png",2;
				changequest 11717,11719;
				ep18_1_main = 30;
				close3;
		}
		if (ep18_1_main == 30) {
				mes "[มิเรียมปลอมตัว]";
				mes "แล้วผมจะเริ่มต้น ^e5555e" + .@md_name$ + "^000000";
				next;
				if (getcharid(1) == 0 || is_party_leader() == false) {
						mes "[มิเรียมปลอมตัว]";
						mes "ฉันคิดว่าจะเป็นความคิดที่ดีที่จะจัด ^ff0000party ก่อนที่จะเข้า ^000000..";
						close;
				}
				switch( select( "Open the door", "Enter", "Stop" ) ) {
				case 1:
						if (instance_create(.@md_name$) < 0) {
								mes "งานสังสรรค์:" + getpartyname(getcharid(1)) + "";
								mes "ผู้นำ:" + strcharinfo(0) + "";
								mes "^0000ff" + .@md_name$ + "^000000- ข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						}
						mes "[มิเรียมปลอมตัว]";
						mes "คุณควรจะรอสักหน่อยก่อนเข้าไป";
						close;
				case 2:
						switch( instance_enter(.@md_name$) ) {
						case IE_OTHER:
								mes "[มิเรียมปลอมตัว]";
								mes "^ff0000เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ^000000";
								close;
						case IE_NOINSTANCE:
								mes "[มิเรียมปลอมตัว]";
								mes "^ff0000หัวหน้าปาร์ตี้ของคุณยังไม่ได้สร้างดันเจี้ยน^000000";
								close;
						case IE_NOMEMBER:
								mes "[มิเรียมปลอมตัว]";
								mes "^ff0000เฉพาะสมาชิกในกลุ่มเท่านั้นที่สามารถเข้าสู่ดันเจี้ยนอนุสรณ์ได้^000000";
								close;
						case IE_OK:
								mapannounce "ra_fild10", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering " + .@md_name$ + ".",bc_map,"0x00ff99",FW_NORMAL ,12;
								end;
						}
				case 3:
						end;
				}
		}
		cutin "ep18_merchant.png",2;
		mes "[ผู้ขาย]";
		mes "ต้องมีใครสักคนอยู่ที่นี่ แต่พวกเขาก็หายไปเฉยๆ พวกเขาไปไหน?";
		close3;

OnInit:
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(11717) == 1" );
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(11719) == 1" );
		end;
}

// Step 1
1@oz,284,164,3	script	Rope#outoz	4_ROPEPILE,{
		mes "ฉันคิดว่าฉันสามารถปีนขึ้นไปด้วยเชือกได้";
		next;
		if (select( "Use a rope to climb", "Do not use" ) == 2)
				end;
		warp "ra_fild10",180,174;
		end;
}

1@oz,282,175,3	script	Miriam#ozmd1	4_EP18_MIRIAM,{
		if ('event_oz != 0)
				end;
		'event_oz = 1;
		.@miriam1$ = instance_npcname("Miriam#ozmd1");
		.@maggi1$ = instance_npcname("Magi#ozmd1");
		.@mark1$ = instance_npcname("Mark#ozmd1");
		.@tamarin1$ = instance_npcname("Tamarin#ozmd1");
		.@miriam2$ = instance_npcname("Miriam#ozmd2");

		setpcblock PCBLOCK_NPC, true;
		npctalk "มิเรียม: นี่คือทางเข้าสู่เขาวงกตแห่งออซ", .@miriam1$;
		cutin "ep18_miriam_03.png",2;
		sleep2 2000;
		npctalk "มิเรียม: ทุกคนโปรดเคลื่อนไหวอย่างระมัดระวัง", .@miriam1$;
		sleep2 2000;
		npctalk "เมไจ: อะไรสักอย่างที่ร้อนและมีฝุ่น...", .@maggi1$;
		cutin "4job_maggi_02.PNG",2;
		enablenpc .@maggi1$;
		enablenpc .@mark1$;
		enablenpc .@tamarin1$;
		sleep2 2000;
		npctalk "มาร์ค : คุณโอเคมั้ย?", .@mark1$;
		cutin "ep18_mark_01.png",2;
		sleep2 2000;
		npctalk "มิเรียม: มีลาวาไหลอยู่ด้านล่าง และกระจายเถ้าถ่านออกมาตลอดเวลา", .@miriam1$;
		cutin "ep18_miriam_03.png",2;
		sleep2 2000;
		npctalk "มิเรียม: ระวังไว้ อาจมีถ่านอยู่ในเถ้าลาวา", .@miriam1$;
		sleep2 2000;
		npctalk "มิเรียม: ถ้าคุณหายใจเข้าไม่ถูกต้อง คุณจะเผาปอดของคุณ", .@miriam1$;
		sleep2 2000;
		npctalk "มิเรียม: ดูเหมือนว่าจะไม่มีสัตว์ประหลาดอยู่แถวนี้เลย ดังนั้นควรจะเคลื่อนไหวช้าๆ และตรวจสอบสถานการณ์อีกครั้ง", .@miriam1$;
		sleep2 2000;
		npctalk "มิเรียม: โปรดไปตามถนนทางทิศเหนือก่อน", .@miriam1$;
		sleep2 2000;
		cutin "",255;
		setpcblock PCBLOCK_NPC, false;

		disablenpc .@maggi1$;
		disablenpc .@mark1$;
		disablenpc .@tamarin1$;
		disablenpc .@miriam1$;
		enablenpc .@miriam2$;
		end;
}

1@oz,279,175,5	duplicate(dummy_npc)	Magi#ozmd1	4_4JOB_MAGGI
1@oz,280,175,3	duplicate(dummy_npc)	Mark#ozmd1	4_EP18_MARK
1@oz,279,177,3	duplicate(dummy_npc)	Tamarin#ozmd1	4_EP18_TAMARIN


// Step 2
1@oz,271,272,3	script	Miriam#ozmd2	4_EP18_MIRIAM,{
		if ('event_oz != 1)
				end;
		'event_oz = 2;
		.@miriam2$ = instance_npcname("Miriam#ozmd2");
		'miriam3$ = instance_npcname("Miriam#ozmd3");
		setpcblock PCBLOCK_NPC, true;
		npctalk "มิเรียม: ถ้าคุณข้ามสะพานตรงหน้าคุณ มันเป็นเขาวงกตจริงๆ", .@miriam2$;
		sleep2 2000;
		npctalk "มิเรียม: เราต้องระวังมากในการข้ามสะพาน คุยกันใหม่อีกครั้งนะ", .@miriam2$;
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		disablenpc .@miriam2$;
		enablenpc 'miriam3$;
		end;

OnInstanceInit:
		'miriam3_id = 0;
		'event_oz = 0;

		'map_name$ = instance_mapname("1@oz");
		disablenpc instance_npcname("Miriam#ozmd2");
		disablenpc instance_npcname("Miriam#ozmd3");
		disablenpc instance_npcname("Miriam#ozmd4");
		disablenpc instance_npcname("Maram#ozmd5");
		disablenpc instance_npcname("Miriam#ozmd5");
		disablenpc instance_npcname("Maram#ozmd7");
		disablenpc instance_npcname("#oz_way");
		disablenpc instance_npcname("Maram#ozmd6");
		disablenpc instance_npcname("Magi#ozmd1");
		disablenpc instance_npcname("Mark#ozmd1");
		disablenpc instance_npcname("Tamarin#ozmd1");
		disablenpc instance_npcname("Magi#ozmd3");
		disablenpc instance_npcname("Mark#ozmd3");
		disablenpc instance_npcname("Tamarin#ozmd3");
		disablenpc instance_npcname("Maram#ozmd4");
		disablenpc instance_npcname("Magi#ozmd5");
		disablenpc instance_npcname("Mark#ozmd5");
		disablenpc instance_npcname("Tamarin#ozmd5");
		disablenpc instance_npcname("Maram#ozmd8");
		disablenpc instance_npcname("Magi#ozmd6");
		disablenpc instance_npcname("Mark#ozmd6");
		disablenpc instance_npcname("Tamarin#ozmd6");
		disablenpc instance_npcname("Miriam#ozmd7");
		disablenpc instance_npcname("Imril#ozmd7");
		end;
}


// Step 3
1@oz,241,272,3	script	Miriam#ozmd3	EP18_NPC_MIRIAM,{
		if ('event_oz != 2)
				end;
		'event_oz = 3;
		'miriam3$ = instance_npcname("Miriam#ozmd3");
		.@maggi3$ = instance_npcname("Magi#ozmd3");
		.@mark3$ = instance_npcname("Mark#ozmd3");
		.@tamarin3$ = instance_npcname("Tamarin#ozmd3");

		setpcblock PCBLOCK_NPC, true;
		npctalk "มิเรียม: สะพานนี้อันตรายนะ ดังนั้นควรแยกกระเป๋าแล้วข้ามไปทีละฝั่งจะดีกว่า", 'miriam3$;
		sleep2 2000;
		npctalk "มิเรียม: งั้นฉันจะข้ามไปก่อน ดังนั้นโปรดเดินตามไปช้าๆ จะได้ไม่มองใต้เท้า", 'miriam3$;
		sleep2 2000;
		npctalk "ทามาริน: ว้าว ฉันมองไม่เห็นก้นเลย", .@tamarin3$;
		enablenpc .@maggi3$;
		enablenpc .@mark3$;
		enablenpc .@tamarin3$;
		sleep2 2000;
		npctalk "เมไจ: ขาของคุณดูแก่จังเลยนะ...", .@maggi3$;
		sleep2 2000;
		npctalk "มาร์ค : ทุกคนระวังตัวด้วยนะ", .@mark3$;
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		disablenpc .@maggi3$;
		disablenpc .@mark3$;
		disablenpc .@tamarin3$;
		'miriam3_id = getnpcid(0);

		unitwalk 'miriam3_id,231,272, 'miriam3$ + "::OnEvent1"; // several events are required to palliate to our too short value of MAX_WALKPATH (32) used to calculate the pathing
		end;
OnEvent1:
		unitwalk 'miriam3_id,211,272, 'miriam3$ + "::OnEvent2";
		end;
OnEvent2:
		unitwalk 'miriam3_id,191,272, 'miriam3$ + "::OnEvent3";
		end;
OnEvent3:
		unitwalk 'miriam3_id,171,272, 'miriam3$ + "::OnEvent4";
		end;
OnEvent4:
		unitwalk 'miriam3_id,157,272, 'miriam3$ + "::OnEvent5";
		end;
OnEvent5:
		disablenpc 'miriam3$;
		enablenpc instance_npcname("Miriam#ozmd4");
		enablenpc instance_npcname("Ash Toad#ozmd1");
		end;
}

1@oz,245,272,3	duplicate(dummy_npc)	Magi#ozmd3	4_4JOB_MAGGI
1@oz,244,273,3	duplicate(dummy_npc)	Mark#ozmd3	4_EP18_MARK
1@oz,244,270,3	duplicate(dummy_npc)	Tamarin#ozmd3	4_EP18_TAMARIN


// Step 4 + 5
1@oz,157,272,3	script	Miriam#ozmd4	4_EP18_MIRIAM,{
		if ('event_oz != 3)
				end;
		'event_oz = 4;

		.@miriam4$ = instance_npcname("Miriam#ozmd4");
		.@tamarin4$ = instance_npcname("Tamarin#ozmd4");
		.@ashtoad2$ = instance_npcname("Ash Toad#ozmd2");
		.@maggi4$ = instance_npcname("Magi#ozmd4");
		.@mark4$ = instance_npcname("Mark#ozmd4");
		.@ashtoad3$ = instance_npcname("Ash Toad#ozmd3");

		setpcblock PCBLOCK_NPC, true;
		mapannounce 'map_name$, "Miriam: Ah!! A monster!!!", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 1500;
		mapannounce 'map_name$, "Miriam: I'll take this rude bastard.", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 1500;
		mapannounce 'map_name$, "Tamarin: Appeared in the back!!!", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 2000;
		enablenpc .@tamarin4$;
		sleep2 500;
		enablenpc .@ashtoad2$;
		mapannounce 'map_name$, "Magi: Crazy!!!", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 500;
		enablenpc .@maggi4$;
		enablenpc .@mark4$;
		mapannounce 'map_name$, "Mark: Please support me in the front!!!", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 1000;
		'mob_count_oz = 0;
		monster 'map_name$,145,274,"Ash Toad",21295,1, .@miriam4$ + "::OnMobDead"; // EP18_ASH_TOAD
		enablenpc .@ashtoad3$;
		monster 'map_name$,143,278,"Ash Toad",21295,1, .@miriam4$ + "::OnMobDead"; // EP18_ASH_TOAD
		setpcblock PCBLOCK_NPC, false;
		end;

// Note: on official the event seems to start right after killing the 2 mobs. The character can't move.
// There is no OnTouch area, a character outside the npc range can't see the dialog. Intended ?
OnMobDead:
		'mob_count_oz++;
		if ('mob_count_oz < 2)
				end;
		if ('event_oz != 4)
				end;
		'event_oz = 5;

		.@tamarin4$ = instance_npcname("Tamarin#ozmd4");
		.@ashtoad2$ = instance_npcname("Ash Toad#ozmd2");
		.@maggi4$ = instance_npcname("Magi#ozmd4");
		.@mark4$ = instance_npcname("Mark#ozmd4");
		.@ashtoad3$ = instance_npcname("Ash Toad#ozmd3");
		.@maram4$ = instance_npcname("Maram#ozmd4");
		.@ashtoad1$ = instance_npcname("Ash Toad#ozmd1");
		.@tamarin4$ = instance_npcname("Tamarin#ozmd4");
		.@miriam4$ = instance_npcname("Miriam#ozmd4");

		setpcblock PCBLOCK_NPC, true;
		npctalk "ทามาริน : ฉันทำได้แล้ว!!!", .@tamarin4$;
		disablenpc .@ashtoad2$;
		sleep2 2000;
		npctalk "เมไจ: ฉันก็เหมือนกัน!!", .@maggi4$;
		disablenpc .@ashtoad3$;
		sleep2 2000;
		npctalk "มิเรียม: ฮะ นั่นไม่ใช่สัตว์ประหลาดที่ยิ่งใหญ่นัก แต่คงจะอันตรายถ้าเป็นพ่อค้าธรรมดา", .@miriam4$;
		disablenpc .@ashtoad1$;
		sleep2 2000;
		npctalk "มาราม :" + strcharinfo(0) + "คุณโอเคมั้ย?", .@maram4$;
		enablenpc .@maram4$;
		sleep2 2000;
		npctalk "มิเรียม: สายแล้ว! มารัม!!!", .@miriam4$;
		sleep2 2000;
		npctalk "มารัม: ฉันเจอสัตว์ประหลาดระหว่างทาง เลยมาช้านิดหน่อย", .@maram4$;
		sleep2 2000;
		npctalk "มิเรียม: ไม่มีสัตว์ประหลาดอยู่บนถนนที่เราผ่านเมื่อก่อน", .@miriam4$;
		sleep2 2000;
		npctalk "มารัม: ห้ะ? สัตว์ประหลาดโผล่มาได้ยังไงวะเนี่ย...", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: มันเป็นเรื่องใหญ่เพราะว่าการเคลื่อนย้ายสินค้าเริ่มยากขึ้นเรื่อยๆ เพราะมีสัตว์ประหลาดพวกนี้", .@maram4$;
		sleep2 2000;
		npctalk "ทามาริน: ถ้าลงไปที่พื้นละ?", .@tamarin4$;
		sleep2 2000;
		npctalk "ทามาริน: ฉันได้ยินมาว่าเคยมีสะพานข้ามหุบเขาใช่ไหม?", .@tamarin4$;
		sleep2 2000;
		npctalk "ทามาริน : เพราะยังมีซากอยู่...", .@tamarin4$;
		sleep2 2000;
		npctalk "มารัม: กล่าวกันว่าสะพานด้านบนถูกทำลายเมื่อเห็นชาวพื้นเมืองข้ามไปเพื่อหนีการโจมตีของกลุ่มหัวรุนแรง", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: พื้นดินที่อยู่ฝั่งตรงข้ามสะพานกลายเป็นพื้นที่ไม่สามารถอยู่อาศัยได้เนื่องมาจากเถ้าภูเขาไฟ ดังนั้นคงเป็นความตั้งใจของเขาที่จะทำให้มันแห้งตายไป", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: สร้างสะพานใหม่ก็ไม่ยากครับ...", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: คุณไม่สามารถโฆษณาว่าคุณกำลังซ่อนตัวอยู่ที่นี่โดยการสร้างสะพานเพราะคุณกำลังซ่อนตัวอยู่ได้", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: ตอนนี้มันอันตรายและไม่สะดวก แต่ฉันไม่มีทางเลือกอื่นนอกจากใช้เขาวงกตแห่งออซ", .@maram4$;
		sleep2 2000;
		npctalk "มารัม : ในสถานะนี้คงไม่มีใครสังเกตเห็นมาก่อน", .@maram4$;
		sleep2 2000;
		npctalk "มิเรียม: ไร้เขาวงกตแห่งออซ...", .@miriam4$;
		sleep2 2000;
		npctalk "มิเรียม: มันคงยากกว่าตอนนี้มาก", .@miriam4$;
		sleep2 2000;
		npctalk "มารัม : ถูกต้องแล้ว", .@maram4$;
		sleep2 2000;
		npctalk "มารัม: ตอนนี้เราจะไปเดินเขาวงกตอันสวยงามของออซอีกครั้งไหม?", .@maram4$;
		sleep2 2000;
		npctalk "มิเรียม: คุณหมายถึงอะไร", .@miriam4$;
		sleep2 2000;
		npctalk "มิเรียม :" + strcharinfo(0) + "จากนี้ไปลงไปจนถึงทางแยก", .@miriam4$;
		sleep2 2000;
		npctalk "มิเรียม: มันคงจะดีถ้าพวกมอนสเตอร์ไม่ปรากฏตัวออกมา...", .@miriam4$;
		sleep2 1000;
		disablenpc .@maram4$;
		setpcblock PCBLOCK_NPC, false;

		disablenpc .@maggi4$;
		disablenpc .@mark4$;
		disablenpc .@tamarin4$;
		disablenpc .@miriam4$;
		enablenpc instance_npcname("Maram#ozmd5");
		end;
}

1@oz,156,272,5	duplicate(dummy_disabled_npc)	Ash Toad#ozmd1	EP18_ASH_TOAD
1@oz,164,272,3	duplicate(dummy_disabled_npc)	Ash Toad#ozmd2	EP18_ASH_TOAD
1@oz,153,275,5	duplicate(dummy_disabled_npc)	Ash Toad#ozmd3	EP18_ASH_TOAD
1@oz,155,275,3	duplicate(dummy_disabled_npc)	Magi#ozmd4	4_4JOB_MAGGI
1@oz,156,274,3	duplicate(dummy_disabled_npc)	Mark#ozmd4	4_EP18_MARK
1@oz,161,272,5	duplicate(dummy_disabled_npc)	Tamarin#ozmd4	4_EP18_TAMARIN

1@oz,162,275,3	duplicate(dummy_npc)	Maram#ozmd4	4_EP18_MARAM


// Step 6
1@oz,98,172,3	script	Maram#ozmd5	EP18_NPC_MARAM,{
		if ('event_oz != 5)
				end;
		'event_oz = 6;
		.@miriam$ = instance_npcname("Miriam#ozmd5");
		.@maram$ = instance_npcname("Maram#ozmd5");

		enablenpc .@miriam$;
		enablenpc instance_npcname("Magi#ozmd5");
		enablenpc instance_npcname("Mark#ozmd5");
		enablenpc instance_npcname("Tamarin#ozmd5");
		npctalk "มารัม: เมื่อก่อนมันไม่เคยเป็นแบบนี้ แต่บางทีมันอาจเป็นผลจากภูเขาไฟทอร์ ที่ทำให้พื้นดินด้านล่างเริ่มละลาย ทำให้มีสัตว์ประหลาดปรากฏตัวมากขึ้น";
		setpcblock PCBLOCK_NPC, true;
		sleep2 2000;
		npctalk "มารัม : โดยเฉพาะในพื้นที่ที่มีดินหลอมละลายมาก เมื่อพยายามหลีกเลี่ยงมอนสเตอร์ลาวา เพราะมีผู้คนจำนวนมากได้รับบาดเจ็บสาหัสหรือไม่กลับมาอีกเลย...";
		sleep2 2000;
		npctalk "มารัม: เขาวงกตแห่งออซจะอยู่ได้นานแค่ไหน...";
		sleep2 3000;
		mapannounce 'map_name$, "??? : ugh!!!!!!!!!!!!!!!!!!!", bc_map, 0x00B7EE,FW_NORMAL,16;
		sleep2 2000;
		npctalk "มารัม: เสียงนี้น่ะเหรอ?!?!?!?!?";
		sleep2 2000;
		npctalk "มิเรียม: อิมริล!!!!", .@miriam$;
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		npctalk "มารัม: ฉันคิดว่าเพื่อนของคุณกำลังมีปัญหา! ฉันจะไปก่อน ดังนั้นโปรดติดตามฉันอย่างระมัดระวัง!";
		disablenpc instance_npcname("Magi#ozmd5");
		disablenpc instance_npcname("Mark#ozmd5");
		disablenpc instance_npcname("Tamarin#ozmd5");
		unitwalk getnpcid(0),68,165, .@maram$ + "::OnMove";
		donpcevent .@miriam$ + "::OnMove";
		end;
OnMove:
		disablenpc instance_npcname("Maram#ozmd5");
		enablenpc instance_npcname("Maram#ozmd7");
		enablenpc instance_npcname("Imril#ozmd6");
		end;
}

1@oz,97,167,3	script	Miriam#ozmd5	EP18_NPC_MIRIAM,{
		end;
OnMove:
		npcwalkto 67,164;
		end;
}

1@oz,92,170,3	duplicate(dummy_npc)	Magi#ozmd5	4_4JOB_MAGGI
1@oz,91,169,3	duplicate(dummy_npc)	Mark#ozmd5	4_EP18_MARK
1@oz,89,170,5	duplicate(dummy_npc)	Tamarin#ozmd5	4_EP18_TAMARIN



// Step 7
1@oz,68,165,3	script	Maram#ozmd7	4_EP18_MARAM,{
		if ('event_oz != 6)
				end;
		'event_oz = 7;
		npctalk "มารัม: คุณโอเคไหม อิมริล?";
		donpcevent instance_npcname("Imril#ozmd6") + "::OnStart";
		disablenpc instance_npcname("Maram#ozmd7");
		end;
}


// Step 8
1@oz,61,163,5	script	Imril#ozmd6	4_EP18_IMRIL,{
		end;

OnStart:
		'mob_count_oz = 0;
		.@npc_name$ = instance_npcname("Imril#ozmd6");
		monster 'map_name$,64,164,"Ash Toad",21295,1, .@npc_name$ + "::OnMobDead"; // EP18_ASH_TOAD
		monster 'map_name$,62,161,"rake hand",21296,1, .@npc_name$ + "::OnMobDead"; // EP18_RAKEHAND
		end;

OnMobDead:
		'mob_count_oz++;
		if ('mob_count_oz < 2)
				end;
		if ('event_oz != 7)
				end;
		'event_oz = 8;

		.@maram8$ = instance_npcname("Maram#ozmd8");
		.@imril6$ = instance_npcname("Imril#ozmd6");
		.@miriam5$ = instance_npcname("Miriam#ozmd5");
		.@maggi6$ = instance_npcname("Magi#ozmd6");
		.@mark6$ = instance_npcname("Mark#ozmd6");
		.@tamarin6$ = instance_npcname("Tamarin#ozmd6");

		setpcblock PCBLOCK_NPC, true;
		enablenpc .@maram8$;
		npctalk "อิมริล: ขอบคุณนะ! เราเกือบตายแล้ว!", .@imril6$;
		sleep2 2000;
		npctalk "อิมริล: อย่าลงไปข้างล่างนะ เพราะบรรยากาศไม่ได้แย่เกินไป", .@imril6$;
		sleep2 2000;
		npctalk "มิเรียม: คุณดูเหมือนจะเห็นสัตว์ประหลาดบ่อยขึ้นกว่าครั้งที่แล้วนะ", .@miriam5$;
		sleep2 2000;
		npctalk "อิมริล: แต่แล้วคนพวกนั้นล่ะ?", .@imril6$;
		enablenpc .@maggi6$;
		enablenpc .@mark6$;
		enablenpc .@tamarin6$;
		sleep2 2000;
		npctalk "มารัม: พวกเขาคือพวกนักผจญภัยที่บางทีก็ช่วยแม่ทำงาน", .@maram8$;
		sleep2 2000;
		npctalk "มารัม: คนน่าเชื่อถืออย่าได้กังวลมากนัก", .@maram8$;
		sleep2 2000;
		npctalk "อิมริล: เพราะคุณไว้ใจคนอื่นง่ายเกินไป", .@imril6$;
		sleep2 2000;
		npctalk "มิเรียม: ใช่ค่ะ.", .@miriam5$;
		sleep2 2000;
		npctalk "มารัม: เอาล่ะ รีบหนีไปก่อนที่สัตว์ประหลาดตัวใหม่จะออกมา", .@maram8$;
		sleep2 2000;
		npctalk "มารัม: เหลือเวลาอีกไม่นานครับ.", .@maram8$;
		sleep2 2000;
		npctalk "มารัม: เห็นยากนิดหน่อย แต่เดินตามถนนขึ้นมานี่สิ!", .@maram8$;
		sleep2 2000;
		npctalk "ทามาริน: ฉันคิดว่านี่แหละคือทาง!", .@tamarin6$;
		disablenpc .@maggi6$;
		disablenpc .@mark6$;
		disablenpc .@tamarin6$;
		disablenpc .@maram8$;
		enablenpc instance_npcname("#oz_way");
		setpcblock PCBLOCK_NPC, false;
		end;
}

1@oz,68,165,3	duplicate(dummy_npc)	Maram#ozmd8	4_EP18_MARAM
1@oz,61,168,3	duplicate(dummy_npc)	Magi#ozmd6	4_4JOB_MAGGI
1@oz,65,167,3	duplicate(dummy_npc)	Mark#ozmd6	4_EP18_MARK
1@oz,59,171,3	duplicate(dummy_npc)	Tamarin#ozmd6	4_EP18_TAMARIN



// Step 9
1@oz,58,256,5	script	#oz_way	1_SHADOW_NPC,3,3,{
		end;
OnTouch:
		if ('event_oz != 8)
				end;
		'event_oz = 9;
		enablenpc instance_npcname("Maram#ozmd6");
		end;
}

// Step 10 (maybe not required ?)
1@oz,44,255,5	script	Maram#ozmd6	4_EP18_MARAM,{
		if ('event_oz != 9)
				end;
		'event_oz = 10;
		npctalk "มารัม: ทางนี้!!!";
		initnpctimer;
		end;
OnTimer2000:
		disablenpc instance_npcname("Maram#ozmd6");
		enablenpc instance_npcname("Maram#ozmd9");
		enablenpc instance_npcname("Rope#outoz2");
		end;
}

// Step 11
// (Step 29 on the main quest).
1@oz,21,189,5	script	Maram#ozmd9	4_EP18_MARAM,{
		if ('event_oz != 10)
				end;
		'event_oz = 11;

		.@maram9$ = instance_npcname("Maram#ozmd9");
		.@miriam7$ = instance_npcname("Miriam#ozmd7");
		.@imril7$ = instance_npcname("Imril#ozmd7");

		setpcblock PCBLOCK_NPC, true;
		npctalk "มารัม: ปีนเชือกข้างหน้านี้เพื่อออกมา", .@maram9$;
		enablenpc .@miriam7$;
		enablenpc .@imril7$;
		sleep2 2000;
		npctalk "อิมริล: ฉันจะไปเอาของซ่อนไว้ที่นี่ ดังนั้นขึ้นไปก่อนเถอะ", .@imril7$;
		sleep2 2000;
		npctalk "มิเรียม: ทุกคนเหนื่อยมากแล้ว ขึ้นไปชั้นบนกันเถอะ", .@miriam7$;
		if (ep18_1_main == 30) {
				changequest 11719,11720;
				ep18_1_main = 31;
		}
		disablenpc .@miriam7$;
		disablenpc .@imril7$;
		setpcblock PCBLOCK_NPC, false;
		end;

OnInstanceInit:
		disablenpc instance_npcname("Maram#ozmd9");
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(11719) == 1" );
		end;
}

1@oz,22,187,5	duplicate(dummy_npc)	Miriam#ozmd7	4_EP18_MIRIAM
1@oz,16,185,3	duplicate(dummy_npc)	Imril#ozmd7	4_EP18_IMRIL


// Step 12
1@oz,28,190,3	script(DISABLED)	Rope#outoz2	4_ROPEPILE,{
		if ('event_oz < 11)
				end;
		mes "ฉันคิดว่าฉันสามารถปีนขึ้นไปด้วยเชือกได้";
		next;
		switch( select( "Use a rope to climb", "Do not use" ) ) {
		case 1:
				close2;
				warp "gw_fild01",275,337;
				end;
		case 2:
				end;
		}
		end;

OnInstanceInit:
		questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(11720) == 1" );
		end;
}


// Spawn monsters
1@oz,22,207,5	script	#oz01	1_SHADOW_NPC,4,4,{
		end;
OnTouch_:
		getmapxy .@map$,.@x,.@y, BL_NPC;
		monster 'map_name$,.@x-1,.@y,"Ash Toad",21295,1;	// EP18_ASH_TOAD
		monster 'map_name$,.@x+1,.@y,"Rake Hand",21296,1;	// EP18_RAKEHAND
		disablenpc instance_npcname( strnpcinfo(0) );
		end;
}

1@oz,30,237,5	duplicate(#oz01)	#oz02	1_SHADOW_NPC,4,4
1@oz,44,264,5	duplicate(#oz01)	#oz03	1_SHADOW_NPC,4,4
1@oz,62,247,5	duplicate(#oz01)	#oz04	1_SHADOW_NPC,4,4
1@oz,72,222,5	duplicate(#oz01)	#oz05	1_SHADOW_NPC,4,4
1@oz,79,203,5	duplicate(#oz01)	#oz06	1_SHADOW_NPC,4,4
1@oz,124,261,5	duplicate(#oz01)	#oz07	1_SHADOW_NPC,4,4
1@oz,139,279,5	duplicate(#oz01)	#oz08	1_SHADOW_NPC,4,4
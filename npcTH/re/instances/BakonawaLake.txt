//===== rAthena Script =======================================
//= Bakonawa Lake
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================

// Instance Creation :: ma_baku
//============================================================
ma_scene01,174,179,4	script	Taho	541,{
		disable_items;
		if (checkweight(1201,1) == 0) {
				mes "คุณพกของเยอะเกินไป กลับมาเอาของบางส่วนออกก่อน";
				close;
		}
		if (MaxWeight - Weight < 10000) {
				mes "คุณมีน้ำหนักเกิน ควรกลับมาลดน้ำหนักอีกครั้ง";
				close;
		}
		if (BaseLevel < 140) {
				mes "[ทาโฮ]";
				mes "เฮ้ ที่นี่มันอันตรายเกินไปสำหรับคุณ คุณไม่สามารถปกป้องตัวเองได้ ออกไปจากที่นี่แล้วไปที่หมู่บ้านทันที!";
				close;
		}
		if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
				mes "[ทาโฮ]";
				mes "พวกเราจะถูกบาโกนาวาฆ่าตายหมดแน่ๆ เราต้องฆ่ามันให้ได้ แต่โชคไม่ดีที่เราไม่ได้เตรียมตัวมา";
				close;
		}

		set .@party_id,getcharid(1);
		set .@md_name$, "Bakonawa Lake";

		set .@baku_time, checkquest(12278,PLAYTIME);
		if (.@baku_time == -1) {
				if (checkquest(12279) == -1) setquest 12279;
		}
		if (!.@party_id) {
				mes "[ทาโฮ]";
				mes "ไม่มีบริษัทไหนไปกับคุณได้เลยหรือ หรือหากคุณต้องการไปคนเดียวก็ไปหลังจากจัดปาร์ตี้เสร็จแล้วก็ได้";
				close;
		}
		if (.@baku_time == -1) {
				if (is_party_leader() == true) {
						mes "[ทาโฮ]";
						mes "คุณคือหัวหน้ากลุ่มนักล่าบาโกนาวาใช่ไหม ถนนไม่ค่อยดี เราเลยต้องลงหน้าผาด้วยเชือก ไม่เป็นไรใช่ไหม";
						next;
						set .@i, select("Please weave a rope.:Now I will go down.:Cancel.");
				} else {
						mes "[ทาโฮ]";
						mes "คุณมาเพื่อสังหารบาโกนาวา หัวหน้ากลุ่มของคุณต้องช่วยฉันทอเชือกเพื่อลงไป";
						next;
						set .@i, select(":Now I will go down.:Cancel.");
				}
				switch(.@i) {
				case 1:
						if (instance_create(.@md_name$) < 0) {
								mes "[ทาโฮ]";
								mes "อ้าว เชือกหลุด ต้องทำใหม่แล้ว";
								close;
						}
						mes "[ทาโฮ]";
						mes "ตอนนี้ฉันกำลังทอผ้าอยู่ ดังนั้นคุณลงมาได้เมื่อฉันทอเสร็จ";
						close;
				case 2:
						switch(instance_enter(.@md_name$)) {
						case IE_OTHER:
								mes "[ทาโฮ]";
								mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						case IE_NOINSTANCE:
								mes "[ทาโฮ]";
								mes "เชือกยังไม่ได้ทอ รอก่อนนะ";
								close;
						case IE_NOMEMBER:
								mes "[ทาโฮ]";
								mes "พลเรือนไม่สามารถเข้าได้";
								close;
						case IE_OK:
								mapannounce "ma_scene01","A party member, "+strcharinfo(0)+" of the party "+getpartyname(.@party_id)+" is entering the dungeon, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
								setquest 12278;
								//warp "1@ma_b",64,51;
								close;
						}
				case 3:
						close;
				}
		} else if (.@baku_time == 0 || .@baku_time == 1) {
				mes "[ทาโฮ]";
				mes "เชือกที่ใช้สำหรับไปยังทะเลสาบขาดแล้ว ตอนนี้เรากำลังทำความสะอาดบริเวณโดยรอบ ดังนั้นกลับมาใหม่อีกครั้งหลังจากผ่านไปสักพัก";
				close;
		} else if (.@baku_time == 2) {
				mes "[ทาโฮ]";
				mes "ที่นี่ทุกอย่างเรียบร้อยหมดแล้ว ตอนนี้คุณสามารถลงมาได้อีกครั้งหลังจากได้เชือกมา";
				erasequest 12278;
				close;
		}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
		callfunc "F_GM_NPC";
		mes "[ทาโฮ]";
		mes "ฉันชื่อมิงกี้";
		if (callfunc("F_GM_NPC",1854,0) == 1) {
				erasequest 12278;
				erasequest 12279;
				set malaya_bakona1,7;
				set malaya_bakona2,15;
		}
		close;
}

// Instance Scripts :: in_baku01
//============================================================
1@ma_b,62,52,4	script	Taho#nf	541,{
		mes "[ทาโฮ]";
		mes "ฮ่าๆๆ ฉันสามารถร่วมการล่าบาโคนาวาได้ แต่ฉันอยากจะซ่อนตัวอยู่หลังก้อนหินนั้นและเชียร์พวกคุณมากกว่า";
		next;
		mes "[ทาโฮ]";
		mes "บาโกนาวาจะไม่มีวันพ่ายแพ้ เมื่อถึงเวลานั้น จงฟังคำแนะนำของฉันอย่างตั้งใจ เชื่อฉันแล้วทุกอย่างจะดีขึ้นเอง";
		next;
		if (is_party_leader() == true) {
				mes "[ทาโฮ]";
				mes "บัดนี้ข้าพเจ้าจะทำให้ต้นบาโกนาวาผุดขึ้นมาจากทะเลสาบ";
				next;
				switch(select("Hold on! I'm not ready yet!:Let's do it!")) {
				case 1:
						mes "[ทาโฮ]";
						mes "โอเค... บอกฉันเมื่อคุณพร้อม";
						close;
				case 2:
						mes "[ทาโฮ]";
						mes "แล้วฉันจะหยดน้ำมัน Albopal ลงในทะเลสาบแห่งนี้";
						next;
						mes "[ทาโฮ]";
						mes "เมื่อบาโกนาวาได้ชิมสิ่งนี้แล้ว เขาจะปรากฏตัว...";
						donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
						disablenpc instance_npcname("Taho#nf");
						close;
				}
		} else {
				mes "[ทาโฮ]";
				mes "ฉันกำลังทิ้ง Alpopal เพื่อทำให้ Bakonawa โกรธ Bakonawa อาจเป็นแหล่งโปรตีนที่ดีเมื่อเราได้มันมา";
				close;
		}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#Bakonawan1");
		end;
OnStart:
		enablenpc instance_npcname("#Bakonawan1");
		set .@map$, instance_mapname("1@ma_b");
		mapannounce .@map$,"Taho: I can see him there on the surface! ATTACK!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		monster .@map$,78,83,"Bakonawa",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		initnpctimer;
		end;
OnFail:
		killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		end;
OnMyMobDead:
		set .@map$, instance_mapname("1@ma_b");
		if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
				donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
				killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
				stopnpctimer;
				disablenpc instance_npcname("#Bakonawan1");
		}
		end;
OnTimer1000:	callsub OnAnnounce,"10 minutes";
OnTimer60000:	callsub OnAnnounce,"9 minutes";
OnTimer120000:	callsub OnAnnounce,"8 minutes";
OnTimer180000:	callsub OnAnnounce,"7 minutes";
OnTimer240000:	callsub OnAnnounce,"6 minutes";
OnTimer300000:	callsub OnAnnounce,"5 minutes";
OnTimer360000:	callsub OnAnnounce,"4 minutes";
OnTimer420000:	callsub OnAnnounce,"3 minutes";
OnTimer480000:	callsub OnAnnounce,"2 minutes";
OnTimer540000:	callsub OnAnnounce,"1 minute";
OnTimer570000:
		mapannounce instance_mapname("1@ma_b"),"Time Limit: 30 seconds",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
		donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
		end;
OnTimer600000:
		set .@map$, instance_mapname("1@ma_b");
		mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
		donpcevent instance_npcname("Taho#Fail")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
		end;
OnAnnounce:
		mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
		end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#SummonPuppetsn1");
		end;
OnStart:
		stopnpctimer;
		enablenpc instance_npcname("#SummonPuppetsn1");
		initnpctimer;
		end;
OnTouch:
		stopnpctimer;
		disablenpc instance_npcname("#SummonPuppetsn1");
		end;
OnTimer5000:
		setarray .@x[0],79,71,60,61,57,89,95,96,99;
		setarray .@y[0],71,72,80,90,99,73,82,90,99;
		set .@map$, instance_mapname("1@ma_b");
		for(set .@i,0; .@i<9; set .@i,.@i+1) {
				set .@rand, rand(1,10);
				if (.@rand > 7)
						monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
				else if (.@rand < 4)
						monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		}
		end;
OnTimer50000:
		killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#SummonPuppetsn1");
		end;
OnMyMobDead:
		end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#BakonawaDeadn1");
		end;
OnStart:
		enablenpc instance_npcname("#BakonawaDeadn1");
		initnpctimer;
		end;
OnTimer100:
		mapannounce instance_mapname("1@ma_b"),"Taho: Good! Bakonawa is hidden under water. He will now try to swallow the moon.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		end;
OnTimer5000:
		mapannounce instance_mapname("1@ma_b"),"Taho: We must make loud noises with caldron and gong so he can never concentrate on the moon!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		end;
OnTimer10000:
		mapannounce instance_mapname("1@ma_b"),"Taho: Do not engage on him even if he come above the surface. Just make loud noises!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		end;
OnTimer15000:
		mapannounce instance_mapname("1@ma_b"),"Mission - Destroy 2 caldrons and 2 gongs on left and right sides of the lake.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
		stopnpctimer;
		disablenpc instance_npcname("#BakonawaDeadn1");
		end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#Bakonawan2");
		end;
OnStart:
		enablenpc instance_npcname("#Bakonawan2");
		set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
		set .@map$, instance_mapname("1@ma_b");
		monster .@map$,95,98,"Caldron",2328,1,.@label$;
		monster .@map$,60,98,"Caldron",2328,1,.@label$;
		monster .@map$,97,104,"Gong",2328,1,.@label$;
		monster .@map$,58,104,"Gong",2328,1,.@label$;
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
		initnpctimer;
		end;
OnMyMobDead:
		set .@map$, instance_mapname("1@ma_b");
		set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
		if (.@mob_dead_num < 1) {
				donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
				stopnpctimer;
				disablenpc instance_npcname("#Bakonawan2");
		} else
				mapannounce .@map$,"Taho: Good! "+.@mob_dead_num+" left to go!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer270000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer280000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer290000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer295000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer296000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer297000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer298000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer299000:	callsub OnAnnounce,"1 second",20,0;
OnTimer300000:
		set .@map$, instance_mapname("1@ma_b");
		mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
		donpcevent instance_npcname("Taho#Fail")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
		killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
		end;
OnAnnounce:
		mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
		if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
		end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#Bakonawan2-1");
		end;
OnStart:
		enablenpc instance_npcname("#Bakonawan2-1");
		monster instance_mapname("1@ma_b"),78,93,"Enraged Bakonawa",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
		end;
OnFail:
		killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
		end;
OnEnd:
		enablenpc instance_npcname("#Bakonawan2-1");
		killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
		initnpctimer;
		end;
OnTimer1000:
		mapannounce instance_mapname("1@ma_b"),"Taho: He is back into the lake again. Is it over?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		end;
OnTimer5000:
		mapannounce instance_mapname("1@ma_b"),"Taho: UHM! Something is moving up rapidly under the water! Ba... Bakonawa is coming!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		end;
OnTimer10000:
		mapannounce instance_mapname("1@ma_b"),"Mission - Eliminate Bakonawa who became more fierce.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2-1");
		end;
OnMyMobDead:
		end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#Bakonawan3");
		end;
OnStart:
		enablenpc instance_npcname("#Bakonawan3");
		initnpctimer;
		monster instance_mapname("1@ma_b"),78,83,"Enraged Bakonawa",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
		end;
OnMyMobDead:
		set .@map$, instance_mapname("1@ma_b");
		set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
		if (.@mob_dead_num < 1) {
				mapannounce .@map$,"Taho: We got him! I can see he has threw up something that looks like a box.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
				donpcevent instance_npcname("Taho#Completed")+"::OnStart";
				donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
				killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
				stopnpctimer;
				disablenpc instance_npcname("#Bakonawan3");
		}
		end;
OnTimer1000:
		callsub OnAnnounce,"10 minutes",15,1;
		end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
		donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
		end;
OnTimer300000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer360000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer420000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer480000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer540000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer570000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer580000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer590000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer595000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer596000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer597000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer598000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer599000:	callsub OnAnnounce,"1 second",20,0;
OnTimer600000:
		set .@map$, instance_mapname("1@ma_b");
		mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
		donpcevent instance_npcname("Taho#Fail")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
		end;
OnAnnounce:
		mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
		if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
		end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
		end;
OnInstanceInit:
		disablenpc instance_npcname("#Bakonawan3-1");
		end;
OnStart:
		initnpctimer;
		end;
OnEnd:
		killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
		//showdigit 0;
		stopnpctimer;
		end;
OnMyMobDead:
		showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
		end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
		killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
		stopnpctimer;
		end;
OnMobSpawn:
		set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
		set .@map$, instance_mapname("1@ma_b");
		killmonster .@map$,.@label$;
		set .@mob_dead_num, mobcount(.@map$,.@label$);
		if (.@mob_dead_num < 50) {
				set .@mob_rg, 50 - .@mob_dead_num;
				if (.@mob_rg > getarg(0))
						set .@mob_rg, getarg(0);
				while(1) {
						areamonster .@map$,74,74,82,74,"Bakonawa's Puppet",2334,1,.@label$;
						set .@mob_dead_num, mobcount(.@map$,.@label$);
						if (.@mob_dead_num >= .@mob_rg) {
								break;
						}
				}
		}
		end;
}

1@ma_b,62,52,4	script	Taho#Completed	541,{
		disable_items;
		if (checkweight(1201,1) == 0) {
				mes "ดูเหมือนว่าคุณจะพกสิ่งของหลายประเภทเกินไป โปรดลองอีกครั้งหลังจากลดขนาด";
				close;
		}
		if (MaxWeight - Weight < 10000) {
				mes "ดูเหมือนว่าคุณกำลังขนของที่มีน้ำหนักมากเกินไป โปรดลองอีกครั้งหลังจากลดน้ำหนัก";
				close;
                            }
		if (checkquest(12279,HUNTING) == 2) {
				erasequest 12279;
				mes "[ทาโฮ]";
				mes "นี่พบจากศพของบาโกนาวา จะดีกว่าถ้าคุณจะเอาสิ่งนี้ไป";
				if (!VIP_SCRIPT || vip_status(VIP_STATUS_ACTIVE))
						getitem 6499,7; //Ancient_Grudge
				else
						getitem 6499,5; //Ancient_Grudge
				getexp 0,100000;
				next;
		}
		mes "[ทาโฮ]";
		mes "เราควรรีบออกไปจากที่นี่ดีกว่า ที่นี่ยังคงอันตรายแม้ว่าจะไม่มีบาโกนาวา...";
		next;
		mes "[ทาโฮ]";
		mes "จากนั้นเราก็มาใช้เชือกขึ้นไปกันเถอะ!";
		close2;
		warp "ma_scene01",175,176;
		end;
OnInstanceInit:
		disablenpc instance_npcname("Taho#Completed");
		end;
OnStart:
		enablenpc instance_npcname("Taho#Completed");
		initnpctimer;
		end;
OnTimer1000:
		monster instance_mapname("1@ma_b"),78,74,"Bakonawa Treasure Box",2335,1;
		end;
OnTimer10000:
		mapannounce instance_mapname("1@ma_b"),"Taho: After opening the treasure box, please visit me at the top of the hill. I've got something to give to you.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		stopnpctimer;
		end;
}

1@ma_b,62,52,4	script	Taho#Fail	541,{
		mes "[ทาโฮ]";
		mes "อืม เราเกือบจะจับมันได้แล้ว แต่เขาเป็นสิ่งมีชีวิตที่เร็วมาก...";
		next;
		if (is_party_leader() == true) {
				mes "[ทาโฮ]";
				mes "เราจะลองอีกครั้งมั้ย?";
				next;
				switch(select("Hold on a sec!:Of course! We cannot stand back now!")) {
				case 1:
						mes "[ทาโฮ]";
						mes "โอ้ย...เกือบพาเขาออกมาได้แล้ว";
						close;
				case 2:
						mes "[ทาโฮ]";
						mes "แล้วผมจะใช้บริการ Albopal อีกครั้งไปที่ทะเลสาบ";
						next;
						mes "[ทาโฮ]";
						mes "เรากำลังจะได้เห็นบาโกนาวาด้วยความโกรธที่รุนแรงยิ่งขึ้น...";
						disablenpc instance_npcname("Taho#Fail");
						donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
						close;
				}
				close;
		} else {
				mes "[ทาโฮ]";
				mes "ฉันจะทิ้งอัลโบปาลเหมือนที่ทำไปเมื่อไม่นานนี้ ฉันหวังว่าเขาคงจะไม่พอใจ...";
				close;
		}

OnInstanceInit:
		disablenpc instance_npcname("Taho#Fail");
		end;
OnStart:
		enablenpc instance_npcname("Taho#Fail");
		end;
}
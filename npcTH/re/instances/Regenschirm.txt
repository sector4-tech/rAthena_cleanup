//===== rAthena Script =======================================
//= Regenschirm
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 - Regenschirm
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@rgsr,1,1,0	script	#171_rgsr_control	HIDDEN_WARP_NPC,{
		end;

OnInstanceInit:
		'map$ = instance_mapname("1@rgsr");
		'rgsr = 'activate = 0;
		deletearray 'rescued;
		deletearray 'rsgr_c;

		disablenpc instance_npcname("Researcher#rgsr_ev_0");
		disablenpc instance_npcname("Access Controller#rgsr");
		disablenpc instance_npcname("#rgsr_touch_mob_0");
		disablenpc instance_npcname("#rgsr_touch_mob_1");
		disablenpc instance_npcname("#rgsr_warp_0");
		disablenpc instance_npcname("Est#rgsr");
		disablenpc instance_npcname("Rebellion Crew#rgsr_dum01");
		disablenpc instance_npcname("Aas#rgsr3");
		disablenpc instance_npcname("Goni#rgsr");
		disablenpc instance_npcname("Est#rgsr2");
		disablenpc instance_npcname("Central Room#rgsr");
		disablenpc instance_npcname("Restrained Specimen#rgsr");
		disablenpc instance_npcname("Control Device#rgsr0");
		disablenpc instance_npcname("Control Device#rgsr1");
		disablenpc instance_npcname("Control Device#rgsr2");
		disablenpc instance_npcname("Control Device#rgsr3");
		disablenpc instance_npcname("Aas#rgsr4");
		end;

OnEvent01:
		enablenpc instance_npcname("#rgsr_touch_mob_0");
		enablenpc instance_npcname("#rgsr_touch_mob_1");
		donpcevent instance_npcname("#rgsr_mob_1") + "::OnSummon";
		end;

OnEvent02:
		enablenpc instance_npcname("#rgsr_warp_0");
		donpcevent instance_npcname("#rgsr_mob_2") + "::OnSummon";
		end;

OnEvent03:
		enablenpc instance_npcname("Restrained Specimen#rgsr");
		enablenpc instance_npcname("Control Device#rgsr0");
		enablenpc instance_npcname("Control Device#rgsr1");
		enablenpc instance_npcname("Control Device#rgsr2");
		enablenpc instance_npcname("Control Device#rgsr3");
		end;

OnEvent04:
		if ('rgsr == 13) {
				'rgsr = 14;
				donpcevent instance_npcname("#rgsr_boss") + "::OnSummon";
		}
		end;
}

1@rgsr,123,42,5	script	Oscar#rgsr	4_M_LGTGUARD,{
		mes "[ออสการ์]";
		mes "กัปตันแอบเข้าไปในห้องแผงควบคุม";
		mes "เคลื่อนไหวอย่างเงียบๆ";
		close;
}

1@rgsr,131,58,3	script	Aas#rgsr	4_EP17_AS,{
		cutin "ep171_as02",1;
		mes "[อาส]";
		mes "โดยปกติประตูจะถูกล็อคด้วยระบบรักษาความปลอดภัย ดังนั้น ฉันต้องอยู่ที่นั่นและทำงานกับตัวควบคุม";
		mes "อย่างที่คุณเห็น ทางเข้าส่วนใหญ่ถูกปิดตายไปแล้ว";
		mes "สวัสดี";
		next;
		mes "[อาส]";
		mes "จะต้องใช้เวลาสักพักเพื่อผ่านไปทีละอัน";
		mes "ฉันจะช่วยคุณผ่านระบบอินเตอร์คอมต่อไป";
		next;
		mes "[อาส]";
		mes "คุณจะต้องเดินผ่านร้านอาหารเพื่อไปถึงทางเดิน";
		mes "เอสท์จะเป็นผู้ดำเนินการเพียงคนเดียว";
		next;
		mes "[อาส]";
		mes "หากมีสิ่งรบกวนเกิดขึ้น เอสท์อาจเป็นสาเหตุได้ ดังนั้นโปรดตรวจสอบกับเธอก่อนดำเนินการต่อ";
		next;
		mes "[อาส]";
		mes "คุณควรไปได้แล้ว นักผจญภัย ฉันจะทำงานเกี่ยวกับการรักษาความปลอดภัยของคอนโทรลเลอร์ต่อไป";
		mes "อาจมีนักวิจัยถูกกักขังอยู่ภายใน";
		next;
		mes "[อาส]";
		mes "โปรดคำนึงถึงความปลอดภัยของพวกเขาเป็นอันดับแรก";
		mes "ตอนนี้ไปข้างหน้าเลย";
		close2;
		if ('rgsr == 0) {
				'rgsr = 1;
				donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent01";
		}
		warp 'map$,127,67;
		end;
}

1@rgsr,60,138,0	script	Access Controller#rgsr	4_ENERGY_WHITE,{
		enablenpc instance_npcname("Researcher#rgsr_ev_0");
		if ('rgsr == 2) {
				'rgsr = 3;
				npctalk "ขอให้มีการสื่อสาร";
				sleep2 2750;
				npctalk "การวิเคราะห์สัญญาณ";
				sleep2 2750;
				npctalk "กำลังเปิดใช้งานการส่งสัญญาณเสียง...";
				sleep2 2750;
				npctalk "เชื่อมต่อหรือยังคะ Aas ค่ะ";
				sleep2 2750;
				npctalk "ฉันได้ควบคุมระบบรักษาความปลอดภัยบางส่วนได้แล้ว แต่ยังไม่สามารถเข้าถึงได้เต็มรูปแบบ";
				sleep2 2750;
				npctalk "ถ้าไม่มีเหตุรบกวนมาก อุปกรณ์แจ้งเตือนหรือระบบรักษาความปลอดภัยก็จะไม่ทำงาน";
				sleep2 2750;
				npctalk "ฉันคิดว่าคุณควรเปิดประตูนี้เพื่อเปิดทางเดิน";
				sleep2 2750;
				npctalk "ขอให้นักวิจัยที่ถูกคุมขังวางมือบนอุปกรณ์";
				sleep2 2750;
				npctalk "...*แซ็ป*....";
				sleep2 2750;
				npctalk "การส่งสัญญาณเสียงสิ้นสุดลง";
				sleep2 2750;
				hideonnpc();
				enablenpc();
				npctalk "นักวิจัย : คุณจะรังเกียจไหมถ้าฉันจะเปิดประตู?",instance_npcname("Researcher#rgsr_ev_0");
				'rgsr = 4;
		}
		end;
}

1@rgsr,59,132,7	script	Researcher#rgsr_ev_0	4_M_NFMAN,{
		if ('rgsr == 4 && 'activate == 0) {
				'activate = 1;
				mes "[นักวิจัย]";
				mes "การสแกนข้อมูลชีวภาพยังใช้งานได้อยู่ ฉันจะอยู่ที่นี่เพื่อเปิดประตูไว้";
				mes "หากคุณรีบก็ไปเถอะ...";
				dispbottom "Control : Identity confirmed.";
				donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent02";
				close2;
				sleep2 800;
				npctalk "นักวิจัย : ... ห๊ะ... นี่มันอะไรวะ...";
				end;
		}
}

1@rgsr,36,211,3	script	Est#rgsr	4_F_ESTLOVELOY,{
		if ('rgsr < 7) {
				cutin "ep162_est03",2;
				mes "[เอส]";
				mes "คุณคือคนที่เอาแก๊สพิษออกไปด้านนอกใช่ไหม?";
				mes "คุณมาถึงในเวลาที่ดี";
				mes "เพราะแก๊สทำให้ประตูถูกล็อคอย่างรุนแรง!";
				next;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "Aas ต้องใช้เวลาสักพักในการแฮ็กคอนโทรลเลอร์และปล่อยแก๊ส";
				mes "ฉันก็จะทำสิ่งนั้นเอง";
				next;
				cutin "ep162_est02",2;
				mes "[เอส]";
				mes "ทหารยามที่นี่สวมชุดเดียวกับนักล่าหัวใจที่เราเห็นมาก่อน";
				mes "ฉันไม่คิดว่าฉันสามารถช่วยนักวิจัยทั้งหมดได้โดยลำพัง";
				next;
				mes "[เอส]";
				mes "ดูเหมือนว่าเราได้รวบรวมส่วนใหญ่ไว้ที่นี่แล้ว";
				mes "มาจัดกลยุทธ์ต่อไปของเรากันดีกว่า";
				next;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "อีกด้านมีห้องทดลองและอาจเกิดความยุ่งวุ่นวายได้";
				mes "ฉันสงสัยว่าจะมีการต่อสู้ครั้งใหญ่หรือไม่";
				next;
				mes "[เอส]";
				mes "จากนี้ไปหน่วยรบพิเศษของกบฏจะให้ความสำคัญกับการช่วยเหลือนักวิจัยที่ถูกคุมขังเป็นหลัก";
				mes "หากได้แล้วรีบดำเนินการทันที!";
				next;
				mes "[เอส]";
				mes "โอเค นักผจญภัย หาอุปกรณ์สื่อสารและติดต่อกับ Aas";
				next;
				mes "[เอส]";
				mes "คุณจะต้องเปิดประตูทางเข้าห้องแล็ปส่วนตัว";
				if ('rgsr == 4)
						'rgsr = 5;
				close3;
		}
		cutin "ep162_est02",2;
		mes "[เอส]";
		mes "ฮะ เราจะเน้นไปที่การช่วยเหลือตัวประกันก่อน";
		mes "ลองไปหาห้องของ รปภ. ที่ Aas พูดถึงดูสิ";
		close3;
}

1@rgsr,26,219,0	script	Access Controller#rgsr2	4_ENERGY_WHITE,{
		if ('rgsr < 5) {
				mes "โค้ดไม่ได้ใช้งานอยู่";
				mes "การสื่อสารได้ถูกยุติแล้ว";
				close;
		}
		if ('rgsr == 5) {
				mes "เชื่อมต่อการส่งสัญญาณเสียงแล้ว";
				close2;
				if ('rgsr == 5)
						'rgsr = 6;
				npctalk "กำลังรอการตอบกลับการสื่อสาร...";
				sleep2 2750;
				npctalk "อาส : ผู้บัญชาการเอส? เราล็อคประตูห้องแล้ว แต่มีปัญหาอะไรเหรอ?";
				sleep2 2750;
				npctalk "AAS: โอ้คุณเป็นนักผจญภัย";
				sleep2 2750;
				npctalk "Aas: ดูเหมือนจะใช้เวลาสักพัก แต่ฉันสามารถควบคุมโมดูลการตรวจสอบส่วนใหญ่ได้แล้ว";
				sleep2 2750;
				npctalk "อาส : ทหารศัตรูส่วนใหญ่อยู่ในล็อบบี้และบริเวณพักผ่อน ดังนั้นบริเวณห้องปฏิบัติการจึงได้รับการป้องกันน้อยกว่า";
				sleep2 2750;
				npctalk "Aas : อย่างไรก็ตาม เพื่อที่จะรักษาความปลอดภัยห้องควบคุมกลาง คุณจะต้องผ่านห้องยามซึ่งตรวจพบวัตถุทดสอบจำนวนมาก";
				sleep2 2750;
				npctalk "Aas : อันดับแรกต้องดูแลความปลอดภัยห้องทดลองก่อน แล้วค่อยดูแลห้องการ์ด.... *คลิก*";
				sleep2 2000;
				npctalk "เอสท์ : เอ๊ะ นั่นอะไรนะ?",instance_npcname("Est#rgsr");
				if ('rgsr == 6)
						'rgsr = 7;
				end;
		}
		if ('rgsr < 7)
				end;
		else {
				mes "โค้ดไม่ได้ใช้งานอยู่";
				mes "การสื่อสารได้ถูกยุติแล้ว";
				close;
		}
}

1@rgsr,235,44,5	script	Aas#rgsr2	4_EP17_AS,{
		if ('rgsr == 7) {
				for (.@i = 0; .@i < 3; .@i++) {
						if (!'rescued[.@i])
								.@miss++;
				}
				if (.@miss) {
						cutin "ep171_as02",2;
						mes "[อาส]";
						mes "เฮ้ คุณตรวจดูห้องแล็ปทั้งหมดแล้วหรือยัง?";
						mes "กรุณาตรวจสอบว่ายังมีนักวิจัยที่ถูกควบคุมตัวอยู่หรือไม่";
						close3;
				}
				cutin "ep171_as02",1;
				mes "[อาส]";
				mes "กุญแจมาสเตอร์สำรองยังอยู่";
				mes "ถ้าไม่มีมัน ฉันคงไม่สามารถควบคุมระบบรักษาความปลอดภัยได้ คุณโอเคไหม?";
				next;
				mes "[อาส]";
				mes "ตอนนี้คุณเพียงต้องตรวจสอบห้องกลางเท่านั้น";
				mes "คุณบอกว่า Heart Hunter ใช่มั้ย?";
				mes "พวกคนทรยศจากชไลน์ได้หลบหนีไปแล้ว";
				next;
				mes "[อาส]";
				mes "เรามุ่งเน้นไปที่ห้องนั้น";
				mes "นั่นคือความคิดหลัก";
				mes "เอสอยู่ไหน?";
				next;
				cutin "16go_01",0;
				mes "[โกนิ]";
				mes "-";
				next;
				cutin "ep171_as02",1;
				mes "[อาส]";
				mes "ต้องคอยค้นหาบรรดาผู้วิจัยทั้งหมดเลยเหรอ?";
				mes "มันจำเป็นฉันเข้าใจ";
				next;
				mes "[อาส]";
				mes "นักผจญภัย พวกเราไปที่ห้องกลาง";
				mes "เจอกันที่นั่นนะ.";
				close2;
				cutin "",255;
				if ('rgsr == 7) {
						enablenpc instance_npcname("Central Room#rgsr");
						'rgsr = 8;
						disablenpc instance_npcname("#rgsr_mob_2");
				}
				end;
		}
		cutin "ep171_as02",1;
		mes "[อาส]";
		mes "นักผจญภัย พวกเราไปที่ห้องกลาง";
		mes "เจอกันที่นั่นนะ.";
		close3;
}

1@rgsr,235,143,5	script	Detained Researcher#rgsr_q01	4_F_SCIENCE,{
		if (!'rescued[0]) {
				mes "[นักวิจัย]";
				mes "ของฉัน... การค้นหาของฉัน... วัสดุ...";
				mes "พวกเขาเอามันไปหมดแล้ว!";
				mes "พวกเขาเอาทุกอย่างไป!";
				next;
				cutin "16go_01",0;
				mes "[โกนิ]";
				mes "-";
				next;
				cutin "",255;
				npctalk "ฉันจะไปอยู่แล้วค่ะ ขอบคุณค่ะ!";
				mes "โอ้ คุณมาที่นี่เพื่อปกป้องฉันเหรอ?";
				mes "ไปด้วยนะคะ ขอบคุณค่ะ!";
				next;
				mes "[นักวิจัย]";
				mes "โอ้ คุณอยู่ที่นั่นเพื่อปกป้องฉันเหรอ?";
				mes "ไปด้วยนะคะ ขอบคุณค่ะ!";
				'rescued[0] = 1;
				close2;
				disablenpc();
		}
		end;
}

1@rgsr,237,108,3	script	Detained Researcher#rgsr_q02	4_LGTSCIENCE,{
		if (!'rescued[1]) {
				mes "[นักวิจัย]";
				mes "ฉันคิดว่าพวกเขาจะไล่ไอ้บ้านั่นออกไปหมดแล้ว...";
				mes "ฉันตีหัวด้านหลังแบบนี้";
				mes "จุ๊ๆ";
				next;
				mes "[นักวิจัย]";
				mes "คุณมาที่นี่เพื่อช่วยเราใช่ไหม?";
				mes "พาฉันออกไปจากที่นี่!";
				mes "เราหนีไม่ได้เพราะพวกเขาเอาแก๊สพิษมาไว้ข้างนอกประตู!";
				next;
				mes "[นักวิจัย]";
				mes "ใช่ ฉันรู้นิดหน่อยเกี่ยวกับ Heart Hunters ฉันจะบอกคุณทุกอย่าง!";
				next;
				cutin "ep162_est02",2;
				mes "[เอส]";
				mes "อ๋อ มีข้อมูลเพิ่มเติมจะมาแชร์ไหมคะ?";
				mes "คุณอยากออกไปคุยกับฉันเกี่ยวกับเรื่องนี้อย่างใจเย็นไหม?";
				next;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "ฉันจะพาผู้ชายคนนี้ออกไปจากที่นี่ แล้วคุณก็ดูห้องอื่นๆ";
				'rescued[1] = 1;
				close2;
				cutin "",255;
				disablenpc();
		}
		end;
}

1@rgsr,237,71,3	script	Detained Researcher#rgsr_q03	4_F_FRUIT,{
		if (!'rescued[2]) {
				mes "[นักวิจัย]";
				mes "ขอความช่วยเหลือหน่อยนะคะ!";
				mes "ฉันทำงานหนักแต่ไม่ได้รับเงิน!";
				next;
				mes "[นักวิจัย]";
				mes "ชไลน์ ชไลน์อยู่ไหน";
				mes "เขาเป็นคนทรยศ!";
				mes "ผมเห็นด้วยตาตัวเอง เขาเพิ่งฆ่าผู้คุม...";
				next;
				select("Calm down.");
				mes "[นักวิจัย]";
				mes "ตอนนี้ปลอดภัยแล้วใช่ไหม?";
				mes "ผมดีใจมากที่ได้รับการจ้างที่ Rekenber แต่ทำไมบริษัทใหญ่โตเช่นนี้ถึงเกิดขึ้นล่ะ...";
				next;
				mes "[นักวิจัย]";
				mes "พูดอะไรนะ? พวกกบฏกำลังจะมาช่วยเรางั้นเหรอ?";
				mes "ฉันจะหนีไปกับคุณแทนไม่ได้เหรอ?";
				next;
				mes "[นักวิจัย]";
				mes "โอ้... คุณเป็นส่วนหนึ่งของทีมลาดตระเวนใช่ไหม?";
				mes "โอเค ฉันจะรอทีมกู้ภัย";
				mes "หลายคนหลบหนีโดยการทุบหน้าต่างแล้ววิ่งหนีไป..";
				next;
				mes "[นักวิจัย]";
				mes "โอ้ คุณรู้แล้ว";
				mes "โอเค ฉันจะเงียบแล้วรอ";
				mes "ชุดของคุณก็เท่จริงๆ นะ";
				'rescued[2] = 1;
				close;
		}
		mes "[นักวิจัย]";
		mes "ฉันรู้ ฉันรู้";
		mes "หากฉันรออยู่ที่นี่ พวกกบฏจะเข้ามาช่วยฉันไหม?";
		mes "ฉันก็จะสบายดี";
		next;
		mes "[นักวิจัย]";
		mes "แต่ชุดของคุณก็เท่จริงๆ นะ";
		close;
}

1@rgsr,241,51,0	script	Scattered Documents#rgsr	4_ENERGY_BLUE,{
		if (isbegin_quest(7863) == 0) {
				mes "นี่คือรายชื่อทีมงานรักษาความปลอดภัยขององค์กร";
				mes "บางอันก็มีวงกลมล้อมรอบ... บางอันก็มีขีดฆ่าออกไป";
				next;
				cutin "ep171_as01",2;
				mes "[อาส]";
				mes "พวกที่วงกลมไว้คงจะเป็นพวกที่ยอมรับที่จะรับสมัคร...";
				mes "ทีมของฉันคือทีมที่มีเครื่องหมาย X ข้างๆ";
				next;
				mes "[อาส]";
				mes "มันเป็นสัญญารับจ้าง...";
				mes "มีแผนคร่าวๆ สำหรับวันโจมตี";
				mes "ฉันไม่เชื่อว่ามันจะมาถึงจุดนี้ได้...";
				next;
				mes "[อาส]";
				mes "นี่ฉันตั้งใจจะยั่วโมโหฉัน";
				mes "คราวนี้มันจะไม่ทำงาน ฉันจะติดตามเรื่องนี้จนจบ";
				next;
				mes "[อาส]";
				mes "ฉันควรเอามันกลับไปให้เทสแล้ววิเคราะห์มัน";
				setquest 7863;
				close3;
		}
		mes "ฉันมีข้อมูลอยู่ที่นี่แล้ว";
		mes "ที่นี่ก็ไม่มีอะไรให้ดูอีกแล้ว";
		close;

OnInstanceInit:
		questinfo(QTYPE_CLICKME,QMARK_YELLOW,"isbegin_quest(7863) == 0");
		end;
}

1@rgsr,125,157,0	script	Central Room#rgsr	4_ENERGY_BLUE,{
		if ('rgsr == 8) {
				'rgsr = 9;
				enablenpc instance_npcname("Aas#rgsr3");
				enablenpc instance_npcname("Goni#rgsr");
				enablenpc instance_npcname("Est#rgsr2");
				npctalk "ฉันสายมั้ย?",instance_npcname("Aas#rgsr3");
				sleep2 1800;
				npctalk "ไม่ คุณมาทันเวลาพอดี",instance_npcname("Est#rgsr2");
				end;
		}
		mes "อุปกรณ์ไม่ทำงาน.";
		close;
}

1@rgsr,128,157,3	script	Aas#rgsr3	4_EP17_AS,{
		if ('rgsr > 13)
				end;
		if ('rgsr == 9) {
				'rgsr = 10;
				.@aas$ = instance_npcname("Aas#rgsr3");
				.@gonie$ = instance_npcname("Goni#rgsr");
				.@erst$ = instance_npcname("Est#rgsr2");
				npctalk "อัศ : ห้องกลางน่าจะว่างนะครับ เดี๋ยวจะเข้าไปดูครับ",.@aas$;
				sleep2 3000;
				npctalk "อาส : ฉันจะไปพาพวกนักวิจัยออกไปจากที่นี่ก่อน ไม่เป็นไรใช่ไหม",.@erst$;
				sleep2 3000;
				npctalk "Aas : ใช่ครับ ไม่พบอะไรเพิ่มเติมในระบบเลย จากความก้าวหน้าของเราจนถึงตอนนี้ ศัตรูทั้งหมดก็ถูกควบคุมไว้หมดแล้ว",.@aas$;
				sleep2 3000;
				npctalk "เอส : จะกลัวอะไรอีกล่ะ ฉันกลับแล้วนะ!",.@erst$;
				sleep2 3000;
				npctalk "โกนิ : ... ฉันก็เหมือนกัน.",.@gonie$;
				sleep2 3000;
				npctalk "เอสท์ : ในห้องแล็ปยังมีนักวิจัยเหลืออยู่สองสามคน ฉันจะพาพวกเขาไปด้วย ฉันจะรอ",.@erst$;
				sleep2 3000;
				npctalk "เอสท์ : โอเค โกนิ ไปที่ล็อบบี้หลักก่อนแล้วจัดการเรื่องถนน ฉันจะพาคนอื่นๆ ไปที่ห้องประชุม",.@erst$;
				sleep2 3000;
				npctalk "โกนิ : *พยักหน้า*",.@gonie$;
				sleep2 3000;
				disablenpc instance_npcname("Goni#rgsr");
				disablenpc instance_npcname("Est#rgsr2");
				npctalk "อาส : นักผจญภัยทั้งหลาย ลองไปดูห้องกลางกันเถอะ",.@aas$;
				if ('rgsr == 10)
						'rgsr = 11;
				end;
		}
		cutin "ep171_as02",1;
		mes "[อาส]";
		mes "นั่นคือห้องกลางตรงไปข้างหน้า";
		next;
		if (select("Enter.:Wait.") == 2) {
				mes "[อาส]";
				mes "กรุณาพูดคุยกับฉันเมื่อคุณพร้อม";
				mes "ผมไม่ทราบว่าทำไม แต่ว่ามันปิดแน่นมาก;. มันต้องใช้เวลาสักพักในการเข้าถึง";
				next;
				mes "[อาส]";
				mes "ตามที่ได้รับการยืนยันจากระบบรักษาความปลอดภัย ไม่พบสิ่งผิดปกติใดๆ ภายใน...";
				mes "ผมยังอยากที่จะยืนยันภายในห้องต่อไป";
				close3;
		}
		mes "[อาส]";
		mes "ขั้นแรกฉันต้องปิดใช้งานกลไกการล็อกอัตโนมัติก่อน จากนั้นฉันจะดำเนินการตามทันที";
		if ('rgsr == 11) {
				'rgsr = 12;
				donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent03";
		}
		close2;
		cutin "",255;
		warp 'map$,126,164;
		end;
}

1@rgsr,124,152,5	script	Goni#rgsr	4_M_GONY,{
		cutin "16go_01",0;
		mes "[โกนิ]";
		mes "-";
		close3;
}

1@rgsr,125,154,5	script	Est#rgsr2	4_F_ESTLOVELOY,{
		cutin "ep162_est01",2;
		mes "[เอส]";
		mes "ฉันไม่เชื่อว่าจะมีศัตรูเหลืออยู่ข้างใน ฉันจะดึงออกมา";
		mes "แค่ตรวจสอบห้องกลางแล้วออกไป";
		close3;
}

1@rgsr,125,188,3	script	Restrained Specimen#rgsr	20353,2,3,{
		if ('rgsr == 12) {
				specialeffect EF_RED_HIT;
				specialeffect EF_BEGINSPELL3;
				npctalk "อย่า...อย่า...เข้ามาใกล้ฉันนะ...!! ฮึ่ย หัวฉัน...มันเจ็บ...";
				mapannounce 'map$,"Do... do not... come close to me...!! Huh, my head... it hurts...",bc_map,0x00FF00;
				'rgsr = 13;
		}
		end;

OnFail:
		specialeffect EF_FREEZE;
		npctalk "อุ๊ยย-!! นั่นสิ...ทำอะไรลงไป!!";
		mapannounce 'map$,"Awesome-!! That's it... What did you do!!",bc_map,0x00FF00;
		end;
}

1@rgsr,144,180,0	script	Control Device#rgsr0	4_ENERGY_BLUE,{
		if ('rgsr < 13) {
				mes "อุปกรณ์นี้มีวัตถุประสงค์ที่ไม่ทราบแน่ชัด";
				mes "ในห้องมีปุ่มอยู่สี่ปุ่ม";
				close;
		}
		.@index = atoi(replacestr(strnpcinfo(2),"rgsr",""));
		if (.@index > 0) {
				if (!'rsgr_c[ (.@index - 1) ]) {
						mes "อุปกรณ์นี้มีวัตถุประสงค์ที่ไม่ทราบแน่ชัด";
						mes "ในห้องมีปุ่มอยู่สี่ปุ่ม";
						next;
						select("Press the button.");
						donpcevent instance_npcname("Restrained Specimen#rgsr") + "::OnFail";
						close;
				}
		}
		if (!'rsgr_c[.@index]) {
				switch (.@index) {
						case 0: .@answer = 2; .@string$ = "three phases"; break;
						case 1: .@answer = 1; .@string$ = "two phases"; break;
						case 2: .@answer = 4; .@string$ = "one phase"; break;
						case 3: .@answer = 3;
				}
				mes "อุปกรณ์นี้มีวัตถุประสงค์ที่ไม่ทราบแน่ชัด";
				mes "ในห้องมีปุ่มอยู่สี่ปุ่ม";
				next;
				.@s = select("Red Button.:Blue Button.:White Button.:Yellow Button.");
				if (.@s != .@answer)
						donpcevent instance_npcname("Restrained Specimen#rgsr") + "::OnFail";
				else {
						specialeffect EF_BEGINSPELL3,AREA,instance_npcname("Restrained Specimen#rgsr");
						if (.@index < 3) {
								mapannounce 'map$,"Gravity control adjustment... completed... There are " + .@string$ +" remaining.",bc_map,0x0D98BA;
								'rsgr_c[.@index] = 1;
						} else {
								'rsgr_c[.@index] = 1;
								mapannounce 'map$,"Gravity control adjustment... completed... Restraints have been disengaged.",bc_map,0x0D98BA;
								sleep2 2500;
								donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent04";
						}
				}
				close;
		}
		mes "อุปกรณ์นี้ได้รับการเปิดใช้งานแล้ว";
		close;
}

1@rgsr,108,168,0	duplicate(Control Device#rgsr0)	Control Device#rgsr1	4_ENERGY_RED
1@rgsr,114,191,0	duplicate(Control Device#rgsr0)	Control Device#rgsr2	4_ENERGY_WHITE
1@rgsr,143,168,0	duplicate(Control Device#rgsr0)	Control Device#rgsr3	4_ENERGY_YELLOW

1@rgsr,127,164,3	script	Aas#rgsr4	4_EP17_AS,{
		if ('rgsr != 15)
				end;
		cutin "ep171_as01",2;
		mes "[อาส]";
		mes "เอสต์จัดการทุกอย่างภายในเรียบร้อยแล้ว";
		mes "ขออภัยที่ไม่สามารถช่วยคุณได้เลย.";
		next;
		mes "[อาส]";
		mes "เรากลับไปที่ Einbroch กัน";
		mes "ฉันจะ...ฉันจะไปที่สำนักงานของฉันแล้วไปร่วมกับคุณ";
		next;
		.@s = select("Move to Einbroch.:Move to Lighthalzen.:Stay for a while.");
		if (.@s == 3) {
				cutin "ep171_as02",2;
				mes "[อาส]";
				mes "คุณมีอะไรที่ต้องตรวจสอบอีกไหม?";
				mes "ฉันจะรออยู่ที่นี่.";
				close3;
		}
		mes "[อาส]";
		mes "เราจะรอคุณอยู่ใน Cat on Bullet";
		mes "โปรดไปที่นั่นและพบเราที่สำนักงานใหญ่";
		completequest 7862;
		close2;
		if (.@s == 1)
				warp "einbroch",301,324;
		else
				warp "lighthalzen",54,272;
		end;
}

//= Monster
1@rgsr,1,1,0	script	#rgsr_mob_1	HIDDEN_WARP_NPC,{
OnSummon:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		setarray .@xy,
		153,80,155,81,154,82, //= Bottom Right
		138,85,136,87,138,89, //= Center Right
		152,94,152,98,153,98, //= Stairs Right
		145,114,151,114,156,114, //= Top Right
		97,114,102,114,107,114, //= Top Left
		102,98,101,96,101,95, //= Stairs Left
		116,88,118,88,121,87, //= Center Left
		117,101,117,101,117,102, //= Center Top
		93,66,93,67,94,68, //= Bottom Door
		58,65,56,57,44,59,31,53,15,55, //= Bottom Room
		59,94,48,98,53,103,36,110,33,126,46,132,55,131; //= Upper Room
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
				monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3627,1,.@event$;
		end;

OnMobKill:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
		if (!mobcount('map$,.@event$) && 'rgsr == 1) {
				'rgsr = 2;
				disablenpc();
				enablenpc instance_npcname("Access Controller#rgsr");
		}
		end;
}

1@rgsr,1,1,0	script	#rgsr_mob_2	HIDDEN_WARP_NPC,{
OnSummon:
		setarray .@xy,102,137,104,136,104,135,126,139,138,137,139,134,137,133,151,135,150,136,151,137,85,151,82,215,84,216,84,218,107,216;
		setarray .@left_xy,85,170,54,186,55,183,58,181,59,186,62,183,62,216,59,214,56,217,54,214,52,217;
		setarray .@right_xy,167,136,167,135,168,135,208,138,208,134,203,138,200,134,187,123,188,
		123,187,122,186,108,188,107,186,105,196,106,203,109,205,105,208,109,187,92,186,90,188,91,
		187,69,187,67,188,67,201,74,202,77,207,76,211,74,187,48,188,45,186,46,197,45,202,45,208,42,211,42;
		setarray .@top_xy,169,217,169,214,172,217,172,214,176,217,176,214,180,217,180,214,184,217,184,214,167,204,168,203,168,203,167,176,167,176,166,175;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
				monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3627,1;
		for (.@i = 0; .@i < getarraysize(.@left_xy); .@i += 2)
				monster 'map$,.@left_xy[.@i],.@left_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnLeftKill";
		for (.@i = 0; .@i < getarraysize(.@right_xy); .@i += 2)
				monster 'map$,.@right_xy[.@i],.@right_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnRightKill";
		for (.@i = 0; .@i < getarraysize(.@top_xy); .@i += 2)
				monster 'map$,.@top_xy[.@i],.@top_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnTopKill";
		end;

OnLeftKill:
		.@event$ = instance_npcname(strnpcinfo(0))+"::OnLeftKill";
		if (!mobcount('map$,.@event$)) {
				mapannounce 'map$,"Est : Who is it? Are you Aas? Are you the adventurer?",bc_map,0x0D98BA;
				enablenpc instance_npcname("Est#rgsr");
				enablenpc instance_npcname("Rebellion Crew#rgsr_dum01");
		}
		end;

OnRightKill:
OnTopKill:
		end;
}

1@rgsr,1,1,0	script	#rgsr_boss	HIDDEN_WARP_NPC,{
		end;
OnSummon:
		mapannounce 'map$,"Ahahaha.. ahahaha... I'll kill you all!!!!",bc_map,0x00FF00;
		sleep 2000;
		mapannounce 'map$,"Aas : What is going on? *click* The lock is...!!",bc_map;
		sleep 2000;
		mapannounce 'map$,"I'm not a defect! I'm not a monster!",bc_map,0x00FF00;
		specialeffect EF_LORD,AREA,instance_npcname("Restrained Specimen#rgsr");
		disablenpc instance_npcname("Restrained Specimen#rgsr");
		monster 'map$,125,188,"Failed Specimen",20353,1,instance_npcname(strnpcinfo(0))+"::OnBossKill";
		end;

OnBossKill:
		if ('rgsr == 14) {
				'rgsr = 15;
				hideonnpc instance_npcname("Aas#rgsr3");
				disablenpc instance_npcname("Aas#rgsr3");
				enablenpc instance_npcname("Aas#rgsr4");
				disablenpc();
		}
		end;
}

1@rgsr,94,68,5	script	#rgsr_touch_mob_0	3622,4,6,{
		end;

OnTouch:
		disablenpc();
		.@v = atoi(replacestr(strnpcinfo(2),"rgsr_touch_mob_",""));
		if (!.@v)
				setarray .@xy,98,66,94,68,95,70,92,68;
		else
				setarray .@xy,91,91,91,93,95,93,94,97,97,92;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
				monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3622,1;
		end;
}

1@rgsr,95,93,5	duplicate(#rgsr_touch_mob_0)	#rgsr_touch_mob_1	3622,4,6

//= Warp/Other
1@rgsr,59,136,0	warp	#rgsr_warp_0	1,2,1@rgsr,66,136

1@rgsr,51,184,0	script	#rgsr_warp_1	WARPNPC,1,2,{
		end;

OnTouch:
		.@event$ = instance_npcname("#rgsr_mob_2")+"::OnLeftKill";
		if (mobcount('map$,.@event$)) {
				dispbottom "Voice Behind Door : Who's outside? exterminate that strange poison gas!!";
				end;
		}
		switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
				case 1: warp 'map$,46,184; break;
				case 2:	warp 'map$,46,216; break;
				case 3: warp 'map$,54,184; break;
				case 4: warp 'map$,54,216;
		}
		end;
}

1@rgsr,51,216,0	duplicate(#rgsr_warp_1)	#rgsr_warp_2	WARPNPC,1,2
1@rgsr,49,184,0	duplicate(#rgsr_warp_1)	#rgsr_warp_3	WARPNPC,1,2
1@rgsr,49,216,0	duplicate(#rgsr_warp_1)	#rgsr_warp_4	WARPNPC,1,2

1@rgsr,186,216,0	script	#rgsr_warp_5	WARPNPC,1,2,{
		.@event$ = instance_npcname("#rgsr_mob_2")+"::OnTopKill";
		if (mobcount('map$,.@event$)) {
				mes "ตรวจพบเอนทิตี้ที่ไม่รู้จัก การเข้าถึงถูกปฏิเสธ";
				close;
		}
		switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
				case 5: warp 'map$,191,216; break;
				case 6: warp 'map$,183,216;
		}
		end;
}

1@rgsr,188,216,0	duplicate(#rgsr_warp_5)	#rgsr_warp_6	WARPNPC,1,2

1@rgsr,214,136,0	script	#rgsr_warp_7	WARPNPC,1,2,{
		.@event$ = instance_npcname("#rgsr_mob_2")+"::OnRightKill";
		if (mobcount('map$,.@event$) > 0 || 'rgsr < 7) {
				mes "ตรวจพบเอนทิตี้ที่ไม่รู้จัก การเข้าถึงถูกปฏิเสธ";
				close;
		}
		switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
				case 7: warp 'map$,219,136; break;
				case 8: warp 'map$,211,136; break;
				case 9: warp 'map$,219,108; break;
				case 10: warp 'map$,211,108; break;
				case 11: warp 'map$,219,76; break;
				case 12: warp 'map$,211,76; break;
				case 13: warp 'map$,219,44; break;
				case 14:  warp 'map$,211,44;
		}
		end;
}

1@rgsr,216,136,0	duplicate(#rgsr_warp_7)	#rgsr_warp_8	WARPNPC,1,2
1@rgsr,214,108,0	duplicate(#rgsr_warp_7)	#rgsr_warp_9	WARPNPC,1,2
1@rgsr,216,108,0	duplicate(#rgsr_warp_7)	#rgsr_warp_10	WARPNPC,1,2
1@rgsr,214,76,0	duplicate(#rgsr_warp_7)	#rgsr_warp_11	WARPNPC,1,2
1@rgsr,216,76,0	duplicate(#rgsr_warp_7)	#rgsr_warp_12	WARPNPC,1,2
1@rgsr,214,44,0	duplicate(#rgsr_warp_7)	#rgsr_warp_13	WARPNPC,1,2
1@rgsr,216,44,0	duplicate(#rgsr_warp_7)	#rgsr_warp_14	WARPNPC,1,2

1@rgsr,37,126,0	script	Researcher#rgsr_dum00	4_F_SCIENCE,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "ทำไมฉันต้องให้อาหารแก่คนทรยศพวกนี้ด้วย?";
		close;
}

1@rgsr,35,136,7	script	Researcher#rgsr_dum01	4_LGTSCIENCE,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "ฉันอยากกลับบ้าน... ฉันแค่อยากกลับบ้าน...";
		close;
}

1@rgsr,38,139,3	script	Researcher#rgsr_dum02	4_M_NFMAN,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "คุณมาที่นี่เพื่อช่วยเราใช่ไหม?";
		mes "ขอบคุณค่ะ! ขอบคุณค่ะ!";
		close;
}

1@rgsr,31,186,3	script	Researcher#rgsr_dum03	4_LGTSCIENCE,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "เกิดอะไรขึ้นวะเนี่ย?";
		mes "อ๊าา...ฉันแค่อยากกลับบ้าน...";
		close;
}

1@rgsr,24,186,5	script	Researcher#rgsr_dum04	4_F_SCIENCE,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "ทำไมคุณมาที่นี่เพื่อช่วยฉัน?";
		mes "องครักษ์ของเอ็นเตอร์ไพรส์... ไม่นะ รอก่อน...";
		next;
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "ฉันคิดว่าคุณเป็นคนกักขังเราเอาไว้ซะอีก...!";
		mes "พวกเขาทำแบบนั้นทำไม?";
		close;
}

1@rgsr,25,182,7	script	Researcher#rgsr_dum05	4_M_HUMAN_01,{
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "ไอ้พวกขยะแขยงนั่นเอารายงานการวิจัยของฉันไป และฉันก็หนีมาที่นี่";
		mes "มันคือสเปรย์ไล่แมลงดูดเลือด!";
		next;
		mes "[นักวิจัยที่ถูกคุมขัง]";
		mes "นั่นเป็นการค้นคว้าส่วนตัวของฉัน ฉันไม่รู้ว่าพวกเขาเอามันไปทำไม!";
		mes "คุณไม่สามารถเอารายงานการวิจัยของคนอื่นไปได้";
		close;
}

1@rgsr,40,186,3	script	Rebellion Crew#rgsr_dum01	4_M_ILYA,{
		mes "[กลุ่มกบฏ]";
		mes "ฉันเตรียมตัวมาดีแล้ว แต่สิ่งนี้มันง่ายกว่าที่คิด";
		mes "ตอนนี้ติดอยู่ที่นี่แล้ว อิอิอิ";
		close;
}

1@rgsr,33,185,3	script	Rebellion Crew#rgsr_dum02	4_M_REBELLION2,{
		mes "[กลุ่มกบฏ]";
		mes "นักวิจัยที่เหลือรวมตัวกันที่นี่เพื่อความปลอดภัยของพวกเขา";
		mes "พวกกบฏจะปกป้องพวกเขาและช่วยให้พวกเขาหนีได้";
		close;
}

1@rgsr,31,183,5	script	Rebellion Crew#rgsr_dum03	4_F_ANYA,{
		mes "[กลุ่มกบฏ]";
		mes "ฉันคิดว่าพวกเขามีกำลังพลน้อยกว่านี้ แต่ฉันไม่คิดว่าพวกเขาจะติดตั้งแก๊สพิษ";
		mes "คนส่วนใหญ่ได้อพยพออกไปแล้ว เหลือเพียงศัตรูเท่านั้นที่ยังคงอยู่";
		close;
}

1@rgsr,195,205,0	script	Research Report#rgsr_00	4_ENERGY_BLUE,{
		mes "มันเป็นข้อมูลเกี่ยวกับประสิทธิภาพใหม่และผลข้างเคียงของยาใหม่";
		mes "อย่างไรก็ตามมีข้อมูลเกี่ยวกับส่วนผสมหลัก";
		close;
}

1@rgsr,198,199,0	script	Research Report#rgsr_01	4_ENERGY_BLUE,{
		mes "มีรูปวาดบางส่วนถูกฉีกขาด";
		mes "มีคนทำลายส่วนที่เกี่ยวข้องโดยตั้งใจ";
		close;
}

1@rgsr,207,210,0	script	Research Report#rgsr_02	4_ENERGY_BLUE,{
		mes "เอกสารบางส่วนได้รับความเสียหายอย่างหนัก";
		mes "ดูเหมือนว่าจะเป็นความพยายามในการสร้างอุปกรณ์โดยใช้การหักเหของแสง";
		close;
}

1@rgsr,226,226,0	script	Research Report#rgsr_03	4_ENERGY_BLUE,{
		mes "การร่ายมนตร์และการประมวลผลข้อมูลของลอยด์";
		mes "เหลือเพียงชื่อเรื่อง ส่วนข้อความทั้งหมดถูกตัดออกไป";
		close;
}

1@rgsr,231,223,0	script	Research Report#rgsr_04	4_ENERGY_BLUE,{
		mes "หนังสือเล่มนี้มีหน้าหายไปบางหน้า มีคนจงใจละเว้นเนื้อหาบางส่วน";
		close;
}

1@rgsr,243,223,0	script	Research Report#rgsr_05	4_ENERGY_BLUE,{
		mes "ข้อมูลการทดลองถูกลบออกไปแล้ว มีร่องรอยชัดเจนว่ามีคนทำลายข้อมูลนี้";
		close;
}
//===== rAthena Script =======================================
//= Instance from Main quest.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Instance of episode 19.
//===== Changelogs: ==========================================
//= 1.0 First version. [Atemo]
//============================================================

// Main Quest: Step 70
jor_que,32,37,5	script	Lehar#i19ms21	EP19_RGAN_D,5,5,{
		if (ep19_main == 59) {
				mes "[เลฮาร์]";
				mes "คุณมาทันเวลาพอดี!!!";
				next;
				select( "Anything happened?" );
				cloakoffnpcself( "Aurelie#i19ms21" );
				npctalk "ฉันอยู่ที่นี่", "Aurelie#i19ms21", bc_self;
				mes "[ขี้เกียจ]";
				mes "ที่นี่เริ่มจะแย่ลงเรื่อยๆ แล้ว";
				next;
				mes "[ร็องกัน]";
				mes "อย่าทำร้ายฉันเลย...ฉันขอร้องนะ...";
				next;
				mes "[ขี้เกียจ]";
				mes "คุณเห็นมันมั้ย?";
				next;
				select( "Why is this? Why is the entrance blocked?" );
				mes "[ออเรลี]";
				mes "ไม่ใช่ว่าพวก rgans ทุกคนที่มีระดับสูงขึ้นไปได้ย้ายมาอยู่โซนขั้นสูงแล้วเหรอ?";
				next;
				mes "[ขี้เกียจ]";
				mes "ฉันหมายถึงอะไร? นั่นคือสิ่งที่ฉันได้ยินมา";
				mes "ฉันกำลังจะไปประชุม มีอะไรขัดขวางอยู่";
				next;
				mes "[เลฮาร์]";
				mes "นั่นแหละ อวัยวะเหล่านี้กำลังถูกจัดระเบียบในส่วนบนที่ชนชั้นสูง Rgan กำลังเคลื่อนไหวอย่างเร่งรีบ แต่ดูเหมือนว่าชนชั้นสูง Rgan จะโจมตีที่ไหนสักแห่งอย่างกะทันหัน";
				next;
				mes "[ออเรลี]";
				mes "อะไรนะ? จู่ๆ?";
				next;
				mes "[เลฮาร์]";
				mes "จากที่ได้ยินมา มันไม่เหมือนกับพวกชนชั้นสูงทั่วๆ ไปอย่าง Rgan หรอก";
				next;
				select( "I've heard this a lot somewhere" );
				npctalk "ไม่ใช่เหรอ?", "Lazy#i19ms21", bc_self;
				mes "[เลฮาร์]";
				mes "เพราะฉะนั้นเขาจึงปิดกั้นทางเข้าเพื่อไม่ให้พวกชนชั้นสูงเข้าออกได้";
				next;
				mes "[ออเรลี]";
				mes "แล้วพวก Bagot, Rasgand และพวก rgan ระดับสูงก็อยู่ที่นั่นด้วยงั้นเหรอ?";
				next;
				mes "[เลฮาร์]";
				mes "ตอนแรกก็คิดว่าแค่นี้ก่อน คิดว่าถ้าผ่านตรงนี้ไปได้คงทำอะไรได้หลายอย่าง";
				next;
				select( "What are you trying to do?" );
				mes "[ขี้เกียจ]";
				mes "บางทีอาจจะมีอะไรบางอย่างที่คุณไม่สามารถพูดต่อหน้าหลานชายตัวน้อยของคุณได้?";
				next;
				mes "[ออเรลี]";
				mes "หากคุณใช้สิ่งนี้ได้ดี... คุณสามารถวางไว้ในที่เดียวและประมวลผลทั้งหมดในคราวเดียวได้";
				next;
				cloakoffnpcself( "Horr#i19ms21" );
				mes "[หืม]";
				mes "นั่นคือเหตุผลว่าทำไมฉันถึงโทรไปหาฝ่ายสนับสนุน";
				next;
				cloakoffnpcself( "Voglinde#i19ms21" );
				cloakoffnpcself( "Leon#i19ms21" );
				sleep2 500;
				npctalk "สถานการณ์เป็นอย่างไรบ้าง?", "Voglinde#i19ms21", bc_self;
				npctalk "มันไม่ได้เริ่มไปแล้วเหรอ?", "Leon#i19ms21", bc_self;
				sleep2 500;
				cloakoffnpcself( "Miriam#i19ms21" );
				sleep2 500;
				npctalk "คุณสายมั้ย?", "Miriam#i19ms21", bc_self;
				sleep2 500;
				cloakoffnpcself( "Iwin#i19ms21" );
				cloakoffnpcself( "Iwin#i19ms22" );
				sleep2 500;
				npctalk "ทีม! มาแล้ว!", "Iwin#i19ms21", bc_self;
				npctalk "ทีม! มาแล้ว!", "Iwin#i19ms22", bc_self;
				mes "[ขี้เกียจ]";
				mes "รีบเข้าแถวเถอะครับ สรุปสั้นๆ นะครับ";
				next;
				mes "[ขี้เกียจ]";
				mes "ว่ากันว่าราสกันด์, บาโกต์ และลแกนส์ที่อยู่เหนือชั้นได้รวมตัวกันอยู่ที่นั่นภายใต้การบังคับบัญชาของราสกันด์";
				mes "ทุกคนเข้าใจมาถึงตรงนี้แล้วหรือยัง?";
				next;
				mes "[ขี้เกียจ]";
				mes "อย่างไรก็ตาม หลังกำแพงนี้จะเป็นที่อยู่อาศัยของพวกที่อยู่ระดับสูง";
				next;
				mes "[ขี้เกียจ]";
				mes "อย่างไรก็ตาม Illusion ได้ปล่อยมันออกมาหลังจากทำสิ่งที่ควรจะเป็นการปรับเปลี่ยนร่างกายของ rgans ระดับสูงขึ้นไป";
				next;
				mes "[โวกลินเด]";
				mes "มันจะเป็นรัฐที่หลบหนี แต่การควบคุมจะไม่ดี";
				mes "แล้วคุณหมายความว่าคุณต้องเอาชนะ Rgan ที่นั่นเพื่อที่จะพบกับ Rasgand ใช่ไหม?";
				next;
				mes "[หืม]";
				mes "ทำลายทางเข้า แบ่งกลุ่มแล้วเข้าทีละคน เราอิวินจะนำทาง";
				next;
				mes "[เลฮาร์]";
				mes "คุณพร้อมแล้วหรือยัง? เข้ามาเลยตอนนี้";
				npctalk "มาดูจากภายในกันบ้างดีกว่า", "Horr#i19ms21", bc_self;
				next;
				cloakonnpcself( "Horr#i19ms21" );
				sleep2 500;
				cloakonnpcself( "Iwin#i19ms21" );
				cloakonnpcself( "Iwin#i19ms22" );
				mes "[ออเรลี]";
				mes "คราวนี้ถึงตาฉันบ้างแล้ว ลีออน ไปกันเถอะ";
				next;
				cloakonnpcself( "Leon#i19ms21" );
				cloakonnpcself( "Aurelie#i19ms21" );
				mes "[โวกลินเด]";
				mes "เลฮาร์ คุณควรอยู่ที่นี่และเฝ้าดูสถานการณ์ก่อน มิเรียม เราไปกันไหม";
				next;
				npctalk "ไม่ต้องกังวล ฉันจะรออยู่กับคุณ", "Lazy#i19ms21", bc_self;
				sleep2 500;
				cloakonnpcself( "Voglinde#i19ms21" );
				cloakonnpcself( "Miriam#i19ms21" );
				mes "[ขี้เกียจ]";
				mes "ทีนี้ก็ถึงคราวของนักผจญภัยแล้ว ฉันกำลังเตรียมตัวอยู่";
				erasequest 17647;
				setquest 17648;
				ep19_main = 60;
				close;
		}
		if (ep19_main == 60) {
				.@md_name$ = "Confused Snake's Nest";
				mes "[เลฮาร์]";
				mes "คุณพร้อมแล้วหรือยัง? เมื่อคุณพร้อมแล้ว ฉันจะส่งสัญญาณยืนยันว่าผู้ที่เข้ามาได้ย้ายออกไปแล้ว";
				next;
				switch( select( "Yes, signal.", "I'll go in.", "I need to get ready." ) ) {
				case 1:
						mes "[เลฮาร์]";
						mes "ฉันจะเปิดทางเข้าทันทีที่มีสัญญาณไฟจราจร กรุณารอสักครู่";
						instance_create(.@md_name$);
						close;
				case 2:
						switch( instance_enter(.@md_name$) ) {
						case IE_OTHER:
								mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						case IE_NOINSTANCE:
								mes "[เลฮาร์]";
								mes "คุณยังไม่พร้อมเหรอ ฉันจะรอนะ";
								close;
						case IE_NOMEMBER:
								mes "[เลฮาร์]";
								mes "^ff0000เฉพาะสมาชิกในกลุ่มเท่านั้นที่สามารถเข้าสู่ดันเจี้ยนอนุสรณ์ได้^000000";
								close;
						case IE_OK:
								mes "[เลฮาร์]";
								mes "เพิ่งได้สัญญาณว่าเข้าได้แล้ว! คุณเข้าได้แล้ว!";
								mapannounce "jor_que", "Operation 3" + strcharinfo(0) + "You and your party" + getpartyname( getcharid(1) ) + "All enter the confused snake's nest.", bc_map, 0xFF99;
								// warp "1@jorchs",272,20;
								end;
						}
						end;
				case 3:
						mes "[เลฮาร์]";
						mes "กรุณาแจ้งให้ฉันส่งสัญญาณเมื่อคุณพร้อม มิฉะนั้น การทำงานภายในอาจเกิดข้อผิดพลาดได้";
						close;
				}
				end;
		}
		end;

OnTouch:
		end;

OnInit:
		questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(17647) == 1" );
		questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(17648) == 1" );
		cloakonnpc();
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_main	-1,{
		end;
OnInstanceInit:
		'map_jorchs$ = instance_mapname("1@jorchs");

		'event = 0;

		'area_1[0] = 'area_1[1] = 0;
		'special_area_1[0] = 'special_area_1[1] = 0;
		'area_2[0] = 'area_2[1] = 0;
		'special_area_2[0] = 'special_area_2[1] = 0;
		'count_area_3_2 = 0;
		'area_4[0] = 'area_4[1] = 'area_4[2] = 0;
		'area_5[0] = 'area_5[1] = 'area_5[2] = 0;
		'ep19_jorchs_area_5_2[0] = 'ep19_jorchs_area_5_2[1] = 'ep19_jorchs_area_5_2[2] = 0;

		// Special mobs area 1
		disablenpc instance_npcname("Rgan the Scared#19m00");
		disablenpc instance_npcname("Rgan the Scared#19m01");
		disablenpc instance_npcname("Rgan the Scared#19m50");
		disablenpc instance_npcname("Rgan the Scared#19m51");

		// Special mobs area 2
		disablenpc instance_npcname("Rgan with feet#19m10");
		disablenpc instance_npcname("Rgan with feet#19m11");
		disablenpc instance_npcname("Rgan with feet tied#19m60");
		disablenpc instance_npcname("Rgan with feet#19m61");

		// Final area 1
		disablenpc instance_npcname("Iwin#19m10");
		disablenpc instance_npcname("Horr#19m10");
		disablenpc instance_npcname("Voglinde#19m10");
		disablenpc instance_npcname("Iwin#19m11");
		disablenpc instance_npcname("Miriam#19m10");
		disablenpc instance_npcname("Aurelie#19m10");
		disablenpc instance_npcname("Leon#19m10");

		setcell 'map_jorchs$,139,59,139,59,cell_walkable,0;
		setcell 'map_jorchs$,138,58,138,58,cell_walkable,0;
		setcell 'map_jorchs$,137,57,137,57,cell_walkable,0;

		// Final area 2
		disablenpc instance_npcname("Horr#19m20");
		disablenpc instance_npcname("Iwin#19m20");
		disablenpc instance_npcname("Horr#19m21");
		disablenpc instance_npcname("Iwin#19m22");
		disablenpc instance_npcname("Iwin#19m23");

		setcell 'map_jorchs$,120,129,120,129,cell_walkable,0;
		setcell 'map_jorchs$,121,128,121,128,cell_walkable,0;
		setcell 'map_jorchs$,122,127,122,127,cell_walkable,0;
		setcell 'map_jorchs$,123,127,123,127,cell_walkable,0;
		setcell 'map_jorchs$,124,126,124,126,cell_walkable,0;
		setcell 'map_jorchs$,125,125,125,125,cell_walkable,0;
		setcell 'map_jorchs$,126,125,126,125,cell_walkable,0;

		// Area 3
		disablenpc instance_npcname("Miriam#19m30");
		disablenpc instance_npcname("Voglinde#19m30");
		disablenpc instance_npcname("#e19p01");
		disablenpc instance_npcname("Injection device#19m30");

		setcell 'map_jorchs$,242,174,242,174,cell_walkable,0;
		setcell 'map_jorchs$,244,176,244,176,cell_walkable,0;
		setcell 'map_jorchs$,247,176,247,176,cell_walkable,0;
		setcell 'map_jorchs$,248,176,248,176,cell_walkable,0;
		setcell 'map_jorchs$,243,175,243,175,cell_walkable,0;
		setcell 'map_jorchs$,245,176,245,176,cell_walkable,0;
		setcell 'map_jorchs$,246,176,246,176,cell_walkable,0;
		setcell 'map_jorchs$,249,177,249,177,cell_walkable,0;

		// Area 4
		disablenpc instance_npcname("Injection device#19m32");
		disablenpc instance_npcname("Injection device#19m31");
		disablenpc instance_npcname("Aurelie#19m40");
		disablenpc instance_npcname("Injection device#19m40");

		setcell 'map_jorchs$,140,183,140,183,cell_walkable,0;
		setcell 'map_jorchs$,140,182,140,182,cell_walkable,0;
		setcell 'map_jorchs$,139,181,139,181,cell_walkable,0;
		setcell 'map_jorchs$,139,180,139,180,cell_walkable,0;
		setcell 'map_jorchs$,138,179,138,179,cell_walkable,0;
		setcell 'map_jorchs$,138,178,138,178,cell_walkable,0;

		// Area 5
		disablenpc instance_npcname("#e19p03");
		disablenpc instance_npcname("Heart hunter#19m50");
		disablenpc instance_npcname("Center door#19m61");
		disablenpc instance_npcname("#19m60");

		setcell 'map_jorchs$,34,255,43,255,cell_walkable,0;
		end;
}

// Area 1

// Main Quest: Step 71
1@jorchs,261,34,3	script	Aurelie#19m00	4_EP19_AURELIE,5,5,{
		end;
OnTouch:
		if ('event != 0)
				end;
		'event = 1;	// note: player can move
		npctalk "ไม่มีเหตุผลที่จะแกล้งทำเป็นเป็นคนเรแกนที่นี่";
		sleep 1000;
		npctalk "เมื่อกองทหารของอิวินได้รุกคืบเข้ามาในพื้นที่แล้ว พวกเขาก็ได้กวาดล้างพวกราแกนในบริเวณใกล้เคียง", instance_npcname("Horr#19m00");
		sleep 1500;
		npctalk "ตอนนี้เราต้องเจาะลึกและทำความสะอาดให้มากขึ้น", instance_npcname("Voglinde#19m00");
		sleep 3000;
		npctalk "เป้าหมายคือการรักษาความปลอดภัยของ Rasgand และ Bagot และหัวใจของ Ymir";
		sleep 3000;
		npctalk "ต้องเข้าไปขวางทางหนี ความเร็วเป็นเรื่องสำคัญ", instance_npcname("Horr#19m00");
		sleep 3000;
		npctalk "มีทางเข้าเพียงทางเดียวแต่เราจะต้องค้นหาให้ทั่วเพื่อดูว่ามีทางเข้าใดที่เราไม่รู้หรือไม่";
		sleep 3000;
		npctalk "ฉันต้องลบ Rgan ออกก่อนเพื่อค้นหาร่องรอยของมันหรือเปล่า?", instance_npcname("Horr#19m00");
		sleep 3000;
		npctalk "ตามเรื่องราวของหน่วยขั้นสูงพวกเขาจะแตกต่างจาก Rgan ทั่วไป ดังนั้นคุณควรเตรียมตัวให้ดี", instance_npcname("Horr#19m00");
		sleep 3000;
		npctalk "ระวังไว้ คู่ต่อสู้ของคุณคือพวกเรแกนที่เก่งเรื่องเวทย์มนตร์";
		sleep 3000;
		npctalk "ขอให้โชคดีทุกคน!", instance_npcname("Horr#19m00");
		sleep 500;
		disablenpc();
		disablenpc instance_npcname("Leon#19m00");
		disablenpc instance_npcname("Miriam#19m00");
		disablenpc instance_npcname("Voglinde#19m00");
		disablenpc instance_npcname("Horr#19m00");
		disablenpc instance_npcname("Iwin#19m00");
		disablenpc instance_npcname("Iwin#19m01");
		donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnStart";
		end;
}
1@jorchs,256,31,7	duplicate(dummy_npc)	Iwin#19m00	EP19_NPC_IWIN_SG
1@jorchs,256,34,5	duplicate(dummy_npc)	Miriam#19m00	4_EP18_MIRIAM
1@jorchs,257,35,5	duplicate(dummy_npc)	Voglinde#19m00	4_EP19_VOGLINDE
1@jorchs,258,32,7	duplicate(dummy_npc)	Horr#19m00	EP19_NPC_IWIN_SR
1@jorchs,259,31,7	duplicate(dummy_npc)	Iwin#19m01	EP19_NPC_IWIN_SG
1@jorchs,260,35,3	duplicate(dummy_npc)	Leon#19m00	4_EP19_LEON


// Main Quest: Step 72
1@jorchs,249,79,7	script	Rgan the Scared#19m00	EP19_RE_RGAN_E,5,5,{
		end;
OnTouch:
		// note: no need to kill the monsters in the area to trigger the event
		// note: player can move while the npc is talking
		disablenpc();
		donpcevent instance_npcname("Rgan the Scared#19m50") + "::OnEvent";
		end;
}

1@jorchs,249,79,7	script	Rgan the Scared#19m50	EP19_RE_RGAN_E,{
		end;
OnEvent:
		enablenpc();
		npctalk "ฉันกลัว...ทำไมร่างกายฉันถึงทำแบบนี้";
		sleep 1500;
		npctalk "นั่น...มนุษย์เหรอ??? นี่ของคุณหมดเลยเหรอ?? คุณทำอะไรกับฉัน!?";
		sleep 200;
		npctalk "พูดถึงเรื่องมนุษย์โสมม! เอาเถอะ ช่วยคืนฉันมาเถอะ...! อ่า...!!";
		sleep 500;
		specialeffect EF_FOOD02;
		sleep 500;
		disablenpc();
		monster 'map_jorchs$,249,79,"Rgan the Scared",21534,1, instance_npcname("ep19_jorchs_area_1") + "::OnSpecialMob1";	// EP19_MD_RGAN_E
		end;
}


// Main Quest: Step 73
1@jorchs,114,20,3	script	Rgan the Scared#19m01	EP19_RE_RGAN_E,5,5,{
		end;
OnTouch:
		disablenpc();
		donpcevent instance_npcname("Rgan the Scared#19m51") + "::OnEvent";
		end;
}

1@jorchs,114,20,3	script	Rgan the Scared#19m51	EP19_RE_RGAN_E,{
		end;
OnEvent:
		enablenpc();
		npctalk "ฉันฉันก็ไม่เป็นแบบนั้นเหมือนกันเหรอ...?";
		sleep 2000;
		npctalk "ฉันใช้ชีวิตอย่างดี แต่มนุษย์พวกนั้นทำอะไรกับฉัน...";
		sleep 2000;
		npctalk "อ่า...หนาวว่ะ...";
		sleep 2500;
		npctalk "...?! มนุษย์? มนุษย์?? มนุษย์ที่ทำให้ฉันเป็นแบบนี้???";
		sleep 500;
		specialeffect EF_FOOD02;
		disablenpc();
		monster 'map_jorchs$,114,20,"Rgan the Scared",21534,1, instance_npcname("ep19_jorchs_area_1") + "::OnSpecialMob2";	// EP19_MD_RGAN_E
		end;
}


1@jorchs,1,1,3	script	ep19_jorchs_area_1	-1,{
		end;
OnStart:
		enablenpc instance_npcname("Rgan the Scared#19m00");	// trigger spawn special mobs
		enablenpc instance_npcname("Rgan the Scared#19m01");

		.@event$ = instance_npcname("ep19_jorchs_area_1") + "::OnNormalMob";
		monster 'map_jorchs$,237,61,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,221,37,"Divided Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,242,66,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,204,38,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,181,48,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,135,29,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,124,26,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		end;

OnNormalMob:
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_1") + "::OnNormalMob" ) > 0)
				end;
		mapannounce 'map_jorchs$, "Wait, everyone. I need to clear things up.", bc_map, 0xFFCC;
		'area_1[0] = 1;
		donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnAreaClear";
		end;

OnSpecialMob1: callsub( S_Check, 0 );
OnSpecialMob2: callsub( S_Check, 1 );
S_Check:
		.@current_area = getarg(0);
		.@other_area = !.@current_area;

		'special_area_1[.@current_area] = 1;
		if ('special_area_1[.@other_area] == 1) {	// the 2 mobs special are dead
				'special_area_1[0] = 'special_area_1[1] = 2;	// just to make sure
				'area_1[1] = 1;
				mapannounce 'map_jorchs$, "You'd better do a more thorough search.", bc_map, 0xFFCC;
				donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnAreaClear";
				end;
		}
		end;

OnAreaClear:	// check if area clear
		if ('area_1[0] == 0 || 'area_1[1] == 0)
				end;
		if ('event != 1)
				end;
		'area_1[0] = 'area_1[1] = 0;	// just to make sure
		'event = 2;

		// dialog final step area 1
		disablenpc instance_npcname("Iwin#19m13");
		disablenpc instance_npcname("Iwin#19m12");

		enablenpc instance_npcname("Iwin#19m10");
		enablenpc instance_npcname("Horr#19m10");
		enablenpc instance_npcname("Voglinde#19m10");
		enablenpc instance_npcname("Iwin#19m11");
		enablenpc instance_npcname("Miriam#19m10");
		enablenpc instance_npcname("Aurelie#19m10");
		enablenpc instance_npcname("Leon#19m10");
		end;
}


// Main Quest: Step 74
1@jorchs,140,58,5	script	Iwin#19m13	EP19_NPC_IWIN_SB,5,5,{
		end;
OnTouch:
		if ('event < 2) {
				npctalk "นอกจากนี้ยังไม่มีการส่งคณะค้นหาออกไป", instance_npcname("Iwin#19m12");
				sleep 1500;
				npctalk "เราจะรวบรวมข้อมูลและรวบรวมไว้ที่นี่ก่อนที่จะเข้ามา ดังนั้นโปรดเน้นการค้นหาของคุณในบริเวณนี้";
				end;
		}
		end;
}
1@jorchs,140,56,5	duplicate(dummy_npc)	Iwin#19m12	EP19_NPC_IWIN_SB


1@jorchs,142,58,5	script	Horr#19m10	EP19_NPC_IWIN_SR,5,5,{
		end;
OnTouch:
		if ('event != 2)
				end;
		'event = 3;
		npctalk "ไอ้ Rgan นี่มันคืออะไร!! ฉันไม่เคยเห็น Rgan แบบนี้ในชีวิตมาก่อน!", instance_npcname("Horr#19m10");
		sleep 2500;
		npctalk "มันดูเหมือน Rgan ระดับกลาง แต่ฉันไม่สามารถระบุได้ ดังนั้นจึงไม่สามารถแน่ใจได้", instance_npcname("Leon#19m10");
		sleep 2000;
		npctalk "ทำไม Rgan ระดับกลางถึงเป็นแบบนี้ มันไม่ใช่ระดับสูงสุดเหรอ?", instance_npcname("Aurelie#19m10");
		sleep 2500;
		npctalk "ภาพลวงตาส่วนใหญ่เกี่ยวข้องกับการสร้างขึ้นใหม่ ดังนั้นฉันคิดว่ามันน่าจะมีส่วนเกี่ยวข้องกับมัน", instance_npcname("Miriam#19m10");
		sleep 2000;
		npctalk "Mods ทำให้สิ่งต่างๆ ดีขึ้นหรือไม่? ไม่ว่าจะมองยังไงก็ตาม...", instance_npcname("Horr#19m10");
		sleep 2500;
		npctalk "คุณคงติดอยู่ในเรื่องแบบนี้ หรือไม่ก็ใช้มันแล้วโยนมันทิ้งไป", instance_npcname("Leon#19m10");
		sleep 2000;
		npctalk "ไม่ใช่เรื่องแปลกอะไร ดังนั้นเราเข้าไปข้างในกันดีกว่า", instance_npcname("Voglinde#19m10");
		sleep 3000;
		npctalk "สถานที่นี้ค่อนข้างซับซ้อน ดังนั้นควรแยกย้ายกันไปค้นหาจะดีกว่า", instance_npcname("Horr#19m10");
		sleep 3000;
		npctalk "Rgan ที่ Rasgand สะสมไว้มีแนวโน้มที่จะอยู่ในที่อยู่อาศัยของ Rasgand", instance_npcname("Miriam#19m10");
		sleep 3000;
		npctalk "Bagot ก็ถูกวิจัยมาด้วย ดังนั้น ฉันจะต้องตรวจสอบที่อยู่อาศัยและห้องทดลองของ Illusion ด้วย", instance_npcname("Horr#19m10");
		sleep 2500;
		npctalk "มันอาจจะเป็นส่วนที่อยู่ด้านในสุด", instance_npcname("Leon#19m10");
		sleep 2500;
		npctalk "คุณกำลังทำบ้าอะไรอยู่?", instance_npcname("Aurelie#19m10");
		setcell 'map_jorchs$,139,59,139,59,cell_walkable,1;
		setcell 'map_jorchs$,138,58,138,58,cell_walkable,1;
		setcell 'map_jorchs$,137,57,137,57,cell_walkable,1;
		disablenpc instance_npcname("Iwin#19m10");
		disablenpc instance_npcname("Horr#19m10");
		disablenpc instance_npcname("Voglinde#19m10");
		disablenpc instance_npcname("Iwin#19m11");
		disablenpc instance_npcname("Miriam#19m10");
		disablenpc instance_npcname("Aurelie#19m10");
		disablenpc instance_npcname("Leon#19m10");
		donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnStart";	// mob spawn
		end;
}
1@jorchs,143,60,5	duplicate(Horr#19m10)	Iwin#19m11	EP19_NPC_IWIN_SG,5,5

1@jorchs,140,57,5	duplicate(dummy_npc)	Iwin#19m10	EP19_NPC_IWIN_SG
1@jorchs,143,56,1	duplicate(dummy_npc)	Voglinde#19m10	4_EP19_VOGLINDE
1@jorchs,145,56,1	duplicate(dummy_npc)	Miriam#19m10	4_EP18_MIRIAM
1@jorchs,145,59,3	duplicate(dummy_npc)	Aurelie#19m10	4_EP19_AURELIE
1@jorchs,146,58,3	duplicate(dummy_npc)	Leon#19m10	4_EP19_LEON




// Area 2
1@jorchs,1,1,3	script	ep19_jorchs_area_2_A	-1,{
		end;
OnStart:
		enablenpc instance_npcname("Rgan with feet#19m10");	// to spawn special mobs
		enablenpc instance_npcname("Rgan with feet#19m11");

		.@event$ = instance_npcname("ep19_jorchs_area_2_A") + "::OnNormalMob";
		monster 'map_jorchs$,96,64,"Troubled Ragan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,75,23,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,51,45,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,39,76,"Uncomfortable RGAN","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,31,59,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,26,90,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,119,80,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,118,103,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		end;

OnNormalMob:
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_A") + "::OnNormalMob" ) > 0)
				end;
		mapannounce 'map_jorchs$, "Wow!!! What is this!!!", bc_map, 0xFFCC;
		'area_2[0] = 1;
		donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnAreaClear";
		end;

OnSpecialMob1: callsub( S_Check, 0 );
OnSpecialMob2: callsub( S_Check, 1 );
S_Check:
		.@current_area = getarg(0);
		.@other_area = !.@current_area;

		'special_area_2[.@current_area] = 1;
		if ('special_area_2[.@other_area] == 1) {
				'special_area_2[0] = 'special_area_2[1] = 0;
				'area_2[1] = 1;
				mapannounce 'map_jorchs$, "There's still some Rgan left in the search area, so we'd better clean it up quickly.", bc_map, 0xFFCC;
				donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnAreaClear";
				end;
		}
		end;

OnAreaClear:	// check if area clear
		if ('area_2[0] == 0 || 'area_2[1] == 0)
				end;
		if ('event != 3)
				end;
		'area_2[0] = 'area_2[1] = 0;
		'event = 4;

		enablenpc instance_npcname("Horr#19m20");
		enablenpc instance_npcname("Iwin#19m20");
		end;
}


// Main Quest: Step 75
1@jorchs,23,57,5	script	Rgan with feet#19m10	EP19_RE_RGAN_E,5,5,{
		end;
OnTouch:
		disablenpc();
		donpcevent instance_npcname("Rgan with feet tied#19m60") + "::OnEvent";
		end;
}

1@jorchs,23,57,5	script	Rgan with feet tied#19m60	EP19_RE_RGAN_E,{
		end;
OnEvent:
		enablenpc();
		npctalk "ทุกคนไปไหนกันหมด... เค้าบอกให้ฉันเฝ้าไข่ แต่ฉันออกไปแบบนี้ไม่ได้...";
		sleep 2000;
		npctalk "มนุษย์? มนุษย์เข้าไม่ได้นะเว้ย! เข้ามาสิ!";
		sleep 1500;
		npctalk "เฮ้ย!! ร่างกายของคุณ...! มันเป็นอาหารเสริม...? ใช่ไหม ?...!!";
		sleep 1000;
		specialeffect EF_FOOD02;
		disablenpc();
		monster 'map_jorchs$,23,57,"Rgan with Feet",21534,1, instance_npcname("ep19_jorchs_area_2_A") + "::OnSpecialMob1";	// EP19_MD_RGAN_E
		end;
}


// Main Quest: Step 76
1@jorchs,21,97,5	script	Rgan with feet#19m11	EP19_RE_RGAN_E,5,5,{
		end;
OnTouch:
		disablenpc();
		donpcevent instance_npcname("Rgan with feet#19m61") + "::OnEvent";
		end;
}

1@jorchs,21,97,5	script	Rgan with feet#19m61	EP19_RE_RGAN_E,5,5,{
		end;
OnEvent:
		enablenpc();
		npctalk "ฉันต้องรีบไปถึงใจกลางแต่จะผ่านไปได้ยังไงเนี่ย ได้ยินเสียงแปลกๆ มานานแล้ว...";
		sleep 1500;
		npctalk "เมื่อไหร่จะแปลงร่าง ต้องแปลงร่างก่อนถึงจะฝ่าฟันได้";
		sleep 2000;
		specialeffect EF_FOOD02;
		npctalk "โอ้! ในที่สุด!!! ฉันก็มีโอกาสได้เป็นระดับบิชอปซะที...!!! ? !!!";
		disablenpc();
		monster 'map_jorchs$,21,97,"Rgans with feet",21534,1, instance_npcname("ep19_jorchs_area_2_A") + "::OnSpecialMob2";	// EP19_MD_RGAN_E
		end;
}


// Main Quest: Step 77
1@jorchs,120,127,3	duplicate(dummy_npc)	Smashed Rgan#19m21	EP19_MD_RGAN_D
1@jorchs,120,129,1	duplicate(dummy_npc)	Warped Rgan#19m20	EP19_MD_RGAN_D
1@jorchs,122,127,3	duplicate(dummy_npc)	Howling Rgan#19m22	EP19_MD_RGAN_D
1@jorchs,124,126,3	duplicate(dummy_npc)	Painful Rgan#19m24	EP19_MD_RGAN_D
1@jorchs,125,125,3	duplicate(dummy_npc)	Combined Rgan#19m25	EP19_MD_RGAN_D
1@jorchs,126,125,3	duplicate(dummy_npc)	Twisted Rgan#19m26	EP19_MD_RGAN_D
1@jorchs,123,127,1	duplicate(dummy_npc)	Modified Advanced Ligan#19m23	EP19_MD_RGAN_E

1@jorchs,118,127,7	duplicate(dummy_npc)	Horr#19m21	EP19_NPC_IWIN_SR
1@jorchs,122,126,7	duplicate(dummy_npc)	Iwin#19m22	EP19_NPC_IWIN_SG
1@jorchs,124,124,7	duplicate(dummy_npc)	Iwin#19m23	EP19_NPC_IWIN_SG

1@jorchs,114,119,3	script	Iwin#19m21	EP19_NPC_IWIN_SG,3,3,{
		end;
OnTouch:
		if ('event < 4) {
				npctalk "คุณอยู่ที่นี่ไหม! พวก Rgan พวกนี้ไม่สามารถทะลุผ่านได้ ไม่โจมตี และแปลกประหลาด พวกมันกำลังมองหาวิธี ดังนั้นจงโฟกัสไปที่การค้นหาของคุณ!";
				end;
		}
		end;
}
1@jorchs,112,116,7	duplicate(dummy_npc)	Iwin#19m20	EP19_NPC_IWIN_SG

1@jorchs,119,123,3	duplicate(dummy_disabled_npc)	Heart hunter#19m20	EP19_MD_HEARTHUNTER_AT
1@jorchs,116,124,3	duplicate(dummy_disabled_npc)	Heart hunter#19m21	EP19_MD_HEARTHUNTER_AT
1@jorchs,123,124,3	duplicate(dummy_disabled_npc)	Heart hunter#19m22	EP19_MD_HEARTHUNTER_AT

1@jorchs,112,119,5	script	Horr#19m20	EP19_NPC_IWIN_SR,5,5,{
		end;
OnTouch:
		if ('event == 4) {
				'event = 5;
				// note: player can walk
				.@hunter_1$ = instance_npcname("Heart hunter#19m20");

				npctalk "แบบนี้ก็ไปต่อได้นะ!";
				sleep 2000;
				npctalk "เดี๋ยวก่อน!! ด้านหน้ามี Rgan เยอะแยะนะ!", instance_npcname("Iwin#19m20");
				sleep 2000;
				npctalk "เจาะได้เลย!";
				sleep 1000;
				disablenpc();
				sleep 500;
				disablenpc instance_npcname("Iwin#19m20");
				disablenpc instance_npcname("Iwin#19m21");
				sleep 1000;
				enablenpc instance_npcname("Horr#19m21");
				enablenpc instance_npcname("Iwin#19m22");
				enablenpc instance_npcname("Iwin#19m23");
				sleep 1000;
				specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("Horr#19m21");
				sleep 500;
				specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("Iwin#19m22");
				specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("Iwin#19m23");
				specialeffect EF_HFLIMOON3, AREA, instance_npcname("Horr#19m21");
				specialeffect EF_HFLIMOON3, AREA, instance_npcname("Iwin#19m22");
				specialeffect EF_HFLIMOON3, AREA, instance_npcname("Iwin#19m23");
				specialeffect EF_M03, AREA, instance_npcname("Horr#19m21");
				specialeffect EF_M03, AREA, instance_npcname("Iwin#19m22");
				specialeffect EF_M03, AREA, instance_npcname("Iwin#19m23");
				npctalk "-", instance_npcname("Horr#19m21");
				sleep 1000;
				disablenpc instance_npcname("Horr#19m21");
				disablenpc instance_npcname("Iwin#19m22");
				disablenpc instance_npcname("Iwin#19m23");
				sleep 1000;
				enablenpc();
				sleep 500;
				enablenpc instance_npcname("Iwin#19m20");
				enablenpc instance_npcname("Iwin#19m21");
				npctalk "ไม่มีทาง!! เยอะเกินไป!!! จะผ่านมันไปได้ยังไง";
				sleep 1500;
				npctalk "แต่นั่นมันไม่แปลกนิดหน่อยเหรอ?", instance_npcname("Iwin#19m20");
				sleep 2500;
				npctalk "เราดูอยู่และพวกมันไม่ได้โจมตีเรา!", instance_npcname("Iwin#19m21");
				sleep 2000;
				enablenpc .@hunter_1$;
				sleep 500;
				npctalk "แค่เปิดเครื่องก็เสร็จแล้ว อะไรนะ ทุกคนมากันครบแล้วเหรอ", .@hunter_1$;
				sleep 1500;
				npctalk "มาสายแล้วเหรอ? ไม่เป็นไรหรอก", .@hunter_1$;
				sleep 2500;
				npctalk "นั่นอะไรอีกล่ะ ภาพลวงตาที่นักล่าหัวใจซูฮาระบอกน่ะเหรอ";
				sleep 2000;
				npctalk "นี่คือแนวป้องกันที่ 5 โปรดสนับสนุนด้วย", .@hunter_1$;
				sleep 2500;
				npctalk "มีผู้บุกรุก มีสิ่งกีดขวาง จึงขยายแนวป้องกันได้ยาก ขอสนับสนุนอีกครั้งครับ", .@hunter_1$;
				sleep 2500;
				npctalk "เรียกการสนับสนุน!!";
				sleep 2000;
				enablenpc instance_npcname("Heart hunter#19m21");
				enablenpc instance_npcname("Heart hunter#19m22");
				npctalk "อุปสรรคคืออะไร?", instance_npcname("Heart hunter#19m22");
				sleep 2000;
				npctalk "อย่างที่คุณเห็น นักผจญภัยมาถึงจุดนี้ได้แล้ว", .@hunter_1$;
				sleep 2500;
				npctalk "อะไรนะ? อวัยวะพวกนี้มันไม่ได้รวมกันจริงๆ เหรอ? อย่างที่บาโกต์พูด", instance_npcname("Heart hunter#19m21");
				sleep 2000;
				npctalk "ใช่แล้ว คนงานทำงานหนักและแข็งแกร่งมาก คอยปิดกั้นทางเข้า!", .@hunter_1$;
				sleep 2500;
				npctalk "ว่าแต่ทำไมคนถึงเยอะขนาดนี้ครับ จะกำจัดได้ไหมครับ", instance_npcname("Heart hunter#19m22");
				sleep 2000;
				npctalk "ฉันพยายามเริ่มต้นและบล็อคอุปกรณ์ก่อนที่จะเข้ามา แต่ก็ทำได้", .@hunter_1$;
				sleep 2500;
				npctalk "คุณควบคุมอุปกรณ์ของคุณ เราจะดูแลเรื่องนี้", .@hunter_1$;
				npctalk "ฉันทำ", instance_npcname("Heart hunter#19m22");
				sleep 2000;
				disablenpc instance_npcname("Heart hunter#19m22");
				npctalk "อืมมม! แยกทางเหรอ นักผจญภัย! เราจะจัดการเรื่องนั้นตรงนั้น ดังนั้นจัดการพวกนั้นซะ!";
				sleep 500;
				disablenpc();
				disablenpc instance_npcname("Iwin#19m20");
				disablenpc instance_npcname("Iwin#19m21");
				disablenpc .@hunter_1$;
				disablenpc instance_npcname("Heart hunter#19m21");
				cloakonnpc instance_npcname("Smashed Rgan#19m21");
				cloakonnpc instance_npcname("Painful Rgan#19m24");
				.@event$ = instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal1";
				monster 'map_jorchs$,119,123,"A modified Heart Hunter",21536,1, .@event$;	// EP19_MD_HEARTHUNTER_AT
				monster 'map_jorchs$,123,124,"A modified heart hunter",21536,1, .@event$;	// EP19_MD_HEARTHUNTER_AT
				monster 'map_jorchs$,120,126,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
				monster 'map_jorchs$,124,125,"Painful Rgan","EP19_MD_RGAN_D",1, .@event$;
				end;
		}
		if ('event == 7) {
				'event = 8;
				npctalk "ฮู- ฉันเคยเจอมาหมดแล้ว อย่างไรก็ตาม ยังมีอุปกรณ์ที่ไม่รู้จักอีกหลายอย่าง", instance_npcname("Horr#19m20");
				sleep 2000;
				npctalk "เขาจับนักล่าหัวใจที่พยายามจะบงการมัน เขาพยายามทำอะไรอยู่กันแน่?", instance_npcname("Horr#19m20");
				sleep 2500;
				npctalk "นักล่าหัวใจผ่านกองขยะอันน่ารังเกียจนั้นมาได้อย่างไร?", instance_npcname("Iwin#19m20");
				sleep 2500;
				npctalk "บางทีนักล่าหัวใจอาจมีอะไรบางอย่างไว้หลบเลี่ยงหรือควบคุมพวกอาร์แกน", instance_npcname("Horr#19m20");
				sleep 2000;
				npctalk "ดังนั้น เมื่อคุณจัดการกับ Hearthunter กำแพงแห่ง rgans ก็ถูกรื้อถอนไปแล้ว!", instance_npcname("Iwin#19m21");
				sleep 2500;
				npctalk "มันจะไม่ง่ายนะนักผจญภัย เราจะแบ่งปันสิ่งนี้กับคนอื่นๆ ไปก่อน", instance_npcname("Horr#19m20");
				sleep 500;
				disablenpc instance_npcname("Horr#19m20");
				disablenpc instance_npcname("Iwin#19m20");
				disablenpc instance_npcname("Iwin#19m21");

				setcell 'map_jorchs$,120,129,120,129,cell_walkable,1;
				setcell 'map_jorchs$,121,128,121,128,cell_walkable,1;
				setcell 'map_jorchs$,122,127,122,127,cell_walkable,1;
				setcell 'map_jorchs$,123,127,123,127,cell_walkable,1;
				setcell 'map_jorchs$,124,126,124,126,cell_walkable,1;
				setcell 'map_jorchs$,125,125,125,125,cell_walkable,1;
				setcell 'map_jorchs$,126,125,126,125,cell_walkable,1;

				specialeffect EF_WIND, AREA, instance_npcname("Warped Rgan#19m20");
				specialeffect EF_WIND, AREA, instance_npcname("Smashed Rgan#19m21");
				specialeffect EF_WIND, AREA, instance_npcname("Howling Rgan#19m22");
				specialeffect EF_WIND, AREA, instance_npcname("Modified Advanced Ligan#19m23");
				specialeffect EF_WIND, AREA, instance_npcname("Painful Rgan#19m24");
				specialeffect EF_WIND, AREA, instance_npcname("Combined Rgan#19m25");

				donpcevent instance_npcname("ep19_jorchs_area_3_1") + "::OnStart";	// mob spawn area 3
				end;
		}
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_2_B	-1,{
		end;
OnMobFinal1:
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal1" ) > 0)
				end;
		if ('event != 5)
				end;
		'event = 6;
		mapannounce 'map_jorchs$, "- Do you think this will end like this? It's not like we didn't prepare anything!", bc_map, 0xFFCC;
		sleep 2000;
		cloakonnpc instance_npcname("Warped Rgan#19m20");	// to display effect on rAthena (officially disabled)
		cloakonnpc instance_npcname("Howling Rgan#19m22");
		cloakonnpc instance_npcname("Combined Rgan#19m25");
		disablenpc instance_npcname("Twisted Rgan#19m26");
		cloakonnpc instance_npcname("Modified Advanced Ligan#19m23");

		.@event$ = instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal2";
		monster 'map_jorchs$,120,128,"Howling Lgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,122,126,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,125,124,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,126,124,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,123,126,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		end;

OnMobFinal2:
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal2" ) > 0)
				end;
		if ('event != 6)
				end;
		'event = 7;
		enablenpc instance_npcname("Horr#19m20");
		enablenpc instance_npcname("Iwin#19m20");
		enablenpc instance_npcname("Iwin#19m21");
		end;

}


// Area 3
1@jorchs,1,1,3	script	ep19_jorchs_area_3_1	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("ep19_jorchs_area_3_1") + "::OnNormalMob";
		monster 'map_jorchs$,142,144,"Howling RGAN","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,159,151,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,180,139,"Painful Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,200,131,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,216,135,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,226,141,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,236,151,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		end;

OnNormalMob:
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_1") + "::OnNormalMob" ) > 0)
				end;
		if ('event != 8)
				end;
		'event = 9;
		mapannounce 'map_jorchs$, "Were you here?", bc_map, 0xFFCC;
		enablenpc instance_npcname("Miriam#19m30");
		enablenpc instance_npcname("Voglinde#19m30");
		end;
}

// Wall of monsters
1@jorchs,242,174,3	duplicate(dummy_npc)	Warped Rgan#19m37	EP19_MD_RGAN_D
1@jorchs,244,176,3	duplicate(dummy_npc)	Smashed Rgan#19m35	EP19_MD_RGAN_D
1@jorchs,247,176,3	duplicate(dummy_npc)	Combined Rgan#19m32	EP19_MD_RGAN_D
1@jorchs,248,176,3	duplicate(dummy_npc)	Warped Rgan#19m31	EP19_MD_RGAN_D

1@jorchs,243,175,3	duplicate(dummy_npc)	Modified Greater RGAN#19m36	EP19_MD_RGAN_E
1@jorchs,245,176,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m34	EP19_MD_RGAN_E
1@jorchs,246,176,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m33	EP19_MD_RGAN_E,5,5
1@jorchs,249,177,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m30	EP19_MD_RGAN_E


// Area 3 - Step 2

// Main Quest: Step 78
1@jorchs,248,173,5	duplicate(dummy_disabled_npc)	Heart hunter#19m30	EP19_MD_HEARTHUNTER_AT
1@jorchs,246,173,5	duplicate(dummy_disabled_npc)	Heart hunter#19m31	EP19_MD_HEARTHUNTER_AT
1@jorchs,243,173,5	duplicate(dummy_disabled_npc)	Heart hunter#19m32	EP19_MD_HEARTHUNTER_AT

1@jorchs,249,170,1	duplicate(dummy_disabled_npc)	Miriam#19m30	4_EP18_MIRIAM

1@jorchs,251,171,1	script	Voglinde#19m30	4_EP19_VOGLINDE,5,5,{
		end;
OnTouch:
		if ('event != 9)
				end;
		'event = 10;
		npctalk "นักผจญภัย! คุณปลอดภัยแล้ว", instance_npcname("Miriam#19m30");
		sleep 2000;
		npctalk "คุณหมายถึงว่า Rgan ตรงหน้าคุณถูกควบคุมโดย Hearthunter ใช่ไหม?", instance_npcname("Voglinde#19m30");
		// missing 1 npctalk
		sleep 1500;
		npctalk "หากคุณมีจิตสำนึกเช่นนั้น คุณคงไม่ได้สร้าง Heart Hunter ขึ้นมาตั้งแต่แรก", instance_npcname("Voglinde#19m30");
		sleep 2000;
		enablenpc instance_npcname("Heart hunter#19m30");
		enablenpc instance_npcname("Heart hunter#19m31");
		enablenpc instance_npcname("Heart hunter#19m32");
		npctalk "คุณกำลังพูดถึงพวกเราอยู่เหรอ ไม่ต้องกังวล พวกเราทำสิ่งนี้เพราะพวกเราต้องการทำ", instance_npcname("Heart hunter#19m30");
		sleep 2500;
		npctalk "พวกเรแกนขั้นสูงเหล่านี้ก็อยู่ที่นี่ด้วย", instance_npcname("Heart hunter#19m31");
		sleep 1500;
		npctalk "พวกเขาบอกว่ามันเป็นโอกาสที่จะกลายเป็นที่หนึ่ง ดังนั้นพวกคุณทั้งสองจึงเข้าแถว", instance_npcname("Heart hunter#19m32");
		sleep 2500;
		npctalk "อวัยวะตรงกลางเหล่านั้นเกิดอะไรขึ้น?", instance_npcname("Voglinde#19m30");
		sleep 1500;
		npctalk "เอ่อ... พวกตัวกลางพวกนั้นช่วยไม่ได้", instance_npcname("Heart hunter#19m30");
		sleep 2000;
		npctalk "ถ้าเกิดฉันบังเอิญไปอยู่ในสถานที่อันตรายในเวลาแปลกๆ จะผิดไหม...?", instance_npcname("Heart hunter#19m32");
		sleep 2000;
		npctalk "ไปไหนกันมาบ้างคะ ทำอะไรกันอยู่คะ", instance_npcname("Miriam#19m30");
		sleep 2000;
		npctalk "ทำไมเราถึงบอกคุณแบบนั้น ฉันไม่ใช่คนแบบนั้น", instance_npcname("Heart hunter#19m32");
		sleep 2000;
		npctalk "ถ้าอย่างนั้นก็หยุดพูดไร้สาระแล้วออกไปจากที่นี่ซะ", instance_npcname("Miriam#19m30");
		sleep 2500;
		npctalk "ทุกคนใน Arunafeltz เป็นคนตรงไปตรงมา", instance_npcname("Heart hunter#19m31");
		npctalk "อย่าปล่อยให้มันผ่านไปเฉยๆ นี่คือร่างกายที่ถูกสั่งมา", instance_npcname("Heart hunter#19m30");
		sleep 2500;
		npctalk "มันเกี่ยวกับการหยุดคุณ ดังนั้นฉันได้เตรียมบางอย่างไว้... เอ่อ...", instance_npcname("Heart hunter#19m32");
		sleep 2500;
		npctalk "ตอนนี้คุณกำลังปิดเวลาอยู่หรือเปล่า?", instance_npcname("Voglinde#19m30");
		sleep 2000;
		npctalk "รู้ได้ไงอ่ะ น่าจะมีอีกนะ แต่ไม่รู้ แปด!!!", instance_npcname("Heart hunter#19m30");
		sleep 1500;
		specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Heart hunter#19m32");
		npctalk "ฉันบอกให้ใช้สิ่งนี้ในเวลาแบบนี้ ดูเหมือนว่าคุณจะใช้สิ่งนี้ต่อหน้าฉันไม่ได้เหรอ?", instance_npcname("Heart hunter#19m30");
		mapannounce 'map_jorchs$, "Injection unit started.", bc_map, 0xFFCC;
		sleep 2500;
		npctalk "พวกมันให้กำเนิดไข่ขั้นสูงในทันทีในรูปแบบ Rgan ขั้นสูงที่ได้รับการปรับปรุง", instance_npcname("Heart hunter#19m32");
		specialeffect EF_CLOUD4, AREA, instance_npcname("Heart hunter#19m32");
		sleep 1500;
		npctalk "คุณจะไปพบปะกับพวกRgansมากมายใช่ไหม?", instance_npcname("Heart hunter#19m31");
		sleep 2500;
		npctalk "ตราบใดที่ไฟฟ้ายังมีอยู่ก็จะมีออกมาเรื่อยๆ", instance_npcname("Heart hunter#19m32");
		sleep 2000;
		npctalk "คุณฟังดูเหมือนคุณสามารถตัดกระแสไฟฟ้าได้ใช่ไหม คุณใจดีกว่าที่ฉันคิดไว้นะ", instance_npcname("Voglinde#19m30");
		emotion ET_HUK, getnpcid(0,"Heart hunter#19m30");
		emotion ET_HUK, getnpcid(0,"Heart hunter#19m31");
		emotion ET_HUK, getnpcid(0,"Heart hunter#19m32");
		sleep 2500;
		npctalk "เฮ้ย หาไม่ยากหรอกเหรอ แล้วยังโทรไปแจ้งฝ่ายสนับสนุนด้วย!!", instance_npcname("Heart hunter#19m31");
		sleep 1500;
		npctalk "เราอยู่ระหว่างมุ่งหน้าไปสู่เป้าหมายต่อไป!", instance_npcname("Heart hunter#19m30");
		sleep 2000;
		disablenpc instance_npcname("Heart hunter#19m30");
		disablenpc instance_npcname("Heart hunter#19m31");
		disablenpc instance_npcname("Heart hunter#19m32");
		npctalk "นักผจญภัย พวกเราจะติดตามคุณ กำจัด Rgan และนำอุปกรณ์กลับมา...!", instance_npcname("Voglinde#19m30");
		sleep 2000;
		disablenpc instance_npcname("Voglinde#19m30");
		disablenpc instance_npcname("Miriam#19m30");
		enablenpc instance_npcname("#e19p01");
		'event = 11;
		end;
}

1@jorchs,243,166,0	script	#e19p01	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch:
		if ('event == 11) {
				'event = 12;
				donpcevent instance_npcname("ep19_jorchs_area_3_2") + "::OnStart";	// spawn mobs
				end;
		}
		if ('event == 12 || 'event == 13 || 'event == 14) {
				viewpoint 1,219,148,1,0xFF3355;
				end;
		}
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_3_2	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("ep19_jorchs_area_3_2") + "::OnNormalMob";

		disablenpc instance_npcname("Modified Advanced Rgan#19m30");
		disablenpc instance_npcname("Warped Rgan#19m37");
		monster 'map_jorchs$,250,177,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,243,174,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		sleep 3000;
		disablenpc instance_npcname("Warped Rgan#19m31");
		disablenpc instance_npcname("Smashed Rgan#19m35");
		monster 'map_jorchs$,249,176,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,245,176,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		sleep 3000;
		disablenpc instance_npcname("Combined Rgan#19m32");
		disablenpc instance_npcname("Modified Advanced Rgan#19m34");
		monster 'map_jorchs$,248,176,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,246,176,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 3000;
		disablenpc instance_npcname("Modified Greater RGAN#19m36");
		disablenpc instance_npcname("Modified Advanced Rgan#19m33");
		monster 'map_jorchs$,244,175,"Combined Logan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,247,176,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		if ('event == 12)
				'event = 13;
		end;

OnNormalMob:
		'count_area_3_2++;
		if ('count_area_3_2 < 8)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_2") + "::OnNormalMob" ) > 0)
				end;
		if ('event != 13)
				end;
		'event = 14;
		mapannounce 'map_jorchs$, "Infuse Greater L'gann's eggs with mixed energy.", bc_map, 0xFFCC;
		enablenpc instance_npcname("Injection device#19m30");

		donpcevent instance_npcname("#e19c01") + "::OnStart";	// spawn temporary mobs
		end;
}

// Main Quest: Step 79
1@jorchs,219,148,5	script	Injection device#19m30	4_ENERGY_BLUE,3,3,{
		if ('event != 14)
				end;
		'event = 15;
		disablenpc();
		viewpoint 2,219,148,1,0xFF3355;
		npctalk "อุปกรณ์ปล่อยฉีด";
		donpcevent instance_npcname("ep19_jorchs_area_3_3") + "::OnStart";
		donpcevent instance_npcname("#e19c01") + "::OnStop";	// stop spawn temporary mobs
		initnpctimer;
		end;
OnTimer10000:
		stopnpctimer;
		donpcevent instance_npcname("#e19c01") + "::OnKill";
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_3_3	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("ep19_jorchs_area_3_3") + "::OnNormalMob";

		monster 'map_jorchs$,224,151,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "The device is operational, but only one has been modified and born?";
		monster 'map_jorchs$,225,141,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "Did you turn off the device? You said that if you turn it off, it will wither and die soon!!!";
		monster 'map_jorchs$,217,141,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "What? Where have you all gone? Did you all go first after calling?";
		end;
OnNormalMob:
		if ('event != 15)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_3") + "::OnNormalMob" ) > 0)
				end;
		'event = 16;
		mapannounce 'map_jorchs$, "Additional injection unit up.", bc_map, 0xFFCC;

		setcell 'map_jorchs$,242,174,242,174,cell_walkable,1;
		setcell 'map_jorchs$,244,176,244,176,cell_walkable,1;
		setcell 'map_jorchs$,247,176,247,176,cell_walkable,1;
		setcell 'map_jorchs$,248,176,248,176,cell_walkable,1;
		setcell 'map_jorchs$,243,175,243,175,cell_walkable,1;
		setcell 'map_jorchs$,245,176,245,176,cell_walkable,1;
		setcell 'map_jorchs$,246,176,246,176,cell_walkable,1;
		setcell 'map_jorchs$,249,177,249,177,cell_walkable,1;

		donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnStart";	// spawn mobs next area
		end;
}

1@jorchs,230,160,0	script	#e19c01	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer30000:
		specialeffect EF_AGIUP;
		specialeffect EF_GUARD2;

		getmapxy .@map$,.@x,.@y, BL_NPC;
		monster 'map_jorchs$,.@x,.@y,"Unstable mod Rgan","EP19_MD_RGAN_E",1, instance_npcname( strnpcinfo(0) ) + "::OnTemporaryMob";
		initnpctimer;
		end;
OnStop:
		stopnpctimer;
		end;
OnTemporaryMob:
		end;
OnKill:
		stopnpctimer;
		killmonster 'map_jorchs$, instance_npcname( strnpcinfo(0) ) + "::OnTemporaryMob";
		end;
}


// Area 4
1@jorchs,244,176,0	script	#e19p04	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch:
		if ('event == 16) {
				viewpoint 2,219,148,1,0xFF3355;
				if ('area_4[1] == 0)
						viewpoint 1,226,244,1,0xFF3355;
				if ('area_4[0] == 0)
						viewpoint 1,191,203,2,0xFF3355;
				end;
		}
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_4_1	-1,{
		end;
OnStart:
		enablenpc instance_npcname("Injection device#19m32");
		enablenpc instance_npcname("Injection device#19m31");

		.@event$ = instance_npcname("ep19_jorchs_area_4_1") + "::OnNormalMob";

		monster 'map_jorchs$,270,198,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,228,211,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,210,204,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,194,200,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,173,179,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,245,235,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,273,254,"Howling Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,247,278,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,224,273,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,238,258,"Unstable Augmented Logan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,198,272,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;

		donpcevent instance_npcname("#e19c02") + "::OnStart";	// spawn temporary mobs
		donpcevent instance_npcname("#e19c03") + "::OnStart";
		end;
OnNormalMob:
		if ('event != 16)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_1") + "::OnNormalMob" ) > 0)
				end;
		if ('area_4[2] != 0)
				end;
		'area_4[2] = 2;
		mapannounce 'map_jorchs$, "Oh, teachers, talk about this.", bc_map, 0xFFCC;
		donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnCheck";	// check if we should trigger the next step
		end;
OnSpecialMob:
		if ('event != 16)
				end;
		if ('area_4[0] == 1 && 'area_4[1] == 1) {
				'area_4[0] = 'area_4[1] = 2;
				mapannounce 'map_jorchs$, "There are still Rgans over there!", bc_map, 0xFFCC;
				donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnCheck";	// check if we should trigger the next step
				end;
		}
		end;
OnCheck:
		if ('area_4[0] == 2 && 'area_4[1] == 2 && 'area_4[2] == 2) {
				'event = 17;
				enablenpc instance_npcname("Heart hunter#19m41");
				enablenpc instance_npcname("Heart hunter#19m40");
				enablenpc instance_npcname("Leon#19m40");
				enablenpc instance_npcname("Heart hunter#19m42");
				enablenpc instance_npcname("Aurelie#19m40");
				end;
		}
		end;
}

1@jorchs,198,205,0	duplicate(#e19c01)	#e19c03	HIDDEN_WARP_NPC

// Main Quest: Step 80
1@jorchs,191,203,7	script	Injection device#19m32	4_ENERGY_BLUE,{
		if ('event != 16)
				end;
		if ('area_4[0] != 0)
				end;
		'area_4[0] = 1;
		viewpoint 2,191,203,2,0xFF3355;
		npctalk "อุปกรณ์ปล่อยฉีด";
		disablenpc();
		donpcevent instance_npcname("#e19c03") + "::OnStop";	// stop spawn temporary mobs
		donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnSpecialMob";	// announce if the 2 temporary spots are disabled
		initnpctimer;
		end;
OnTimer10000:	// the temporary mobs are killed after ~10s
		stopnpctimer;
		donpcevent instance_npcname("#e19c03") + "::OnKill";
		end;
}


1@jorchs,239,255,0	duplicate(#e19c01)	#e19c02	HIDDEN_WARP_NPC

// Main Quest: Step 81
1@jorchs,226,244,7	script	Injection device#19m31	4_ENERGY_BLUE,{
		if ('event != 16)
				end;
		if ('area_4[1] != 0)
				end;
		'area_4[1] = 1;
		viewpoint 2,226,244,1,0xFF3355;
		npctalk "อุปกรณ์ปล่อยฉีด";
		disablenpc();
		donpcevent instance_npcname("#e19c03") + "::OnStop";	// stop spawn temporary mobs
		donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnSpecialMob";	// announce if the 2 temporary spots are disabled
		initnpctimer;
		end;
OnTimer10000:	// the temporary mobs are killed after ~10s
		stopnpctimer;
		donpcevent instance_npcname("#e19c02") + "::OnKill";
		end;
}


// Wall of monsters
1@jorchs,139,181,3	script	Modified Advanced Rgan#19m42	EP19_MD_RGAN_E,5,5,{
		unittalk getcharid(3), "" + strcharinfo(0) + " : Looks like you can't get past the Rgan wall yet. I'll have to finish the search.";
		end;
}
1@jorchs,138,178,3	duplicate(dummy_npc)	Modified Greater Rgan#19m45	EP19_MD_RGAN_E
1@jorchs,138,179,3	duplicate(dummy_npc)	Warped Rgan#19m44	EP19_MD_RGAN_D
1@jorchs,139,180,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m43	EP19_MD_RGAN_E
1@jorchs,140,182,3	duplicate(dummy_npc)	Smashed Rgan#19m41	EP19_MD_RGAN_D
1@jorchs,140,183,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m40	EP19_MD_RGAN_E


1@jorchs,149,178,5	duplicate(dummy_disabled_npc)	Heart hunter#19m41	EP19_MD_HEARTHUNTER_AT
1@jorchs,149,179,5	duplicate(dummy_disabled_npc)	Heart hunter#19m40	EP19_MD_HEARTHUNTER_AT
1@jorchs,150,180,5	duplicate(dummy_disabled_npc)	Heart hunter#19m42	EP19_MD_HEARTHUNTER_AT
1@jorchs,150,177,1	duplicate(dummy_disabled_npc)	Leon#19m40	4_EP19_LEON,3,3

// Main Quest: Step 82, 84
1@jorchs,151,179,1	script	Aurelie#19m40	4_EP19_AURELIE,5,5,{
		if ('event == 17) {
				'event = 18;
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Heart hunter#19m40");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Heart hunter#19m41");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Heart hunter#19m42");
				npctalk "มีปัญหาอะไรไหมครับ บอกว่าเข้าร่วมไม่ได้แต่ไม่ครอบคลุมทำให้มีปัญหาเหรอครับ?", instance_npcname("Aurelie#19m40");
				sleep 3000;
				npctalk "ฉันไม่ได้พูดสิ่งที่ฉันพูดมันเพราะฉันไม่เข้าใจ", instance_npcname("Aurelie#19m40");
				sleep 1500;
				npctalk "นักผจญภัยทั้งหลาย เรามาถึงที่นี่แล้ว...", instance_npcname("Leon#19m40");
				mapannounce 'map_jorchs$, "Injection unit is up.", bc_map, 0xFFCC;
				sleep 3000;
				npctalk "เราจะถือดอกเบี้ยไว้ ดังนั้นจะดีกว่าถ้าจะหาอุปกรณ์ก่อนแล้วปิดมัน ฉันจะทำ", instance_npcname("Aurelie#19m40");
				sleep 2000;
				npctalk "ทำอะไรไปแล้วพักหนึ่งแล้วกลับมาเริ่มใหม่อีกครั้ง?", instance_npcname("Leon#19m40");
				sleep 2000;
				npctalk "มาคุยเรื่องนี้กันดีกว่าครับคุณครู", instance_npcname("Heart hunter#19m40");
				sleep 2000;
				npctalk "นี่มันอะไรวะ ทำไมฉันถึงขยับมันไม่ได้?", instance_npcname("Heart hunter#19m41");
				sleep 2000;
				npctalk "ก็เพราะว่าฉันบ้านิดหน่อย", instance_npcname("Aurelie#19m40");
				sleep 2000;
				npctalk "แล้วเราจะจบเรื่องที่คุยกันไว้ดีไหม?", instance_npcname("Leon#19m40");
				sleep 3000;
				npctalk "ปล่อยให้เป็นหน้าที่ของเรา", instance_npcname("Aurelie#19m40");
				npctalk "นักผจญภัยอย่าเพียงแค่ไปโน้มน้าวพวกเขาเท่านั้น!", instance_npcname("Heart hunter#19m42");
				'event = 19;
				enablenpc instance_npcname("Injection device#19m40");
				enablenpc instance_npcname("#e19p02");
				donpcevent instance_npcname("#e19c04") + "::OnStart";	// spawn temporary mobs
				end;
		}
		if ('event == 19) {
				npctalk "ทิ้งเราไว้ที่นี่แล้วปิดเครื่อง", instance_npcname("Aurelie#19m40");
				end;
		}
		if ('event == 21) {
				'event = 22;
				npctalk "มาสิ พวกนี้แพ็คมาอย่างดี คุณจึงไปต่อได้เลย", instance_npcname("Aurelie#19m40");
				sleep 1500;
				npctalk "พวกเขากล่าวว่าพวกเขาได้เรียกเหล่าราแกนทั้งหมดที่มีตำแหน่งสูงกว่าขึ้นไปมาที่บ้านของราสกานด์", instance_npcname("Aurelie#19m40");
				sleep 3000;
				npctalk "นั่น...อย่าพูดแบบนั้นสิ ได้โปรด", instance_npcname("Heart hunter#19m40");
				sleep 2000;
				npctalk "เขาบอกว่ามีทางเข้าเพียงทางเดียว ไม่น่าเชื่อ?", instance_npcname("Leon#19m40");
				npctalk "เราพูดแต่ความจริงเท่านั้น เชื่อฉันสิ", instance_npcname("Heart hunter#19m41");
				sleep 2000;
				npctalk "ไม่ว่าจะด้วยวิธีใดก็ตาม เป็นที่ชัดเจนว่าสิ่งที่ Rasgand และ Bagot ต้องการบรรลุนั้นอยู่ใกล้แค่เอื้อม", instance_npcname("Aurelie#19m40");
				sleep 2000;
				npctalk "ฉันต้องรีบ", instance_npcname("Aurelie#19m40");
				sleep 1500;
				npctalk "ครูทั้งหลาย เรามิได้บอกกล่าวแก่ท่านเลย เราได้บอกท่านไปหมดแล้วทุกอย่างที่เรารู้", instance_npcname("Heart hunter#19m42");
				sleep 1500;
				npctalk "นอกจากนั้น...", instance_npcname("Leon#19m40");
				emotion ET_SURPRISE, getnpcid(0,"Leon#19m40");
				specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Leon#19m40");
				mapannounce 'map_jorchs$, "Power up the horsepower booster.", bc_map, 0xFFCC;
				sleep 2000;
				npctalk "ห้ะ? กระแสเวทย์มันแปลกๆ หน่อยเหรอ?", instance_npcname("Leon#19m40");
				sleep 2000;
				npctalk "ใช่แล้ว ฉันไม่คิดว่าเด็กๆ จะจัดการเรื่องนี้ได้", instance_npcname("Aurelie#19m40");
				sleep 2000;
				npctalk "นักผจญภัย ฉันฝากเรื่องนี้ให้กับนักผจญภัยได้ไหม?", instance_npcname("Leon#19m40");
				sleep 3000;
				npctalk "รีบจัดการเลยดีกว่าไหม ไม่ใช่เรื่องแปลกอะไร เรามาเริ่มกันก่อนเลย", instance_npcname("Leon#19m40");
				sleep 2000;
				npctalk "ฮ่า! จริงเหรอ! เราดูดีขนาดนั้นเลยเหรอ?", instance_npcname("Heart hunter#19m40");
				disablenpc instance_npcname("Aurelie#19m40");
				disablenpc instance_npcname("Leon#19m40");
				sleep 2000;
				npctalk "หากพี่น้องที่น่ากลัวหายไป เราก็สามารถทำมันได้เช่นกัน!", instance_npcname("Heart hunter#19m41");
				sleep 2000;
				npctalk "คุณควรจะมัดปากนั่นไว้ก่อน แล้วฉันจะถามคุณ", instance_npcname("Aurelie#19m40");
				donpcevent instance_npcname("ep19_jorchs_area_4_3") + "::OnStart";	// spawn mobs
				end;
		}
		end;
}


1@jorchs,156,177,0	script	#e19p02	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch:
		if ('event == 19) {
				viewpoint 1,192,206,3,0xFF3355;
				end;
		}
		end;
}

1@jorchs,199,197,0	duplicate(#e19c01)	#e19c04	HIDDEN_WARP_NPC

// Main Quest: Step 83
1@jorchs,192,206,5	script	Injection device#19m40	4_ENERGY_BLUE,3,3,{
		if ('event == 19) {
				'event = 20;
				viewpoint 2,192,206,3,0xFF3355;
				npctalk "ถอดอุปกรณ์ออก";
				disablenpc();
				donpcevent instance_npcname("#e19c04") + "::OnKill";	// stop spawn and kill temporary mobs
				mapannounce 'map_jorchs$, "Come here when the device is off.", bc_map, 0xFFCC;
				donpcevent instance_npcname("ep19_jorchs_area_4_2") + "::OnStart";
				end;
		}
		end;
OnTouch:	// unknown effect
		end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_4_2	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("ep19_jorchs_area_4_2") + "::OnNormalMob";

		monster 'map_jorchs$,191,199,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "I told you to keep that off";
		monster 'map_jorchs$,199,197,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "Is it better to get out of there?";
		monster 'map_jorchs$,202,207,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "You're a big deal.";
		end;
OnNormalMob:
		if ('event != 20)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_2") + "::OnNormalMob" ) > 0)
				end;
		'event = 21;
		end;
}

// Main Quest: Step 84
// 1@jorchs,151,179,1	script	Aureli#19m40	4_EP19_AURELIE,5,5,{

1@jorchs,1,1,3	script	ep19_jorchs_area_4_3	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("ep19_jorchs_area_4_3") + "::OnNormalMob";
		'ep19_jorchs_area_4_3 = 0;

		disablenpc instance_npcname("Smashed Rgan#19m41");
		disablenpc instance_npcname("Heart hunter#19m41");
		monster 'map_jorchs$,141,182,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,149,178,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		sleep 3000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m40");
		disablenpc instance_npcname("Warped Rgan#19m44");
		monster 'map_jorchs$,141,183,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,139,179,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		sleep 3000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m42");
		disablenpc instance_npcname("Modified Advanced Rgan#19m43");
		monster 'map_jorchs$,140,181,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,140,180,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 1000;
		disablenpc instance_npcname("Heart hunter#19m42");
		disablenpc instance_npcname("Heart hunter#19m40");
		monster 'map_jorchs$,150,180,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		monster 'map_jorchs$,149,179,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		sleep 1000;
		disablenpc instance_npcname("Modified Greater Rgan#19m45");
		monster 'map_jorchs$,139,178,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		end;
OnNormalMob:
		if ('event != 22)
				end;
		'ep19_jorchs_area_4_3++;
		if ('ep19_jorchs_area_4_3 < 9)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_3") + "::OnNormalMob" ) > 0)
				end;
		'event = 23;
		mapannounce 'map_jorchs$, "Illusion Sector is unusual. Somebody...!", bc_map, 0xFFCC;
		enablenpc instance_npcname("#e19p03");
		setcell 'map_jorchs$,140,183,140,183,cell_walkable,1;
		setcell 'map_jorchs$,140,182,140,182,cell_walkable,1;
		setcell 'map_jorchs$,139,181,139,181,cell_walkable,1;
		setcell 'map_jorchs$,139,180,139,180,cell_walkable,1;
		setcell 'map_jorchs$,138,179,138,179,cell_walkable,1;
		setcell 'map_jorchs$,138,178,138,178,cell_walkable,1;

		donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnStart";	// spawn mobs next area
		end;
}


// Area 5
1@jorchs,137,181,0	script	#e19p03	HIDDEN_WARP_NPC,7,7,{
		end;
OnTouch:
		if ('event == 23) {
				if ('area_5[1] == 0)
						viewpoint 1,20,195,4,0xFF3355;
				if ('area_5[0] == 0)
						viewpoint 1,24,142,5,0xFF3355;
				end;
		}
		end;
}


// Main Quest: Step 85

1@jorchs,1,1,3	script	ep19_jorchs_area_5_1	-1,{
		end;
OnStart:
		enablenpc instance_npcname("Research device#19m41");

		.@event$ = instance_npcname("ep19_jorchs_area_5_1") + "::OnNormalMob";

		monster 'map_jorchs$,121,190,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,103,215,"Modified Advanced Ligan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,82,221,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,86,165,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,79,154,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,45,153,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,32,135,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,18,133,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,25,172,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,24,188,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;

		donpcevent instance_npcname("#e19c06") + "::OnStart";	// spawn temporary mobs
		donpcevent instance_npcname("#e19c05") + "::OnStart";
		end;
OnNormalMob:
		if ('event != 23)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_1") + "::OnNormalMob" ) > 0)
				end;
		if ('area_5[2] != 0)
				end;
		'area_5[2] = 1;
		donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
		end;
OnCheck:
		if ('event == 23 && 'area_5[0] == 1 && 'area_5[1] == 1 && 'area_5[2] == 1) {
				'event = 24;
				mapannounce 'map_jorchs$, "Who will arrive first?", bc_map, 0xFFCC;
				enablenpc instance_npcname("Heart hunter#19m50");
				enablenpc instance_npcname("Heart hunter#19m52");
				enablenpc instance_npcname("Heart hunter#19m51");
		}
		end;
}

// Area 5 - South
1@jorchs,17,133,0	duplicate(#e19c01)	#e19c06	HIDDEN_WARP_NPC

1@jorchs,24,142,7	script	Research device#19m41	4_ENERGY_BLUE,{
		if ('event == 23 && 'area_5[0] == 0) {
				'area_5[0] = 1;
				disablenpc();
				viewpoint 2,24,142,5,0xFF3355;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Is this... important?";
				.@event$ = instance_npcname("Research device#19m41") + "::OnNormalMob";
				monster 'map_jorchs$,20,136,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
				unittalk $@mobid[0], "You pretended to be a ragan, so you came here to soak up your memories?";
				monster 'map_jorchs$,30,139,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
				unittalk $@mobid[0], "I told you not to touch it!";
				initnpctimer;
				end;
		}
		end;
OnNormalMob:
		end;
OnTimer10000:	// the hunters and the temporary mobs are killed after 10s
		stopnpctimer;
		donpcevent instance_npcname("#e19c06") + "::OnKill";
		killmonster 'map_jorchs$, instance_npcname("Research device#19m41") + "::OnNormalMob";
		donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
		end;
}

1@jorchs,16,135,3	script	Laboratory trace#19m40	4_ENERGY_BLUE,{
		unittalk getcharid(3), "" + strcharinfo(0) + " : few left";
		end;
}


// Area 5 - Middle
1@jorchs,17,188,0	duplicate(#e19c01)	#e19c05	HIDDEN_WARP_NPC

1@jorchs,20,195,7	script	Research device#19m40	4_ENERGY_BLUE,{
		if ('event == 23 && 'area_5[1] == 0) {
				'area_5[1] = 1;
				disablenpc();
				viewpoint 2,20,195,4,0xFF3355;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Ymir's heart... Of course not... I don't see any useful research data.";
				mapannounce 'map_jorchs$, "We still have tons of Rgan to deal with!", bc_map, 0xFFCC;
				.@event$ = instance_npcname("Research device#19m40") + "::OnNormalMob";
				monster 'map_jorchs$,26,194,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
				unittalk $@mobid[0], "Are you really looking for data?";
				monster 'map_jorchs$,26,188,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
				unittalk $@mobid[0], "Raise your hand!!";
				monster 'map_jorchs$,19,185,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
				unittalk $@mobid[0], "Can you see?";
				initnpctimer;
				end;
		}
		end;
OnNormalMob:
		end;
OnTimer20000:	// the hunters and the temporary mobs are killed after 20s
		stopnpctimer;
		donpcevent instance_npcname("#e19c05") + "::OnKill";
		killmonster 'map_jorchs$, instance_npcname("Research device#19m40") + "::OnNormalMob";
		donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
		end;
}


// Area 5 - North

// Wall
1@jorchs,34,255,1	duplicate(dummy_npc)	Modified Advanced Ligan#19m50	EP19_MD_RGAN_E
1@jorchs,35,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m51	EP19_MD_RGAN_E
1@jorchs,36,255,3	duplicate(dummy_npc)	Modified Greater Rgan#19m52	EP19_MD_RGAN_E
1@jorchs,37,255,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m53	EP19_MD_RGAN_E
1@jorchs,38,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m54	EP19_MD_RGAN_E,5,5
1@jorchs,39,255,3	duplicate(dummy_npc)	Modified Advanced Ligan#19m55	EP19_MD_RGAN_E
1@jorchs,40,255,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m56	EP19_MD_RGAN_E
1@jorchs,41,255,3	duplicate(dummy_npc)	Modified Advanced Ligan#19m57	EP19_MD_RGAN_E
1@jorchs,42,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m58	EP19_MD_RGAN_E
1@jorchs,43,255,3	duplicate(dummy_npc)	Modified Advanced Lgan#19m59	EP19_MD_RGAN_E
1@jorchs,35,256,5	duplicate(dummy_npc)	Modified Advanced Rgan#19m61	EP19_MD_RGAN_E
1@jorchs,37,256,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m63	EP19_MD_RGAN_E

1@jorchs,38,256,5	script	Modified Advanced Rgan#19m64	EP19_MD_RGAN_E,5,5,{
		if ('event < 24) {
				unittalk getcharid(3), "" + strcharinfo(0) + " : This is the wall of the Rgan. I must search first and then come back.";
				end;
		}
		end;
}
1@jorchs,39,256,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m65	EP19_MD_RGAN_E
1@jorchs,41,256,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m67	EP19_MD_RGAN_E
1@jorchs,42,256,5	duplicate(dummy_npc)	Modified Advanced Rgan#19m68	EP19_MD_RGAN_E


1@jorchs,39,251,5	script	Heart hunter#19m50	EP19_MD_HEARTHUNTER_AT,5,5,{
		end;
OnTouch:
		if ('event == 24) {
				'event = 25;
				npctalk "นั่นใครเหรอ?", instance_npcname("Heart hunter#19m50");
				sleep 1500;
				npctalk "นักผจญภัย?", instance_npcname("Heart hunter#19m52");
				sleep 2000;
				npctalk "พวกนักผจญภัยมาถึงก่อนไหม?", instance_npcname("Heart hunter#19m51");
				sleep 1500;
				npctalk "ใครจะมาถึงที่นี่ก่อน?", instance_npcname("Heart hunter#19m52");
				npctalk "ฉันชนะแล้ว", instance_npcname("Heart hunter#19m50");
				sleep 2000;
				enablenpc instance_npcname("Bagot#19m50");
				npctalk "คุณเป็นนักผจญภัยเพียงกลุ่มเดียวที่นี่หรือเปล่า?", instance_npcname("Bagot#19m50");
				sleep 2000;
				npctalk "ใช่ คนอื่นๆ ดูเหมือนจะกำลังรับมือกับ Magic Runaway Rgan", instance_npcname("Heart hunter#19m50");
				sleep 2000;
				npctalk "ใช่เหรอ? แต่ที่นี่ยังอยากเจอพวกนักผจญภัยอยู่เหรอ?", instance_npcname("Bagot#19m50");
				sleep 3000;
				npctalk "คุณคงจะต้องหลอก Bagot ด้วยกลอุบายน่ารักๆ เมื่อวันก่อนใช่มั้ยล่ะ", instance_npcname("Bagot#19m50");
				sleep 3000;
				npctalk "ฉันเลยเตรียมของขวัญมาให้คุณนี่ไง", instance_npcname("Bagot#19m50");
				sleep 3000;
				npctalk "ดูเหมือนพวกเขาจะได้เจอคนอื่นแล้ว เส้นทางมาบรรจบกัน", instance_npcname("Bagot#19m50");
				enablenpc instance_npcname("Rgan the Berserk#19m50");
				enablenpc instance_npcname("Rgan the Berserk#19m51");
				enablenpc instance_npcname("Rgan the Berserk#19m52");
				enablenpc instance_npcname("Rgan the Berserk#19m53");
				enablenpc instance_npcname("Rgan the Berserk#19m54");
				enablenpc instance_npcname("Rgan the Berserk#19m55");
				enablenpc instance_npcname("Rgan the Berserk#19m56");
				npctalk "เอ่อ---", instance_npcname("Rgan the Berserk#19m50");
				npctalk "เกร็ก--เสียงดังกึกก้อง--", instance_npcname("Rgan the Berserk#19m51");
				npctalk "ทำ...ไป...ก...", instance_npcname("Rgan the Berserk#19m52");
				npctalk "-", instance_npcname("Rgan the Berserk#19m53");
				npctalk "มะ... ฮะ... .ไม่...", instance_npcname("Rgan the Berserk#19m54");
				npctalk "เอ่อ-เอ่อ---เอ่อ---", instance_npcname("Rgan the Berserk#19m55");
				npctalk "ฉัน...ก็...", instance_npcname("Rgan the Berserk#19m56");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m50");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m51");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m52");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m53");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m54");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m55");
				specialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m56");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m50");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m51");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m52");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m53");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m54");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m55");
				specialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m56");
				sleep 2500;
				npctalk "แต่ไม่เป็นไร ฉันดีใจที่สามารถมอบมันให้กับคุณได้ด้วยตัวเอง", instance_npcname("Bagot#19m50");
				sleep 3000;
				npctalk "คุณคงสงสัยว่าบาโกต์จะทำอะไร? มันคงทำให้คุณเกิดแรงบันดาลใจที่จะทำแบบนั้นเพื่อจุนเซีย", instance_npcname("Bagot#19m50");
				sleep 2500;
				npctalk "แต่มันไม่ได้ไปไกลกว่าจำนวนตื้น ๆ เช่นนั้น", instance_npcname("Bagot#19m50");
				sleep 3000;
				npctalk "เพราะบาโกต์จะบอกคุณเอง เพื่อทำเช่นนั้น ฉันจึงโรยเศษขนมปังที่ไม่จำเป็นลงไปแล้วเดินตามเขาไป", instance_npcname("Bagot#19m50");
				sleep 1500;
				npctalk "มันน่ารำคาญเล็กน้อยเมื่อคิดว่าแมลงวันจะยังคงติดอยู่ในโลกใหม่ที่ Bagot จะสร้างขึ้น", instance_npcname("Bagot#19m50");
				sleep 2500;
				npctalk "คุณหมายความว่าราสกานด์ก็โกรธมนุษย์ที่หลอกเขามากเหมือนกันใช่ไหม?", instance_npcname("Bagot#19m50");
				sleep 2500;
				npctalk "ตอนนี้มันเป็นของขวัญจากบาโกต์ ดังนั้นฉันคงต้องไปเอาอันนี้แล้วออกไปจากที่นี่", instance_npcname("Bagot#19m50");
				sleep 500;
				disablenpc instance_npcname("Bagot#19m50");
				donpcevent instance_npcname("ep19_jorchs_area_5_2") + "::OnWave1";
				end;
		}
		end;
}
1@jorchs,41,253,5	duplicate(dummy_disabled_npc)	Heart hunter#19m52	EP19_MD_HEARTHUNTER_AT
1@jorchs,36,251,5	duplicate(dummy_disabled_npc)	Heart hunter#19m51	EP19_MD_HEARTHUNTER_AT

1@jorchs,37,252,5	duplicate(dummy_disabled_npc)	Bagot#19m50	4_EP18_BAGOT

1@jorchs,31,249,5	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m50	EP19_MD_RGAN_ES
1@jorchs,47,237,1	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m51	EP19_MD_RGAN_ES
1@jorchs,47,249,3	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m52	EP19_MD_RGAN_ES
1@jorchs,31,237,7	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m53	EP19_MD_RGAN_ES
1@jorchs,27,243,5	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m54	EP19_MD_RGAN_ES
1@jorchs,50,243,3	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m55	EP19_MD_RGAN_ES
1@jorchs,39,235,1	duplicate(dummy_disabled_npc)	Rgan the Berserk#19m56	EP19_MD_RGAN_ES


1@jorchs,1,1,3	script	ep19_jorchs_area_5_2	-1,{
		end;
OnWave1:
		.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave2";

		mapannounce 'map_jorchs$, "Prepare for enchantment. Phase 1 enchantment begins.", bc_map, 0xFFCC;
		disablenpc instance_npcname("Modified Advanced Rgan#19m51");
		disablenpc instance_npcname("Modified Advanced Rgan#19m58");
		monster 'map_jorchs$,35,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,42,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m53");
		disablenpc instance_npcname("Modified Advanced Rgan#19m56");
		monster 'map_jorchs$,37,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,40,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Rgan the Berserk#19m50");
		disablenpc instance_npcname("Rgan the Berserk#19m51");
		monster 'map_jorchs$,31,249,"Rgan the Wild","EP19_MD_RGAN_ES",1, .@event$;
		monster 'map_jorchs$,47,237,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m50");
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m51");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m50");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m51");
		end;
OnWave2:
		if ('event != 25)
				end;
		'ep19_jorchs_area_5_2[0]++;
		if ('ep19_jorchs_area_5_2[0] < 6)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave2" ) > 0)
				end;
		.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave3";

		mapannounce 'map_jorchs$, "Prepare for enchantment. Phase 2 enchantment begins.", bc_map, 0xFFCC;
		sleep 6000;
		disablenpc instance_npcname("Modified Advanced Ligan#19m50");
		disablenpc instance_npcname("Modified Advanced Lgan#19m59");
		monster 'map_jorchs$,34,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,43,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m54");
		disablenpc instance_npcname("Modified Advanced Ligan#19m55");
		monster 'map_jorchs$,38,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,39,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Modified Greater Rgan#19m52");
		disablenpc instance_npcname("Modified Advanced Ligan#19m57");
		monster 'map_jorchs$,36,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,41,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Rgan the Berserk#19m52");
		disablenpc instance_npcname("Rgan the Berserk#19m53");
		monster 'map_jorchs$,47,249,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
		monster 'map_jorchs$,31,237,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m52");
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m53");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m52");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m53");
		end;
OnWave3:
		if ('event != 25)
				end;
		'ep19_jorchs_area_5_2[1]++;
		if ('ep19_jorchs_area_5_2[1] < 8)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave3" ) > 0)
				end;
		.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave4";

		mapannounce 'map_jorchs$, "Prepare for enchantment. Phase 3 enchantment begins.", bc_map, 0xFFCC;
		sleep 6000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m61");
		disablenpc instance_npcname("Modified Advanced Rgan#19m65");
		monster 'map_jorchs$,35,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,39,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m63");
		disablenpc instance_npcname("Modified Advanced Rgan#19m67");
		monster 'map_jorchs$,37,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,41,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Modified Advanced Rgan#19m64");
		disablenpc instance_npcname("Modified Advanced Rgan#19m68");
		monster 'map_jorchs$,38,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		monster 'map_jorchs$,42,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Heart hunter#19m50");
		disablenpc instance_npcname("Heart hunter#19m52");
		disablenpc instance_npcname("Heart hunter#19m51");
		monster 'map_jorchs$,39,250,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		monster 'map_jorchs$,36,250,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		monster 'map_jorchs$,41,252,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		sleep 4000;
		disablenpc instance_npcname("Rgan the Berserk#19m54");
		disablenpc instance_npcname("Rgan the Berserk#19m55");
		disablenpc instance_npcname("Rgan the Berserk#19m56");
		monster 'map_jorchs$,27,243,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
		monster 'map_jorchs$,50,243,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
		monster 'map_jorchs$,39,235,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
		// Note: only the 5th take 10% dmg
		unitskilluseid $@mobid[0], 771, 9;	// NPC_RELIEVE_ON
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m54");
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m55");
		removespecialeffect EF_NPC_STOP, AREA, instance_npcname("Rgan the Berserk#19m56");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m54");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m55");
		removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("Rgan the Berserk#19m56");
		end;
OnWave4:
		if ('event != 25)
				end;
		'ep19_jorchs_area_5_2[2]++;
		if ('ep19_jorchs_area_5_2[2] < 12)
				end;
		if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave4" ) > 0)
				end;
		'event = 26;
		mapannounce 'map_jorchs$, "Is that Rasgand's abode?", bc_map, 0xFFCC;
		enablenpc instance_npcname("Center door#19m61");
		setcell 'map_jorchs$,34,255,43,255,cell_walkable,1;
		end;
}


1@jorchs,21,254,5	duplicate(dummy_disabled_npc)	Iwin#19m60	EP19_NPC_IWIN_SG
1@jorchs,35,271,7	duplicate(dummy_disabled_npc)	Miriam#19m60	4_EP18_MIRIAM
1@jorchs,36,272,7	duplicate(dummy_disabled_npc)	Voglinde#19m60	4_EP19_VOGLINDE
1@jorchs,38,272,1	duplicate(dummy_disabled_npc)	Aureli#19m60	4_EP19_AURELIE
1@jorchs,39,271,1	duplicate(dummy_disabled_npc)	Leon#19m60	4_EP19_LEON
1@jorchs,40,274,1	duplicate(dummy_disabled_npc)	Horr#19m60	EP19_NPC_IWIN_SR



1@jorchs,37,274,5	script	Center door#19m61	4_ENERGY_BLUE,3,3,{
		if ('event == 26) {
				'event = 27;
				enablenpc instance_npcname("Aureli#19m60");
				sleep 500;
				enablenpc instance_npcname("Leon#19m60");
				npctalk "นักผจญภัย คุณปลอดภัยแล้ว", instance_npcname("Aureli#19m60");
				sleep 1000;
				npctalk "ทันใดนั้น ก็มี Rgan ชั้นสูงผู้เปี่ยมไปด้วยพลังเวทย์มนตร์โจมตีฉัน และฉันก็ติดอยู่ตรงนั้น", instance_npcname("Leon#19m60");
				sleep 2000;
				enablenpc instance_npcname("Horr#19m60");
				npctalk "โอ้โห~ จริงด้วย โชคชะตาช่างโหดร้ายจริงๆ อะไรกันเนี่ย", instance_npcname("Horr#19m60");
				sleep 1000;
				enablenpc instance_npcname("Voglinde#19m60");
				sleep 500;
				enablenpc instance_npcname("Miriam#19m60");
				npctalk "ดูเหมือนทุกคนคงเคยเจอมาแล้ว", instance_npcname("Voglinde#19m60");
				sleep 1000;
				npctalk "คุณเคยเก็บเกี่ยวอะไรบ้างไหม?", instance_npcname("Miriam#19m60");
				sleep 2000;
				npctalk "เมื่อเลียไปทั่วสถานที่ที่ซับซ้อนนี้ ฉันไม่พบร่องรอยใดๆ เลย", instance_npcname("Horr#19m60");
				sleep 2000;
				npctalk "งานวิจัยของ Illusion ทั้งหมดได้รับการจัดการเรียบร้อยแล้ว และตอนนี้เหลือเพียงที่นี่แห่งเดียวเท่านั้น", instance_npcname("Aureli#19m60");
				sleep 2500;
				npctalk "ถึงคุณจะคว้า Heart Hunter มาแล้วถาม พวกมันทั้งหมดก็ย้ายไปที่ศูนย์กลางแล้ว", instance_npcname("Voglinde#19m60");
				sleep 1500;
				npctalk "ไม่มีใครหนีออกจากเรือเหาะได้ พวกเขาคงทำไม่ได้", instance_npcname("Miriam#19m60");
				sleep 2000;
				npctalk "นี่ไม่ใช่ส่วนที่เหลือของสถานที่นั้นหรือ?", instance_npcname("Leon#19m60");
				sleep 2000;
				npctalk "แล้วถ้าคุณเปิดประตูนี้ คุณสามารถจับทั้ง Illusion และ Rasgand ได้ใช่ไหม?", instance_npcname("Horr#19m60");
				sleep 1500;
				npctalk "ละทิ้งเหล่ายศชั้นสูงทั้งหมดแล้วย้ายไปยังสถานที่ที่ไม่มีใครเข้าได้?", instance_npcname("Aureli#19m60");
				sleep 2000;
				npctalk "ทำไม", instance_npcname("Leon#19m60");
				sleep 1500;
				npctalk "ลองเปิดดูครับ สอบถามได้โดยตรงครับ", instance_npcname("Horr#19m60");
				sleep 1000;
				specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Center door#19m61");
				sleep 500;
				npctalk "ล็อคแน่นเกินไปมั้ย?", instance_npcname("Leon#19m60");
				sleep 2000;
				npctalk "นี่มันเรื่องไร้สาระใช่ไหม? เอาทุกอย่างที่คุณมีออกไปซะ", instance_npcname("Horr#19m60");
				sleep 2500;
				specialeffect EF_M03, AREA, instance_npcname("Center door#19m61");
				sleep 500;
				specialeffect EF_NPC_EARTHQUAKE, AREA, instance_npcname("Center door#19m61");
				sleep 500;
				specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("Center door#19m61");
				specialeffect EF_DESPERADO, AREA, instance_npcname("Center door#19m61");
				specialeffect EF_SPREADATTACK, AREA, instance_npcname("Center door#19m61");
				specialeffect EF_DRAGONFEAR, AREA, instance_npcname("Center door#19m61");
				sleep 1000;
				npctalk "ประตูไปทำอะไรมาวะเนี่ย เปิดประตูซิ เปิดซิ!!! ฉันรู้ทุกอย่างข้างใน!!", instance_npcname("Horr#19m60");
				sleep 2000;
				npctalk "ฉันเปิดมันไม่ได้แม้แต่ด้วยกุญแจที่ได้มาจาก Hearthunter", instance_npcname("Voglinde#19m60");
				sleep 1500;
				npctalk "การแช่เวทย์มนตร์ก็เช่นเดียวกัน", instance_npcname("Aureli#19m60");
				sleep 1500;
				npctalk "นั่นไม่ใช่ป้อมปราการที่ไม่สามารถโจมตีได้หรือ?", instance_npcname("Horr#19m60");
				sleep 2000;
				npctalk "งั้นฉันจะตั้งนาฬิกาที่นี่และออกไปจากที่นี่วันนี้", instance_npcname("Voglinde#19m60");
				sleep 2000;
				npctalk "แล้วที่ซ่อนของ Rgan ล่ะ เผามันซะ?", instance_npcname("Horr#19m60");
				sleep 2000;
				npctalk "เอาล่ะ คุณควรทิ้งมันไว้ที่นี่ดีกว่า", instance_npcname("Aureli#19m60");
				sleep 2500;
				npctalk "ที่จะเชื่อว่าเรายอมแพ้แล้วจากไป?", instance_npcname("Leon#19m60");
				sleep 2000;
				npctalk "ใช่แล้ว แล้วบางทีฉันอาจเปิดประตูแล้วออกมา", instance_npcname("Aureli#19m60");
				sleep 1500;
				npctalk "ผมไม่ค่อยชอบเท่าไรแต่ก็ไม่มีทางอื่นแล้วผมคงต้องเพิ่มการติดตามตรงนี้ให้มากขึ้น", instance_npcname("Horr#19m60");
				sleep 2000;
				npctalk "ใจเย็นๆ หน่อย ฉันจะเปิดประตูวาร์ปลงไปข้างล่างแล้วกลับกันตามสบาย", instance_npcname("Aureli#19m60");
				sleep 500;
				enablenpc instance_npcname("#19m60");
				specialeffect EF_ELECTRIC4, AREA, instance_npcname("#19m60");
				specialeffect EF_YELLOWFLY3, AREA, instance_npcname("#19m60");
				end;
		}
		end;
}

1@jorchs,36,257,0	script	#19m60	WARPNPC,2,2,{
		if ('event == 27 && ep19_main == 60) {
				erasequest 17648;
				setquest 17649;
				ep19_main = 61;
		}
		warp "jor_back3",67,316;
		end;
}
//===== rAthena Script =======================================
//= Devil Tower
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Devil Tower Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

dali02,134,119,5	script	Historian Shep#tnm01	4_F_NOVICE,{
		if (BaseLevel < 130) {
				mes "[เชพ]";
				mes "คุณเป็นอาสาสมัครไปสำรวจหอคอยปีศาจหรือเปล่า?";
				mes "มันอันตรายมาก ดังนั้นคุณต้องมีอย่างน้อย ^4d4dffเลเวล 130^000000";
				next;
				mes "[เชพ]";
				mes "เมื่อคุณแข็งแกร่งขึ้นแล้วคุณจะกลับมาไหม?";
				mes "คุณรู้ว่าฉันหมายถึงอะไรใช่ไหม?";
				close;
		}
		switch( checkquest(7577,PLAYTIME) ) {// daily timer
		case -1:
				break;
		case 0:
		case 1:
				mes "[เชพ]";
				mes "คุณอยากจะสำรวจอีกครั้งในเร็วๆ นี้หรือเปล่า?";
				mes "ร่างกายของคุณจำเป็นต้องฟื้นตัวจากการเดินทางข้ามกาลเวลา";
				next;
				mes "[เชพ]";
				mes "พักผ่อนสักวันแล้วค่อยกลับมาพรุ่งนี้.!";
				mes "คุณเข้าใจมั้ย?";
				close;
		case 2:
				mes "[เชพ]";
				mes "คุณได้พักผ่อนเพียงพอแล้วหรือยัง?";
				mes "ฉันมีกำหนดที่จะออกสำรวจวันนี้!";
				mes "ฉันก็รอสมาชิกร่วมเดินทางอยู่เสมอ!";
				erasequest 7577;// Space distortion
				devil_tower_can_reenter = 0;
				devil_tower_enter = 0;
				next;
				mes "!- สังเกต -!";
				mes "ตอนนี้คุณสามารถเข้าสู่หอคอยปีศาจได้อีกครั้งแล้ว";
				close;
		}
		if (isbegin_quest(7576) > 0) {// instance completed
				mes "[เชพ]";
				mes "ดูเหมือนว่าเวลาและอวกาศจะมาบรรจบกันที่นั่นใช่ไหม?";
				mes "ฉันประหลาดใจที่การเดินทางข้ามเวลาเป็นไปได้";
				next;
				mes "[เชพ]";
				mes "นี่คือทั้งหมดที่คุณมี";
				mes "คุณทำได้ดีแล้ว.";
				mes "ฉันรอคอยการค้นพบเพิ่มเติมเพิ่มเติม!";
				setquest 7577;// Space distortion
				erasequest 7568;// Explore the tower
				erasequest 7571;// Tower Expedition
				erasequest 7574;// Thanatos Tower
				erasequest 7576;// Morocc castle seal
				getexp 450000,450000;
				devil_tower_can_reenter = 0;
				devil_tower_enter = 0;
				mes "โปรดกลับมาใหม่พรุ่งนี้!";
				close;
		}
		if (isbegin_quest(7574) > 0) {// failed after seal
				mes "[เชพ]";
				mes "โอ้ คุณกลับมาเร็วเหลือเกิน...";
				mes "โอเค ฉันเข้าใจสิ่งที่เกิดขึ้นแล้ว";
				next;
				mes "[เชพ]";
				mes "นี่คือทั้งหมดที่คุณมี";
				mes "คุณทำได้ดีแล้ว.";
				mes "ฉันรอคอยการค้นพบเพิ่มเติมเพิ่มเติม!";
				setquest 7577;// Space distortion
				erasequest 7568;// Explore the tower
				erasequest 7571;// Tower Expedition
				erasequest 7574;// Thanatos Tower
				getexp 300000,300000;
				devil_tower_can_reenter = 0;
				devil_tower_enter = 0;
				mes "โปรดกลับมาใหม่พรุ่งนี้!";
				close;
		}
		if (isbegin_quest(7571) > 0) {// failed at stair 2 tnm1
				mes "[เชพ]";
				mes "โอ้ คุณกลับมาแล้ว";
				mes "ให้ฉันดูหน่อย... คุณหมายความว่าหอคอยนี้มีทางเดินเชื่อมต่อไปยังโลกปีศาจใช่ไหม?";
				next;
				mes "[เชพ]";
				mes "นี่คือทั้งหมดที่คุณมี";
				mes "คุณทำได้ดีแล้ว.";
				mes "ฉันรอคอยการค้นพบเพิ่มเติมเพิ่มเติม!";
				setquest 7577;// Space distortion
				erasequest 7568;// Explore the tower
				erasequest 7571;// Tower Expedition
				if (isbegin_quest(7572)) erasequest 7572;// Lucile...?
				if (isbegin_quest(7573)) erasequest 7573;// Magic Swordman Thanatos
				getexp 200000,200000;
				devil_tower_can_reenter = 0;
				devil_tower_enter = 0;
				mes "โปรดกลับมาใหม่พรุ่งนี้!";
				close;
		}
		switch( checkquest(7568,PLAYTIME) ) {// entrance timer - 1h30 to enter
		case -1:
				mes "[เชพ]";
				mes "คุณกำลังอาสาไปสำรวจหอคอยปีศาจหรือเปล่า ฉันรอคนแบบคุณอยู่นะ!";
				next;
				mes "[เชพ]";
				mes "ในที่สุดก็มีคนมาแบ่งปันความลึกลับของหอคอยนี้ให้ฉันแล้ว";
				mes "ไม่มีใครอาสาขึ้นมาจนถึงจุดนี้เลย ฉันจึงเริ่มเป็นกังวล";
				next;
				mes "[เชพ]";
				mes "กรุณารายงานสิ่งใดก็ตามที่คุณพบในหอคอยให้ฉันทราบ";
				mes "ยิ่งข้อมูลดีมาก~ ฉันจะจ่ายให้คุณก็ยิ่งดี!";
				next;
				mes "[เชพ]";
				mes "นักปราชญ์ Laphine ที่นั่นจะเปิดช่องว่างมิติด้วยอุปกรณ์เวลานี้";
				mes "มันยังไม่เสถียรนักดังนั้นเวลาที่คุณจะสำรวจจึงจะจำกัด";
				next;
				mes "[เชพ]";
				mes "หากคุณถูกส่งกลับมาที่นี่อีกครั้งโดยไม่คาดคิด คุณควรกลับมาโดยเร็วที่สุด";
				mes "หากทำเช่นนั้น คุณอาจจะกลับเข้ามาใหม่ได้หรือไม่ได้ ขึ้นอยู่กับว่าคุณเร็วแค่ไหน";
				next;
				mes "[เชพ]";
				mes "แต่ถ้าคุณหมดเวลาการเข้าถึงหอคอยก็จะหายไป";
				mes "คุณอยากจะเป็นอาสาสมัครไหม?";
				next;
				if (select( "Volunteer", "No Thanks" ) == 2) {
						mes "[เชพ]";
						mes "โอ้ นั่นมันแย่มาก";
						mes "ไม่มีอะไรที่ฉันทำได้ ฉันเดานะ";
						mes "ฉันจะหาคนอื่นแทน...";
						close;
				}
				mes "[เชพ]";
				mes "ขอบคุณ";
				mes "โอเค รวมกลุ่มของคุณแล้วอย่าลืมรายงานให้ฉันทราบว่าคุณพบอะไร";
				next;
				mes "[เชพ]";
				mes "^4d4dffระยะเวลาจำกัดในการสำรวจนั้นเพื่อความปลอดภัยของคุณ^000000!";
				setquest 7568;// Explore the tower
				close;
		case 0:
		case 1:
				if (devil_tower_can_reenter == 0)// player can re-enter
						break;
				// fall through
		case 2:
				mes "[เชพ]";
				mes "คุณดูเหมือนจะสายไป";
				mes "คุณใช้เวลานานมากอย่างไม่น่าเชื่อ";
				mes "-";
				mes "ฉันไม่สามารถใช้ข้อมูลของคุณเลย";
				next;
				mes "[เชพ]";
				mes "ไม่มีอะไรที่เราสามารถทำได้";
				mes "โอกาสสำหรับการเดินทางอื่นๆ ยังมีเสมอ";
				setquest 7577;// Space distortion
				erasequest 7568;
				if (isbegin_quest(7569)) erasequest 7569;// Treat the injured
				if (isbegin_quest(7570)) erasequest 7570;// Destroy the demons
				devil_tower_can_reenter = 0;
				devil_tower_enter = 0;
				close;
		}
		mes "[เชพ]";
		mes "อันดับแรกคุณจะต้องจองคิวที่ประตู";
		mes "นักวิชาการ Laphine Artie จะจองคิวให้กับคุณ";
		next;
		mes "[เชพ]";
		mes "เนื่องจากช่องว่างมิติยังไม่เสถียรจึงมีเวลาจำกัด";
		mes "ทำให้การสำรวจของคุณสั้นและน่าสนใจ";
		next;
		mes "[เชพ]";
		mes "รางวัลจะแตกต่างกันไปขึ้นอยู่กับเวลาของคุณ";
		mes "ขอให้โชคดี!";
		next;
		if (select( "Trust me!", "Cancel my reservation" ) == 1) {
				mes "[เชพ]";
				mes "ฉันเชื่อว่าคุณสามารถจัดการภายใต้ข้อจำกัดเหล่านี้ได้";
				mes "ฉันเชื่อในตัวคุณ!";
				close;
		}
		if (devil_tower_enter == 1) {
				mes "[เชพ]";
				mes "คุณดูเหมือนจะเหนื่อยกับการสำรวจ";
				mes "^4d4dffหากคุณยกเลิกตอนนี้ คุณจะสามารถท้าทายได้ในวันถัดไป^000000";
				mes "แบบนี้โอเคมั้ย?";
				next;
				if (select( "Yes Cancel.", "Wait, don't cancel!" ) == 2) {
						mes "[เชพ]";
						mes "โอเค ฉันจะไม่ยกเลิก";
						close;
				}
				mes "[เชพ]";
				mes "จริงหรือ.";
				mes "งั้นค่อยกลับมาพรุ่งนี้นะ.";
				setquest 7577;// Space distortion
				erasequest 7568;
				if (isbegin_quest(7569)) erasequest 7569;// Treat the injured
				if (isbegin_quest(7570)) erasequest 7570;// Destroy the demons
				devil_tower_enter = 0;
				devil_tower_can_reenter = 0;
				close;
		}
		mes "[เชพ]";
		mes "จริงหรือ";
		mes "หลังจากฟังคำอธิบายทั้งหมดนี้แล้ว?";
		erasequest 7568;// Explore the tower
		close;
}

dali02,137,121,3	script	Magic Scholar Artie#tnm	4_M_FAIRYKID6,{
		.@party_id = getcharid(1);
		if (.@party_id == 0) {
				mes "[อาร์ตี้]";
				mes "มาสนุกกันเถอะเนื่องจากเราเพิ่งออกจากบิฟรอสต์มา";
				mes "เลขที่?";
				mes "คุณกำลังสมัครเป็นอาสาสมัครใช่ไหม?";
				mes "คุณควรจะรวบรวมกลุ่มสำหรับการเดินทางครั้งนี้";
				next;
				mes "[อาร์ตี้]";
				mes "อย่าเสี่ยงอะไรที่ไม่จำเป็นนะ";
				close;
		}
		if (is_party_leader() == false) {
				mes "[อาร์ตี้]";
				mes "นำหัวหน้าคณะสำรวจมาด้วย";
				mes "Devil's Tower ไม่ใช่สถานที่ที่จะมาเที่ยวคนเดียว";
				mes "พาผู้จัดการมาด้วย";
				close;
		}
		if (devil_tower_enter == 1) {
				mes "[อาร์ตี้]";
				mes "คุณได้เข้ามาเชื่อมต่อเรียบร้อยแล้ว";
				mes "เชื่อมต่อกับพื้นที่อีกครั้ง? ฉันไม่สามารถทำแบบนั้นได้";
				close;
		}
		switch( checkquest(7568,PLAYTIME) ) {
		case 0:
		case 1:
				break;
		case -1:
		case 2:
				mes "[อาร์ตี้]";
				mes "ฮืม... จะเชื่อมต่อก็ยากครับ ยกเว้นจะถึงเวลากำหนดสำรวจ";
				mes "ไม่ต้องพูดถึงเรื่องอันตรายด้วย";
				close;
		}
		mes "[อาร์ตี้]";
		mes "คุณเป็นอาสาสมัครสำรวจหอคอยปีศาจใช่ไหม?";
		mes "คุณต้องการให้ฉันเปิดใช้งานอุปกรณ์มิติหรือไม่?";
		next;
		mes "[อาร์ตี้]";
		mes "ตัดสินใจให้เร็วเพราะคุณมีเวลาจำกัด";
		next;
		if (select( "Yes, activate the device.", "Wait for a minute." ) == 2) {
				mes "[อาร์ตี้]";
				mes "เวลาของคุณในการสำรวจกำลังจะหมดลง";
				mes "เวลาเป็นสิ่งสำคัญมาก";
				mes "คุณกำลังรออะไรอยู่?";
				close;
		}
		mes "[อาร์ตี้]";
		mes "อุปกรณ์มิติได้รับการเปิดใช้งานแล้ว";
		mes "หากคุณโชคดี คุณสามารถผ่านเรื่องนี้ไปได้อย่างรวดเร็ว";
		mes "ฉันรอคอยที่จะได้ยินข่าวดี";
		instance_create("Devil's Tower");
		close;
}

dali02,141,120,3	script	Dimensional Device#tnm0	PORTAL,{
		if (devil_tower_can_reenter == 1) {// can't re-enter
				mes "[อาร์ตี้]";
				mes "เฮ้ เฮ้";
				mes "การส่งให้คุณผ่านอุปกรณ์ในขณะนี้เป็นเรื่องยาก";
				mes "วันนี้เราควรหยุดสำรวจแล้วไปอีกครั้งในวันอื่น";
				close;
		}
		switch( checkquest(7568,PLAYTIME) ) {
		case 0:
		case 1:
				break;
		case -1:
		case 2:
				mes "[อาร์ตี้]";
				mes "ดูสิ ฉันขอโทษแต่ไม่อนุญาตให้เข้ายกเว้นในช่วงเวลาที่อนุญาต Devil's Tower เป็นอันตราย";
				close;
		}
		mes "[อาร์ตี้]";
		mes "ใช่ครับนี่คืออุปกรณ์มิติ";
		mes "ใช้เพื่อเดินทางข้ามเวลา";
		mes "ถ้าออกไปจะเข้าใหม่ยาก";
		next;
		mes "[อาร์ตี้]";
		mes "อย่ากังวลไปเลย ฉันเป็นอัจฉริยะนะ~";
		mes "อย่างไรก็ตาม พร้อมที่จะเข้าร่วมหรือยัง?";
		next;
		if (select( "Enter the device.", "Stop." ) == 2) {
				mes "[อาร์ตี้]";
				mes "ขวา.";
				mes "ดูเหมือนว่าจะผ่านมานานมากแล้วตั้งแต่คุณเปิดใช้งานอุปกรณ์ ฉันพูดถูกไหม?";
				close;
		}
		.@md_name$ = "Devil's Tower";
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
				mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
				close;
		case IE_NOINSTANCE:
				mes "[เชพ]";
				mes "ต้องเปิดใช้งานอุปกรณ์มิติก่อนจึงจะเข้าได้";
				mes "ดูเหมือนจะไม่ได้เปิดใช้งานเลย";
				next;
				mes "[เชพ]";
				mes "กรุณาขออนุญาตเปิดใช้งานโดยคุยกับ Magic Scholar Artie";
				close;
		case IE_NOMEMBER:
				mes "[อาร์ตี้]";
				mes "การสำรวจหอคอยปีศาจจะได้รับอนุญาตเฉพาะการเดินทางแบบกลุ่มเท่านั้น";
				mes "ตรวจสอบให้แน่ใจว่าคุณพร้อมแล้ว";
				close;
		case IE_OK:
				mapannounce "dali02", "Party member " + strcharinfo(0) + " of party's " + getpartyname( getcharid(1) ) + " enters Room " + .@md_name$ + "", bc_map,0xFF99;
				// warp "1@tnm1",50,104;
				devil_tower_enter = 1;
				end;
		}
}

dali02,134,112,3	script	#tnmtks01	HIDDEN_WARP_NPC,3,3,{
		end;
OnTouch_:
		disablenpc "#tnmtks01";
		initnpctimer;
		end;
OnTimer1000:
		npctalk "อาร์ตี้: เฮ้ คุณล่ะ งานวิจัยของคุณเป็นยังไงบ้าง?", "Magic Scholar Artie#tnm";
		end;
OnTimer3000:
		npctalk "เชพ: เธอเรียกใครว่าเฮ้ เธอมันนางฟ้าตัวน้อยที่ฝุ่นจับ!", "Historian Shep#tnm01";
		end;
OnTimer6000:
		npctalk "อาร์ตี้: อะไรวะเนี่ย? ฉันมียีนที่เหนือกว่าสายพันธุ์ของคุณนะไอ้ลิง!", "Magic Scholar Artie#tnm";
		end;
OnTimer9000:
		npctalk "เชพ: ฉันจะสนใจสิ่งที่คุณพูดไปทำไม แม้แต่ลูกๆ ของเรายังตัวใหญ่กว่านางฟ้าตัวน้อยๆ เหล่านั้นอีกนะ!", "Historian Shep#tnm01";
		end;
OnTimer12000:
		npctalk "เชพ: คุณคิดว่าคุณตัวใหญ่ขนาดนั้นเพียงเพราะคุณเป็นนักวิชาการด้านเวทมนตร์เหรอ?", "Historian Shep#tnm01";
		end;
OnTimer14000:
		npctalk "อาร์ตี้: คุณไม่รู้จักมนุษย์เลย ช่วยเตือนฉันอีกครั้งว่าใครเป็นคนพบทางเข้าหอคอยนี้", "Magic Scholar Artie#tnm";
		end;
OnTimer18000:
		npctalk "เชพ: *ถอนหายใจ*... ย...คุณทำแล้ว", "Historian Shep#tnm01";
		end;
OnTimer21000:
		npctalk "อาร์ตี้: ย....เธอเหรอ ฉันพูดติดขัดเหรอ แล้วใครเป็นคนแก้สูตรคำนวณช่องว่างมิติที่เชื่อมกับหอคอย", "Magic Scholar Artie#tnm";
		end;
OnTimer24000:
		npctalk "เชพ: ใช่ ใช่ เป็นคุณเอง!", "Historian Shep#tnm01";
		end;
OnTimer27000:
		npctalk "อาร์ตี้: ถ้าอย่างนั้นคุณก็คงไม่ต้องสงสัยอีกต่อไปแล้วว่าฉันฉลาดแค่ไหนใช่ไหม?", "Magic Scholar Artie#tnm";
		end;
OnTimer30000:
		npctalk "เชพ: โอเค โอเค ฉันต้องการให้ผู้ผจญภัยมาสำรวจหอคอยให้เรา", "Historian Shep#tnm01";
		end;
OnTimer33000:
		npctalk "เชพ: ไม่มีใครสมัครใจเพราะทุกคนบอกว่ามันอันตรายเกินไป!", "Historian Shep#tnm01";
		end;
OnTimer36000:
		npctalk "อาร์ตี้: ไม่มีอาสาสมัครเหรอ? รีบๆ หน่อย ใช้ทักษะสร้างแรงบันดาลใจของคุณแล้วหาใครสักคนมาช่วยสิ", "Magic Scholar Artie#tnm";
		end;
OnTimer37000:
		emotion ET_KIK, getnpcid(0, "Magic Scholar Artie#tnm");
		end;
OnTimer39000:
		npctalk "เชพ: สวัสดี!!! กำลังรับสมัครสมาชิกใหม่เพื่อไปสำรวจหอคอยปีศาจ!", "Historian Shep#tnm01";
		end;
OnTimer42000:
		npctalk "เชพ: เชิญมาเลย! ตอนนี้เป็นโอกาสของคุณที่จะได้เผชิญหน้ากับเหล่าลูกสมุนอันน่าอับอายของซาตานโมร็อคเอง!", "Historian Shep#tnm01";
		end;
OnTimer45000:
		npctalk "อาร์ตี้: คุณแน่ใจนะว่านั่นคือสิ่งที่ถูกต้องที่จะพูด? บางทีกลยุทธ์ในการสรรหาบุคลากรของคุณอาจต้องได้รับการปรับปรุง...", "Magic Scholar Artie#tnm";
		stopnpctimer;
		enablenpc "#tnmtks01";
		end;
}

// Entrance
1@tnm1,50,104,0	script	#event01	HIDDEN_WARP_NPC,1,1,{
		end;
OnTouch:
		disablenpc instance_npcname("#event01");
		initnpctimer;
		end;
OnTimer2000:
		npctalk "นายตำรวจไฮม์ : นี่เป็นเหตุฉุกเฉิน.. เป็นไงบ้าง?", instance_npcname("Officer Heim#heim0");
		end;
OnTimer5000:
		npctalk "คำเตือน: ชั้นถัดไปเป็นชั้นที่สูงที่สุด... แต่ปีศาจเยอะมาก...", instance_npcname("A tirailleur#tnm01");
		end;
OnTimer6000:
		emotion ET_THINK, getnpcid(0, instance_npcname("Officer Heim#heim0"));
		end;
OnTimer8000:
		npctalk "A tirailleur: มันเป็นไปไม่ได้ที่จะฝ่าทะลุไปด้วยจำนวนคนของเรา", instance_npcname("A tirailleur#tnm01");
		emotion ET_PROFUSELY_SWEAT, getnpcid(0, instance_npcname("A tirailleur#tnm01"));
		end;
OnTimer11000:
		npctalk "นายตำรวจไฮม์: ตอนนี้มีทหารรอดชีวิตกี่นายแล้ว ทีมสนับสนุนจะมาถึงเมื่อไหร่", instance_npcname("Officer Heim#heim0");
		end;
OnTimer12000:
		emotion ET_THINK, getnpcid(0, instance_npcname("An aide#tnm02"));
		end;
OnTimer16000:
		npctalk "ผู้ช่วย : โทรไปก็ไม่มากพอ...", instance_npcname("An aide#tnm02");
		end;
OnTimer17000:
		npctalk "ผู้ช่วย: ข่าวดีคือพวกปีศาจหยุดโจมตีแล้ว", instance_npcname("An aide#tnm02");
		end;
OnTimer21000:
		npctalk "นายตำรวจไฮม์: เราไม่สามารถติดอยู่ที่นี่ต่อไปได้ และสามารถไปต่อได้ ทำไมปีศาจถึงหยุดโจมตี?", instance_npcname("Officer Heim#heim0");
		end;
OnTimer24000:
		npctalk "ช่างซ่อมรถ: ดูเหมือนว่าจะมีอะไรบางอย่างกำลังเกิดขึ้นอยู่บนนั้น...", instance_npcname("A tirailleur#tnm01");
		end;
OnTimer26000:
		disablenpc instance_npcname("Officer Heim#heim0");
		enablenpc instance_npcname("Officer Heim#heim");
		npctalk "นายตำรวจไฮม์ : คุณเป็นใคร?", instance_npcname("Officer Heim#heim");
		stopnpctimer;
		end;
}

1@tnm1,57,112,5	script	Officer Heim#heim0	4_TOWER_05,{ end; }

1@tnm1,56,110,3	script	Officer Heim#heim	4_TOWER_05,{
		if (is_party_leader() == false) {
				mes "[นายตำรวจไฮม์]";
				mes "ใครพาพวกคุณมาที่นี่?";
				mes "ฉันจะพูดคุยกับตัวแทน";
				close;
		}
		switch('devil_tower) {
		case 0:
				mes "[นายตำรวจไฮม์]";
				mes "ถอยทัพเหรอ? แล้วทำไมคุณถึงยืนอยู่เฉยๆ ล่ะ?";
				npctalk "นายตำรวจไฮม์: ถอยทัพมาเหรอ แล้วทำไมคุณถึงยืนอยู่เฉยๆ ล่ะ";
				next;
				select("Right now?");
				if (is_party_leader() == true && 'devil_tower == 0) {
						enablenpc instance_npcname("Healer Fama#feima");
						'devil_tower = 1;
				}
				mes "[ฮีลเลอร์ ฟาม่า]";
				mes "ท่านผู้บัญชาการ ช่วยฉันด้วย ฉันทำเองไม่ได้~!";
				npctalk "ฮีลเลอร์ ฟามา: ท่านผู้บัญชาการ โปรดช่วยฉันด้วย ฉันทำเองไม่ได้~!", instance_npcname("Healer Fama#feima");
				next;
				mes "[นายตำรวจไฮม์]";
				mes "คุณได้ยินสิ่งนั้นไหม?";
				mes "ช่วยฟามาดูแลทหารที่ได้รับบาดเจ็บ";
				mes "ฟามาจะมอบชุดปฐมพยาบาลให้คุณใช้ช่วยเหลือผู้ได้รับบาดเจ็บ";
				close2;
				setpcblock PCBLOCK_NPC, true;
				npctalk "นายตำรวจไฮม์ : คุณได้ยินไหม?";
				sleep2 2000;
				npctalk "นายตำรวจไฮม์ : ช่วยฟามาดูแลทหารที่ได้รับบาดเจ็บ";
				sleep2 2000;
				npctalk "ฮีลเลอร์ ฟามา: โอเค~ คุณจะช่วยฉันเหรอ? มาที่นี่และรับกล่องปฐมพยาบาลมาสิ~!";
				setpcblock PCBLOCK_NPC, false;
				end;
		case 1:
				mes "[นายตำรวจไฮม์]";
				mes "โดยจะให้ความสำคัญต่อการรักษาทหารที่ได้รับบาดเจ็บโดยการช่วยเหลือฟามา";
				mes "โปรดทำดีที่สุด แม้ว่าสภาพแวดล้อมจะเลวร้ายที่สุดก็ตาม";
				close;
		default:
				mes "[นายตำรวจไฮม์]";
				mes "โชคดีที่ยังไม่มีใครเสียชีวิต";
				mes "ฉันไม่อยากเห็นทหารของฉันตาย";
				close;
		}
}

1@tnm1,47,109,3	script	Healer Fama#feima	4_TOWER_10,{
		if ('devil_tower == 1) {
				if (countitem(7641) < 1) {
						mes "[ฮีลเลอร์ ฟาม่า]";
						mes "โปรดรักษาผู้ได้รับบาดเจ็บด้วยชุดปฐมพยาบาลนี้";
						mes "ไม่ยากครับ เพียงทำตามคำแนะนำครับ";
						next;
						mes "[ฮีลเลอร์ ฟาม่า]";
						mes "เรามีเสบียงไม่เพียงพอ ดังนั้นฉันจะจัดหาให้ทีละครั้ง";
						mes "ขออภัยกรุณาขอใหม่อีกครั้งถ้าหมด";
						if ('devil_tower == 1)
								getitem 7641,1;// Medical_Cure_Box
						close;
				}
				mes "[ฮีลเลอร์ ฟาม่า]";
				mes "ขออภัย...ขณะนี้การจัดซื้อจัดหาไม่ดีนัก ดังนั้น ฉันสามารถจัดหาชุดปฐมพยาบาลได้ครั้งละหนึ่งชุดเท่านั้น";
				mes "กรุณาขอใหม่อีกครั้งหากหมด";
				close;
		}
		mes "[ฮีลเลอร์ ฟาม่า]";
		mes "อืม...โอเค พักกันได้แล้ว";
		mes "ขอบคุณมาก!";
		close;
}

1@tnm1,39,114,3	script	Injured Soldier#1_1	4_TOWER_01,3,3,{
		if ('devil_tower != 1)
				end;
		if (countitem(7641) < 1) {
				mes "ฉันต้องการชุดปฐมพยาบาล 1 ชุดเพื่อรักษาทหารที่ได้รับบาดเจ็บ";
				close;
		}
		mes "ทหารรายนี้หมดสติและครวญคราง";
		mes "ฉันควรใช้ชุดปฐมพยาบาลหรือไม่?";
		next;
		if (select( "Use first-aid kit.", "Observe condition." ) == 2) {
				mes "คุณตัดสินใจสังเกตอาการของทหารที่ได้รับบาดเจ็บมากขึ้น";
				close;
		}
		mes "คุณแจกชุดปฐมพยาบาลให้ทหาร";
		next;
		specialeffect EF_SPHERE;
		progressbar "000000",10;
		if ('devil_tower == 1) {
				delitem 7641,1;
				specialeffect EF_ENTRY;
				disablenpc();
				enablenpc instance_npcname( strnpcinfo(1) + "#" + replacestr( strnpcinfo(2), "_1", "" ) );
				specialeffect EF_ENTRY,AREA, instance_npcname( strnpcinfo(1) + "#" + replacestr( strnpcinfo(2), "_1", "" ) );
				mes "มันได้ผลนะ";
				'heal_count++;
				if ('heal_count == 7) {
						'devil_tower = 2;
						npctalk "ฮีลเลอร์ ฟามา: นักฆ่า?!", instance_npcname("Healer Fama#feima");
						enablenpc instance_npcname("Lucile#tnm01");
						enablenpc instance_npcname("Magic Swordman#tnm01");
						enablenpc instance_npcname("Assassin Huey#tnm01");
						enablenpc instance_npcname("Assassin Louie#tnm01");
				}
		}
		close;

OnTouch:
		.@num = atoi( replacestr( strnpcinfo(2), "_1", "" ) );
		if (.@num == 1)
				npctalk "ทหารบาดเจ็บ: ครางครวญคราง....";
		else if (.@num == 2)
				npctalk "ทหารที่ได้รับบาดเจ็บ: โอย...";
		else if (.@num == 3)
				npctalk "ทหารที่ได้รับบาดเจ็บ: .... อ๊า.. อ๊า..";
		else if (.@num == 4)
				npctalk "ทหารที่ได้รับบาดเจ็บ: โอย...";
		else if (.@num == 5)
				npctalk "ทหารบาดเจ็บ: ผมไม่อยากตาย...";
		else if (.@num == 6)
				npctalk "ทหารที่ได้รับบาดเจ็บ: โอ้ย~";
		else
				npctalk "ทหารบาดเจ็บ: ครางครวญ...";
		end;
}

1@tnm1,30,120,3	duplicate(Injured Soldier#1_1)	Injured Soldier#2_1	4_TOWER_03,3,3
1@tnm1,25,117,5	duplicate(Injured Soldier#1_1)	Injured Soldier#3_1	4_TOWER_06,3,3
1@tnm1,19,118,4	duplicate(Injured Soldier#1_1)	Injured Soldier#4_1	4_TOWER_08,3,3
1@tnm1,18,110,4	duplicate(Injured Soldier#1_1)	Injured Soldier#5_1	4_TOWER_11,3,3
1@tnm1,24,104,7	duplicate(Injured Soldier#1_1)	Injured Soldier#6_1	4_TOWER_13,3,3
1@tnm1,31,110,3	duplicate(Injured Soldier#1_1)	Injured Soldier#7_1	4_TOWER_03,3,3

1@tnm1,39,114,3	script	Injured Soldier#1	4_TOWER_02,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "โอ้โห.. ดูเหมือนตอนนี้จะโอเคแล้ว";
		mes "ขอบคุณ";
		close;
}

1@tnm1,30,120,3	script	Injured Soldier#2	4_TOWER_04,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "คร่ำครวญ...ปล่อยให้ฉันตายซะเถอะ";
		mes "ไม่ มันเจ็บมากเกินไป";
		close;
}

1@tnm1,25,117,5	script	Injured Soldier#3	4_TOWER_07,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "จิตสำนึกของเขายังดูเหมือนไม่ชัดเจน";
		mes "ปล่อยให้เขาได้พักผ่อนบ้าง";
		close;
}

1@tnm1,19,118,4	script	Injured Soldier#4	4_TOWER_09,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "ฉันคิดว่าฉันจะตาย.";
		mes "แต่ฉันยังมีชีวิตอยู่!";
		close;
}

1@tnm1,18,110,4	script	Injured Soldier#5	4_TOWER_12,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "ฉันตายที่นี่ไม่ได้ ฉันต้องขอแฟนแต่งงานเมื่อกลับถึงบ้าน";
		mes "เราคงจะดีใจแน่ใช่ไหม?";
		close;
}

1@tnm1,24,104,7	script	Injured Soldier#6	4_TOWER_14,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "ฉันรู้สึกดีขึ้นมากแล้วแต่ฉันยังขยับไม่ได้";
		mes "ฉันยังโชคดีที่ยังมีชีวิตอยู่";
		close;
}

1@tnm1,31,110,3	script	Injured Soldier#7	4_TOWER_04,{
		mes "[ทหารที่ได้รับบาดเจ็บ]";
		mes "ขอบคุณค่ะ..ขอบคุณค่ะ..";
		close;
}

1@tnm1,61,110,3	script	An aide#tnm02	4_M_MOCASS2,{
		mes "[ผู้ช่วย]";
		mes "ภัยพิบัติที่เกิดขึ้นเมื่อหลายร้อยปีก่อนคงจะไม่เกิดขึ้นอีกใช่ไหม?";
		mes "เหตุใดปีศาจจึงโผล่มาอีกแบบกะทันหันเช่นนี้?";
		close;
}

1@tnm1,61,113,3	script	A tirailleur#tnm01	4_M_MOCASS1,{
		mes "[ช่างตีเหล็ก]";
		mes "คุณเป็นสมาชิกทีมสำรองหรือเปล่า?";
		mes "ดี. สิ่งต่างๆ ที่นี่กำลังแย่ลง";
		mes "ดีใจที่ทุกท่านมาช่วยกัน";
		close;
}

1@tnm1,46,105,5	script	Lucile#tnm01	4_F_LUCILE,{
		if (is_party_leader() == false) {
				cutin "tnm_lucile01.bmp",2;
				mes "[ลูซิล]";
				mes "ใครเป็นผู้รับผิดชอบทีมสำรอง?";
				close3;
		}
		if ('devil_tower == 2) {
				.@lucile$ = instance_npcname("Lucile#tnm01");
				.@heim$ = instance_npcname("Officer Heim#heim");
				'devil_tower = 3;
				setpcblock PCBLOCK_NPC, true;
				cutin "tnm_lucile01.bmp",2;
				sleep2 1500;
				npctalk "ลูซิล: ฉันคือลูซิลจากกิลด์นักฆ่า", .@lucile$;
				sleep2 2000;
				npctalk "นายตำรวจไฮม์: กองกำลังสำรองของเรามาจากสมาคมนักฆ่าใช่ไหม ขอบคุณที่มาทันเวลา", .@heim$;
				sleep2 3000;
				npctalk "ลูซิล: สถานการณ์ดูจะไม่ดีเลย", .@lucile$;
				sleep2 3000;
				npctalk "ลูซิล: เกิดอะไรขึ้นกับอัศวินผู้กล้าหาญแห่งพรอนเทรา?", .@lucile$;
				sleep2 3000;
				npctalk "นายตำรวจไฮม์ : คุณกำลังล้อเลียนฉันอยู่เหรอ?", .@heim$;
				sleep2 3000;
				npctalk "ลูซิล: ไม่เลย ฉันอยากรู้จริงๆ ว่าเกิดอะไรขึ้นชั้นบน?", .@lucile$;
				sleep2 2500;
				emotion ET_OHNO, getnpcid(0, .@heim$);
				sleep2 1000;
				npctalk "นายตำรวจไฮม์: คุณรู้ได้ยังไง? ใช่แล้ว ปีศาจหยุดการโจมตีจากชั้นบนแล้ว", .@heim$;
				sleep2 3000;
				emotion ET_SMILE, getnpcid(0, .@lucile$);
				npctalk "ลูซิล: คุณคิดอย่างไรกับสถานการณ์นี้นะ เมจิกซอร์ดแมน?", .@lucile$;
				cutin "tnm_lucile01.bmp",0;
				sleep2 3000;
				npctalk "นักดาบวิเศษ: ฮึม ฉันจะไปสืบสวน", instance_npcname("Magic Swordman#tnm01");
				cutin "tnm_loki.bmp",2;
				sleep2 2000;
				cutin "",255;
				disablenpc instance_npcname("Magic Swordman#tnm01");
				sleep2 1000;
				npctalk "ลูซิล: คุณเร็วมากเลยเหรอ?", .@lucile$;
				cutin "tnm_lucile01.bmp",2;
				sleep2 2000;
				npctalk "นายตำรวจไฮม์ : ทำอะไรอยู่ครับ ถ้าไปคนเดียวจะโดนฆ่านะครับ!", .@heim$;
				sleep2 3000;
				npctalk "ลูซิล: ฉันจะไม่อยู่คนเดียว ยังมีทหารที่ไม่ได้รับบาดเจ็บอยู่บ้าง!", .@lucile$;
				sleep2 2500;
				npctalk "ลูซิล: ตกลง ฉันจะขอให้คุณเป็นตัวสำรองของฉัน ฮ่าๆ ใช่มั้ย ดิวอี้?", .@lucile$;
				sleep2 500;
				enablenpc instance_npcname("Assassin Dewey#tnm01");
				enablenpc instance_npcname("#tnm1stepmob");
				npctalk "ดิวอี้: โอเค ฉันจะทำ", instance_npcname("Assassin Dewey#tnm01");
				'devil_tower = 4;
				sleep2 500;
				cutin "",255;
				disablenpc .@lucile$;
				disablenpc instance_npcname("Assassin Huey#tnm01");
				disablenpc instance_npcname("Assassin Louie#tnm01");
				sleep2 2000;
				npctalk "เจ้าหน้าที่ไฮม์: พวกมันคาดเดาไม่ได้เลย คุณรู้ไหมว่าเกิดอะไรขึ้น? ช่วยเหลือพวกมันหน่อย...", .@heim$;
				sleep2 2500;
				npctalk "นายตำรวจไฮม์ : ช่วยเราจัดการกับปีศาจที่ลงมาจากชั้นบนหน่อย", .@heim$;
				setpcblock PCBLOCK_NPC, false;
				end;
		}
}

1@tnm1,52,104,2	duplicate(Officer Heim#heim0)	Magic Swordman#tnm01	4_M_ROKI
1@tnm1,42,104,4	duplicate(Officer Heim#heim0)	Assassin Huey#tnm01	4_TOWER_15
1@tnm1,44,101,5	duplicate(Officer Heim#heim0)	Assassin Louie#tnm01	4_M_ACROSS

1@tnm1,70,108,3	script	Assassin Dewey#tnm01	4_M_ACROSS,{
		if ('devil_tower < 4)
				end;
		if (isbegin_quest(7569) == 0)
				setquest 7569;// Treat the injured
		if (isbegin_quest(7570) == 0)
				setquest 7570;// Destroy the demons
		mes "[ดิวอี้]";
		mes "ฉันจะพาคุณไปที่ชั้นบน";
		mes "อย่าลืมกำจัดปีศาจใดๆ ที่คุณพบระหว่างทาง";
		next;
		if (select( "Move now", "I need more time to prepare." ) == 2) {
				mes "[ดิวอี้]";
				mes "เตรียมพร้อมรับมือกับทุกสิ่ง";
				close;
		}
		mes "[ดิวอี้]";
		mes "เดินทางอย่างระมัดระวัง.";
		close2;
		warp 'map_tnm1$,91,23;
		devil_tower_can_reenter = 1;// nb. different from official (condition overcomplicated)
		end;
}

// Stairs tnm1
// spawn inaccurate
1@tnm1,91,23,0	script	#tnm1stepmob	HIDDEN_WARP_NPC,1,1,{
		end;
OnTouch:
		if ('devil_tower != 4)
				end;
		disablenpc instance_npcname("#tnm1stepmob");
		enablenpc instance_npcname("Assassin Louie#tnm02");
		enablenpc instance_npcname("Lucile#tnm02");
		initnpctimer;
		monster 'map_tnm1$,99,24,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		'mob_id = $@mobid[0];
		end;
OnTimer2000:
		if (unitexists('mob_id) == true)
				unittalk 'mob_id, "What is this? A human? How dare they must have lost their minds!";
		end;
OnTimer5000:
		monster 'map_tnm1$,103,21,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		unittalk $@mobid[0], "Did you make a resolution?";
		monster 'map_tnm1$,103,26,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		unittalk $@mobid[0], "Ha ha ha ha....Let's fight..!!!";
		end;
OnTimer11000:
		monster 'map_tnm1$,106,25,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,106,27,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,110,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,112,24,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,107,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,114,24,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		end;
OnTimer17000:
		monster 'map_tnm1$,120,20,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,122,23,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		end;
OnTimer23000:
		monster 'map_tnm1$,116,25,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,116,21,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		end;
OnTimer29000:
		monster 'map_tnm1$,134,21,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,130,25,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,129,27,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,120,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,118,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		end;
OnTimer35000:
		monster 'map_tnm1$,138,27,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,144,23,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,129,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		end;
OnTimer41000:
		monster 'map_tnm1$,140,24,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,144,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,130,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,127,24,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,142,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,140,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
		stopnpctimer;
		end;
OnMobDead:
		end;
}

1@tnm1,158,24,3	script	Assassin Louie#tnm02	4_M_ACROSS,{
		if ('devil_tower < 4)
				end;
		if (checkquest(7570,HUNTING) != 2) {
				mes "[หลุยส์]";
				mes "กัปตันบอกว่าเราต้องกำจัดปีศาจทั้งหมดออกไป...";
				mes "รีบไปทำซะดีกว่า";
				close;
		}
		mes "[หลุยส์]";
		mes "ทหารจำนวนมากยังคงรอดชีวิตจากการโจมตี";
		mes "พวกเขาได้กำจัดปีศาจทั้งหมดออกไป";
		next;
		mes "[หลุยส์]";
		mes "ฉันกำลังแน่ใจว่าไม่มีใครขึ้นไปชั้นบน ฉันกลัวว่าอาจจะเกิดเรื่องอีก";
		next;
		mes "[หลุยส์]";
		mes "ฉันหวังว่าคุณจะช่วยฉัน";
		mes "ฉันจะนำทางคุณ.";
		if (isbegin_quest(7571) == 0) {
				erasequest 7569;// Treat the injured
				erasequest 7570;// Destroy the demons
				setquest 7571;// Tower Expedition
		}
		next;
		mes "ฉันได้สำรวจทางเดินของหอคอยนี้มาสักพักแล้ว";
		close2;
		warp 'map_tnm1$,124,86;
		end;
}

// Stairs 2 tnm1
1@tnm1,138,96,3	script	Lucile#tnm02	4_F_LUCILE,{
		if (isbegin_quest(7571) == 0)
				end;
		if (is_party_leader() == false) {
				mes "คุณโบกมือต่อหน้าเธอแต่เธอไม่ตอบสนอง";
				close;
		}
		if ('devil_tower < 4)
				end;
		if ('devil_tower < 6) {
				if ('devil_tower == 4) {
						mes "[ลูซิล]";
						mes "ฉันขอโทษถ้าฉันทำให้คุณมีทัศนคติที่ไม่ดี แต่ฉันไม่สามารถขยับตัวได้";
						npctalk "ลูซิล: ฉันขอโทษถ้าฉันทำให้คุณไม่พอใจ แต่ฉันขยับตัวไม่ได้เลย";
						cutin "tnm_lucile02.bmp",2;
						next;
						select("Can I help?");
						mes "[ลูซิล]";
						mes "ฉันสามารถฟื้นฟูตัวเองได้แต่ฉันไม่มีเวลาสำหรับสิ่งนั้น";
						mes "คุณจะแบ่งปันพลังของคุณกับฉันไหม?";
						npctalk "ลูซิล: ฉันสามารถใช้การฟื้นฟูตัวเองได้ แต่ฉันไม่มีเวลาสำหรับสิ่งนั้น คุณจะแบ่งปันพลังของคุณกับฉันไหม";
						next;
						mes "[ลูซิล]";
						mes "ฉันต้องการความช่วยเหลือจากคุณในการเปิดใช้งานระบบการฟื้นฟูของฉัน";
						mes "กรุณาจับให้แน่นๆ";
						npctalk "ลูซิล: ฉันต้องการความช่วยเหลือจากคุณในการเปิดใช้งานระบบฟื้นฟูของฉัน";
						next;
						sleep2 750;
						npctalk "ลูซิล: โปรดจับให้แน่นนะ";
						if (is_party_leader() == true && 'devil_tower == 4) {
								donpcevent instance_npcname("Huey#hui02") + "::OnEnable";
								'devil_tower = 5;
						}
						percentheal -30,0;
				}
				cutin "tnm_lucile03.bmp",2;
				mes "ลูซิลดูเหมือนเป็นเครื่องจักร";
				mes "ฉันได้ยินเสียงเครื่องจักรกะทันหัน";
				specialeffect EF_LEVEL99_3;
				progressbar "000000",40;
				if (is_party_leader() == true && 'devil_tower == 5) {
						if (isbegin_quest(7572) == 0)
								setquest 7572;// Lucile...?
						'devil_tower = 6;
				}
				mes "ระบบได้รับการคืนค่าแล้ว";
				next;
				mes "[ลูซิล]";
				mes "ว้าว! คุณแก้ไขได้เร็วกว่าที่คิด";
				mes "มันทำให้คุณแปลกใจมั้ย?";
				mes "ฉันต้องได้รับการแก้ไขเป็นครั้งคราวเพราะฉันไม่มีร่างกายปกติ";
				cutin "tnm_lucile02.bmp",2;
				next;
				mes "[ลูซิล]";
				mes "ร่างกายนี้เป็นของขวัญจากเทพเจ้า";
				mes "คุณเห็นประตูตรงกลางมั้ย?";
				cutin "tnm_lucile01.bmp",2;
				next;
				mes "[ลูซิล]";
				mes "รีบหน่อยไม่งั้นโลกนี้จะตกอยู่ในอันตราย";
				close2;
				cutin "",255;
				// continue
		}
		if ('devil_tower == 6) {
				sleep 1500;
				npctalk "ลูซิล: ฉันขอตัวก่อนนะ ถ้าเธออยากจะช่วยโลกก็ติดตามฉันไปสิ";
				sleep 2000;
				npctalk "ฮิวอี้ : พี่สาว...", instance_npcname("Huey#hui02");
				if ('devil_tower == 6) {
						disablenpc instance_npcname("Lucile#tnm02");
						enablenpc instance_npcname("Lucile#tnm03");
						enablenpc instance_npcname("Magic Swordman#tnm02");
						enablenpc instance_npcname("Demon Morocc#tnm01");
						'devil_tower = 7;
				}
		}
		end;
}

1@tnm1,135,99,5	script	Huey#hui02	4_TOWER_15,{
		if ('devil_tower < 5)
				end;
		if ('devil_tower == 5 || 'devil_tower == 6) {
				mes "[ฮิวอี้]";
				mes "ฉันเป็นห่วงเธอ.";
				mes "มันเกิดขึ้นทุกครั้งที่เธอได้รับบาดเจ็บ";
				mes "เพราะเธอเป็นเครื่องจักรซะส่วนใหญ่...";
				close;
		}
		if ('mob_count > 0) {
				mes "[ฮิวอี้]";
				mes "ฉันรู้ทางขึ้นไปชั้นบนแล้ว...";
				mes "ก่อนอื่นเราจะต้องกำจัดปีศาจพวกนี้ให้ได้!";
				close;
		}
		mes "[ฮิวอี้]";
		mes "สงสัยว่าคนอื่นเป็นยังไงบ้างนะ...";
		mes "มีเพียงธานาทอสและลูซิลเท่านั้นที่สามารถขึ้นไปชั้นบนได้...";
		next;
		mes "[ฮิวอี้]";
		mes "หากคุณต้องการช่วยพวกเขาคุณต้องรีบหน่อย";
		mes "ฉันจะนำทางให้กับคุณ";
		next;
		if (select( "Move immediately.", "Do not follow." ) == 2) {
				mes "[ฮิวอี้]";
				mes "...ฉันเห็น.";
				close;
		}
		mes "[ฮิวอี้]";
		mes "ทางนี้ครับ.";
		mes "ระวัง.";
		close2;
		warp 'map_tnm2$,152,130;
		end;

OnEnable:
		enablenpc instance_npcname("Huey#hui02");
		initnpctimer;
		end;
OnTimer3000:
		npctalk "ฮิวอี้: โอ้ ไม่นะ พี่สาว ระวังหน่อยสิ จู่ๆ ปีศาจก็ปรากฏตัวขึ้น!";
		'mob_count = 16;
		monster 'map_tnm1$,142,90,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,139,91,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,130,86,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,143,95,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW3
		monster 'map_tnm1$,134,90,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		end;
OnTimer6000:
		npctalk "ฮิวอี้ : พวกมันมาจากไหนวะ ปีศาจอีกแล้ว......";
		end;
OnTimer8000:
		monster 'map_tnm1$,137,103,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,140,102,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,132,103,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW2
		monster 'map_tnm1$,146,100,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,142,105,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,135,107,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW3
		end;
OnTimer11000:
		npctalk "ฮิวอี้: โอ้ ไม่นะ...";
		end;
OnTimer12000:
		monster 'map_tnm1$,135,107,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
		monster 'map_tnm1$,146,100,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,130,86,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";		// MM_EVIL_SHADOW2
		monster 'map_tnm1$,137,103,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
		monster 'map_tnm1$,143,95,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";		// MM_EVIL_SHADOW3
		stopnpctimer;
		end;
OnMobDead:
		'mob_count--;
		if ('mob_count == 0)
				npctalk "ฮิวอี้: ทางนี้ ทางนี้... เราจะขึ้นไปชั้นบนได้โดยใช้ช่องว่างที่พวกปีศาจเดินออกมา!";
		end;
}

// Morocc - Thanatos Battle
1@tnm2,136,139,3	script	Magic Swordman#tnm02	THANATOS_KEEP,{
		mes "[ดาบมนตร์ธานาทอส]";
		mes "ฉันขอโทษนะ แต่ฉันได้ผ่านช่วงเวลาที่ยากลำบากมา...";
		close;
}

1@tnm2,133,139,5	duplicate(Officer Heim#heim0)	Demon Morocc#tnm01	1916

1@tnm2,144,137,3	script	Lucile#tnm03	4_F_LUCILE,{
		if (is_party_leader() == false) {
				mes "[ลูซิล]";
				mes "ฉันกำลังคุยกับหัวหน้าพรรคของคุณอยู่ ดังนั้นกรุณาถอยห่างออกมาสักพัก";
				cutin "tnm_lucile01.bmp",2;
				close3;
		}
		if ('devil_tower == 7) {
				mes "[ลูซิล]";
				mes "โอ้ คุณมาถึงทันเวลาพอดี";
				mes "คุณอยากไปดูตรงนั้นมั้ย?";
				cutin "tnm_lucile01.bmp",2;
				next;
				mes "[ลูซิล]";
				mes "เราต้องหยุดปีศาจ...";
				mes "คุณทำแบบนั้นได้ไหม?";
				npctalk "ลูซิล: เราต้องหยุดปีศาจนั่น... คุณทำได้ไหม?";
				if (is_party_leader() == true && 'devil_tower == 7) {
						if (isbegin_quest(7573) == 0)
								setquest 7573;// Magic Swordman Thanatos
						'demon$ = instance_npcname("Demon Morocc#tnm01");
						'thanatos$ = instance_npcname("Magic Swordman#tnm02");
						initnpctimer;
						'devil_tower = 8;
				}
				close3;
		}
		mes "[ลูซิล]";
		mes "เราต้องยึดจุดนี้ไว้จนกว่านักดาบวิเศษจะวางกับดักไว้";
		cutin "tnm_lucile01.bmp",2;
		close3;

OnTimer2000:
		npctalk "ปีศาจโมร็อค: เจ้ามนุษย์โง่เขลา เจ้าคิดว่าจะมัดข้าไว้ได้นานแค่ไหนกัน!", 'demon$;
		end;
OnTimer5000:
		npctalk "ปีศาจโมร็อค: เงา! ออกมาและสังหารศัตรูแห่งความมืดทั้งหมด!", 'demon$;
		end;
OnTimer6000:
		donpcevent instance_npcname("#devil_seal_timer") + "::OnStart";
		end;
OnTimer8000:
		npctalk "ธานาทอส: คุณสิ้นหวังมากขนาดที่คุณต้องขอร้องให้ฉันช่วยคุณด้วย", 'thanatos$;
		end;
OnTimer11500:
		npctalk "ลูซิล: ธานาทอส ท่านควรจะพูดจาโอ้อวดอย่างนั้นหรือ?";
		end;
OnTimer12000:
		npctalk "อสูรโมร็อค: จงกลัวการทำลายล้างที่ข้าจะนำมาให้เมื่อข้าถูกปลดปล่อย!", 'demon$;
		end;
OnTimer15000:
		npctalk "ธานาทอส: หุบปากไปซะ! เจ้าจะออกไปจากที่นี่ไม่ได้ เว้นแต่ข้าจะฆ่าเจ้า!", 'thanatos$;
		end;
OnTimer16500:
		npctalk "ธานาทอส: ลูซิล! ระวังหลังข้าไว้ขณะที่ข้าเสกโล่ขึ้นมาเพื่อประคองเขาไว้!", 'thanatos$;
		end;
OnTimer19500:
		npctalk "ลูซิล: ครับท่าน.";
		end;
OnTimer22500:
		npctalk "ลูซิล: โอเคทุกคน ตอนนี้เรารู้แล้วว่าต้องทำอย่างไร เราต้องป้องกันไม่ให้ผนึกเวทมนตร์มาถึงที่นี่!";
		stopnpctimer;
		end;
}

1@tnm2,1,1,0	script	#devil_flamecross	HIDDEN_WARP_NPC,{
		end;
OnStart:
		npctalk "อสูรโมร็อค: ไม่มีทาง!! พวกก่อปัญหา!", 'demon$;
		mapannounce 'map_tnm2$, "Demon Morocc: No way!! Troublemakers!", bc_map,0xFF0000;
		initnpctimer;
		end;
OnTimer1000:
		setarray .@north[0], 138,151, 138,160, 140,165, 140,176, 140,170, 140,181, 140,186, 140,191;
		setarray .@north_west[0], 126,149, 122,151, 118,153, 113,154, 107,156, 101,158, 96,160, 91,161, 86,162, 81,163;
		setarray .@south_west[0], 128,130, 125,127, 122,124, 118,121, 115,118, 112,115, 109,114, 106,106, 102,102, 99,98;
		setarray .@south_east[0], 145,130, 148,126, 151,122, 156,120, 160,117, 164,113, 168,109, 172,105, 176,101, 180,97;
		setarray .@north_east[0], 146,149, 148,149, 150,151, 155,152, 162,153, 167,154, 173,156, 179,158, 185,160, 191,161;
		.@label$ = instance_npcname("#devil_flamecross") + "::OnFlameDead";
		for ( .@i = 0; .@i < 20; .@i += 2 ) {
				if (.@north[.@i] > 0)	// 8 mobs for north
						monster 'map_tnm2$,.@north[.@i],.@north[.@i+1], "",2960,1, .@label$;
				monster 'map_tnm2$,.@north_west[.@i],.@north_west[.@i+1], "",2960,1, .@label$;
				monster 'map_tnm2$,.@south_west[.@i],.@south_west[.@i+1], "",2960,1, .@label$;
				monster 'map_tnm2$,.@south_east[.@i],.@south_east[.@i+1], "",2960,1, .@label$;
				monster 'map_tnm2$,.@north_east[.@i],.@north_east[.@i+1], "",2960,1, .@label$;
				sleep 200;
		}
		end;
OnTimer6000:
		killmonster 'map_tnm2$, instance_npcname("#devil_flamecross") + "::OnFlameDead";
		end;
OnTimer20000:
		stopnpctimer;
		donpcevent instance_npcname("#devil_flamecross") + "::OnStart";
		end;
OnStop:
		stopnpctimer;
		killmonster 'map_tnm2$, instance_npcname("#devil_flamecross") + "::OnFlameDead";
		end;
OnFlameDead:
		end;
}

1@tnm2,1,1,0	script	#devil_seal_timer	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#devil_seal_timer");
		enablenpc instance_npcname("#devil_flamecross");
		setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
		for ( .@i = 0; .@i < 5; .@i++ ) {
				enablenpc instance_npcname("devil_seal_spawn#" + .@direction$[.@i]);
				enablenpc instance_npcname("seal_reach_center#" + .@direction$[.@i]);
		}
		enablenpc instance_npcname("#devil_seal_dead");
		donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnCoord";
		initnpctimer;
		end;
OnTimer5000:
		mapannounce 'map_tnm2$, "A Magic Seal has just appeared in southeast direction of the tower.", bc_map,0xFFFFFF;
		donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnFirstSpawn";
		end;
OnTimer10000:
		donpcevent instance_npcname("#devil_flamecross") + "::OnStart";
		end;
OnTimer25000:
		mapannounce 'map_tnm2$, "Lucile: We should stop before Magic Seal has just started to move again.", bc_map,0xFFFFFF;
		end;
OnTimer65000:
		mapannounce 'map_tnm2$, "Lucile: We should stop before Magic Seal reaches the center.", bc_map,0xFFFFFF;
		end;
OnTimer95000:
		mapannounce 'map_tnm2$, "Lucile: The Magic Seal has just started to move again.", bc_map,0xFFFFFF;
		end;
OnTimer125000:
		mapannounce 'map_tnm2$, "Lucile: It is coming closer... No... It's too dangerous.", bc_map,0xFFFFFF;
		end;
OnTimer155000:
		mapannounce 'map_tnm2$, "Lucile: The Magic Seal is almost approaching the center!!", bc_map,0xFFFFFF;
		end;
OnTimer180000:
		donpcevent instance_npcname("#devil_flamecross") + "::OnStop";
		end;
OnTimer185000:
		mapannounce 'map_tnm2$, "Lucile: We should stop it now!", bc_map,0xFFFFFF;
		end;
OnTimer191000:
		npctalk "ธานาทอส: คุณกำลังทำอะไรอยู่!!! กำลังเล่นเกมอยู่เหรอ?!", 'thanatos$;
		mapannounce 'map_tnm2$, "Thanatos: What are you doing!!! Are you playing a game now?!", bc_map,0xFF00;
		end;
OnTimer193000:
		specialeffect EF_MAGNUMBREAK,AREA, 'thanatos$;
		specialeffect EF_LORD,AREA, 'thanatos$;
		setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
		for ( .@i = 0; .@i < 5; .@i++ )
				donpcevent instance_npcname("devil_seal_spawn#" + .@direction$[.@i]) + "::OnStop";
		end;
OnTimer195000:
		npctalk "อสูรโมร็อค: เจ้าจะคอยกวนใจข้าอีกนานไหม!!!", 'demon$;
		mapannounce 'map_tnm2$, "Demon Morocc: Are you going to keep disturbing me!!!", bc_map,0xFF0000;
		end;
OnTimer198000:
		npctalk "Demon Morocc: โอเค มาดูกันว่าใครจะชนะ!!! ออกมาเลย!", 'demon$;
		mapannounce 'map_tnm2$, "Demon Morocc: Okay, Let's see who is going to win!!! Come out!", bc_map,0xFF0000;
		end;
OnTimer201000:
		npctalk "ธานาทอส: ไม่ว่าจะพยายามอีกกี่ครั้ง มันก็ยังเป็นเหมือนเดิม! ยอมแพ้แล้วกลับไปสู่โลกปีศาจซะ!!", 'thanatos$;
		mapannounce 'map_tnm2$, "ธานาทอส: ไม่ว่าจะพยายามอีกกี่ครั้ง มันก็ยังเป็นเหมือนเดิม! ยอมแพ้แล้วกลับไปสู่โลกปีศาจซะ!!", bc_map,0xFF00;
		initnpctimer;
		end;
OnStop:
		stopnpctimer;
		donpcevent instance_npcname("#devil_flamecross") + "::OnStop";
		disablenpc instance_npcname("#devil_flamecross");
		disablenpc instance_npcname("#devil_seal_timer");
		end;
}

1@tnm2,1,1,0	script	#devil_seal_spawn_global	HIDDEN_WARP_NPC,{
		end;
OnCoord:
		enablenpc instance_npcname("#devil_seal_spawn_global");
		// setarray 'coord_seal_DIR_NORTHWEST[0],  68,167,	 78,165,	88,163,		98,160,		108,156,	118,152,	127,147;
		setarray 'coord_seal_DIR_NORTHWEST[0],  68,167,	 78,165,	88,163,		98,160,		108,156,	118,152,	118,152;// officially
		setarray 'coord_seal_DIR_SOUTHWEST[0],  90, 86,	 96, 95,	103,103,	110,110,	116,116,	122,122,	129,130;
		setarray 'coord_seal_DIR_SOUTHEAST[0], 191,86,	183, 94,	174,103,	165,112,	156,120,	147,128,	143,131;
		setarray 'coord_seal_DIR_NORTHEAST[0], 197,162,	188,161,	179,158,	170,155,	161,152,	152,149,	144,148;
		setarray 'coord_seal_DIR_NORTH[0], 142,214,	140,205,	140,195,	140,185,	140,173,	139,161,	138,150;
		end;
OnFirstSpawn:
		'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnFirstSpawn";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnFirstSpawn";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnFirstSpawn";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnFirstSpawn";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnFirstSpawn";
		initnpctimer;
		end;
OnTimer2000:
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnSlaves";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnSlaves";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnSlaves";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnSlaves";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnSlaves";
		end;
OnTimer20000:
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnStart";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnStart";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnStart";
		end;
OnTimer30000:
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnStart";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnStart";
		end;
OnStop:
		stopnpctimer;
		disablenpc instance_npcname("#devil_seal_spawn_global");
		end;
}

1@tnm2,1,1,0	script	devil_seal_spawn#DIR_NORTHWEST	HIDDEN_WARP_NPC,{
		end;
OnStart:
		.@num = getd( strnpcinfo(2) );
		if ('seal_id[.@num] == 0 || unitexists('seal_id[.@num]) == 0)
				stopnpctimer;
		else {
				getunitdata 'seal_id[.@num], .@data;
				'seal_hp[.@num] = .@data[UMOB_HP];
				killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);
				initnpctimer;
		}
		end;
OnFirstSpawn:
OnTimer5000:
		.@num = getd( strnpcinfo(2) );
		.@index = 'round[.@num] * 2;
		if (.@index == 0 || 'seal_id[.@num]) {
				.@direction$ = strnpcinfo(2);
				.@x = getd( "'coord_seal_" + .@direction$ + "[" + .@index + "]" );
				.@y = getd( "'coord_seal_" + .@direction$ + "[" + (.@index+1) + "]" );

				monster 'map_tnm2$,.@x,.@y,"Magic Seal",2938,1, instance_npcname("#devil_seal_dead") + "::OnSeal_" + .@direction$;	// MM_MAGIC_SEAL
				'seal_id[.@num] = $@mobid[0];
				if (.@index == 0)
						setunitdata 'seal_id[.@num], UMOB_HP, 3000000;
				else if ('seal_hp[.@num] > 0)
						setunitdata 'seal_id[.@num], UMOB_HP, 'seal_hp[.@num];
				else
						stopnpctimer;
				'round[.@num]++;
				if ('round[.@num] == 7)
						donpcevent instance_npcname("seal_reach_center#" + .@direction$) + "::OnStart";
		}
		end;
OnSlaves:
OnTimer7000:
		killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";
		.@num = getd( strnpcinfo(2) );
		if ('seal_id[.@num] && unitexists('seal_id[.@num])) {
				.@direction$ = strnpcinfo(2);
				.@index = ('round[.@num] - 1) * 2;
				.@x = getd( "'coord_seal_" + .@direction$ + "[" + .@index + "]" );
				.@y = getd( "'coord_seal_" + .@direction$ + "[" + (.@index+1) + "]" );
				areamonster 'map_tnm2$,(.@x-1),(.@y-1),(.@x+1),(.@y+1),"Evil Shadow",2939,2, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// MM_EVIL_SHADOW1
				areamonster 'map_tnm2$,(.@x-1),(.@y-1),(.@x+1),(.@y+1),"Evil Shadow",2940,2, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// MM_EVIL_SHADOW2
				if ('round[.@num] == 7)
						stopnpctimer;
		}
		end;
OnTimer30000:
		donpcevent instance_npcname("devil_seal_spawn#" + strnpcinfo(2)) + "::OnStart";
		end;

OnDisable:
		stopnpctimer;
		killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// note : officially still alive
		killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);	// note : officially still alive
		disablenpc();
		end;
OnStop:
		stopnpctimer;
		killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";
		killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);
		end;
OnMobDead:
		end;
}
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_SOUTHWEST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_SOUTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_NORTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_NORTH	HIDDEN_WARP_NPC

1@tnm2,1,1,0	script	seal_reach_center#DIR_NORTHWEST	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc();
		initnpctimer;
		end;
OnTimer2000:
		npctalk "อสูรโมร็อค: คู๊ ฮ่า ฮ่า ฮ่า ชิ้นส่วนพลังของข้า! รวมตัวกันที่นี่อีกครั้ง!!!", 'demon$;
		mapannounce 'map_tnm2$, "Demon Morocc: Kuh ha ha ha ha Pieces of my power! Gather here again!!!", bc_map,0xFF0000;
		end;
OnTimer4000:
		npctalk "ธานาทอส: หยุดมันซะ! ป้องกันไม่ให้มันเข้ามาใกล้!!!", 'thanatos$;
		mapannounce 'map_tnm2$, "Thanatos: Stop him! Prevent him from coming closer!!!", bc_map,0xFF00;
		stopnpctimer;
		disablenpc();
		end;
}
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_SOUTHWEST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_SOUTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_NORTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_NORTH	HIDDEN_WARP_NPC

1@tnm2,1,1,0	script	#devil_seal_dead	HIDDEN_WARP_NPC,{
		end;
OnSeal_DIR_NORTHWEST: callsub( S_Sealdead, DIR_NORTHWEST );
OnSeal_DIR_SOUTHWEST: callsub( S_Sealdead, DIR_SOUTHWEST );
OnSeal_DIR_SOUTHEAST: callsub( S_Sealdead, DIR_SOUTHEAST );
OnSeal_DIR_NORTHEAST: callsub( S_Sealdead, DIR_NORTHEAST );
OnSeal_DIR_NORTH: callsub( S_Sealdead, DIR_NORTH );

S_Sealdead:
		'seal_id[ getarg(0) ] = 0;
		'seal_dead[ getarg(0) ] = 1;
		.@total_seal = 'seal_dead[DIR_NORTHWEST] + 'seal_dead[DIR_SOUTHWEST] + 'seal_dead[DIR_SOUTHEAST] + 'seal_dead[DIR_NORTHEAST] + 'seal_dead[DIR_NORTH];
		if (.@total_seal != 5) {
				if (getarg(0) == DIR_NORTHWEST)
						mapannounce 'map_tnm2$, "Lucile: It is too early to feel easy! There are Magic Seals that haven't been destroyed yet.", bc_map,0xFF00;
				else
						mapannounce 'map_tnm2$, "There are still Magic Seals to destroy.", bc_map,0xFF00;
		}
		else {
				donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnStop";
				donpcevent instance_npcname("#devil_seal_timer") + "::OnStop";	// stop timer of npc #devil_flamecross as well
				donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnDisable";// note: on official seals and slaves are alive
				donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnDisable";
				donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnDisable";
				donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnDisable";
				donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnDisable";
				mapannounce 'map_tnm2$, "Demon Morocc: No.. This can't be.. Who the hell.. are you!", bc_map,0xFF0000;
				npctalk "Demon Morocc: ไม่นะ.. นี่มันเป็นไปไม่ได้.. คุณเป็นใครกัน!", 'demon$;
				initnpctimer;
		}
		end;
OnTimer3000:
		mapannounce 'map_tnm2$, "Thanatos: It will be the last time to show off!! Good bye!!!", bc_map,0xFF0000;
		npctalk "ธานาทอส : จะเป็นครั้งสุดท้ายที่ได้โชว์ออฟ!! ลาก่อน!!!", 'thanatos$;
		end;
OnTimer5000:
		specialeffect EF_LORD,AREA, 'thanatos$;
		end;
OnTimer7000:
		stopnpctimer;
		disablenpc 'demon$;
		disablenpc 'thanatos$;
		disablenpc instance_npcname("Lucile#tnm03");
		enablenpc instance_npcname("Lucile#tnm04");
		enablenpc instance_npcname("#tnm3event01");
		enablenpc instance_npcname("Young Girl#tnm01");
		'devil_tower = 9;

		'demon$ = 'thanatos$ = "";
		'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
		'seal_id[DIR_NORTHWEST] = 'seal_id[DIR_SOUTHWEST] = 'seal_id[DIR_SOUTHEAST] = 'seal_id[DIR_NORTHEAST] = 'seal_id[DIR_NORTH] = 0;
		'seal_hp[DIR_NORTHWEST] = 'seal_hp[DIR_SOUTHWEST] = 'seal_hp[DIR_SOUTHEAST] = 'seal_hp[DIR_NORTHEAST] = 'seal_hp[DIR_NORTH] = 0;
		'seal_dead[DIR_NORTHWEST] = 'seal_dead[DIR_SOUTHWEST] = 'seal_dead[DIR_SOUTHEAST] = 'seal_dead[DIR_NORTHEAST] = 'seal_dead[DIR_NORTH] = 0;
		deletearray 'coord_seal_DIR_NORTHWEST[0],14;
		deletearray 'coord_seal_DIR_SOUTHWEST[0],14;
		deletearray 'coord_seal_DIR_SOUTHEAST[0],14;
		deletearray 'coord_seal_DIR_NORTHEAST[0],14;
		deletearray 'coord_seal_DIR_NORTH[0],14;
		end;
}

1@tnm2,144,137,3	script	Lucile#tnm04	4_F_LUCILE,{
		if ('devil_tower < 9)
				end;
		if (isbegin_quest(7574) == 0) {
				if (isbegin_quest(7572) == 1)
						erasequest 7572;// Lucile...?
				if (isbegin_quest(7573) == 1)	// leader
						erasequest 7573;// Magic Swordman Thanatos
				setquest 7574;// Thanatos Tower
				mes "[ลูซิล]";
				mes "ฉันคิดว่าพลังของ Demon Morocc ในการทำลายโล่ผนึกได้ปะทะกับ Thanatos แล้ว";
				cutin "tnm_lucile01.bmp",2;
				next;
				mes "[ลูซิล]";
				mes "ที่ไหนสักแห่งทางใต้มันไปไกลเกินไปแล้ว";
				mes "คุณจะไปที่นั่นมั้ย?";
		}
		else {
				mes "[ลูซิล]";
				mes "การปะทะกันของพลังทั้งสองนี้มากเกินกว่าจะรับมือได้";
				mes "มันอาจมีอิทธิพลต่อทางใต้ของรูน-มิดการ์ต";
		}
		next;
		if (select( "Follow.", "Stop." ) == 2) {
				mes "[ลูซิล]";
				mes "...ผมเดาว่านี่คือสิ่งที่คุณสามารถทำได้ใช่ไหม?";
				cutin "tnm_lucile02.bmp",2;
				close3;
		}
		mes "[ลูซิล]";
		mes "ขอให้โชคดี.";
		close2;
		warp 'map_tnm3$,21,171;
		end;
}

// tnm3 Stairs 1
1@tnm3,21,171,0	script	#tnm3event01	HIDDEN_NPC,1,1,{
		end;
OnTouch:
		if ('devil_tower != 9)
				end;
		setpcblock PCBLOCK_NPC, true;
		disablenpc instance_npcname("#tnm3event01");
		enablenpc instance_npcname("Young Girl#tnm01");
		sleep2 4000;
		mapannounce 'map_tnm3$, "Young Girl: Odin Preserve me! A demon!! Help me!", bc_map,0xFF00;
		'mob_count = 12;
		initnpctimer;
		setpcblock PCBLOCK_NPC, false;
		end;
OnTimer1000:
		monster 'map_tnm3$,33,171,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
		end;
OnTimer2000:
		monster 'map_tnm3$,39,172,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
		end;
OnTimer3000:
		monster 'map_tnm3$,34,177,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
		end;
OnTimer4000:
		monster 'map_tnm3$,36,176,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
		end;
OnTimer5000:
		monster 'map_tnm3$,37,175,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
		end;
OnTimer6000:
		monster 'map_tnm3$,37,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		monster 'map_tnm3$,36,172,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		end;
OnTimer7000:
		monster 'map_tnm3$,29,178,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		monster 'map_tnm3$,35,172,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		end;
OnTimer8000:
		monster 'map_tnm3$,34,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		monster 'map_tnm3$,35,172,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
		end;
OnTimer9000:
		monster 'map_tnm3$,34,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
		stopnpctimer;
		end;
}

1@tnm3,43,175,3	script	Young Girl#tnm01	4_F_DST_CHILD,{
		if ('devil_tower < 9)
				end;
		if ('devil_tower == 9) {
				mes "[เด็กสาว]";
				mes "ทำไมถึงมีปีศาจอยู่ในปราสาทล่ะ?!";
				mes "ช่วยฉันด้วย!";
				close;
		}
		if ('devil_tower == 10) {
				if (is_party_leader() == false) {
						mes "[เด็กสาว]";
						mes "*ร้องไห้* ปีศาจมันน่ากลัวจริงๆ";
						close;
				}
				mes "[เด็กสาว]";
				mes "ทำไมจู่ๆ ปีศาจถึงมา";
				mes "เจ้าหน้าที่รักษาความปลอดภัยได้รับบาดเจ็บจำนวนมาก";
				npctalk "เด็กสาว : ทำไมปีศาจถึงมาอยู่ดีๆ ก็มีทหารยามได้รับบาดเจ็บมากมาย";
				next;
				select("Where are they?");
				unittalk getcharid(3), "" + strcharinfo(0) + " : Where are they?";
				mes "[เด็กสาว]";
				mes "พวกเขาอยู่ในปราสาทโมร็อกโก พ่อของฉันเป็นอัศวิน";
				npctalk "พวกเขาอยู่ในปราสาทโมร็อกโก พ่อของฉันเป็นอัศวิน";
				next;
				mes "[เด็กสาว]";
				mes "ดูเหมือนว่าจะมีอะไรบางอย่างเกิดขึ้น ฉันได้ยินเสียงแปลก ๆ ข้างล่าง";
				npctalk "เด็กสาว: ดูเหมือนว่าจะมีอะไรบางอย่างเกิดขึ้น ฉันได้ยินเสียงแปลก ๆ ข้างล่าง";
				next;
				mes "[เด็กสาว]";
				mes "ฉันเห็นคนชุดดำเดินเข้ามา มีอะไรเกิดขึ้นในปราสาทนะ";
				npctalk "เด็กสาว: ฉันเห็นผู้ชายชุดดำเดินเข้ามา มีอะไรเกิดขึ้นในปราสาทเหรอ?";
				next;
				mes "[เด็กสาว]";
				mes "ป๊า..ป๊าขอให้ปลอดภัยนะ *สะอื้น*";
				mes "โปรดมาพร้อมฉันผ่านช่องทางหลังชั้นวางอันนั้น!";
				npctalk "เด็กสาว: โปรดมาทางช่องทางหลังชั้นวางนั่นกับฉันสิ!";
				close2;
				if (is_party_leader() == true && 'devil_tower == 10) {
						enablenpc instance_npcname("#tnm3gate01");
						enablenpc instance_npcname("#tnm3event02ect");
						'devil_tower = 11;
				}
				end;
		}
		if ('devil_tower == 11) {
				mes "[เด็กสาว]";
				mes "ด้านหลังชั้นวางนั้นคือทางลับสู่ห้องใต้ดิน!";
				mes "โปรดมาพร้อมกับฉันเพื่อให้แน่ใจว่าพ่อของฉันปลอดภัย";
				close;
		}
		end;

OnMobDead:
		'mob_count--;
		if ('mob_count == 0) {
				'devil_tower = 10;
				npctalk "เด็กสาว : ขอบคุณที่ช่วยฉันไว้...แต่ทำไมถึงมีปีศาจล่ะ?";
		}
		end;
}

1@tnm3,52,176,0	warp	#tnm3gate01	2,2,1@tnm3,85,178

// tnm3 Stairs 2
1@tnm3,85,178,0	script	#tnm3event02ect	HIDDEN_NPC,1,1,{
		end;
OnTouch:
		if ('devil_tower != 11)
				end;
		disablenpc instance_npcname("#tnm3event02ect");
		enablenpc instance_npcname("Young Girl#tnm02");
		npctalk "เด็กสาว: ดูสิ ปีศาจเพิ่มมาอีก พวกมันจะยึดปราสาทเร็วๆ นี้!", instance_npcname("Young Girl#tnm02");
		'mob_count = 18;
		initnpctimer;
		end;
OnTimer1000:
		.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
		monster 'map_tnm3$,97,174,"Evil Shadow",2939,1, .@label$;
		monster 'map_tnm3$,98,175,"Evil Shadow",2939,1, .@label$;
		monster 'map_tnm3$,100,166,"Evil Shadow",2939,1, .@label$;
		monster 'map_tnm3$,108,171,"Evil Shadow",2940,1, .@label$;
		monster 'map_tnm3$,104,172,"Evil Shadow",2940,1, .@label$;
		end;
OnTimer3000:
		.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
		monster 'map_tnm3$,109,169,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,111,173,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,113,167,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,101,176,"Evil Shadow",2939,1, .@label$;
		monster 'map_tnm3$,116,170,"Evil Shadow",2941,1, .@label$;
		end;
OnTimer5000:
		.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
		monster 'map_tnm3$,107,175,"Evil Shadow",2940,1, .@label$;
		monster 'map_tnm3$,106,174,"Evil Shadow",2940,1, .@label$;
		monster 'map_tnm3$,106,169,"Evil Shadow",2939,1, .@label$;
		monster 'map_tnm3$,103,177,"Evil Shadow",2940,1, .@label$;
		end;
OnTimer7000:
		stopnpctimer;
		.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
		monster 'map_tnm3$,121,171,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,119,173,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,127,166,"Evil Shadow",2941,1, .@label$;
		monster 'map_tnm3$,125,166,"Evil Shadow",2941,1, .@label$;
		end;
}

1@tnm3,89,179,4	script	Young Girl#tnm02	4_F_DST_CHILD,{
		end;
OnMobDead:
		'mob_count--;
		if ('mob_count == 0) {
				mapannounce 'map_tnm3$, "Young Girl: The way underground is along this aisle!", bc_map,0xFF00;
				disablenpc instance_npcname("Young Girl#tnm02");
				enablenpc instance_npcname("Young Girl#tnm03");
				npctalk "สาวน้อย : ทางใต้ดินอยู่ตามทางเดินนี้นี่เอง!", instance_npcname("Young Girl#tnm03");
				enablenpc instance_npcname("#tnm3gate02");
				enablenpc instance_npcname("Evil Shadow#tnm3mob01");
		}
		end;
}

1@tnm3,128,164,3	script	Young Girl#tnm03	4_F_DST_CHILD,{
		mes "[เด็กสาว]";
		mes "จากตรงนี้..ผมไม่ทราบดี..";
		mes "ฉันเห็นบันไดมากมาย...";
		mes "ดูสิ มีปีศาจมากมายเหลือเกิน พ่อของฉันจะตกอยู่ในอันตราย!";
		mes "โปรดช่วยเขาด้วย";
		close;
}

1@tnm3,134,156,0	warp	#tnm3gate02	1,1,1@tnm3,169,165

// tnm3 Stairs 3
1@tnm3,179,172,3	script	Evil Shadow#tnm3mob01	2941,4,4,{
		end;
OnTouch_:
		if ('devil_tower == 11) {
				'devil_tower = 12;
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "Evil Shadow: ฉันคิดว่าฉันได้กลิ่นหนูแอบเข้ามาที่นี่ ฉันคิดถูกแล้ว!!";
				sleep2 3000;
				npctalk "เงาแห่งความชั่วร้าย: เจ้าจะต้องเสียใจกับความอยากรู้อยากเห็นและการยุ่งเกี่ยวของเจ้า ถึงเวลาให้อาหารแล้ว!";
				sleep2 3000;
				npctalk "เงาแห่งความชั่วร้าย: ฉันเรียกความมืดออกมา!!";
				disablenpc instance_npcname("Evil Shadow#tnm3mob01");
				donpcevent instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnSummon";
				setpcblock PCBLOCK_NPC, false;
		}
		end;
}

1@tnm3,1,1,0	script	Evil Shadow kill#tnm3mob01	HIDDEN_WARP_NPC,{
		end;
OnSummon:
		enablenpc instance_npcname("Evil Shadow kill#tnm3mob01");
		'mob_count = 5;
		initnpctimer;
		monster 'map_tnm3$,179,172, "Evil Shadow",2941,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW3
		end;
OnTimer1000:
		monster 'map_tnm3$,181,172, "Evil Shadow",2940,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW2
		monster 'map_tnm3$,178,172, "Evil Shadow",2940,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";
		end;
OnTimer2000:
		monster 'map_tnm3$,179,172, "Evil Shadow",2939,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW1
		monster 'map_tnm3$,176,173, "Evil Shadow",2939,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";
		stopnpctimer;
		end;
OnMobDead:
		'mob_count--;
		if ('mob_count == 0) {
				enablenpc instance_npcname("#tnm3gate03");
				enablenpc instance_npcname("Demonic Shade#mobmaster");
				enablenpc instance_npcname("Morocc Lord Knight#mocl");
				enablenpc instance_npcname("Loki#tnmloki01");
		}
		end;
}

1@tnm3,183,177,0	warp	#tnm3gate03	1,1,1@tnm3,97,6

// Demonic Shade (Boss room)
1@tnm3,97,18,3	script	Demonic Shade#mobmaster	2939,5,5,{
		end;
OnTouch_:// possibility to skip this step
		if ('devil_tower < 12)
				end;
		if ('shade == 0) {
				'shade = 1;
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "Demonic Shade: พวกคุณนั่นแหละที่เป็นตัวก่อปัญหา!";
				sleep2 2000;
				npctalk "Demonic Shade: จุ๊ๆ แกก็ปกป้องไม่ได้เลย..?.อะไรก็ตาม.. แกจะต้องตายที่นี่กันหมดแน่";
				sleep2 3000;
				npctalk "Demonic Shade: ความจริงนิรันดร์ก็คือ ฉันจะเปลี่ยนความตายของคุณให้เป็นชีวิตของปีศาจด้วยเลือดของหัวใจของคุณ!!!";
				disablenpc instance_npcname("Demonic Shade#mobmaster");
				donpcevent instance_npcname("#mobmaster_kill") + "::OnSummon";
				setpcblock PCBLOCK_NPC, false;
		}
		end;
}

1@tnm3,1,1,0	script	#mobmaster_kill	HIDDEN_WARP_NPC,{
		end;
OnSummon:
		enablenpc instance_npcname("#mobmaster_kill");
		initnpctimer;
		monster 'map_tnm3$,97,18, "Demonic Shade",2939,1, instance_npcname("#mobmaster_kill") + "::OnMobDead";
		end;
OnTimer1000:
		.@label$ = instance_npcname("#mobmaster_kill") + "::OnMobDead";
		stopnpctimer;
		monster 'map_tnm3$,94,18, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,96,18, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,113,18, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,119,22, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,116,18, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,116,18, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,114,12, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,116,21, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,74,18, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,75,12, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,76,21, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,69,16, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,57,43, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,58,44, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,59,46, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,60,48, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,56,45, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,57,44, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,80,71, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,59,77, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,77,73, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,77,72, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,59,70, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,81,66, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,60,69, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,57,73, "Demonic Shade",2940,1, .@label$;
		monster 'map_tnm3$,62,74, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,78,67, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,75,22, "Demonic Shade",2941,1, .@label$;
		monster 'map_tnm3$,81,69, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,77,11, "Demonic Shade",2939,1, .@label$;
		monster 'map_tnm3$,58,74, "Demonic Shade",2941,1, .@label$;
		end;

OnMobDead:
		end;
}

1@tnm3,93,52,4	script	Morocc Lord Knight#mocl	1_M_MOC_LORD,{
		mes "[อัศวินแห่งโมร็อกโก]";
		mes "แกเป็นหนูที่แอบมาแถวนี้นี่!";
		close;
}

1@tnm3,97,48,3	script	Loki#tnmloki01	4_M_ROKI2,{
		if ('devil_tower == 12) {
				'devil_tower = 13;
				donpcevent instance_npcname("#morocc_lord_talk") + "::OnStart";
				end;
		}
		mes "[โลกิ]";
		mes "คุณไม่มีอำนาจที่นี่";
		close;
}

1@tnm3,94,51,4	duplicate(Officer Heim#heim0)	Young Girl#tnm04	4_F_DST_CHILD

1@tnm2,1,1,0	script	#morocc_lord_talk	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#morocc_lord_talk");
		'Morocc$ = instance_npcname("Morocc Lord Knight#mocl");
		'loki$ = instance_npcname("Loki#tnmloki01");
		npctalk "อัศวินแห่งโมร็อค: งั้นหนูนั่นก็เป็นนักฆ่างั้นเหรอ? คุณมาที่นี่เพื่อตายงั้นเหรอ?", 'Morocc$;
		initnpctimer;
		end;
OnTimer3000:
		npctalk "โลกิ: คุณจะสำเร็จอะไรจากการปลุกปีศาจบนกำแพง?", 'loki$;
		end;
OnTimer6000:
		npctalk "โลกิ: ภารกิจของฉันพาฉันมาที่นี่เพื่อฝังมันอีกครั้ง", 'loki$;
		end;
OnTimer9000:
		npctalk "อัศวินแห่งโมร็อค: จริงๆ แล้ว.. กิลด์นักฆ่าที่ฉันคิดว่ากำลังทำภารกิจเพื่อรักษาสมดุลของอำนาจในโลก", 'Morocc$;
		end;
OnTimer12000:
		npctalk "อัศวินแห่งโมร็อค: การชุบชีวิตปีศาจจะนำมาซึ่งความสมดุล! แม้แต่ผู้ที่แข็งแกร่งที่สุดก็ยังเป็นทาสของเทพเจ้า..", 'Morocc$;
		end;
OnTimer15000:
		npctalk "อัศวินแห่งโมร็อค: เหล่าเทพเจ้าต่างก็มีแผนของตนเอง คุณรู้ไหมว่าเหล่าเทพเจ้ามีแผนอะไรสำหรับคุณหรือฉัน?", 'Morocc$;
		end;
OnTimer18000:
		npctalk "อัศวินแห่งโมร็อกโก: พวกเขาต้องการให้โลกนี้เป็นแบบของพวกเขาเท่านั้น แล้วเราจะไปอยู่ในนั้นได้ยังไง!", 'Morocc$;
		end;
OnTimer20000:
		npctalk "โลกิ: หุบปาก!", 'loki$;
		end;
OnTimer22000:
		npctalk "โลกิ: ฉันไม่สนใจเทพเจ้าหรือปีศาจ หรือแผนการที่พวกมันวางไว้ สิ่งเดียวที่สำคัญคือการฆ่าคุณ", 'loki$;
		end;
OnTimer25000:
		npctalk "โลกิ: นั่นคือภารกิจของฉัน!", 'loki$;
		end;
OnTimer28000:
		npctalk "อัศวินแห่งโมร็อคโค: สิ่งเดียวที่เจ้าต้องการก็คือทำภารกิจให้สำเร็จงั้นเหรอ..", 'Morocc$;
		end;
OnTimer31000:
		npctalk "โลกิ: *ยิ้ม* แน่นอน!!", 'loki$;
		specialeffect EF_BEGINSPELL3,AREA, 'loki$;
		end;
OnTimer33000:
		npctalk "โลกิ: ระเบิดจิตใจ-!!!", 'loki$;
		end;
OnTimer34000:
		enablenpc instance_npcname("Young Girl#tnm04");
		npctalk "สาวน้อย: ไม่นะ..!! อย่าทำร้ายพ่อของฉัน!!!", instance_npcname("Young Girl#tnm04");
		specialeffect EF_HIT3,AREA, instance_npcname("Young Girl#tnm04");
		end;
OnTimer34500:
		specialeffect EF_HIT2,AREA, instance_npcname("Young Girl#tnm04");
		end;
OnTimer35500:
		specialeffect EF_ICECRASH,AREA, instance_npcname("Young Girl#tnm04");
		end;
OnTimer36500:
		npctalk "-", 'loki$;
		end;
OnTimer38500:
		npctalk "อัศวินแห่งโมร็อค: โอ้ ไม่นะ.. พวกเขาควรส่งคนที่ดีกว่าคุณมาเพื่อทำภารกิจนี้ มิสเตอร์นักฆ่า", 'Morocc$;
		end;
OnTimer41500:
		npctalk "เด็กสาว : มีอะไรบางอย่างผิดปกติ....", instance_npcname("Young Girl#tnm04");
		end;
OnTimer44500:
		npctalk "เด็กสาว: พ่อของฉันจะไม่ทำตัวแบบนี้เด็ดขาด.. อึ๋ย..", instance_npcname("Young Girl#tnm04");
		end;
OnTimer47500:
		npctalk "อัศวินแห่งโมร็อคโค: มนุษย์.. ทำเรื่องโง่ๆ แบบนี้.. เป็นการเสียสละ", 'Morocc$;
		end;
OnTimer50500:
		npctalk "อัศวินแห่งโมร็อคโค: คุณรู้ไหม ความจริงก็คือมันไม่มีประโยชน์", 'Morocc$;
		end;
OnTimer53500:
		npctalk "อัศวินแห่งโมร็อค: กวูอาฮ่าฮ่าฮ่า พวกมนุษย์โง่เง่าทั้งหลาย ข้าจะฆ่าพวกเจ้าทั้งหมดเพื่อสังเวยให้กับปีศาจ!!", 'Morocc$;
		specialeffect EF_COUPLECASTING,AREA, 'Morocc$;
		specialeffect EF_VOLCANO,AREA, 'Morocc$;
		end;
OnTimer54500:
		npctalk "โลกิ: ... จ๊าก! ไร้ประโยชน์....!", 'loki$;
		end;
OnTimer55500:
		enablenpc instance_npcname("Loki#tnmloki02");
		mapwarp 'map_tnm3$, 'map_tnm3$,136,62;// todo area 1x1
		'devil_tower = 13;
		stopnpctimer;
		disablenpc instance_npcname("#morocc_lord_talk");
		disablenpc instance_npcname("Morocc Lord Knight#mocl");
		disablenpc instance_npcname("Loki#tnmloki01");
		disablenpc instance_npcname("Young Girl#tnm04");
		end;
}

1@tnm3,137,65,3	script	Loki#tnmloki02	4_M_ROKI2,{
		if ('devil_tower != 13)
				end;
		if (is_party_leader() == false) {
				mes "[โลกิ]";
				mes "ฉันกำลังคุยกับเจ้านายของคุณ";
				close;
		}
		if (getmercinfo(0) > 0) {
				mes "[โลกิ]";
				mes "...น่าอาย.";
				mes "คุณควร ^4d4dffยกเลิกสัญญาทหารรับจ้าง^000000 หากคุณต้องการช่วยฉันในภารกิจนี้";
				close;
		}
		mes "[โลกิ]";
		mes "คุณมาจากไหน?";
		mes "ฉันอาจต้องการความช่วยเหลือที่นี่";
		npctalk "โลกิ: คุณมาจากไหน ฉันอาจต้องการความช่วยเหลือที่นี่", instance_npcname("Loki#tnmloki02");
		next;
		mes "[โลกิ]";
		mes "อัศวินผู้นี้ไม่ใช่มนุษย์อีกต่อไปแล้ว ฉันต้องส่งเขาไปสู่ความเป็นนิรันดร์เพื่อติดตามเหล่าปีศาจของเขา";
		npctalk "โลกิ: อัศวินผู้นี้ไม่ใช่มนุษย์อีกต่อไปแล้ว ข้าจะต้องส่งเขาไปสู่ความเป็นนิรันดร์", instance_npcname("Loki#tnmloki02");
		next;
		mes "[โลกิ]";
		mes "^4d4dffผู้ที่ได้รับพรจากปีศาจจะไม่มีวันพ่ายแพ้ เราสามารถตัดพรนั้นได้ด้วย Mind Blaster^000000";
		npctalk "โลกิ: ผู้ที่ได้รับพรจากปีศาจจะไม่มีวันพ่ายแพ้ เราสามารถตัดพรนั้นได้ด้วย Mind Blaster", instance_npcname("Loki#tnmloki02");
		next;
		mes "[โลกิ]";
		mes "คุณจะเป็นผู้รับผิดชอบในการเปิดใช้งาน Mind Blaster";
		npctalk "โลกิ: คุณจะรับผิดชอบในการเปิดใช้งาน Mind Blaster", instance_npcname("Loki#tnmloki02");
		next;
		if (is_party_leader() == true && 'devil_tower == 13) {
				disablenpc instance_npcname("Loki#tnmloki02");
				mes "^4d4dffLoki, Guillotine Cross แห่ง Assassin Guild ได้เข้าร่วมกลุ่มของคุณแล้ว";
				mes "เราต้องกำจัดอัศวินปีศาจโดยใช้ทักษะของโลกิในการโจมตีจิตใจเขา^000000";
				'devil_tower = 14;
				mercenary_create 2937,1800000;// M_LOKI
				mapannounce 'map_tnm3$, "Demon Worshipper's Voice: Such rubbish! Where did you hide yourself?", bc_map,0xFF00;
				initnpctimer;
		}
		close;
OnTimer500:
		enablenpc instance_npcname("#boss_skills_timer");
		monster 'map_tnm3$,93,52,"Evil Believer",2942,1, instance_npcname("#boss_skills_timer") + "::OnBossDead";// MM_EVIL_FANATICS
		'boss_id = $@mobid[0];
		end;
OnTimer2000:
		mapannounce 'map_tnm3$, "Demon Worshipper's Voice: You will all be sacrificed to awaken the Demon!!!", bc_map,0xFF00;
		end;
OnTimer12000:
		donpcevent instance_npcname("#boss_skills_timer") + "::OnStart";
		stopnpctimer;
		end;
}

1@tnm3,1,1,0	script	#boss_skills_timer	HIDDEN_WARP_NPC,{
		end;
OnStart:
		'label$ = instance_npcname("#boss_skills_timer") + "::OnMobDead";
		initnpctimer;
		end;
OnTimer1000:// + form
		getunitdata 'boss_id, .@data;
		'x_boss = .@data[UMOB_X];
		'y_boss = .@data[UMOB_Y];
		monster 'map_tnm3$, 'x_boss, 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
		monster 'map_tnm3$, ('x_boss-5), 'y_boss, "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss+5), 'y_boss, "",2943,1, 'label$;
		monster 'map_tnm3$, 'x_boss, ('y_boss-5), "",2943,1, 'label$;
		monster 'map_tnm3$, 'x_boss, ('y_boss+5), "",2943,1, 'label$;
		end;
OnTimer3000:
		monster 'map_tnm3$, ('x_boss-10), 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
		monster 'map_tnm3$, ('x_boss+10), 'y_boss, "",2943,1, 'label$;
		monster 'map_tnm3$, 'x_boss, ('y_boss-10), "",2943,1, 'label$;
		monster 'map_tnm3$, 'x_boss, ('y_boss+10), "",2943,1, 'label$;
		end;
OnTimer6000:
		killmonster 'map_tnm3$, 'label$;
		end;
OnTimer21000:// X form
		getunitdata 'boss_id, .@data;
		'x_boss = .@data[UMOB_X];
		'y_boss = .@data[UMOB_Y];
		monster 'map_tnm3$, ('x_boss+3), ('y_boss+3), "",2960,1, 'label$;// MM_FLAMECROSS
		monster 'map_tnm3$, ('x_boss+3), ('y_boss-3), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-3), ('y_boss+3), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-3), ('y_boss-3), "",2960,1, 'label$;
		end;
OnTimer21350:
		monster 'map_tnm3$, ('x_boss+6), ('y_boss+6), "",2960,1, 'label$;// MM_FLAMECROSS
		monster 'map_tnm3$, ('x_boss+6), ('y_boss-6), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-6), ('y_boss+6), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-6), ('y_boss-6), "",2960,1, 'label$;
		end;
OnTimer21700:
		monster 'map_tnm3$, ('x_boss+9), ('y_boss+9), "",2960,1, 'label$;// MM_FLAMECROSS
		monster 'map_tnm3$, ('x_boss+9), ('y_boss-9), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-9), ('y_boss+9), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-9), ('y_boss-9), "",2960,1, 'label$;
		end;
OnTimer22050:
		monster 'map_tnm3$, ('x_boss+13), ('y_boss+13), "",2960,1, 'label$;// MM_FLAMECROSS
		monster 'map_tnm3$, ('x_boss+13), ('y_boss-13), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-13), ('y_boss+13), "",2960,1, 'label$;
		monster 'map_tnm3$, ('x_boss-13), ('y_boss-13), "",2960,1, 'label$;
		end;
OnTimer30000:
		killmonster 'map_tnm3$, 'label$;
		end;
OnTimer51000:// X form
		getunitdata 'boss_id, .@data;
		'x_boss = .@data[UMOB_X];
		'y_boss = .@data[UMOB_Y];
		monster 'map_tnm3$, 'x_boss, 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
		monster 'map_tnm3$, ('x_boss+5), ('y_boss+5), "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss-5), ('y_boss-5), "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss+10), ('y_boss+10), "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss-10), ('y_boss-10), "",2943,1, 'label$;
		end;
OnTimer53000:
		monster 'map_tnm3$, ('x_boss-5), ('y_boss+5), "",2943,1, 'label$;// MM_ICE_MINE
		monster 'map_tnm3$, ('x_boss+5), ('y_boss-5), "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss-10), ('y_boss+10), "",2943,1, 'label$;
		monster 'map_tnm3$, ('x_boss+10), ('y_boss-10), "",2943,1, 'label$;
		end;
OnTimer56000:
		killmonster 'map_tnm3$, 'label$;
		end;
OnTimer81000:// S form
		getunitdata 'boss_id, .@data;
		'x_boss = .@data[UMOB_X];
		'y_boss = .@data[UMOB_Y];
		monster 'map_tnm3$, ('x_boss-2), ('y_boss+5), "",2960,1, 'label$;// MM_FLAMECROSS
		end;
OnTimer81300:
		monster 'map_tnm3$, ('x_boss+4), ('y_boss+8), "",2960,1, 'label$;
		end;
OnTimer81600:
		monster 'map_tnm3$, ('x_boss+10), ('y_boss+2), "",2960,1, 'label$;
		end;
OnTimer82000:
		monster 'map_tnm3$, ('x_boss+10), ('y_boss-1), "",2960,1, 'label$;
		end;
OnTimer82300:
		monster 'map_tnm3$, ('x_boss+7), ('y_boss-7), "",2960,1, 'label$;
		end;
OnTimer82600:
		monster 'map_tnm3$, ('x_boss+1), ('y_boss-10), "",2960,1, 'label$;
		end;
OnTimer83000:
		monster 'map_tnm3$, ('x_boss-11), ('y_boss-1), "",2960,1, 'label$;
		end;
OnTimer83300:
		monster 'map_tnm3$, ('x_boss-11), ('y_boss+5), "",2960,1, 'label$;
		end;
OnTimer83600:
		monster 'map_tnm3$, ('x_boss-8), ('y_boss+11), "",2960,1, 'label$;
		end;
OnTimer84000:
		monster 'map_tnm3$, ('x_boss-2), ('y_boss+17), "",2960,1, 'label$;
		end;
OnTimer84300:
		monster 'map_tnm3$, ('x_boss+7), ('y_boss+20), "",2960,1, 'label$;
		end;
OnTimer90000:
		killmonster 'map_tnm3$, 'label$;
		initnpctimer;
		end;
OnMobDead:
		end;
OnBossDead:
		stopnpctimer;
		killmonster 'map_tnm3$, 'label$;
		'x_boss = 'y_boss = 'boss_id = 0;
		'label$ = "";
		mapannounce 'map_tnm3$, "Demon Worshipper's Voice: NO it is impossible. It cannot be.. This force.. power.. you.. may..be you are..", bc_map,0xFF0000;
		sleep2 1000;
		warpparty 'map_tnm3$,71,70, getcharid(1), 'map_tnm3$,1,1;
		enablenpc instance_npcname("Loki#tnmloki03");
		enablenpc instance_npcname("box#tnmbosang");
		disablenpc instance_npcname("#boss_skills_timer");
		'devil_tower = 15;
		end;
}

1@tnm3,69,70,3	script	box#tnmbosang	4_TREASURE_BOX,{
		if ('devil_tower != 15)
				end;
		specialeffect EF_COIN;
		disablenpc instance_npcname("box#tnmbosang");
		makeitem 7293,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Rose Quartz
		makeitem 749,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Frozen Rose
		makeitem 7511,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Rune of Darkness
		if (rand(100) < 75)
				makeitem 616,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Old Card Album
		if (rand(100) < 50)
				makeitem 748,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Witherless_Rose

		setarray .@item_id_list[0],
				1671,	// Devil_Won_Staff
				13094,	// Devil_Pierced_Dagger
				16027,	// Hammer_Of_Evil_Slayer
				18120, 	// Bow_Of_Evil_Slayer
				21010, 	// Tw_Sword_Of_Evil_Slayer
				28001; 	// Katar_Of_Evil_Slayer
		setarray .@bonus_list1[0],
				4800,	// SP50
				4811,	// Fighting_Spirit1
				4810,	// Fighting_Spirit2
				4809,	// Fighting_Spirit3
				4808,	// Fighting_Spirit4
				4820,	// Fighting_Spirit5
				4821,	// Fighting_Spirit6
				4815,	// Spell1
				4814,	// Spell2
				4813,	// Spell3
				4812,	// Spell4
				4786;	// Mdef2
		setarray .@bonus_list2[0],
				4815,	// Spell1
				4814,	// Spell2
				4813,	// Spell3
				4812,	// Spell4
				4826,	// Spell5
				4811,	// Fighting_Spirit1
				4810,	// Fighting_Spirit2
				4809,	// Fighting_Spirit3
				4808,	// Fighting_Spirit4
				4820,	// Fighting_Spirit5
				4786;	// Mdef2
		setarray .@bonus_list3[0],
				4815,	// Spell1
				4814,	// Spell2
				4813,	// Spell3
				4812,	// Spell4
				4811,	// Fighting_Spirit1
				4810,	// Fighting_Spirit2
				4809,	// Fighting_Spirit3
				4808,	// Fighting_Spirit4
				4820,	// Fighting_Spirit5
				4821,	// Fighting_Spirit6
				4791,	// DEF+3
				4786;	// Mdef2
		.@item_id = .@item_id_list[ rand( getarraysize(.@item_id_list) ) ];
		.@refine = rand(1,6);
		.@bonus1 = .@bonus_list1[ rand( getarraysize(.@bonus_list1) ) ];
		.@bonus2 = .@bonus_list1[ rand( getarraysize(.@bonus_list2) ) ];
		.@bonus3 = .@bonus_list1[ rand( getarraysize(.@bonus_list3) ) ];

		makeitem2 .@item_id,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5)), 0, .@refine,0, 0,.@bonus1,.@bonus2,.@bonus3;
		end;
}

//1@tnm3,69,72,3	script	Loki#tnmloki03	4_M_ROKI2,5,5,{
1@tnm3,69,72,3	script	Loki#tnmloki03	4_M_ROKI2,{
		if ('devil_tower != 15)
				end;
		if (isbegin_quest(7576) == 0) {
				mes "[โลกิ]";
				mes ".. น่ารำคาญ";
				mes "ปีศาจถูกทำลายเสียก่อนที่พิธีฟื้นคืนชีพจะเสร็จสิ้น";
				mes "สักพักหนึ่งทุกอย่างจะดีขึ้น";
				setquest 7576;// Morocc castle seal
				next;
				mes "ฉันได้รับข้อมูลที่ไม่คาดคิดระหว่างการสำรวจหอคอย";
				next;
				mes "[โลกิ]";
				mes "ฉันจะอยู่ห่างจากความรู้สึกผิดชอบชั่วดีของฉันอีกสักพักหนึ่ง";
				mes "สักวันฉันจะปล่อยให้ตัวเองรู้สึกอีกครั้ง และวันนี้จะครอบครองความคิดของฉัน..";
				close;
		}
		mes "[โลกิ]";
		mes "น่าสนใจ..มีลูกสาวด้วย..";
		mes "จะต้องทำอย่างไรคะ?";
		mes "ฉันไม่แน่ใจว่าฉันจะทำอย่างไรต่อไป..";
		next;
		mes "[โลกิ]";
		mes "แต่ไม่เป็นไร คุณไม่ควรอยู่ที่นี่ตอนนี้";
		mes "จะอยู่ที่นี่ต่อไปอีกนานไหม?";
		mes "ฉันสามารถส่งคุณกลับที่ไหนและเมื่อไหร่ก็ได้ที่คุณอยู่";
		next;
		if (select( "Get out of here.", "Take a look around." ) == 2) {
				mes "[โลกิ]";
				mes "การมัวแต่มัวแต่เสียเวลาเป็นสิ่งที่ไม่ฉลาด";
				close;
		}
		mes "[โลกิ]";
		mes "บางทีภารกิจของเราอาจจะกลับมาเป็นพันธมิตรกันใหม่ในอนาคต..";
		close2;
		warp "dali02",134,112;
		end;

OnInstanceInit:
		'heal_count = 0;
		'devil_tower = 0;
		'mob_count = 0;
		'shade = 0;

		'map_tnm1$ = instance_mapname("1@tnm1");
		'map_tnm2$ = instance_mapname("1@tnm2");
		'map_tnm3$ = instance_mapname("1@tnm3");

		// Entrance
		disablenpc instance_npcname("Officer Heim#heim");
		disablenpc instance_npcname("Healer Fama#feima");
		for ( .@i = 1; .@i < 8; .@i++ )
				disablenpc instance_npcname( "Injured Soldier#" + .@i );
		disablenpc instance_npcname("Lucile#tnm01");
		disablenpc instance_npcname("Magic Swordman#tnm01");
		disablenpc instance_npcname("Assassin Huey#tnm01");
		disablenpc instance_npcname("Assassin Louie#tnm01");
		disablenpc instance_npcname("Assassin Dewey#tnm01");

		// Stairs tnm1
		disablenpc instance_npcname("#tnm1stepmob");
		disablenpc instance_npcname("Assassin Louie#tnm02");

		// Stairs 2 tnm1
		disablenpc instance_npcname("Lucile#tnm02");
		disablenpc instance_npcname("Huey#hui02");

		// Morocc - Thanatos Battle
		disablenpc instance_npcname("Magic Swordman#tnm02");
		disablenpc instance_npcname("Demon Morocc#tnm01");
		disablenpc instance_npcname("Lucile#tnm03");
		disablenpc instance_npcname("Lucile#tnm04");
		disablenpc instance_npcname("#devil_flamecross");
		disablenpc instance_npcname("#devil_seal_timer");
		disablenpc instance_npcname("#devil_seal_spawn_global");
		setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
		for ( .@i = 0; .@i < 5; .@i++ ) {
				disablenpc instance_npcname("devil_seal_spawn#" + .@direction$[.@i]);
				disablenpc instance_npcname("seal_reach_center#" + .@direction$[.@i]);
		}
		disablenpc instance_npcname("#devil_seal_dead");

		'demon$ = 'thanatos$ = "";
		'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
		'seal_id[DIR_NORTHWEST] = 'seal_id[DIR_SOUTHWEST] = 'seal_id[DIR_SOUTHEAST] = 'seal_id[DIR_NORTHEAST] = 'seal_id[DIR_NORTH] = 0;
		'seal_hp[DIR_NORTHWEST] = 'seal_hp[DIR_SOUTHWEST] = 'seal_hp[DIR_SOUTHEAST] = 'seal_hp[DIR_NORTHEAST] = 'seal_hp[DIR_NORTH] = 0;
		'seal_dead[DIR_NORTHWEST] = 'seal_dead[DIR_SOUTHWEST] = 'seal_dead[DIR_SOUTHEAST] = 'seal_dead[DIR_NORTHEAST] = 'seal_dead[DIR_NORTH] = 0;
		deletearray 'coord_seal_DIR_NORTHWEST[0],14;
		deletearray 'coord_seal_DIR_SOUTHWEST[0],14;
		deletearray 'coord_seal_DIR_SOUTHEAST[0],14;
		deletearray 'coord_seal_DIR_NORTHEAST[0],14;
		deletearray 'coord_seal_DIR_NORTH[0],14;

		// tnm3 Stairs 1
		disablenpc instance_npcname("#tnm3event01");
		disablenpc instance_npcname("Young Girl#tnm01");
		disablenpc instance_npcname("#tnm3gate01");

		// tnm3 Stairs 2
		disablenpc instance_npcname("#tnm3event02ect");
		disablenpc instance_npcname("Young Girl#tnm02");
		disablenpc instance_npcname("Young Girl#tnm03");
		disablenpc instance_npcname("#tnm3gate02");

		// tnm3 Stairs 3
		disablenpc instance_npcname("Evil Shadow#tnm3mob01");
		disablenpc instance_npcname("Evil Shadow kill#tnm3mob01");
		disablenpc instance_npcname("#tnm3gate03");

		// Demonic Shade (Boss room)
		disablenpc instance_npcname("Demonic Shade#mobmaster");
		disablenpc instance_npcname("#mobmaster_kill");

		// Boss
		disablenpc instance_npcname("Morocc Lord Knight#mocl");
		disablenpc instance_npcname("#morocc_lord_talk");
		disablenpc instance_npcname("Loki#tnmloki01");
		disablenpc instance_npcname("Young Girl#tnm04");
		disablenpc instance_npcname("Loki#tnmloki02");
		disablenpc instance_npcname("#boss_skills_timer");
		'x_boss = 'y_boss = 'boss_id = 0;
		'label$ = "";

		// Final
		disablenpc instance_npcname("Loki#tnmloki03");
		disablenpc instance_npcname("box#tnmbosang");
		end;
}
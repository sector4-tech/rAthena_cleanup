//===== rAthena Script =======================================
//= Horror Toy Factory
//===== Description: =========================================
//= [Official Conversion]
//= Horror Toy Factory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. Uses official script structure and text
//=     from iRO. [Capuche]
//============================================================

xmas,237,303,3	script	Catherine Jet Johnson	4_F_SKULL06GIRL,{
		if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "อ้าว..จะเข้าไปเลยเหรอจ๊ะ?";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "คุณควรปลดปล่อยการแปลงร่างของคุณหากคุณต้องการที่จะเข้าไป มันอาจจะเป็นอันตรายได้ในหลายๆ ด้าน";
				close;
		}

		if (BaseLevel < 140) {
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "อ่ารอ!"+ ( Sex ? "ผู้ชาย" : "ผู้หญิง" ) +"เหมือนคุณ!";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ถ้าต้องกลับมาก็ค่อยมาทีหลังก็ได้ หึๆ.. คงต้อง Lv.140 สินะ?";
				close;
		}

		mes "^ff0000ระวังการฝึกฝนหรือวิธีอื่น ๆ นอกเหนือจากการล่ามอนสเตอร์ซึ่งไม่ถือเป็นการฆ่าตามปกติ^000000";
		next;
		mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
		switch( isbegin_quest(12330) ) {
		case 0:
				mes "อย่ากลัวฉันเลย ตอนนี้ฉันดูน่าเกลียด แต่ก่อนฉันไม่เคยเป็นแบบนี้";
				next;
				mes "^0000ffหญิงสาวหน้ากะโหลกที่อยู่ตรงหน้าคุณกำลังพูดด้วยน้ำเสียงที่คลอเบ้า ช่องว่างที่ควรจะมีดวงตากำลังจ้องมองคุณอยู่^000000";
				next;
				select("What's wrong with your face?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "เรื่องนี้มันยาวนะ.. จะว่าอะไรไหมถ้าฉันจะคุยกับคุณ ถ้าคุณเสียเวลากับคนอย่างฉันล่ะ?";
				next;
				if (select( "Quit.", "Please keep talking." ) == 1) {
						mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
						mes "โอเค ดูแลตัวเองด้วย และอย่าเข้าใกล้ทางเข้าแปลกๆ แบบนั้นเด็ดขาด";
						close;
				}
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "อืมม..จะเริ่มจากตรงไหนดี.. เรื่องราวของเมืองนี้ก่อนดีคะ?..";
				next;
				mes "^0000ffหญิงสาวถอนหายใจแล้วเริ่มพูดด้วยน้ำเสียงแผ่วเบาเหมือนกวีที่เล่าเรื่องเก่าๆ^000000";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "มีโรงงานผลิตของเล่นในเมืองนี้ คนในเมืองนี้ทุกคนทำงานที่นั่น และฉันก็เป็นหนึ่งในพนักงาน";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ตอนแรกพวกเราก็มีความสุขดี เราเคยมารวมตัวกันและพูดคุยกันอย่างเป็นกันเองโดยกินขนมปังขิงและดื่มชาร้อนหลังเลิกงาน";
				next;
				select("I couldn't even imagine, seeing this mood...");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ใช่ เพราะเราไม่สามารถหารายได้จากการห่อของเล่นได้ตลอดไป สถานการณ์เริ่มเลวร้ายลงเมื่อชาวบ้านออกไปทีละคนและสองคน";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ผมก็เป็นคนทำงานเป็นผู้ช่วยเคียงข้างกับช่างทำตุ๊กตาที่ทำตุ๊กตาจนสำเร็จ";
				next;
				mes "^0000ffฉันรู้สึกได้ว่าขากรรไกรอันเรียวเล็กของเธอกำลังสั่นไหวเนื่องมาจากการเปลี่ยนแปลงอารมณ์อันละเอียดอ่อนของเธอ^000000";
				next;
				select("Ask her to continue.");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ในวันที่โรงงานตัดสินใจปิดตัวลง ช่างทำตุ๊กตาได้แต่งตัวตุ๊กตาตัวสุดท้ายที่เขาทำและหลั่งน้ำตา";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "บางทีเขาคิดว่าเขาจะไม่ทำตุ๊กตาอีกแล้ว";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "เขาตั้งชื่อตุ๊กตาตัวสุดท้ายว่า ^0000ffCeline Kimi^000000 และหยุดสายการผลิตทั้งหมดในโรงงาน";
				next;
				select("Did he give a doll a name?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "บางครั้งตุ๊กตาต้องทำงานหนักมากราวกับว่ามันเป็นลูกของผู้ผลิตตุ๊กตา คิมิคือคนที่ใช่สำหรับเขา";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฉันเดาว่าความจริงจังของเขาทำให้คิมิมีชีวิตขึ้นมา";
				next;
				select("Do you mean the doll is alive?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ใช่แล้ว ^0000ffเธอ^000000 กลายเป็นสิ่งมีชีวิต ฉันไม่รู้ว่ามันทำได้อย่างไร แต่เธอก็เดินอย่างเศร้าโศกและโกรธแค้นอยู่ในโรงงานของเล่น";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "เมื่อฉันเห็นเธอครั้งแรก เธอยืนอยู่ข้างช่างทำตุ๊กตาที่เสียชีวิตและล้มลง เธอยืนนิ่งอยู่ที่นั่นด้วยสีหน้าเรียบเฉย";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "จบแค่นี้แล้ว ตื่นมาอีกทีก็อยู่ข้างนอกแล้วหลังจากที่สลบไป หน้าก็เป็นแบบนี้";
				next;
				select("Did Kimi do that?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฉันไม่รู้.. เธอคงจะทำอย่างนั้น หรืออาจเป็นปัญหาอื่นก็ได้ ฉันจำเรื่องนั้นไม่ได้เลย";
				next;
				select("Do you blame her?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "อ๋อ ไม่หรอก ฉันไม่โทษเธอหรอก ฉันแค่อยากรู้ว่าเกิดอะไรขึ้นกับช่างทำตุ๊กตาในวันนั้น";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฉันไม่คิดว่าฉากที่เห็นจะเป็นทุกอย่างหรอกนะ แต่การได้เข้าไปในโรงงานอีกครั้งนั้น... การกลับเข้าไปที่นั่นอีกครั้งนั้น...";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ใช่ค่ะ กลัวและไม่กล้าที่จะทำค่ะ ถ้ามีใครช่วยได้..";
				next;
				if (select( "I am not very helpful to you.", "Maybe I can help you with it." ) == 1) {
						mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
						mes "ขอบคุณที่..รับฟังเรื่องราวของฉัน หวังว่าคุณจะสนุกกับการเดินทางของคุณ..";
						close;
				}
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฮะ? คุณจะช่วยฉันหน่อยได้ไหม?";
				next;
				select("Let's get into the factory.");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "พวกเรา.. ฉันช่วยอะไรคุณไม่ได้เลย นอกจากการบอกทางเท่านั้น นอกจากนี้ มันยังอาจเป็นอันตรายต่อคุณอีกด้วย";
				next;
				select("You can reunite with Kimi, can't you?");
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "คิมิ คุณหมายถึงคิมิใช่ไหม..";
				next;
				mes "^0000ffเธอกำลังคิดเรื่องบางอย่างอย่างตั้งใจพร้อมกับเบิกตากว้าง^000000";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฉันตัดสินใจแล้ว มันคงไม่เป็นประโยชน์กับคุณมากนัก แต่ฉันสามารถบอกแนวทางให้คุณได้";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "รอก่อนนะ รอก่อนจนกว่าฉันจะพร้อม ฉันจะคอยแนะนำคุณได้เมื่อฉันพร้อม";
				setquest 12330;// Skull-faced Girl
				close;
		case 1:
				mes "เสร็จแล้ว ขอฉันลองเปิดประตูที่ปิดอยู่ดู";
				completequest 12330;
				break;
		case 2:
				mes "ดวงวิญญาณของคิมิยังคงประทับอยู่ในผืนดินแห่งนี้ เมื่อใดที่ฉันจะปล่อยให้เธอได้พักผ่อนอย่างสงบ...";
				break;
		}
		next;
		if (getcharid(1) < 1) {
				mes "^0000ffคุณต้องจัดปาร์ตี้ที่มีสมาชิกมากกว่าหนึ่งคนเพื่อจะเข้าไปใน Horror Toy Factory ได้^000000";
				close;
		}
		switch( checkquest(12331,PLAYTIME) ) {
		case -1:
				if (is_party_leader() == false) {
						mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
						mes "เอ่อ..เดี๋ยวก่อน ฉันกำลังคุยกับคนรับผิดชอบอยู่ กรุณารอสักครู่";
						close;
				}
				if (select( "Unlock Horror Toy Factory", "Cancel" ) == 1) {
						mes "ประตูก็จะเปิดเร็วๆ นี้.. คุณจะรอสักครู่ไหม?";
						instance_create("Horror Toy Factory");
				}
				close;
		case 0:
		case 1:
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "กลิ่นสารกันบูดจากโรงงานยังไม่หายไป ไม่สามารถเข้าได้ภายใต้สภาวะนี้";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "ฉันจะห้ามคุณเข้ามาจนกว่ากลิ่นสารกันบูดจะหมดไปจนหมด";
				close;
		case 2:
				mes "^0000ffกลิ่นของสารกันบูดหายไปแล้ว คุณสามารถพูดคุยกับแคเธอรีน เจ็ต จอห์นสันได้^000000";
				erasequest 12331;
				close;
		}
}

xmas,233,305,4	script	Factory Dimension Telep	PORTAL,{
		if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
				mes "[เครื่องเทเลพอร์ตมิติโรงงาน]";
				mes "มีโปรแกรมการศึกษาการทำงานบางอย่างในฐานะคนงานมอนสเตอร์ระหว่างทัวร์ Horror Toy Factory ดังนั้น ^ff0000คุณจึงไม่สามารถเข้าไปได้เมื่อคุณถูกแปลงร่าง^000000";
				close;
		}
		if (BaseLevel < 140) {
				mes "[เครื่องเทเลพอร์ตมิติโรงงาน]";
				mes "คุณไม่มีสิทธิ์ในการเลื่อนมิติ คุณต้องมีเลเวลอย่างน้อย 140";
				close;
		}
		if (getcharid(1) < 1) {
				mes "[เครื่องเทเลพอร์ตมิติโรงงาน]";
				mes "ไม่ได้รับการสนับสนุนใดๆ ครับ กรุณาจัดปาร์ตี้ แล้วมาใหม่หากไปคนเดียวครับ";
				close;
		}
		switch( checkquest(12331,PLAYTIME) ) {
		case -1:
				switch( instance_enter("Horror Toy Factory") ) {
				case 3:
						mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
						close;
				case 2:
						mes "เกตเวย์สำหรับการเลื่อนมิติไม่ได้รับการเปิดใช้งาน";
						close;
				case 1:// dummy
						mes "[เครื่องเทเลพอร์ตมิติโรงงาน]";
						mes "ไม่ได้รับการสนับสนุนใดๆ ครับ กรุณาจัดปาร์ตี้ แล้วมาใหม่หากไปคนเดียวครับ";
						close;
				case 0:
						mapannounce "xmas", getpartyname( getcharid(1) ) +" party's party member "+ strcharinfo(0) +" has entered the Horror Toy Factory.", bc_map, "0x00ff99";
						setquest 12331;// Trail of Toy Factory
						end;
				}
		case 0:
		case 1:
				mes "คุณมีเส้นทางของการเลื่อนมิติอยู่แล้ว การเข้าถึงของคุณถูกปฏิเสธ";
				close;
		case 2:
				mes "^0000ffเส้นทางการเคลื่อนตัวของมิติหายไปแล้ว คุณได้รับอนุญาตให้ใช้เครื่องเคลื่อนย้ายมิติโรงงานได้^000000";
				erasequest 12331;
				close;
		}
}

1@xm_d,112,20,6	script	Catherine Jet Johnson#0	4_F_SKULL06GIRL,{
		if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "คุณจะมาที่นี่ไม่ได้หากคุณอยู่ภายใต้ผลของการแปลงร่างเป็นสัตว์ประหลาด";
				next;
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "กรุณารอจนกว่าการแปลงร่างจะเสร็จสิ้น คุณไม่สามารถย้ายไปยังพื้นที่อื่นได้จนกว่าคุณจะเข้าสู่สภาวะปกติ";
				close;
		}
		if (is_party_leader() == false) {
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "เอ่อ..เดี๋ยวก่อน ฉันกำลังคุยกับคนรับผิดชอบอยู่ กรุณารอสักครู่";
				close;
		}
		mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
		mes "ทุกคนก็อยู่ในสภาพที่ต้องเสื่อมสลายใช่ไหม?";
		npctalk "แคทเธอรีน เจ็ท จอห์นสัน: ทุกคนอยู่ในสภาพที่ต้องเสื่อมทรามใช่ไหม?";
		next;
		switch( select( "Quit the story", "Listen to her story.", "I know your situation. Let's start quickly!" ) ) {
		case 1:
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "โอ้ที่รัก บอกฉันด้วยเมื่อคุณพร้อม";
				close;
		case 2:
				donpcevent instance_npcname("Catherine Jet Johnson#01") + "::OnStart";
				close;
		case 3:
				donpcevent instance_npcname("Catherine Jet Johnson#01") + "::OnStart2";
				close;
		}
}

1@xm_d,112,20,1	script	Catherine Jet Johnson#01	4_F_SKULL06GIRL,{
		end;
OnStart:
		enablenpc instance_npcname("#bgm01");
		enablenpc instance_npcname("Catherine Jet Johnson#01");
		disablenpc instance_npcname("Catherine Jet Johnson#0");
		sleep 3000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: บริเวณนี้เป็นโรงงานหมายเลข 1 เดิมทีใช้เป็นสถานที่เก็บของเล่นและตุ๊กตา ก่อนจะนำมาให้เป็นของขวัญ";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: อ้อ ฉันจำได้หลายอย่าง ถ้ามีใครไม่อยู่ในเครื่องแบบพนักงาน เจ้าหน้าที่ก็จะเข้ามาดุเขา...";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ท จอห์นสัน: อย่างไรก็ตาม...";
		donpcevent instance_npcname("#fac1ct") + "::OnStart";
		sleep 3000;
		mapannounce 'xm_d_map$, "Factory announcement: Wake up, toy factory working time has come...", bc_map, "0x00ff44";
		sleep 6000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: อะ...เกิดอะไรขึ้น ของเล่นและตุ๊กตาถูกทิ้งร้างแทนที่จะเป็นคนงาน ราวกับว่าเป็นลูกจ้าง...";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันคิดว่าวิญญาณอื่นคงอาศัยอยู่ในโรงงานที่ว่างเปล่านี้มานานเกินไปแล้ว";
		sleep 3000;
		mapannounce 'xm_d_map$, "ประกาศของโรงงาน: ควรเก็บขยะและเศษวัสดุอื่นๆ ออกจากพื้นที่ทำงาน เพื่อความปลอดภัยของคุณตลอดเวลา", bc_map, "0x00ff44";
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ท จอห์นสัน: มันเหมือนกับตอนที่มนุษย์ทำงานที่นี่";
		sleep 3000;
		mapannounce 'xm_d_map$, "Factory announcement: Let's make presents for every child's dream today.", bc_map, "0x00ff44";
		sleep 3000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: แต่ไม่ใช่เวลาที่จะมีความสุข เมื่อเราพบพื้นที่โรงงานแห่งสุดท้ายแล้ว เราจะต้องหยุดสายการผลิตทั้งหมด";
		sleep 1000;
		mapannounce 'xm_d_map$, "Factory announcement: Please start product line No.1. Don't forget to wear a safety helmet! This means you Bob!", bc_map, "0x00ff44";
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: บางทีเราควรบูรณะของเล่นและกล่องของขวัญเหล่านั้นให้กลับไปสู่สภาพเดิมเสียดีกว่า";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: เอ๊ะ? ฉัน... ฉันหมายถึง... เพื่อให้พวกเขากลับไปเป็นเหมือนเดิม... ใช่... เราต้องต่อสู้กับพวกเขาเหรอ? ฉันไม่รู้ว่าต้องทำยังไง";
		sleep 2000;
		mapannounce 'xm_d_map$, "Factory announcement: All employees must wear a proper uniform and identification. Please check with security if you do not have yours.", bc_map, "0x00ff44";
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: โอ้พระเจ้า ฉันหวังว่าเจ้าหน้าที่รักษาความปลอดภัยยังคงเป็นมนุษย์อยู่ ในอดีตเคยมีกล่องเครื่องแบบวางอยู่ใกล้ๆ ที่นี่";
		enablenpc instance_npcname("Employees' Uniform Box#1");
		enablenpc instance_npcname("Employees' Uniform Box#2");
		enablenpc instance_npcname("Employees' Uniform Box#3");
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: อ๋อ พวกเขาอยู่ข้างหลังฉัน คุณควรเปลี่ยนเป็นชุดยูนิฟอร์มดีกว่า โชคดีที่ฉันยังมีบัตรพนักงานอยู่";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันจะหาทางไปแถวนี้เอง เจอกันแถวๆ สายการผลิตที่สองนะ เชียร์ๆ หน่อย มันเป็นโรงงานของเล่น ไม่น่าจะแย่ขนาดนั้นนะ!!";
		sleep 6000;
		disablenpc instance_npcname("Catherine Jet Johnson#01");
		disablenpc instance_npcname("#bgm01");
		end;

OnStart2:
		enablenpc instance_npcname("#bgm01");
		enablenpc instance_npcname("Catherine Jet Johnson#01");
		disablenpc instance_npcname("Catherine Jet Johnson#0");
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ท จอห์นสัน: อ๋อ.. เราเคยสำรวจที่นี่มาก่อนแล้วเหรอ โอเค เจอกันที่เดิมอีกครั้งนะ";
		sleep 6000;
		disablenpc instance_npcname("Catherine Jet Johnson#01");
		enablenpc instance_npcname("Employees' Uniform Box#1");
		enablenpc instance_npcname("Employees' Uniform Box#2");
		enablenpc instance_npcname("Employees' Uniform Box#3");
		mapannounce 'xm_d_map$, "Factory announcement: Wake up, toy factory working time has come...", bc_map, "0x00ff44";
		sleep 6000;
		mapannounce 'xm_d_map$, "ประกาศของโรงงาน: ควรเก็บขยะและเศษวัสดุอื่นๆ ออกจากพื้นที่ทำงาน เพื่อความปลอดภัยของคุณตลอดเวลา", bc_map, "0x00ff44";
		sleep 6000;
		mapannounce 'xm_d_map$, "Factory announcement: Let's make presents for every child's dream today.", bc_map, "0x00ff44";
		donpcevent instance_npcname("#fac1ct") + "::OnStart";
		disablenpc instance_npcname("#bgm01");
		end;
}

1@xm_d,112,20,0	script	#bgm01	-1,9,9,{
		end;
OnTouch:
		playBGM "99";
		end;
}

1@xm_d,13,105,6	script	Employees' Uniform Box#1	4_NONMYSTCASE,{
		progressbar "ffff00",1;
		playBGM "52";
		.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@mob_id_transform == 1246 || .@mob_id_transform < 1) {
				mes "^0000ffคุณใส่ชุดยูนิฟอร์มโรงงาน ดูเหมือนว่าชุดยูนิฟอร์มจะยังเหลืออยู่ในกล่องอีกสองสามชุด^000000";
				transform 1246,180000;// COOKIE_XMAS
				close;
		}
		mes "^ff0000คุณกำลังอยู่ในช่วงการเปลี่ยนแปลง";
		mes "คุณไม่สามารถใช้กล่องเครื่องแบบได้หากแปลงร่างไปแล้ว^000000";
		close;
}
1@xm_d,116,16,6	duplicate(Employees' Uniform Box#1)	Employees' Uniform Box#2	4_NONMYSTCASE
1@xm_d,10,20,6	duplicate(Employees' Uniform Box#1)	Employees' Uniform Box#3	4_NONMYSTCASE

1@xm_d,1,5,3	script	#fac1ct	CLEAR_NPC,{
		end;
OnStart:
		.@event$ = instance_npcname("#fac1ct") + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,16,24,114,112, "Vicious Cookie",2989,31, .@event$;	// XM_COOKIE
		areamonster 'xm_d_map$,16,24,114,112, "Evil Dwelling Box",2991,36, .@event$;// XM_MYSTCASE
		end;

OnMyMobDead:
		if (mobcount( 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead" ) < 30)
				initnpctimer;
		end;

OnTimer1000:
		killmonster 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead";
		enablenpc instance_npcname("#fac1bs");
		mapannounce 'xm_d_map$, "Coordinator's announcement: Huh? Where's everyone! How dare workers be absent from the line!", bc_map, "0xff8800";
		for ( .@i = 61; .@i <= 89; .@i++ )
				disablenpc instance_npcname( "alert#"+ .@i );
		stopnpctimer;
		end;
}

1@xm_d,71,129,3	script	#fac1bs	4_M_COOKIE,{
		if (is_party_leader() == true) {
				.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
				mes "[ผู้ประสานงาน]";
				if (.@mob_id_transform == 1246) {
						mes "ฉันหาคนงานคนอื่นไม่เจอ เราไม่มีเวลามานั่งชักช้า เด็กๆ กำลังรอของขวัญอยู่";
						next;
						mes "[ผู้ประสานงาน]";
						mes "รีบๆ หน่อยสิ! เอากล่องของขวัญไปไว้ตรงนั้นสิ แล้วมุ่งหน้าไปทางทิศตะวันออก";
						enablenpc instance_npcname("#pck1");
						npctalk "ผู้ประสานงาน : รีบๆ หน่อยสิ! ถือกล่องของขวัญไปที่นั่นแล้วมุ่งหน้าไปทางทิศตะวันออก";
				}
				else if (.@mob_id_transform == 1249)
						mes "ทำไมคุณถึงอยู่แถวนี้ล่ะ เอากล่องของขวัญมาไว้ตรงนี้สิ แล้วเดินไปทางทิศตะวันออก";
				else {
						mes "อะไรนะ!? มนุษย์!!!";
						donpcevent instance_npcname("#fac1bs") + "::OnAlert";
				}
				close;
		}
		end;

OnAlert:
		.@npc_name$ = instance_npcname("#fac1bs");
		.@event$ = .@npc_name$ + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		npctalk "ผู้ประสานงาน: การ์ด! การ์ดอยู่ไหน! มนุษย์กำลังรุกราน!!";
		sleep 3000;
		disablenpc .@npc_name$;
		for ( .@i = 0; .@i <= 10; .@i++ ) {
				areamonster 'xm_d_map$,61,118,71,128, "Guard",2990,1, .@event$;// XM_CRUISER
				sleep 300;
		}
		initnpctimer;
		end;
OnMyMobDead:
		end;

OnTimer60000:
		.@npc_name$ = instance_npcname("#fac1bs");
		killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
		enablenpc .@npc_name$;
		npctalk "ผู้ประสานงาน: เรื่องแบบนี้เกิดขึ้นบ่อยมากในสมัยนี้...";
		stopnpctimer;
		end;
}

1@xm_d,65,127,6	script	#pck1	4_NONMYSTCASE,{
		progressbar "ffff00",1;
		.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@mob_id_transform == 1246) {
				mes "^0000ffคุณกำลังถือของขวัญที่บรรจุหีบห่อไว้^000000";
				transform 1249,180000;// MYSTCASE
		}
		else if (.@mob_id_transform == 1249)
				mes "^009900กล่องอยู่ในมือคุณแล้ว คุณไม่สามารถถือกล่องได้เกินหนึ่งกล่อง^000000";
		else
				mes "^ff0000คุณไม่ได้อยู่ในเครื่องแบบ ผู้ประสานงานไม่อนุญาตให้คุณหยิบมันขึ้นมา^000000";
		close;
}

1@xm_d,76,129,0	script	#fac1wp	WARPNPC,2,2,{
		end;
OnTouch:
		if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
				warp 'xm_d_map$,88,129;
				playBGM "54";
		}
		end;
}

1@xm_d,179,129,0	script	#fac2wp	WARPNPC,2,2,{
		end;
OnTouch:
		if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
				warp 'xm_d_map$,183,100;
				playBGM "54";
		}
		end;
}

1@xm_d,1,5,3	script	#alert1	CLEAR_NPC,{
		end;
OnStart:
		.@r = rand(1,10);
		if (.@r == 1)
				mapannounce 'xm_d_map$, "Factory announcement: Outsiders are spotted on the product line. Guards, please move out immediately.",bc_map,"0x00ffff";
		else if (.@r == 2)
				mapannounce 'xm_d_map$, "Factory announcement: Invaders have been identified. Distinction Code AX0829. Type: Human. Chase them away.", bc_map, "0x00ffff";
		else if (.@r == 3)
				mapannounce 'xm_d_map$, "Guard's announcement: Please leave here immediately, outsiders.", bc_map, "0xffff00";
		else if (.@r == 4)
				mapannounce 'xm_d_map$, "Factory announcement: Outsiders, hold up your hands and surrender. Otherwise, I will shoot you.", bc_map, "0x00ffff";
		else if (.@r == 5)
				mapannounce 'xm_d_map$, "Factory announcement: Dispatch the guards. Suppress the invaders.", bc_map, "0x00ffff";
		else if (.@r == 6)
				mapannounce 'xm_d_map$, "CAUTION: The plant manger is coming to inspect. Wipe out the outsiders.", bc_map, "0xff4444";
		else if (.@r == 7)
				mapannounce 'xm_d_map$, "Factory announcement: Not good news. Outside creatures are detected. Guards, please mobilize.", bc_map, "0x00ff88";
		else if (.@r == 8)
				mapannounce 'xm_d_map$, "Factory announcement: Outsiders or invaders are obstacles to factory's operation. You can kill them if necessary.", bc_map, "0xff9999";
		else if (.@r == 9)
				mapannounce 'xm_d_map$, "Factory announcement: Okay that's it, party's over! Get out of my house!", bc_map, "0x00ffff";
		else
				mapannounce 'xm_d_map$, "Guard's announcement: Invaders are spotted! They seem human! I will blip them off!", bc_map, "0xffff00";
		end;
}

1@xm_d,10,24,0	script	alert#61	-1,10,10,{
		end;
OnTouch_:
		if (getstatus(SC_MONSTER_TRANSFORM,1) != 1246) {
				switch( atoi(strnpcinfo(2)) ) {
						case 63: case 66:
						case 69: case 72:
						case 79: case 82:
						case 85: case 88:
								.@count = 4;
								break;
						case 61: case 64:
						case 67: case 70:
						case 73: case 75:
						case 77: case 80:
						case 83: case 86:
						case 89:
								.@count = 6;
								break;
						default:
								.@count = 5;
								break;
				}
				getmapxy .@map$, .@x, .@y;
				.@npc_name$ = instance_npcname( strnpcinfo(0) );
				.@event$ = .@npc_name$ + "::OnMyMobDead";
				playBGM "125";
				specialeffect EF_VENOMDUST;
				donpcevent instance_npcname("#alert1") + "::Onstart";
				disablenpc .@npc_name$;
				killmonster 'xm_d_map$, .@event$;
				areamonster 'xm_d_map$,(.@x-10),(.@y-10),(.@x+10),(.@y+10), "Toy Factory Guard",2990,.@count, .@event$;// XM_CRUISER
				initnpctimer;
		}
		end;

OnTimer45000:
		enablenpc();
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) +"::OnMyMobDead";
		stopnpctimer;
		end;

OnMyMobDead:
		end;
}
1@xm_d,30,24,0	duplicate(alert#61)	alert#62	-1,10,10
1@xm_d,50,24,0	duplicate(alert#61)	alert#63	-1,10,10
1@xm_d,70,24,0	duplicate(alert#61)	alert#64	-1,10,10
1@xm_d,90,24,0	duplicate(alert#61)	alert#65	-1,10,10
1@xm_d,10,44,0	duplicate(alert#61)	alert#66	-1,10,10
1@xm_d,30,44,0	duplicate(alert#61)	alert#67	-1,10,10
1@xm_d,50,44,0	duplicate(alert#61)	alert#68	-1,10,10
1@xm_d,70,44,0	duplicate(alert#61)	alert#69	-1,10,10
1@xm_d,90,44,0	duplicate(alert#61)	alert#70	-1,10,10
1@xm_d,110,44,0	duplicate(alert#61)	alert#71	-1,10,10
1@xm_d,10,64,0	duplicate(alert#61)	alert#72	-1,10,10
1@xm_d,30,64,0	duplicate(alert#61)	alert#73	-1,10,10
1@xm_d,50,64,0	duplicate(alert#61)	alert#74	-1,10,10
1@xm_d,70,64,0	duplicate(alert#61)	alert#75	-1,10,10
1@xm_d,90,64,0	duplicate(alert#61)	alert#76	-1,10,10
1@xm_d,110,64,0	duplicate(alert#61)	alert#77	-1,10,10
1@xm_d,10,84,0	duplicate(alert#61)	alert#78	-1,10,10
1@xm_d,30,84,0	duplicate(alert#61)	alert#79	-1,10,10
1@xm_d,50,84,0	duplicate(alert#61)	alert#80	-1,10,10
1@xm_d,70,84,0	duplicate(alert#61)	alert#81	-1,10,10
1@xm_d,90,84,0	duplicate(alert#61)	alert#82	-1,10,10
1@xm_d,110,84,0	duplicate(alert#61)	alert#83	-1,10,10
1@xm_d,10,104,0	duplicate(alert#61)	alert#84	-1,10,10
1@xm_d,30,104,0	duplicate(alert#61)	alert#85	-1,10,10
1@xm_d,50,104,0	duplicate(alert#61)	alert#86	-1,10,10
1@xm_d,70,104,0	duplicate(alert#61)	alert#87	-1,10,10
1@xm_d,90,104,0	duplicate(alert#61)	alert#88	-1,10,10
1@xm_d,110,104,0	duplicate(alert#61)	alert#89	-1,10,10
1@xm_d,155,20,0	duplicate(alert#61)	alert#90	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#91	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#92	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#93	-1,10,10
1@xm_d,180,20,0	duplicate(alert#61)	alert#94	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#95	-1,10,10
1@xm_d,180,80,0	duplicate(alert#61)	alert#96	-1,10,10
1@xm_d,205,20,0	duplicate(alert#61)	alert#97	-1,10,10
1@xm_d,205,50,0	duplicate(alert#61)	alert#98	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#99	-1,10,10
1@xm_d,205,110,0	duplicate(alert#61)	alert#100	-1,10,10
1@xm_d,230,20,0	duplicate(alert#61)	alert#101	-1,10,10
1@xm_d,230,50,0	duplicate(alert#61)	alert#102	-1,10,10
1@xm_d,230,80,0	duplicate(alert#61)	alert#103	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#104	-1,10,10


1@xm_d,185,100,6	script	Catherine Jet Johnson#2	4_F_SKULL06GIRL,{
		if (is_party_leader() == true) {
				mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
				mes "คุณโชคดีที่มาถึงที่นี่อย่างปลอดภัย";
				next;
				switch( select( "Quit the story", "Listen to her tactics.", "I know what to do. Proceed quickly!" ) ) {
				case 1:
						mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
						mes "โอ้ที่รัก บอกฉันด้วยเมื่อคุณพร้อม";
						close;
				case 2:
						donpcevent instance_npcname("Catherine Jet Johnson#21") + "::OnStart";
						close;
				case 3:
						donpcevent instance_npcname("Catherine Jet Johnson#21") + "::OnStart2";
						close;
				}
		}
		mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
		mes "รอสักครู่ ฉันกำลังคุยกับผู้จัดการของคุณ ดังนั้นกรุณารอก่อน";
		close;
}

1@xm_d,185,100,6	script	Catherine Jet Johnson#21	4_F_SKULL06GIRL,{
		end;
OnStart:
		callsub S_Skip,0;
OnStart2:
		callsub S_Skip,1;
S_Skip:
		enablenpc instance_npcname("#bgm06");
		enablenpc instance_npcname("Catherine Jet Johnson#21");
		disablenpc instance_npcname("Catherine Jet Johnson#2");
		sleep 3000;
		if (getarg(0) == 1)
				npctalk "แคเธอรีน เจ็ต จอห์นสัน: หากคุณพบเบาะแสทั้งหมดแล้ว ให้ลองค้นหาที่อื่น ฉันจะมองหาจุดที่ฉันพบกับช่างทำตุ๊กตาเมื่อครั้งที่แล้วที่ฉันมาที่นี่";
		else {
				npctalk "แคทเธอรีน เจ็ท จอห์นสัน: ที่นี่คือโรงงานหมายเลข 2";
				sleep 5000;
				npctalk "แคทเธอรีน เจ็ท จอห์นสัน: เมื่อก่อนมันเคยเต็มไปด้วยผู้คน แต่ตอนนี้มันไม่ใช่แล้ว";
				sleep 5000;
				npctalk "แคทเธอรีน เจ็ต จอห์นสัน: อ๋อ ฉันเพิ่งตระหนักบางอย่างขณะมาที่นี่ เด็กๆ...";
				sleep 5000;
				npctalk "แคเธอรีน เจ็ต จอห์นสัน: ฉันจะพูดอย่างไรดี... มีวิญญาณมากมายที่ดูน่ากลัว แต่ก็เศร้าโศกอย่างยิ่ง";
				sleep 5000;
				npctalk "แคทเธอรีน เจ็ต จอห์นสัน: พวกมันน่าสงสารมาก แต่โปรดทำให้พวกมันสงบลงหากพวกมันโจมตีคุณ โชคดีที่พวกมันจะไม่เข้ามาหาฉัน";
				sleep 5000;
				npctalk "แคทเธอรีน เจ็ท จอห์นสัน: หากคุณเห็นคนงานเล่นอยู่ขณะกำลังค้นหา คุณจะถามเกี่ยวกับผู้ผลิตตุ๊กตาหรือไม่?";
				sleep 5000;
				npctalk "แคเธอรีน เจ็ต จอห์นสัน: เขาอาจถูกส่งไปยังสวรรค์หากพวกเขาระลึกถึงความทรงจำของช่างทำตุ๊กตา นั่นคือความหวังที่ดีที่สุด";
				sleep 5000;
				npctalk "แคเธอรีน เจ็ต จอห์นสัน: หากคุณพบเบาะแสทั้งหมดแล้ว ให้ลองค้นหาที่อื่น ฉันจะมองหาจุดที่ฉันพบกับช่างทำตุ๊กตาเมื่อครั้งที่แล้วที่ฉันมาที่นี่";
				sleep 5000;
				npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ขอโทษที ฉันช่วยคุณได้ไม่มาก เจอกันใหม่";
		}
		sleep 6000;
		disablenpc instance_npcname("Catherine Jet Johnson#21");
		enablenpc instance_npcname("Employees' Uniform Box#4");
		donpcevent instance_npcname("#fac2ct") + "::OnStart";

		for ( .@i = 1; .@i <= 10; .@i++ )
				enablenpc instance_npcname( "Worker#"+ .@i );
		disablenpc instance_npcname("#bgm06");
		end;
}

1@xm_d,185,100,0	script	#bgm06	-1,9,9,{
		end;
OnTouch:
		playBGM "99";
		end;
}

1@xm_d,185,94,6	script	Employees' Uniform Box#4	4_NONMYSTCASE,{
		progressbar "ffff00",1;
		playBGM "128";
		.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@mob_id_transform == 1246 || .@mob_id_transform == 1249 || .@mob_id_transform < 1) {
				mes "^0000ff>คุณเปลี่ยนชุดแล้วนะ^000000";
				transform 1246,300000;// COOKIE_XMAS
				close;
		}
		mes "^ff0000คุณได้แปลงร่างเป็นอีกแบบหนึ่งแล้ว";
		mes "คุณดูเหมือนจะใส่เครื่องแบบทับเขี้ยวของคุณไม่ได้เลย^000000";
		close;
}

1@xm_d,1,5,3	script	#fac2ct	CLEAR_NPC,{
		end;
OnEnd:
		killmonster 'xm_d_map$, instance_npcname("#fac2ct") + "::OnMyMobDead";
		end;

OnStart:
		.@event$ = instance_npcname("#fac2ct") + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,140,18,240,120, "Malicious Baby Ghost",2993,19, .@event$;	// XM_HYLOZOIST
		areamonster 'xm_d_map$,140,18,240,120, "Evil Dwelling Box",2991,16, .@event$;		// XM_MYSTCASE
		areamonster 'xm_d_map$,140,18,240,120, "Abandoned Teddy bear",2995,22, .@event$;	// XM_TEDDY_BEAR
		areamonster 'xm_d_map$,140,18,240,120, "Creepy Demon",2992,16, .@event$;			// XM_LUDE
		end;
OnMyMobDead:
		end;
}

1@xm_d,1,2,3	script	#fac2wpc	CLEAR_NPC,{
		end;
OnStart:
		.@fac_open = 'worker[1] + 'worker[2] + 'worker[3] + 'worker[4] + 'worker[5] + 'worker[6] + 'worker[7] + 'worker[8] + 'worker[9] + 'worker[10];
		if (.@fac_open == 10) {
				enablenpc instance_npcname("#fac3wp");
				enablenpc instance_npcname("#fac3wp2");
				donpcevent instance_npcname("#fac2ct") + "::OnEnd";
				mapannounce 'xm_d_map$, "Factory announcement: Everyone has gone home. The line is shut down and the employee's lounge is open.", bc_map, "0x00ff44";
				for ( .@i = 90; .@i <= 104; .@i++ )
						disablenpc instance_npcname( "alert#"+ .@i );
		}
		else
				mapannounce 'xm_d_map$, "Factory announcement: Now there are " + (10 - .@fac_open) + " people on the packaging line. Take care of yourself.", bc_map, "0x00ff44";
		end;
}

1@xm_d,155,98,3	script	Worker#1	4_M_COOKIE,{
		if (getstatus(SC_MONSTER_TRANSFORM,1) == 1246) {
				.@num = atoi(strnpcinfo(2));
				mes "[คนงาน]";
				mes "ห๊ะ? เกิดอะไรขึ้น?";
				next;
				if (select( "No, nothing special", "Do you know about the doll maker?" ) == 1) {
						mes "[คนงาน]";
						if (.@num == 8)
								mes "ตอนนี้ฉันกำลังทำงานอยู่ค่ะ ยุ่งมากๆ เลย!";
						else
								mes "ฉันยุ่งกับงานมาก คุณหยุดคุยได้ไหม?";
						close;
				}
				close2;
				setpcblock PCBLOCK_NPC, true;
				switch(.@num) {
				case 1:
						npctalk "คนงาน: อ๋อ คุณปู่เหรอ เขาเป็นคนดีมาก เขาทาน้ำมันให้เราทุกวันและทำความสะอาดฝุ่นบ่อยมาก";
						sleep2 3000;
						npctalk "คนงาน : ขอให้ไม่ตายไปเสียก่อนนะครับ อ่า... อ่า... รู้สึกดีขึ้นแล้วครับ";
						break;
				case 2:
						npctalk "คนงาน: คิมิฆ่าปู่ช่างฝีมือเหรอ ใครเป็นคนพูด ไม่จริงหรอก คิมิจะไปช่วยเขาต่างหาก!";
						sleep2 3000;
						npctalk "คนงาน : อ่า... เอ่อ เอ่อ แล้วทำไมร่างกายของฉันถึงได้...";
						break;
				case 3:
						npctalk "คนงาน: เขามีโรคหัวใจเรื้อรัง แต่เกิดขึ้นกะทันหันและเขาก็เสียชีวิต คิมิพบเขาและปฐมพยาบาลเขา แต่..";
						sleep2 3000;
						npctalk "คนงาน : เอ่อ..คุณทำอะไรกับฉันรึเปล่า ฉันรู้สึกแปลกๆ...";
						break;
				case 4:
						npctalk "คนงาน: คิมิไม่ขยับตัวเลยสักพักแล้วนั่งลงเหมือนตุ๊กตาธรรมดาๆ";
						sleep2 3000;
						npctalk "คนงาน : เธอกำลังจะบินวิญญาณของเธอไปตลอดกาล... เหมือนฉันตอนนี้... โอ้ พระเจ้า... เกิดอะไรขึ้นกับฉันนะ?";
						break;
				case 5:
						npctalk "คนงาน: โอ้ คิมิ..ที่รัก.. เธอคิดว่าคนทำตุ๊กตาเสียชีวิตแล้วเพราะเธอทำให้เขาตกใจ";
						sleep2 3000;
						npctalk "คนงาน: คุณปู่ดีใจมากที่คิมิได้ย้ายบ้าน..";
						break;
				case 6:
						npctalk "คนงาน: เขาเห็นตุ๊กตามีชีวิตอยู่ชั่วขณะ นั่นคือสิ่งที่คิมิเห็นเป็นอย่างแรก";
						sleep2 3000;
						npctalk "คนงาน: และสิ่งแรกที่เธอเห็นคือการตายของเขา ช่างเป็นความทุกข์ทรมานจริงๆ";
						break;
				case 7:
						npctalk "คนงาน : คิมิสามารถได้ยินเสียงปู่และรู้สึกได้ก่อนที่เธอจะถูกสิงสู่ในร่าง";
						sleep2 3000;
						npctalk "คนงาน : บางทีความรักของปู่คงทำให้เธอขยับตัวและมีชีวิตชีวาขึ้น อ่า... ฉันง่วงแล้ว...";
						break;
				case 8:
						npctalk "คนงาน: คุณปู่เป็นคนดี สงสารคิมิ คิมิรักคุณปู่ แต่คุณปู่เสียแล้ว ฉันเสียใจ!";
						sleep2 3000;
						break;
				case 9:
						npctalk "คนงาน: เขาประหลาดใจมากเมื่อเห็นดวงตาที่เปิดกว้างของคิมิและหัวใจของเขา... มันกลายเป็นแบบนั้น";
						sleep2 3000;
						npctalk "คนงาน : ยังไงก็ตาม ทำไมร่างกายของฉันถึง... ดูเหมือนจะลอยอยู่กลางอากาศ... มีอะไรแปลกๆ นะ";
						break;
				case 10:
						npctalk "คนงาน: คิมิไม่ได้ทำร้ายปู่! คิมิต้องการช่วยปู่! ผู้คนกลัวคิมิ แต่คิมิเป็นคนใจดี! คิมิเป็นเด็กดี!";
						sleep2 3000;
						npctalk "คนงาน: คุณปู่... ผมคิดถึงคุณปู่ คุณปู่...";
						break;
				}
				sleep2 3000;
				disablenpc();
				if ('worker[.@num] == 0) {
						'worker[.@num] = 1;
						donpcevent instance_npcname("#fac2wpc") + "::OnStart";
				}
				setpcblock PCBLOCK_NPC, false;
				end;
		}
		mes "[คนงาน]";
		mes "อะไรนะ!? คุณไม่ใช่คนของเรา!!";
		donpcevent instance_npcname( strnpcinfo(0) ) + "::OnAlert";
		close;

OnAlert:
		.@npc_name$ = instance_npcname( strnpcinfo(0) );
		killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
		.@num = atoi(strnpcinfo(2));
		if (.@num == 8)
				npctalk "คนงาน : ยาม! ยาม!!";
		else
				npctalk "คนงาน: ยาม ยาม! คุณอยู่ไหน! มนุษย์อยู่ที่นี่!!";
		sleep 3000;
		switch(.@num) {
		case 9:
				setarray .@coord[0],233,27;
				break;
		case 10:
				setarray .@coord[0],209,27;
				break;
		default:
				getmapxy .@map$, .@coord[0], .@coord[1], BL_NPC;
				break;
		}
		areamonster 'xm_d_map$,(.@coord[0]-8),(.@coord[1]-8),(.@coord[0]+8),(.@coord[1]+8), "Guard",2990,21, .@npc_name$ + "::OnMyMobDead";
		disablenpc .@npc_name$;
		if ('worker[.@num] == 0) {
				'worker[.@num] = 1;
				donpcevent instance_npcname("#fac2wpc") + "::OnStart";
		}
		initnpctimer;
		end;
OnTimer60000:
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		stopnpctimer;
		end;

OnMyMobDead:
		end;
}
1@xm_d,130,72,3	duplicate(Worker#1)	Worker#2	4_M_COOKIE
1@xm_d,134,34,1	duplicate(Worker#1)	Worker#3	4_M_COOKIE
1@xm_d,195,28,3	duplicate(Worker#1)	Worker#4	4_M_COOKIE
1@xm_d,228,30,1	duplicate(Worker#1)	Worker#5	4_M_COOKIE
1@xm_d,203,55,3	duplicate(Worker#1)	Worker#6	4_M_COOKIE
1@xm_d,132,52,1	duplicate(Worker#1)	Worker#7	4_M_COOKIE
1@xm_d,162,52,1	duplicate(Worker#1)	Worker#8	4_M_COOKIE
1@xm_d,242,17,5	duplicate(Worker#1)	Worker#9	4_M_COOKIE
1@xm_d,209,15,3	duplicate(Worker#1)	Worker#10	4_M_COOKIE


// Note : aegis script have OnClick part
1@xm_d,131,208,0	script	Captured Santa#2	4_M_SANTA,10,10,{
		end;
OnTouch_:
		disablenpc instance_npcname("Captured Santa#2");
		enablenpc instance_npcname("Captured Santa#3");
		donpcevent instance_npcname("Antonio#1") + "::OnStart";
		enablenpc instance_npcname("#bgm04");
		end;
}

1@xm_d,131,208,8	script	Captured Santa#3	4_M_SANTA,{
		mes "[ซานต้าถูกจับ]";
		mes "แม้ว่าจะไม่มีใครเป็นเจ้าของมันก็ไม่ถูกต้องที่จะขโมยมันมาจากทุกคน";
		close;
}

1@xm_d,131,213,4	script	Antonio#1	4_M_ANTONIO,{
		mes "[อันโตนิโอ]";
		mes "เฮ้ ซานต้า อย่าทำตัวน่ารำคาญนักสิ คุณลุง ความยุติธรรมก็คือความยุติธรรม 555";
		close;

OnStart:
		.@antonio$ = instance_npcname("Antonio#1");
		.@santa$ = instance_npcname("Captured Santa#3");
		sleep 3000;
		npctalk "อันโตนิโอ: ฟังฉันนะซานต้า~ ฉันหมายถึง ฉันชอบโรงงานแห่งนี้นะ", .@antonio$;
		sleep 3000;
		npctalk "อันโตนิโอ: ไม่มีใครเป็นเจ้าของมัน~ และมีสิ่งของหลายอย่างที่ฉันสามารถใช้ได้", .@antonio$;
		sleep 3000;
		npctalk "ซานต้าที่ถูกจับ: คุณโง่จัง! ฉันแค่ต้องการจะแน่ใจว่าคุณติดตามคุณมา ตอนนี้คุณขโมยจากโรงงานของทุกคน!", .@santa$;
		sleep 4000;
		npctalk "อันโตนิโอ: เฮ้ ถ้าฉันมีของขวัญเหลือเยอะจะถือเป็นเรื่องใหญ่ไหม ฉันไม่รู้เรื่องนั้นเลย~", .@antonio$;
		sleep 4000;
		npctalk "ซานต้าที่ถูกจับ: เฮ้อ... โอเค แต่ลองคิดดูว่าเด็กๆ จะรู้สึกอย่างไรเมื่อพวกเขาได้รับของขวัญเหล่านั้น", .@santa$;
		sleep 4000;
		npctalk "ซานต้าที่ถูกจับ: พวกเขาจะรู้สึกอย่างไรหากรู้ว่าคุณแจกของที่ไม่มีเจ้าของให้กับพวกเขา?", .@santa$;
		sleep 4000;
		npctalk "อันโตนิโอ : อืม...", .@antonio$;
		sleep 2000;
		npctalk "อันโตนิโอ: บางที... ฉันอาจจะมีความสุขก็ได้นะ?! ยังไงก็ตาม นั่นคือของขวัญ คิคิคิคิ", .@antonio$;
		sleep 5000;
		npctalk "ซานต้าถูกจับ: ใครบอกว่าของขวัญคือปัญหา! คุณควรหยุดการขโมยนี้ได้แล้ว อันโตนิโอ!", .@santa$;
		sleep 5000;
		mapannounce 'xm_d_map$, "Factory announcement: The sending preparations have finished in Factory No.3.", bc_map, "0x00ff44";
		sleep 5000;
		mapannounce 'xm_d_map$, "Factory announcement: Delivery employees, please stand by.", bc_map, "0x00ff44";
		sleep 3000;
		npctalk "อันโตนิโอ: โอ้! โห! ของขวัญมาช้าจังเลยนะ วันนี้ไปปาร์ตี้กันไหมล่ะ!", .@antonio$;
		sleep 4000;
		npctalk "อันโตนิโอ: เฮ้ มนุษย์ ถ้าอยากช่วยก็ติดตามฉันมาได้ ฉันไม่เป็นไร เว้นแต่ว่าคุณจะรบกวนฉัน!", .@antonio$;
		sleep 4000;
		disablenpc .@antonio$;
		sleep 2000;
		npctalk "ซานต้าที่ถูกจับ: ฮะ.. ไอ้หมอนั่นมันไม่มีจรรยาบรรณเลยนะ!", .@santa$;
		sleep 4000;
		npctalk "ซานต้าที่ถูกจับ: เฮ้ คุณช่วยไล่อันโตนิโอออกไปหน่อยได้ไหม เขาอาจจะวิ่งหนีไปได้ถ้าคุณตีเขาหลายๆ ครั้ง", .@santa$;
		sleep 5000;
		npctalk "ซานต้าถูกจับ: ไม่ถูกต้องที่จะขโมยของจากชุมชน!", .@santa$;

		disablenpc instance_npcname("#bgm04");
		enablenpc instance_npcname("#fac4wp");
		enablenpc instance_npcname("#fac4wp2");
		donpcevent instance_npcname("#fac3ct") + "::OnStart";
		donpcevent instance_npcname("#fac3ct2") + "::OnStart";
		donpcevent instance_npcname("#fac3ct3") + "::OnStart";
		sleep 3000;
		enablenpc instance_npcname("#bgm05");
		end;
}

1@xm_d,131,208,0	script	#bgm04	-1,9,9,{
		end;
OnTouch:
		playBGM "54";
		end;
}

1@xm_d,131,208,0	script	#bgm05	-1,9,9,{
		end;
OnTouch:
		playBGM "105";
		end;
}

1@xm_d,1,5,3	script	#fac3ct	CLEAR_NPC,{
		end;
OnEnd:
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		end;
OnStart:
		.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,13,144,121,248, "Dancing Marionette",2994,37, .@event$;	// XM_MARIONETTE
		areamonster 'xm_d_map$,13,144,121,248, "Decorated Evil Tree",2987,31, .@event$;	// XM_TREE
		areamonster 'xm_d_map$,13,144,121,248, "Abandoned Teddy bear",2995,43, .@event$;// XM_TEDDY_BEAR
		areamonster 'xm_d_map$,13,144,121,248, "Creepy Demon",2992,31, .@event$;		// XM_LUDE
		end;
OnMyMobDead:
		end;
}

1@xm_d,1,5,3	script	#fac3ct2	CLEAR_NPC,{
		end;
OnEnd:
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		end;
OnStart:
		.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,159,215,241,247, "Malicious Baby Ghost",2993,13, .@event$;	// XM_HYLOZOIST
		areamonster 'xm_d_map$,159,215,241,247, "Decorated Evil Tree",2987,11, .@event$;	// XM_TREE
		areamonster 'xm_d_map$,159,215,241,247, "Abandoned Teddy bear",2995,15, .@event$;	// XM_TEDDY_BEAR
		areamonster 'xm_d_map$,159,215,241,247, "Creepy Demon",2992,11, .@event$;			// XM_LUDE
		end;
OnMyMobDead:
		end;
}

1@xm_d,1,5,3	script	#fac3ct3	CLEAR_NPC,{
		end;
OnStart:
		if (rand(1,10) > 3)
				areamonster 'xm_d_map$,13,144,121,248 ,"Antonio",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";// ANTONIO
		else
				areamonster 'xm_d_map$,159,215,241,247, "Antonio",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";
		end;

OnMyMobDead:
		if (mobcount( 'xm_d_map$, instance_npcname("#fac3ct3") + "::OnMyMobDead" ) < 1)
				initnpctimer;
		end;

OnTimer1000:
OnEnd:
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		donpcevent instance_npcname("#fac3ct") + "::OnEnd";
		donpcevent instance_npcname("#fac3ct2") + "::OnEnd";
		donpcevent instance_npcname("#finalbs") + "::OnStart";
		disablenpc instance_npcname("Captured Santa#3");
		mapannounce 'xm_d_map$, "???: I won't harm you if you leave quietly without spoiling it for me.", bc_map, "0xff8800";
		stopnpctimer;
		end;
}

1@xm_d,1,5,3	script	#finalbs	CLEAR_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("Catherine Jet Johnson#5");
		enablenpc instance_npcname("Celine Kimi#0");
		enablenpc instance_npcname("#fac5wp");
		// enablenpc instance_npcname("#fac5wp2");// never enabled
		enablenpc instance_npcname("#jeton1");
		end;
}

/*
// never enabled
1@xm_d,160,208,0	script	#fac5wp2	WARPNPC,2,2,{
		end;
OnTouch:
		warp 'xm_d_map$,145,208;
		end;
}
*/

1@xm_d,233,183,3	script	Celine Kimi#0	4_F_KIMI,{
		mes "[เซลีน คิมิ]";
		mes "มาที่นี่ด้วยหรือ? ทำไมมนุษย์จึงอยากทำลายสิ่งที่เราสร้างไว้นัก?";
		close;
}

1@xm_d,222,183,0	script	#jeton1	-1,7,7,{
		end;
OnTouch_:
		disablenpc instance_npcname("#jeton1");
		donpcevent instance_npcname("Catherine Jet Johnson#5") + "::OnStart";
		enablenpc instance_npcname("#bgm02");
		end;
}

1@xm_d,222,183,0	script	#bgm02	-1,9,9,{
		end;
OnTouch:
		playBGM "101";
		end;
}

1@xm_d,222,183,6	script	Catherine Jet Johnson#5	4_F_SKULL06GIRL,{
		mes "[แคทเธอรีน เจ็ท จอห์นสัน]";
		mes "ว้า..ระวังไว้เถอะ คิมิไม่ได้อยู่ในสถานะปกตินะ";
		close;

OnStart:
		.@kimi$ = instance_npcname("Celine Kimi#0");
		sleep 2000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: คิมิ ฟังฉันนะ ฉันไม่ได้มาที่นี่เพื่อตำหนิคุณ";
		sleep 3000;
		npctalk "Celine Kimi: ทุกคนเกลียดฉัน!... คุณก็ไม่ชอบฉันเหมือนกันเหรอ? ตุ๊กตาขี้เหร่และน่าขนลุกตัวนี้ชอบฉัน...", .@kimi$;
		sleep 4000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: คิมิ ฉันได้ยินเกี่ยวกับคุณจากตุ๊กตาที่นี่ ช่างทำตุ๊กตาชื่นชอบคุณมาก!";
		sleep 4000;
		mapannounce 'xm_d_map$, "Phantom's yell: Lie!", bc_map, "0xff8800";
		sleep 1000;
		npctalk "เซลีน คิมิ: อย่าโกหก ถ้าเป็นเรื่องจริง ทำไมเขาถึงไม่เห็นฉันด้วยสายตารักล่ะ", .@kimi$;
		sleep 4000;
		npctalk "เซลีน คิมิ: ทำไมเขาไม่พูดชื่อฉันล่ะ [คิมิ] [คิมิ!] ฉันแทบรอไม่ไหวที่จะได้ยินเสียงคุณปู่", .@kimi$;
		sleep 4000;
		mapannounce 'xm_d_map$, "Phantom's yell: Yes, Kimi~ Father was frightened! He was scared of you~", bc_map, "0xff8800";
		sleep 3000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: ไม่นะ อย่าฟังนะ คิมิ เขารักคุณจริงๆ";
		sleep 3000;
		npctalk "Celine Kimi: เขา... รักฉันบ้างไหม?", .@kimi$;
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ใช่ค่ะ เมื่อคุณเริ่มเคลื่อนไหว เขาแสดงความกรุณาอย่างมาก...";
		sleep 1000;
		mapannounce 'xm_d_map$, "Phantom's yell: He was scared of your appearance and his heart stopped, Kimi~ You killed him!", bc_map, "0xff8800";
		sleep 3000;
		npctalk "Celine Kimi : ด.. ฉัน.. ฆ่าเขาเหรอ?...", .@kimi$;
		sleep 2000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ไม่นะ คิมิ ฉันเพิ่งรู้ตัว ฉันเข้าใจคุณผิด เขาป่วย!";
		sleep 3000;
		npctalk "Celine Kimi: ดี..ดี.. ฉัน.. ฆ่า.. เขาเหรอ?", .@kimi$;
		sleep 2000;
		mapannounce 'xm_d_map$, "Phantom's yell: See how you look, Kimi~ See the mirror~ How do yo feel? ", bc_map, "0xff8800";
		sleep 3000;
		npctalk "เซลีน คิมิ: ฉัน... ฉัน...", .@kimi$;
		sleep 1000;
		mapannounce 'xm_d_map$, "Phantom's yell: Don't you agree you are frightening? No one loves you, Kimi~", bc_map, "0xff8800";
		sleep 3000;
		npctalk "เซลีน คิมิ: เพราะฉัน ปู่ถึงตาย...", .@kimi$;
		specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: มันร้ายแรงมาก คิมิกำลังทุกข์ทรมาน มันจะเป็นอันตรายหากอยู่ในสภาวะเช่นนี้!";
		specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
		sleep 1000;
		mapannounce 'xm_d_map$, "Phantom's yell: Rage~ Sorrow~ No one cries for you, Kimi~", bc_map, "0xff8800";
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันจะหาทางออกฉุกเฉินเพื่อหลบหนี หนีไปซะ นักผจญภัย!";
		sleep 3000;
		disablenpc instance_npcname("Catherine Jet Johnson#5");
		sleep 2000;
		disablenpc .@kimi$;
		donpcevent instance_npcname("#finalbs2") + "::OnStart";
		disablenpc instance_npcname("#bgm02");
		end;
}

1@xm_d,1,5,3	script	#finalbs2	CLEAR_NPC,{
		end;
OnStart:
		stopnpctimer;
		enablenpc instance_npcname("#bgm03");
		.@event$ = instance_npcname("#finalbs2") + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		monster 'xm_d_map$,231,184, "Celine Kimi",2996,1, .@event$;// XM_CELINE_KIMI
		'celene_id = $@mobid[0];
		monster 'xm_d_map$,226,190, "Kimi's Phantom",2997,1, .@event$;// G_XM_CELINE_KIMI
		'phantom_id = $@mobid[0];
		setunitdata 'celene_id, UMOB_HP,20000000;
		setunitdata 'phantom_id,UMOB_HP,20000000;

		unittalk 'celene_id, "I don't want to be deserted. I don't want to be abandoned.";
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) < 1)
				donpcevent instance_npcname("#finalbs2") + "::OnEnd";
		end;

OnEnd:
		stopnpctimer;
		killmonster 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead";
		disablenpc instance_npcname("#bgm03");
		donpcevent instance_npcname("#finalbs_e") + "::OnStart";
		end;

OnTalk:
		.@chat_r = rand(1,10);
		if (.@chat_r == 1)
				unittalk 'celene_id, "I will burn you with hell's inferno.";
		else if (.@chat_r == 2)
				unittalk 'celene_id, "Will you bear this flame?!";
		else if (.@chat_r == 3)
				unittalk 'celene_id, "I shouldn't have, but...";
		else if (.@chat_r == 4)
				unittalk 'celene_id, "It's boiling!... Bear it if you can.";
		else if (.@chat_r == 5)
				unittalk 'celene_id, "Breathe as much as you can. It'll be the last breath you take.";
		else if (.@chat_r == 6)
				unittalk 'celene_id, "Frankly, I don't like fire.";
		else
				unittalk 'celene_id, "Everyone is afraid of me! What have I done so wrong?!";
		end;

OnTimer1000:
		if (unitexists('celene_id) == true) {
				getunitdata 'celene_id, .@data;
				if ((.@data[UMOB_X] < 211 || .@data[UMOB_X] > 241 || .@data[UMOB_Y] < 166 || .@data[UMOB_Y] > 201) && (.@data[UMOB_X] > 0 || .@data[UMOB_Y] > 0)) {
						mapannounce 'xm_d_map$, "Celine Kimi: No! I should keep my place!",bc_map,"0xff6666",FW_NORMAL,15;
						donpcevent instance_npcname("#finalbs2") + "::Onfail";
				}
		}
		end;

Onfail:
		stopnpctimer;
		killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		enablenpc instance_npcname("Celine Kimi#2");
		disablenpc instance_npcname("#bgm03");
		enablenpc instance_npcname("#kimion1");
		end;

OnTimer5000:
		if (unitexists('celene_id) == true)
				donpcevent instance_npcname("#bssk01") + "::OnStart";
		end;

OnTimer10000:
		if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) > 1) {
				.@npc_name$ = instance_npcname("#finalbs2");
				getunitdata 'celene_id, .@MOB_HP1;
				getunitdata 'phantom_id, .@MOB_HP2;
				if (.@MOB_HP1[UMOB_HP] > .@MOB_HP2[UMOB_HP]) {
						setarray .@mob_hp[0], .@MOB_HP1[UMOB_HP], .@MOB_HP2[UMOB_HP];
						setarray .@string$[0],
								"You and I are the one! I will recover you!",
								"Celine Kimi recovers herself and her phantom ";
						.@talk = 'celene_id;
				}
				else if (.@MOB_HP2[UMOB_HP] > .@MOB_HP1[UMOB_HP]) {
						setarray .@mob_hp[0], .@MOB_HP2[UMOB_HP], .@MOB_HP1[UMOB_HP];
						setarray .@string$[0],
								"I will restore you!!",
								"Celine Kimi's phantom recovers herself and her master ";
						.@talk = 'phantom_id;
				}
				.@diff_hp = .@mob_hp[0] - .@mob_hp[1];
				if (.@diff_hp > 100000) {
						.@set_bs_hp = (.@diff_hp * 5) / 10;
						.@MOB_HP3 = .@mob_hp[0] + .@set_bs_hp;
						if (.@MOB_HP3 > 66666666)
								.@MOB_HP3 = 66666666;
						setunitdata 'celene_id, UMOB_HP, .@MOB_HP3;
						setunitdata 'phantom_id, UMOB_HP, .@MOB_HP3;
						donpcevent instance_npcname("#eff_f01") + "::OnStart";
						unittalk .@talk, .@string$[0];
						sleep 1000;
						mapannounce 'xm_d_map$,  .@string$[1] + .@set_bs_hp +" HP has been recovered.", bc_map, "0xff6666";
						donpcevent instance_npcname("#heal_c") + "::OnStart";
				}
				initnpctimer;
		}
		end;
}

1@xm_d,216,193,3	script	#eff_f01	CLEAR_NPC,{
		end;
OnStart:
		for ( .@i = 1; .@i < 10; .@i++ )
				specialeffect EF_HEARTCASTING, AREA, instance_npcname( "#eff_f0"+ .@i );
		end;
}

1@xm_d,226,193,3	script	#eff_f02	CLEAR_NPC,{
		end;
}
1@xm_d,236,193,3	duplicate(#eff_f02)	#eff_f03	CLEAR_NPC
1@xm_d,216,183,3	duplicate(#eff_f02)	#eff_f04	CLEAR_NPC
1@xm_d,226,183,3	duplicate(#eff_f02)	#eff_f05	CLEAR_NPC
1@xm_d,236,183,3	duplicate(#eff_f02)	#eff_f06	CLEAR_NPC
1@xm_d,216,173,3	duplicate(#eff_f02)	#eff_f07	CLEAR_NPC
1@xm_d,226,173,3	duplicate(#eff_f02)	#eff_f08	CLEAR_NPC
1@xm_d,236,173,3	duplicate(#eff_f02)	#eff_f09	CLEAR_NPC

1@xm_d,1,5,3	script	#bssk01	CLEAR_NPC,{
		end;
OnStart:
		.@r = rand(1,3);
		if (.@r == 1)
				donpcevent instance_npcname("#bssk02") + "::OnStart";
		else if (.@r == 2)
				donpcevent instance_npcname("#bssk03") + "::OnStart";
		end;
}

1@xm_d,1,5,3	script	#bssk02	CLEAR_NPC,{
		end;
OnStart:
		donpcevent instance_npcname("#finalbs2") + "::OnTalk";
		for ( .@i = 1; .@i < 5; .@i++ )
				donpcevent instance_npcname( "#crssk"+ .@i ) + "::OnStart";
		end;
}

1@xm_d,1,5,3	script	#bssk03	CLEAR_NPC,{
		end;
OnStart:
		.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		while (unitexists('celene_id) == true) {
				getunitdata 'celene_id, .@data;
				.@x = .@data[UMOB_X] + rand(1,20) - 10;
				.@y = .@data[UMOB_Y] + rand(1,20) - 10;
				monster 'xm_d_map$,.@x,.@y, "#f_w_1",3038,1, .@event$;// HIDDEN_MOB7
				.@mon_num++;
				if (.@mon_num > 20)
						break;
				sleep 200;
		}
		sleep 6000;
		killmonster 'xm_d_map$, .@event$;
		end;

OnMyMobDead:
		end;
}

1@xm_d,1,5,3	script	#crssk1	CLEAR_NPC,{
		end;
OnStart:
		.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
		killmonster 'xm_d_map$, .@event$;
		getunitdata 'celene_id, .@data;
		setarray .@coord[0], .@data[UMOB_X], .@data[UMOB_Y];
		.@num = atoi(strnpcinfo(2));
		.@index = ( .@num > 2 ? 1 : 0 );// x or y
		.@signe = pow(-1,.@num+1);
		while(1) {
				.@coord[.@index] = .@coord[.@index] + (2 * .@signe);
				.@coord[!.@index] = .@coord[!.@index] + rand(0,2) - 1;
				monster 'xm_d_map$,.@coord[0], .@coord[1], "#f_w_1",3038,1, .@event$;
				if (.@coord[0] < 211 || .@coord[0] > 241 || .@coord[1] < 166 || .@coord[1] > 201)
						break;
				sleep 200;
		}
		sleep 6000;
		killmonster 'xm_d_map$, .@event$;
		end;
OnMyMobDead:
		end;
}
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk2	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk3	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk4	CLEAR_NPC

1@xm_d,233,183,0	script	#kimion1	-1,7,7,{
		end;
OnTouch_:
		disablenpc instance_npcname("#kimion1");
		donpcevent instance_npcname("Celine Kimi#2") + "::OnStart";
		end;
}

1@xm_d,233,183,3	script	Celine Kimi#2	4_F_KIMI,{
		mes "[เซลีน คิมิ]";
		mes "คุณก็...มาที่นี่เพื่อฆ่าฉันงั้นเหรอ?";
		close;

OnStart:
		npctalk "Celine Kimi: คุณก็... มาที่นี่เพื่อฆ่าฉันงั้นเหรอ?";
		specialeffect EF_MAPPILLAR2;
		sleep 5000;
		disablenpc instance_npcname("Celine Kimi#2");
		donpcevent instance_npcname("#finalbs2") + "::OnStart";
		end;
}

1@xm_d,228,183,0	script	#bgm03	-1,25,25,{
		end;
OnTouch:
		playBGM "123";
		end;
}

1@xm_d,1,5,3	script	#heal_c	CLEAR_NPC,{
		end;
OnStart:
		if (rand(1,10) > 4)
				initnpctimer;
		end;
OnTimer3000:
		mapannounce 'xm_d_map$, "Celine Kimi and her phantom have shared their strength. They will be stronger than ever!",bc_map,"0xff6666";
		stopnpctimer;
		end;
}

1@xm_d,1,5,3	script	#finalbs_e	CLEAR_NPC,{
		end;
OnStart:
		mapannounce 'xm_d_map$, "Celine Kimi: It is no use to defeat me! My body will be restored.",bc_map,"0xff6666",FW_NORMAL,15;
		enablenpc instance_npcname("#fac6wp");
		enablenpc instance_npcname("#jeton2");
		enablenpc instance_npcname("Catherine Jet Johnson#6");
		for ( .@i = 1; .@i <= 10; .@i++ )
				enablenpc instance_npcname("Packaged Present#"+ .@i);
		sleep 6000;
		mapannounce 'xm_d_map$, "Catherine Jet Johnson's yell: Are you okay? Flee to the south emergency exit!",bc_map,"0xffff00";
		end;
}

1@xm_d,218,145,0	script	#jeton2	-1,4,4,{
		end;
OnTouch_:
		disablenpc instance_npcname("#jeton2");
		donpcevent instance_npcname("Catherine Jet Johnson#6") + "::OnStart";
		end;
}

1@xm_d,218,145,5	script	Catherine Jet Johnson#6	4_F_SKULL06GIRL,{
		end;
OnStart:
		sleep 1000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: น่าเสียดายที่ฉันไม่สามารถโน้มน้าวคิมิได้";
		sleep 3000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: เสียงของผีตนนั้นคืออะไร ทำไมมันถึงทรมานคิมิมากขนาดนั้น...";
		sleep 4000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันเดาว่าคำสาปที่ปรากฏบนใบหน้าของฉันคงมาจากเสียงที่ไม่ปรากฏชื่อ";
		sleep 4000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ไม่ว่าจะอย่างไรก็ตาม แม้แต่คิมิก็ยังเกลียดทุกคน และเธอยังได้รวบรวมความทรงจำของเธอไว้ที่นี่มากมาย";
		sleep 6000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันจะจบเรื่องนี้อย่างปลอดภัย แม้ว่าวิญญาณของเธอจะกลับมาและเธอจะไม่ผิดหวังก็ตาม";
		sleep 5000;
		npctalk "แคเธอรีน เจ็ต จอห์นสัน: ขอบคุณมาก ที่ทำให้ทุกสิ่งที่ฉันสงสัยได้รับการแก้ไขอย่างสมบูรณ์แล้ว โปรดโทรหาฉันอีกครั้ง หากคุณจะส่งเธอไปสวรรค์ในครั้งหน้า";
		sleep 5000;
		npctalk "แคทเธอรีน เจ็ต จอห์นสัน: ฉันจะเปิดทางออก ฉันจะออกไปข้างนอกก่อน ดังนั้นแค่ตามฉันมา";
		sleep 3000;
		disablenpc instance_npcname("Catherine Jet Johnson#6");
		enablenpc instance_npcname("#exwp1");
		end;
}

1@xm_d,218,150,5	script	#exwp1	PORTAL,{
		mes "คุณจะออกไหม?";
		next;
		if (select( "Take a look around", "Go outside" ) == 1) {
				mes "หยุดเครื่องจักร";
				close;
		}
		close2;
		warp "xmas",233,300;
		end;
}

1@xm_d,210,141,3	script	Packaged Present#1	4_TREASURE_BOX,{
		specialeffect EF_COIN;
		disablenpc();
		initnpctimer;
		end;

OnTimer1000:
		switch( atoi(strnpcinfo(2)) ) {
		case 1:
				.@num = rand(4,8);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(208,212), rand(139,143);// Bloody_Coin
				if (rand(1,1000) > 150)
						makeitem 644,1, 'xm_d_map$,209,141;// Gift_Box;
				if (rand(1,1000) > 600)
						makeitem 617,1, 'xm_d_map$,210,141;// Old_Violet_Box;
				if (rand(1,1000) > 900)
						makeitem 22534,1, 'xm_d_map$,211,141;// Closedmind_Box
				break;
		case 2:
				.@num = rand(3,7);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(139,143);// Bloody_Coin
				if (rand(1,1000) > 400)
						makeitem 603,1, 'xm_d_map$,213,141;// Old_Blue_Box
				if (rand(1,1000) > 700)
						makeitem 616,1, 'xm_d_map$,214,141;// Old_Card_Album
				if (rand(1,1000) > 950)
						makeitem 13442,1, 'xm_d_map$,215,141;// Old_Parasol
				break;
		case 3:
				.@num = rand(2,6);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(139,143);// Bloody_Coin
				if (rand(1,1000) > 850)
						makeitem 7229,1, 'xm_d_map$,217,141;// Silver_Bullion
				if (rand(1,1000) > 800)
						makeitem 12246,1, 'xm_d_map$,218,141;// Magic_Card_Album
				if (rand(1,1000) > 950)
						makeitem 2486,1, 'xm_d_map$,219,141;// Shadow_Walk_
				break;
		case 4:
				.@num = rand(4,8);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(139,143);// Bloody_Coin
				if (rand(1,1000) > 700)
						makeitem 7228,1, 'xm_d_map$,221,141;// Gold_Bullion
				if (rand(1,1000) > 600)
						makeitem 617,1, 'xm_d_map$,222,141;// Old_Violet_Box
				if (rand(1,1000) > 900)
						makeitem 22534,1, 'xm_d_map$,223,141;// Closedmind_Box
				break;
		case 5:
				.@num = rand(3,7);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(139,143);// Bloody_Coin
				if (rand(1,1000) > 150)
						makeitem 644,1, 'xm_d_map$,225,141;// Gift_Box
				if (rand(1,1000) > 700)
						makeitem 616,1, 'xm_d_map$,226,141;// Old_Card_Album
				if (rand(1,1000) > 950)
						makeitem 2976,1, 'xm_d_map$,227,141;// Red_Lantern
				break;
		case 6:
				.@num = rand(2,6);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(208,212), rand(134,138);// Bloody_Coin
				if (rand(1,1000) > 400)
						makeitem 603,1, 'xm_d_map$,209,136;// Old_Blue_Box
				if (rand(1,1000) > 800)
						makeitem 12246,1, 'xm_d_map$,210,136;// Magic_Card_Album
				if (rand(1,1000) > 950)
						makeitem 2977,1, 'xm_d_map$,211,136;// Hurt_Mind
				break;
		case 7:
				.@num = rand(4,8);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(134,138);// Bloody_Coin
				if (rand(1,1000) > 850)
						makeitem 7229,1, 'xm_d_map$,213,136;// Silver_Bullion
				if (rand(1,1000) > 600)
						makeitem 617,1, 'xm_d_map$,214,136;// Old_Violet_Box
				if (rand(1,1000) > 900)
						makeitem 22534,1, 'xm_d_map$,215,136;// Closedmind_Box
				break;
		case 8:
				.@num = rand(3,7);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(134,138);// Bloody_Coin
				if (rand(1,1000) > 700)
						makeitem 7228,1, 'xm_d_map$,217,136;// Gold_Bullion
				if (rand(1,1000) > 700)
						makeitem 616,1, 'xm_d_map$,218,136;// Old_Card_Album
				if (rand(1,1000) > 950)
						makeitem 2978,1, 'xm_d_map$,219,136;// KindHeart
				break;
		case 9:
				.@num = rand(2,6);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(134,138);// Bloody_Coin
				if (rand(1,1000) > 150)
						makeitem 644,1, 'xm_d_map$,221,136;// Gift_Box
				if (rand(1,1000) > 800)
						makeitem 12246,1, 'xm_d_map$,222,136;// Magic_Card_Album
				if (rand(1,1000) > 950)
						makeitem 18848,1, 'xm_d_map$,223,136;// Lush_Rose
				break;
		case 10:
				.@num = rand(4,8);
				for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(134,138);// Bloody_Coin
				if (rand(1,1000) > 400)
						makeitem 603,1, 'xm_d_map$,225,136;// Old_Blue_Box
				if (rand(1,1000) > 600)
						makeitem 617,1, 'xm_d_map$,226,136;// Old_Violet_Box
				if (rand(1,1000) > 900)
						makeitem 22534,1, 'xm_d_map$,227,136;// Closedmind_Box
				break;
		}
		stopnpctimer;
		end;
}
1@xm_d,214,141,3	duplicate(Packaged Present#1)	Packaged Present#2	4_TREASURE_BOX
1@xm_d,218,141,3	duplicate(Packaged Present#1)	Packaged Present#3	4_TREASURE_BOX
1@xm_d,222,141,3	duplicate(Packaged Present#1)	Packaged Present#4	4_TREASURE_BOX
1@xm_d,226,141,3	duplicate(Packaged Present#1)	Packaged Present#5	4_TREASURE_BOX
1@xm_d,210,136,3	duplicate(Packaged Present#1)	Packaged Present#6	4_TREASURE_BOX
1@xm_d,214,136,3	duplicate(Packaged Present#1)	Packaged Present#7	4_TREASURE_BOX
1@xm_d,218,136,3	duplicate(Packaged Present#1)	Packaged Present#8	4_TREASURE_BOX
1@xm_d,222,136,3	duplicate(Packaged Present#1)	Packaged Present#9	4_TREASURE_BOX
1@xm_d,226,136,3	duplicate(Packaged Present#1)	Packaged Present#10	4_TREASURE_BOX

// Warps
//==========================================
1@xm_d,79,129,0	warp2	#fac1wp2	2,2,1@xm_d,73,129
1@xm_d,184,109,0	warp2	#fac2wp2	2,2,1@xm_d,170,129
1@xm_d,130,178,0	warp2	#fac3wp	2,2,1@xm_d,130,193
1@xm_d,130,184,0	warp2	#fac3wp2	2,2,1@xm_d,129,173
1@xm_d,107,208,0	warp2	#fac4wp	2,2,1@xm_d,87,208
1@xm_d,95,208,0	warp2	#fac4wp2	2,2,1@xm_d,115,208
1@xm_d,152,208,0	warp2	#fac5wp	2,2,1@xm_d,167,208
1@xm_d,205,159,0	warp2	#fac6wp	2,2,1@xm_d,205,147

// Instance GM Functions :: mx_xm_ad
//==========================================
1@xm_d,1,1,3	script	#adsw	CLEAR_NPC,{
		if (callfunc("F_GM_NPC",1854,0) == 1) {
				mes "[ผู้จัดการเวลา]";
				mes "คุณต้องการกลับมากี่โมง?";
				next;
				switch (select("Cancel:Factory No.1 Complete:Factory No.2 Complete:Factory No.3 Complete:Boss Start:Boss Complete")) {
				case 1:
						break;
				case 2:
						transform 1246,180000;
						enablenpc instance_npcname("#fac1bs");
						mapannounce 'xm_d_map$, "Coordinator's announcement: Huh? Where's everyone! How dare the workers be absent from their line and muck around!",bc_map,"0xff8800";
						warp 'xm_d_map$,70,125;
						break;
				case 3:
						for ( .@i = 1; .@i <= 10; ++.@i )
								disablenpc instance_npcname( "Worker#" + .@i );
						donpcevent instance_npcname("#fac2wpc") + "::OnStart";
						warp 'xm_d_map$,131,210;
						break;
				case 4:
						donpcevent instance_npcname("#fac3ct3") + "::OnEnd";
						warp 'xm_d_map$,131,210;
						break;
				case 5:
						donpcevent instance_npcname("#finalbs2") + "::OnStart";
						warp 'xm_d_map$,215,182;
						break;
				case 6:
						donpcevent instance_npcname("#finalbs2") + "::OnEnd";
						warp 'xm_d_map$,215,182;
						break;
				}
		}
		close;
}

1@xm_d,3,1,3	script	#adsw2	CLEAR_NPC,{
		if (callfunc("F_GM_NPC",1854,0) == 1) {
				if (unitexists('Antonio) == true)
						getunitdata 'Antonio, .@mob;
				mapannounce 'xm_d_map$, "Factory announcement: Exist in " + .@mob[UMOB_X] + " - " + .@mob[UMOB_Y] + ".",bc_map,"0x00ff44";
		}
		end;

OnInstanceInit:
		'xm_d_map$ = instance_mapname("1@xm_d");

		// Warps
		disablenpc instance_npcname("#fac3wp");
		disablenpc instance_npcname("#fac3wp2");
		disablenpc instance_npcname("#fac4wp");
		disablenpc instance_npcname("#fac4wp2");
		disablenpc instance_npcname("#fac5wp");
		// disablenpc instance_npcname("#fac5wp2");
		disablenpc instance_npcname("#fac6wp");

		// Bgm
		disablenpc instance_npcname("#bgm01");
		disablenpc instance_npcname("#bgm02");
		disablenpc instance_npcname("#bgm03");
		disablenpc instance_npcname("#bgm04");
		disablenpc instance_npcname("#bgm05");
		disablenpc instance_npcname("#bgm06");

		// Step 1
		disablenpc instance_npcname("Catherine Jet Johnson#01");
		disablenpc instance_npcname("Employees' Uniform Box#1");
		disablenpc instance_npcname("Employees' Uniform Box#2");
		disablenpc instance_npcname("Employees' Uniform Box#3");
		hideonnpc instance_npcname("#fac1ct");
		disablenpc instance_npcname("#fac1bs");
		disablenpc instance_npcname("#pck1");
		disablenpc instance_npcname("#alert1");

		// Step 2
		disablenpc instance_npcname("Catherine Jet Johnson#21");
		disablenpc instance_npcname("Employees' Uniform Box#4");
		hideonnpc instance_npcname("#fac2ct");
		disablenpc instance_npcname("#fac2wpc");
		for ( .@i = 1; .@i < 11; ++.@i )
				disablenpc instance_npcname("Worker#" + .@i);

		// Step 3
		disablenpc instance_npcname("Captured Santa#3");
		hideonnpc instance_npcname("#fac3ct");
		hideonnpc instance_npcname("#fac3ct2");
		hideonnpc instance_npcname("#fac3ct3");

		// Boss
		disablenpc instance_npcname("#finalbs");
		hideonnpc instance_npcname("#finalbs2");
		disablenpc instance_npcname("Celine Kimi#0");
		disablenpc instance_npcname("#jeton1");
		disablenpc instance_npcname("Catherine Jet Johnson#5");
		for ( .@i = 1; .@i < 10; ++.@i )
				hideonnpc instance_npcname("#eff_f0" + .@i);
		disablenpc instance_npcname("#bssk01");
		disablenpc instance_npcname("#bssk02");
		for ( .@i = 1; .@i < 5; ++.@i )
				hideonnpc instance_npcname("#crssk" + .@i);
		disablenpc instance_npcname("#kimion1");
		disablenpc instance_npcname("Celine Kimi#2");
		disablenpc instance_npcname("#heal_c");

		// Exit
		disablenpc instance_npcname("#finalbs_e");
		disablenpc instance_npcname("#jeton2");
		disablenpc instance_npcname("Catherine Jet Johnson#6");
		disablenpc instance_npcname("#exwp1");
		for ( .@i = 1; .@i < 11; ++.@i )
				disablenpc instance_npcname("Packaged Present#" + .@i);
		end;
}
//===== rAthena Script =======================================
//= Twilight Garden
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Twilight Garden
//= Episode 17.2 Hey Sweety
//= Note:
// - NPC_LOCKON_LASER skill is currently no implemented.
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//============================================================

1@bamn,1,1,0	script	#twilight_garden_control	-1,{
		end;
OnInstanceInit:
		'twilight_story = 0;
		'sweety = 0;
		'map_bamn$ = instance_mapname("1@bamn");
		'map_bamq$ = instance_mapname("1@bamq");

		// npcs from story
		//----------------------------
		// npcs on 1@bamn

		// disablenpc instance_npcname("Repeater#wifi03");	// (not disabled)
		// disablenpc instance_npcname("Repeater#wifi02");	// (not disabled)
		// disablenpc instance_npcname("Repeater#wifi01");	// (not disabled)

		//----------------------------
		// Daily: Hey! Sweety
		if (instance_live_info(ILI_NAME) == "Hey! Sweety")
				disablenpc instance_npcname("#bamn_evt01");	// story starter npc
		else
				disablenpc instance_npcname("#sweety_evt01");	// daily starter npc
		end;
}

// Story
1@bamn,100,320,0	script	#bamn_evt01	HIDDEN_WARP_NPC,2,2,{
		end;
OnTouch:
		if (!is_party_leader())
				end;
		disablenpc();
		enablenpc instance_npcname("Est#est01");
		end;
}

1@bamn,96,318,5	script(DISABLED)	Est#est01	4_F_ESTLOVELOY,{
		if (!is_party_leader())
				end;
		if ('twilight_story == 0) {
				'twilight_story = 1;
				npctalk "เอสท์ : มันมืดและเงียบ ถ้าคุณต้องการดำเนินการตามแผน ตอนนี้คือเวลาที่สมบูรณ์แบบ ใช่ไหม?";
				sleep 2000;
				npctalk "เอสต์ : เอเลน่าบอกว่าเธอจะเข้าร่วมกับเราในภายหลัง แต่มีโอกาสสูงที่เธอจะไม่สามารถเข้าร่วมได้";
				sleep 2000;
				npctalk "เอสท์ : ถึงอย่างนั้นก็ไม่ต้องกังวลไปหรอก คนอื่นๆ ก็เตรียมพร้อมรอสัญญาณซุ่มโจมตีอยู่แล้ว";
				sleep 2000;
				npctalk "เอสท์ : สิ่งที่เราต้องทำคือเดินไปรอบๆ เหมือนลาดตระเวน และจับพวกเขาโดยไม่ทันตั้งตัว";
				sleep 2000;
				npctalk "เอสท์ : เดินไปตามถนนจนถึงตัวอาคารหลักของคฤหาสน์ ฉันจะคอยอยู่ข้างหลังคุณ";
				sleep 2000;
				npctalk "เอสท์ : อย่ามองรอบๆ มากเกินไป และแกล้งทำเป็นว่าไม่ได้ระมัดระวัง เข้าใจไหม?";
				sleep 2000;
				npctalk "เอสท์ : ไปกันเถอะ.";
				for ( .@i = 1; .@i < 6; ++.@i )
						enablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
				end;
		}
		if ('twilight_story == 1)
				end;
		if ('twilight_story == 2) {
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "พวกเขาก็คงจะรอเช่นกัน";
				mes "มาทำกันแบบเงียบๆ มากขึ้นดีกว่า";
				close3;
		}
		end;
}

1@bamn,119,299,3	script(DISABLED)	Heart Hunter#md_hh01	G_EP17_2_HEART_HUNTER,3,3,{
		end;
OnTouch_:
		if (!is_party_leader())
				end;
		if ('twilight_story == 1) {
				'twilight_story = 2;
				npctalk "ยอมจำนนอย่างเงียบๆ หรือกลับสู่ที่ที่คุณมา";
				sleep 2000;
				npctalk "นั่นไม่หมายความว่าเราจะปล่อยคุณกลับไป";
				sleep 1000;
				for ( .@i = 1; .@i < 6; ++.@i )
						disablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
				donpcevent instance_npcname("twilight_story_mob_1") + "::OnStart";
		}
		end;
}

1@bamn,119,305,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh02	G_EP17_2_HEART_HUNTER
1@bamn,119,302,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh03	G_EP17_2_HEART_HUNTER
1@bamn,119,296,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh04	G_EP17_2_HEART_HUNTER
1@bamn,119,293,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh05	G_EP17_2_HEART_HUNTER


1@bamn,1,1,0	script	twilight_story_mob_1	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("twilight_story_mob_1") + "::OnMobDead";
		monster 'map_bamn$,119,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,128,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,121,300,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,118,293,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,124,294,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,130,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		end;
OnMobDead:
		.@event$ = instance_npcname("twilight_story_mob_1") + "::OnMobDead";
		if (mobcount('map_bamn$, .@event$) < 1) {
				disablenpc instance_npcname("Est#est01");
				enablenpc instance_npcname("Est#est02");
				if ('twilight_story == 2)
						'twilight_story = 3;
		}
		end;
}

1@bamn,119,299,5	script(DISABLED)	Est#est02	4_F_ESTLOVELOY,{
		if (!is_party_leader())
				end;
		if ('twilight_story == 3) {
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "ฉันไม่รู้ว่าเราควรวางกับดักหรือให้โอกาสคุณหรือไม่";
				mes "เปิดใช้งานตัวติดตามที่เราได้รับจาก Elyumina";
				next;
				cutin "",255;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : According to the intuition of the great Elyumina... Too Doo Too Doo Too~";
				sleep2 2000;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Over there!! Beep!";
				navigateto("1@bamn",206,273);
				sleep2 2000;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "นั่นมันเสียงดัง";
				mes "คุณจะย้ายออกก่อนไหม?";
				mes "ฉันมีที่ไปสักพักหนึ่ง";
				next;
				mes "[เอส]";
				mes "ฉันจะเข้าร่วมกับคุณเร็วๆ นี้";
				close2;
				if ('twilight_story == 3) {
						'twilight_story = 4;
						donpcevent instance_npcname("twilight_story_mob_2") + "::OnStart";
				}
				cutin "",255;
				disablenpc();
				end;
		}
		end;
}

1@bamn,1,1,0	script	twilight_story_mob_2	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("twilight_story_mob_2") + "::OnMobDead";
		monster 'map_bamn$,208,283,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,210,285,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,210,282,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,213,284,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,213,281,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		end;
OnMobDead:
		.@event$ = instance_npcname("twilight_story_mob_2") + "::OnMobDead";
		if (mobcount('map_bamn$, .@event$) < 1) {
				enablenpc instance_npcname("Est#est03");
				enablenpc instance_npcname("Almond#amond01");
				if ('twilight_story == 4)
						'twilight_story = 5;
		}
		end;
}

1@bamn,202,274,5	script(DISABLED)	Est#est03	4_F_ESTLOVELOY,{
		if ('twilight_story == 5) {
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "ฉันคิดว่าคงจะดีกว่าถ้าใส่เหยื่อเพิ่ม ฉันเลยไปขอความช่วยเหลือจากตุ๊กตาอัตโนมัติ";
				mes "นอกจากนี้ อัลมอนด์ยังบอกว่าเธอต้องออกไปตรวจสอบบางอย่าง";
				next;
				cutin "ep172_beta",0;
				mes "[อัลมอนด์]";
				mes "รีพีทเตอร์เสียอีกแล้วครับ ต้องซ่อมครับ";
				mes "รีพีทเตอร์เป็นเป้าหมายโปรดของผู้บุกรุก ดังนั้นเราไม่สามารถปล่อยให้มันเสียหายไปได้ใช่ไหม?";
				next;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "ด้วยเหตุนี้ ฉันจึงอยากขอให้คุณพาอัลมอนมาที่นี่เพื่อตรวจสอบเครื่องขยายสัญญาณ";
				mes "ฉันจะมองดูรอบๆแล้วหาเส้นทางอื่น";
				next;
				cutin "ep172_beta",0;
				mes "[อัลมอนด์]";
				mes "ไปด้วยกันเมื่อคุณพร้อมนะ";
				close2;
				cutin "",255;
				if ('twilight_story == 5)
						'twilight_story = 6;
				end;
		}
		if ('twilight_story == 6) {
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "คุยกับอัลมอนด์เมื่อคุณพร้อมที่จะเคลื่อนไหว";
				close3;
		}
		end;
}

1@bamn,199,275,5	script(DISABLED)	Almond#amond01	EP17_2_BETA_BASIC,{
		if ('twilight_story < 6) {
				cutin "ep172_beta",2;
				mes "[อัลมอนด์]";
				mes "วันนี้ทุกคนควรจะอยู่ในคฤหาสน์";
				mes "มีเรื่องสำคัญเกิดขึ้น นั่นคือเหตุผลว่าทำไมฉันถึงอยู่ที่นี่กับเอสต์";
				close3;
		}
		if ('twilight_story == 6) {
				'guide = getnpcid(0);
				'twilight_story = 7;
				npcspeed 200;
				npctalk "ถ้าอย่างนั้นก็ไปกันเถอะ";
				unitwalk 'guide,199,248, instance_npcname("Almond#amond01") + "::OnEvent00";
				enablenpc instance_npcname("Intruder#sweety02");
				end;
		}
		if ('twilight_story == 7)
				end;
		if ('twilight_story == 8) {
				'twilight_story = 9;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! There!!";
				setpcblock PCBLOCK_NPC, true;
				navigateto("1@bamn",242,206);
				sleep2 2000;
				npctalk "ฮ่าๆๆ เสียงดังจัง";
				sleep2 2000;
				npctalk "ฉันอยากรู้ว่าเราควรไปเร็วๆ นี้มั้ย?";
				sleep2 2000;
				npctalk "แขกครับ มีผู้บุกรุก!";
				donpcevent instance_npcname("twilight_story_mob_3") + "::OnStart";
				setpcblock PCBLOCK_NPC, false;
				sleep 200;
				npcspeed 80;
				unitwalk 'guide,245,229, instance_npcname("Almond#amond01") + "::OnEvent02";
				end;
		}
		if ('twilight_story == 10 || 'twilight_story == 11 || 'twilight_story == 12) {
				npctalk "ระวัง!";
				end;
		}
		if ('twilight_story == 13) {
				'twilight_story = 14;
				npcspeed 200;
				setpcblock PCBLOCK_NPC, true;
				npctalk "ไปเช็คตัวทวนสัญญาณกันดีกว่า";
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				sleep2 1000;
				unitwalk 'guide,244,208, instance_npcname("Almond#amond01") + "::OnEvent04";
				end;
		}
		if ('twilight_story == 14)
				end;
		if ('twilight_story == 15) {
				'twilight_story = 16;
				setpcblock PCBLOCK_NPC, true;
				for ( .@i = 1; .@i < 5; ++.@i )
						enablenpc instance_npcname("Rebellion#md_rb0" + .@i);
				for ( .@i = 6; .@i < 9; ++.@i )
						enablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
				unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Kakakakakakakil! The culprit is around! They're on my radar! It's that way!";
				navigateto("1@bamn",332,143);
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				sleep2 1000;
				npcspeed 80;
				npctalk "มีผู้บุกรุกอีกคนแล้ว! ไม่แปลกใจเลยที่ฉันเริ่มตื่นเต้นแล้ว~!";
				unitwalk 'guide,332,169;
				end;
		}
		if ('twilight_story < 18)
				end;
		if ('twilight_story == 18) {
				npcspeed 200;
				'twilight_story = 19;
				npctalk "คุณไม่เป็นไรใช่ไหม นักผจญภัย? ถ้าอย่างนั้น เราก็สามารถเดินทางต่อได้";
				unitwalk 'guide,332,143, instance_npcname("Almond#amond01") + "::OnEvent11";
				end;
		}
		if ('twilight_story == 19)
				end;
		if ('twilight_story == 20) {
				npctalk "คุณไม่ควรคุยกับเอสท์เหรอ ดูเหมือนเธอจะรออยู่นะ";
				end;
		}
		end;

OnEvent00:
		npctalk "ทางนี้ครับ.";
		sleep 1000;
		unitwalk 'guide,225,248, instance_npcname("Almond#amond01") + "::OnFollow00";
		end;
OnFollow00:
		unitwalk 'guide,245,248, instance_npcname("Almond#amond01") + "::OnEvent01";
		end;
OnEvent01:
		npctalk "มาดู Repeater ที่มักโดนแฮ็กกันดีกว่า";
		sleep 2000;
		npctalk "ห้ะ? ตอนนี้คุณถืออะไรอยู่? เอาออกมาให้ฉันดูหน่อยสิ";
		'twilight_story = 8;
		end;

OnEvent02:
		npctalk "นักผจญภัย มีผู้บุกรุก!";
		unitwalk 'guide,245,215, instance_npcname("Almond#amond01") + "::OnEvent03";
		end;
OnEvent03:
		'twilight_story = 10;
		end;

OnEvent04:
		npctalk "...กำลังตรวจสอบสัญญาณ.";
		sleep 2000;
		npctalk "...ตรวจสอบช่องทางการแฮ็คแล้ว";
		sleep 2000;
		npctalk "...กำลังฟื้นตัว...";
		sleep 2000;
		npctalk "...กำลังฟื้นตัว...84%";
		sleep 2000;
		npctalk "...การกู้คืนเสร็จสมบูรณ์แล้ว";
		sleep 2000;
		npctalk "เราจะไปที่เครื่องทวนสัญญาณกันไหม?";
		sleep 2000;
		unitwalk 'guide,245,204, instance_npcname("Almond#amond01") + "::OnEvent05";
		end;
OnEvent05:
		npctalk "นักผจญภัย คุณชอบอัลมอนด์ไหม ลูกค้าที่ชอบอัลมอนด์ตั้งชื่อให้ฉันว่า อัลมอนด์เหมาะกับฉันไหม";
		sleep 3000;
		unitwalk 'guide,265,204, instance_npcname("Almond#amond01") + "::OnEvent06";
		end;
OnEvent06:
		npctalk "เด็กคนนั้นเมื่อไม่นานนี้ ฉันเคยเห็นเขามาก่อน";
		unitwalk 'guide,285,204, instance_npcname("Almond#amond01") + "::OnEvent07";
		end;
OnEvent07:
		npctalk "แต่เขาก็วิ่งได้ดีมากเลยนะ นี่เพิ่งเคยเห็นเขาวิ่งแบบจริงจังครั้งแรกเอง~";
		unitwalk 'guide,300,204, instance_npcname("Almond#amond01") + "::OnEvent08";
		end;
OnEvent08:
		npctalk "พิทาย่าที่นี่น่ารักจัง~ เขาว่าอัลมอนด์อร่อยนะ คุณเคยกินอัลมอนด์มาก่อนมั้ย?";
		sleep 2000;
		npctalk "ทางนี้ครับ.";
		unitwalk 'guide,300,190, instance_npcname("Almond#amond01") + "::OnEvent09";
		end;
OnEvent09:
		npctalk "นานแล้วนะที่ฉันไม่ได้สบายตัวขนาดนี้~";
		unitwalk 'guide,327,189, instance_npcname("Almond#amond01") + "::OnEvent10";
		end;
OnEvent10:
		npctalk "นักผจญภัย เจ้าสิ่งที่ส่งเสียงดังนั้นยังคงสั่นไหวอยู่อีกหรือ? ลองตรวจสอบดูสักครู่";
		'twilight_story = 15;
		end;

OnEvent11:
		npctalk "...กำลังตรวจสอบสัญญาณ.";
		sleep 2000;
		npctalk "...ตรวจสอบช่องทางการแฮ็คแล้ว";
		sleep 2000;
		npctalk "...กำลังฟื้นตัว...";
		sleep 2000;
		npctalk "...กำลังฟื้นตัว...72%";
		sleep 2000;
		npctalk "...การกู้คืนเสร็จสมบูรณ์แล้ว";
		sleep 2000;
		npctalk "ไปที่ repeater ถัดไปกันเถอะ~";
		sleep 2000;
		unitwalk 'guide,320,139, instance_npcname("Almond#amond01") + "::OnFollow12";
		end;
OnFollow12:
		unitwalk 'guide,300,139, instance_npcname("Almond#amond01") + "::OnEvent12";
		end;
OnEvent12:
		npctalk "เป็นเด็กจากเมื่อก่อนใช่มั้ย เขาเป็นผู้บุกรุก นั่นมันแปลก ๆ นะ คล้าย ๆ อัลมอนด์...";
		unitwalk 'guide,285,139, instance_npcname("Almond#amond01") + "::OnEvent13";
		end;
OnEvent13:
		npctalk "ไปเร็วกว่านี้หน่อยมั้ย? มีเสียงอะไรบางอย่างดังมาจากตรงนั้น...";
		unitwalk 'guide,285,118, instance_npcname("Almond#amond01") + "::OnEvent14";
		end;
OnEvent14:
		npctalk "เงียบกว่าที่คิดไว้ คนบุกรุกไปไหนหมด";
		unitwalk 'guide,285,98, instance_npcname("Almond#amond01") + "::OnEvent15";
		end;
OnEvent15:
		npctalk "ทางนี้ครับ.";
		unitwalk 'guide,261,98, instance_npcname("Almond#amond01") + "::OnEvent16";
		end;
OnEvent16:
		npcspeed 80;
		npctalk "มันเอสแล้ว!";
		'twilight_story = 20;
		unitwalk 'guide,261,87;
		end;
}

1@bamn,1,1,0	script	twilight_story_mob_3	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("twilight_story_mob_3") + "::OnMobDead";
		monster 'map_bamn$,246,208,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,248,208,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,246,206,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		monster 'map_bamn$,248,206,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
		enablenpc instance_npcname("Intruder#sweety02");
		end;
OnMobDead:
		.@event$ = instance_npcname("twilight_story_mob_3") + "::OnMobDead";
		if (mobcount('map_bamn$, .@event$) < 1) {
				if ('twilight_story == 10)
						'twilight_story = 11;
		}
		end;
}

1@bamn,243,208,5	script	Repeater#wifi03	CLEAR_NPC,{
		mes "ไม่มีทางรู้ได้ว่าอุปกรณ์ขยายสัญญาณอยู่ในสถานะใด ชิปการสื่อสารดูเหมือนจะได้รับการติดตั้งอย่างถูกต้อง";
		close;
}

1@bamn,242,206,7	script(DISABLED)	Intruder#sweety02	4_EP17_SWEETY,{
		if ('twilight_story < 11) {
				npctalk "แกช้าไปหนึ่งก้าวแล้วนะไอ้พวกงี่เง่า!";
				end;
		}
		if ('twilight_story == 11) {
				'twilight_story = 12;
				npctalk "ฮ่าฮ่า คิดว่าจะจับฉันได้มั๊ย?";
				sleep 2000;
				npctalk "ไปทนทุกข์ซะ!";
				sleep 500;
				disablenpc();
				donpcevent instance_npcname("twilight_story_mob_4") + "::OnStart";
				end;
		}
		end;
}

1@bamn,1,1,0	script	twilight_story_mob_4	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("twilight_story_mob_4") + "::OnMobDead";
		monster 'map_bamn$,248,206,"Heart Hunter Commander","G_BELLARE3",1, .@event$;
		monster 'map_bamn$,248,208,"Heart Hunter Commander","G_BELLARE3",1, .@event$;
		end;
OnMobDead:
		.@event$ = instance_npcname("twilight_story_mob_4") + "::OnMobDead";
		if (mobcount('map_bamn$, .@event$) < 1) {
				if ('twilight_story == 12)
						'twilight_story = 13;
		}
		end;
}


1@bamn,331,161,3	script(DISABLED)	Rebellion#md_rb02	4_F_REBELLION,7,7,{
		end;
OnTouch:
		if ('twilight_story == 16) {
				'twilight_story = 17;
				npctalk "เราจะยึดครองที่นี่ แค่ดูสิ!";
				sleep 1000;
				specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh06");
				sleep 1000;
				npctalk "คาคาก้า มาจัดการพวกมันให้กระจุยกันเถอะ!", instance_npcname("Rebellion#md_rb04");
				specialeffect EF_TRIPLEACTION, AREA, instance_npcname("Heart Hunter#md_hh07");
				sleep 1000;
				specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh07");
				sleep 2000;
				npctalk "ขอบคุณที่แสดงตัวออกมานะไอ้เวร!", instance_npcname("Rebellion#md_rb03");
				sleep 2000;
				specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh08");
				sleep 2000;
				npctalk "-", instance_npcname("Heart Hunter#md_hh07");
				sleep 2000;
				npctalk "...(พยักหน้า)", instance_npcname("Heart Hunter#md_hh06");
				sleep 1000;
				npctalk "-", instance_npcname("Heart Hunter#md_hh08");
				sleep 1000;
				npctalk "คุณกำลังส่งสัญญาณอะไรถึงกัน?";
				sleep 1000;
				specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh07");
				for ( .@i = 1; .@i < 5; ++.@i )
						specialeffect EF_SPREADATTACK, AREA, instance_npcname("Rebellion#md_rb0" + .@i);
				sleep 1000;
				for ( .@i = 6; .@i < 9; ++.@i )
						disablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
				sleep 1000;
				npctalk "อะไรนะ พวกมันวิ่งหนีเหรอ หลบไป!";
				sleep 2000;
				for ( .@i = 1; .@i < 5; ++.@i )
						disablenpc instance_npcname("Rebellion#md_rb0" + .@i);
				enablenpc instance_npcname("Intruder#sweety03");
				npctalk "อะไร...?", instance_npcname("Intruder#sweety03");
		}
		end;
}
1@bamn,321,161,5	duplicate(dummy_disabled_npc)	Rebellion#md_rb01	4_M_REBELLION
1@bamn,331,155,1	duplicate(dummy_disabled_npc)	Rebellion#md_rb03	4_M_REBELLION
1@bamn,321,155,7	duplicate(dummy_disabled_npc)	Rebellion#md_rb04	4_F_REBELLION
1@bamn,324,158,7	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh06	G_EP17_2_HEART_HUNTER
1@bamn,326,159,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh07	G_EP17_2_HEART_HUNTER
1@bamn,328,158,1	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh08	G_EP17_2_HEART_HUNTER


1@bamn,333,141,7	script(DISABLED)	Intruder#sweety03	4_EP17_SWEETY,{
		if ('twilight_story == 17) {
				setpcblock PCBLOCK_NPC, true;
				'twilight_story = 18;
				npctalk "อะไรนะ คนอื่นๆไปไหนกัน?";
				sleep2 2000;
				npctalk "คุณทำแล้วใช่ไหม?";
				sleep2 2000;
				npctalk "คุณเก่งมากเลยใช่มั้ยล่ะ?";
				sleep2 2000;
				npctalk "รีพีทเตอร์ : (... .. ..)", instance_npcname("Repeater#wifi02");
				npctalk "อ๋อ มีข้อความอยู่ค่ะ อยู่ตรงนั้นสักพักนะคะ สวัสดีค่ะ";
				sleep2 2000;
				npctalk "รีพีทเตอร์ : (... ??... ??)", instance_npcname("Repeater#wifi02");
				sleep2 2000;
				npctalk "ตอนนี้เหรอ? มีเวลาอีก... ใช่ ไม่... อ่า...";
				sleep2 2000;
				npctalk "... ...จนถึงตอนนี้...ยังไม่มี?!... โอเค...";
				sleep2 2000;
				emotion ET_THINK;
				sleep2 2000;
				npctalk "เธอ! เธอคือสาเหตุที่ฉันไม่มีเวลาในตอนนี้ ฉันจะฆ่าเธอครั้งหน้าที่เราได้เจอกัน!";
				sleep2 2000;
				disablenpc();
				enablenpc instance_npcname("Est#est04");
				for ( .@i = 5; .@i < 9; ++.@i )
						enablenpc instance_npcname("Rebellion#md_rb0" + .@i);
				for ( .@i = 0; .@i < 3; ++.@i )
						enablenpc instance_npcname("Heart Hunter#md_hh1" + .@i);
				setpcblock PCBLOCK_NPC, false;
		}
		end;
}
1@bamn,334,143,5	duplicate(dummy_npc)	Repeater#wifi02	2_POSTBOX


1@bamn,257,84,3	script(DISABLED)	Est#est04	4_F_ESTLOVELOY,{
		if ('twilight_story == 20) {
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "ได้ยินข่าวมาว่าเจอคนหน้าตาเหมือนผู้นำเหรอ";
				mes "ผมก็ว่าสมาชิกที่เหลือก็เก่งอยู่บ้างเหมือนกัน";
				next;
				mes "[เอส]";
				mes "แล้วตัวทวนสัญญาณเกิดอะไรขึ้น?";
				next;
				cutin "ep172_beta",2;
				mes "[อัลมอนด์]";
				mes "เราอยู่ระหว่างทางไปอีกแห่งหนึ่ง";
				mes "แต่มันไกลจากที่นี่มากเลยนะ";
				next;
				cutin "ep162_est01",2;
				mes "[เอส]";
				mes "อืม...จะทำแบบนี้ดีไหม?";
				mes "ผู้ชายที่คุณคิดถึงเป็นเด็กผู้ชายที่มีผมสีม่วงใช่ไหม?";
				mes "เราจะไปล่อมันออกมา";
				next;
				mes "[เอส]";
				mes "ฉันจะจัดการกับคนที่เหลือที่ซ่อนตัวอยู่ในสวน";
				mes "อ่า ฉันจะพาอัลมอนด์ไปด้วย";
				next;
				mes "[เอส]";
				mes "เขาคงไปทางทิศตะวันตกของคฤหาสน์ ดังนั้นมุ่งหน้าไปที่นั่นเลย";
				mes "คุณมีระบบติดตามของเอลิวมินา มันจะเปิดใช้งานหากคุณเข้าใกล้เขา";
				if ('twilight_story == 20) {
						'twilight_story = 21;
						enablenpc instance_npcname("#to_bamq");
						enablenpc instance_npcname("Intruder#sweety");
						enablenpc instance_npcname("#to_swty01");
						enablenpc instance_npcname("#to_swty02");
				}
				close3;
		}
		if ('twilight_story == 21) {
				mes "[เอส]";
				mes "เขาคงไปทางทิศตะวันตกของคฤหาสน์ ดังนั้นมุ่งหน้าไปที่นั่นเลย";
				mes "คุณมีเครื่องติดตามของ Elyumina หากคุณอยู่ใกล้เขา เครื่องจะเปิดใช้งาน";
				next;
				mes "[เอส]";
				mes "ไม่ต้องกังวล ฉันจะดูแลอัลมอนด์จากที่นี่เอง";
				close3;
		}
		end;
}

1@bamn,250,80,7	script(DISABLED)	Rebellion#md_rb05	4_M_REBELLION,{
		npctalk "ส่วนที่เหลือปล่อยให้เป็นหน้าที่ของเรา";
		end;
}

// 1@bamn,254,80,7	script	Rebellion#md_rb06	4_F_REBELLION,7,7,{	// unknown effect
1@bamn,254,80,7	script(DISABLED)	Rebellion#md_rb06	4_F_REBELLION,{
		npctalk "อิอิอิ... ในที่สุดก็ถึงเวลาแก้แค้นแล้ว อิอิอิ...";
		end;
}

1@bamn,258,80,1	script(DISABLED)	Rebellion#md_rb07	4_M_REBELLION,{
		npctalk "หากมีศัตรูก่อให้เกิดความวุ่นวาย เราควรโจมตีพวกมันด้วยกำลังที่มากขึ้น";
		end;
}

1@bamn,202,84,3	script(DISABLED)	Rebellion#md_rb08	4_M_REBELLION2,{
		npctalk "คุณกำลังมองหาเด็กชายผมสีม่วงใช่ไหม เขาเดินไปที่ห้องทดลอง";
		end;
}


1@bamn,67,173,0	script(DISABLED)	#to_swty01	HIDDEN_WARP_NPC,5,5,{
		end;
OnTouch:
		if ('twilight_story == 21) {
				unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! That way! Pyo-pyo~!";
				navigateto("1@bamn",33,224);
		}
		end;
}

1@bamn,121,204,0	duplicate(#to_swty01)	#to_swty02	HIDDEN_WARP_NPC,7,7

1@bamn,67,195,3	script(DISABLED)	Heart Hunter#md_hh10	G_BELLARE3,5,5,{
		end;
OnTouch:
		monster 'map_bamn$,67,195,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,65,199,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,71,197,"Heart Hunter Commander","G_BELLARE3",1;
		disablenpc();
		end;
}

1@bamn,78,204,5	script(DISABLED)	Heart Hunter#md_hh11	G_BELLARE3,5,5,{
		end;
OnTouch:
		emotion ET_GO;
		monster 'map_bamn$,78,204,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,83,204,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,76,200,"Heart Hunter Commander","G_BELLARE3",1;
		disablenpc();
		end;
}

1@bamn,61,214,7	script(DISABLED)	Heart Hunter#md_hh12	G_BELLARE3,5,5,{
		end;
OnTouch:
		emotion ET_GO;
		monster 'map_bamn$,61,214,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,58,212,"Heart Hunter Commander","G_BELLARE3",1;
		monster 'map_bamn$,64,214,"Heart Hunter Commander","G_BELLARE3",1;
		disablenpc();
		end;
}

1@bamn,145,106,5	duplicate(dummy_npc)	Repeater#wifi01	CLEAR_NPC


1@bamq,125,39,3	script(DISABLED)	Intruder#sweety	4_EP17_SWEETY,5,5,{
		end;
OnTouch:
		if ('twilight_story == 21) {
				setpcblock PCBLOCK_NPC, true;
				'twilight_story = 22;
				npctalk "สายไปแล้วไอ้โง่!";
				sleep2 2000;
				npctalk "พวกเขาหลบหนีโดยใช้เรือเหาะไปแล้ว!";
				sleep2 2000;
				npctalk "ตอนนี้ฉัน สวีทตี้ จะเป็นคนจัดการกับพวกโง่ๆ ที่เพิ่งเข้ามาเอง!";
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				disablenpc();
				donpcevent instance_npcname("twilight_story_mob_5") + "::OnStart";
				end;
		}
		end;
}

1@bamq,1,1,7	script	twilight_story_mob_5	-1,{
		end;
OnStart:
		monster 'map_bamq$,125,39,"Sweety",20642,1, instance_npcname("twilight_story_mob_5") + "::OnMobDead";
		setunitdata $@mobid[0],UMOB_HP,500000;
		end;
OnMobDead:
		killmonster 'map_bamq$, instance_npcname("twilight_story_mob_5") + "::OnMobDead";
		if ('twilight_story == 22)
				'twilight_story = 23;
		mapannounce 'map_bamq$, "Est: Did you hear a loud noise from here?", bc_map, 0xFF00;
		enablenpc instance_npcname("Broken Sweety#sweety04");
		enablenpc instance_npcname("Est#est05");
		enablenpc instance_npcname("#tgd_bamq_exit");
		end;
}

1@bamq,123,39,3	script(DISABLED)	Broken Sweety#sweety04	4_EP17_SWEETY,{
		specialeffect EF_NPC_STOP;
		npctalk "...ใช่...ส...ไป...ไม่...สอง...";
		mes "ไฟฟ้าเด้งไม่หมุนเหมือนเครื่องเสีย";
		mes "เมื่อเห็นว่าเขาละสายตาไป ก็ดูเหมือนว่าเขาไม่ได้เสียใจจนหมดสิ้น";
		close;
}

1@bamq,108,41,5	script(DISABLED)	Est#est05	4_F_ESTLOVELOY,{
		cutin "ep162_est01",2;
		mes "[เอส]";
		mes "มันช้าไปนิดหน่อย แต่ก็จบแล้ว.";
		mes "นั่นก็คือคุณนั่นเอง ผู้ชายที่กำลังลากเท้าอยู่ในสวน";
		next;
		mes "[เอส]";
		mes "ไอ้นี่มันคิดว่าการควบคุมตุ๊กตาอัตโนมัติจะช่วยซื้อเวลาได้งั้นเหรอ?";
		next;
		cutin "ep162_est02",2;
		mes "[เอส]";
		mes "สุดท้ายแล้วพวกมันก็เป็นแค่ตุ๊กตาอัตโนมัติใช่ไหมล่ะ?";
		mes "ฉันไม่มีทางเลือกอื่นนอกจากต้องระเบิดตุ๊กตาอัตโนมัติของคฤหาสน์...";
		next;
		cutin "ep162_est01",2;
		mes "[เอส]";
		mes "เราออกไปจากที่นี่กันเถอะ";
		mes "ฉันจะรออยู่ที่เกสต์เฮาส์";
		close3;
}

// warps story
1@bamn,33,224,0	script(DISABLED)	#to_bamq	WARPNPC,1,1,{
		end;
OnTouch_:
		if ('twilight_story == 21)
				warp 'map_bamq$,103,39;
		end;
}

1@bamq,101,39,0	warp2(DISABLED)	#tgd_bamq_exit	1,1,ba_maison,33,220


// Daily
1@bamq,16,39,0	script	#sweety_evt01	HIDDEN_WARP_NPC,2,2,{
		end;
OnTouch:
		if (!is_party_leader())
				end;
		disablenpc();
		enablenpc instance_npcname("Almond#amond02");
		enablenpc instance_npcname("Sweety#sweety_boss");
		enablenpc instance_npcname("#to_bamq2");
		enablenpc instance_npcname("#to_bamn");

		monster 'map_bamn$,120,257,"--ja--",20681,1;	// G_EP17_2_HEART_HUNTER
		monster 'map_bamn$,130,255,"--ja--",20681,1;
		monster 'map_bamn$,130,244,"--ja--",20681,1;
		monster 'map_bamn$,127,184,"--ja--",20681,1;
		monster 'map_bamn$,188,240,"--ja--",20699,1;	// G_BELLARE3
		monster 'map_bamn$,203,248,"--ja--",20699,1;
		monster 'map_bamn$,203,252,"--ja--",20699,1;
		monster 'map_bamn$,301,234,"--ja--",20681,1;
		monster 'map_bamn$,302,239,"--ja--",20681,1;
		monster 'map_bamn$,297,244,"--ja--",20681,1;
		monster 'map_bamn$,197,52,"--ja--",20699,1;
		monster 'map_bamn$,120,83,"--ja--",20681,1;
		monster 'map_bamn$,117,84,"--ja--",20681,1;
		monster 'map_bamn$,137,176,"--ja--",20681,1;
		monster 'map_bamn$,143,181,"--ja--",20681,1;
		monster 'map_bamn$,62,165,"--ja--",20699,1;
		end;
}

1@bamq,32,49,3	script(DISABLED)	Sweety#sweety_boss	4_EP17_Sweety,{
		if ('sweety == 0) {
				cutin "ep172_Sweety01",2;
				mes "[หวาน]";
				mes "เฮ้ คุณพร้อมหรือยัง?";
				mes "มาหาฉันได้ตลอดเวลา";
				mes "ฉันแน่ใจว่าฉันจะรู้สึกดีขึ้นเมื่อฉันเอาชนะคุณได้";
				next;
				if (select( "Wait a bit.", "Let's go!" ) == 1) {
						mes "[หวาน]";
						mes "อะไรนะคุณมาที่นี่เพื่อสำรวจสวนเหรอ?";
						mes "นักล่าหัวใจไม่รู้ว่าเกิดอะไรขึ้นกับฉัน แต่...";
						next;
						cutin "ep172_Sweety02",2;
						mes "[หวาน]";
						mes "อะไร ทำไม?";
						mes "ฉันไม่เหมือนพวกเขาสักนิด ฉันเป็นสิ่งมีชีวิตที่ยอดเยี่ยม!";
						mes "ครูก็เช่นกัน";
						mes "แต่คนเหล่านั้นก็เป็นแค่คนล้มเหลว";
						close3;

				}
				cutin "",255;
				npctalk "คราวนี้ฉันจะไม่แพ้เธอแล้ว!";
				disablenpc();
				donpcevent instance_npcname("twilight_daily") + "::OnStart";
				'sweety = 1;
				close;
		}
		specialeffect EF_NPC_STOP;
		mes "เขาไม่ตอบสนองอะไรเลย";
		mes "ฉันคิดว่าเขาคงหมดสติจากการโจมตีของฉัน";
		npctalk "-";
		close;
}

1@bamq,1,1,7	script	twilight_daily	-1,{
		end;
OnStart:
		monster 'map_bamq$,32,49,"Sweety",20642,1, instance_npcname("twilight_daily") + "::OnMobDead";
		end;
OnMobDead:
		killmonster 'map_bamq$, instance_npcname("twilight_daily") + "::OnMobDead";
		mapannounce 'map_bamq$, "Almond: Sweety~ Do it in moderation~", bc_map, 0xFF00;
		enablenpc instance_npcname("Sweety#sweety_boss");
		'sweety = 2;
		end;
}

1@bamq,32,43,3	script(DISABLED)	Stunned Sweety#sweety_bo	4_EP17_SWEETY,{
		setpcblock PCBLOCK_NPC, true;
		specialeffect EF_NPC_STOP;
		sleep2 500;
		setpcblock PCBLOCK_NPC, false;
		npctalk "-";
		mes "ฉันหมดสติไปจากอาการตกใจ";
		mes "ดูเหมือนว่าเขาจะหมดสติไป";
		close;
}

1@bamq,37,59,3	script(DISABLED)	Almond#amond02	EP17_2_BETA_BASIC,{
		if ('sweety < 2) {
				cutin "ep172_beta",2;
				mes "[อัลมอนด์]";
				mes "ฉันไม่เข้าใจเลย.";
				mes "ทำไมสวีทตี้ถึงชอบผลักดันร่างกายของตัวเองจนถึงขีดสุด...";
				next;
				mes "[อัลมอนด์]";
				mes "และมันเป็นหน้าที่ของฉันที่จะซ่อมแซมร่างกายที่ถูกทำลายของเขา";
				mes "โปรดจัดการกับเขาพอเพียงเพื่อให้เขาพึงพอใจ";
				close3;
		}
		cutin "ep172_beta",2;
		mes "[อัลมอนด์]";
		mes "ฉันจะจัดการเรื่องการย้ายของเองนะที่รัก";
		mes "คุณต้องการจะสรุปตอนนี้หรือไม่?";
		next;
		if (select( "Let's wrap up.", "I'll explore the garden first." ) == 2) {
				mes "[อัลมอนด์]";
				mes "ยังมีผู้บุกรุกอยู่บ้างอยู่ที่นั่น";
				mes "คุณทราบเรื่องนี้มั้ย?";
				close3;
		}
		mes "[อัลมอนด์]";
		mes "แล้วเราจะทำอย่างนั้นไหม?";
		close2;
		warp "ba_in01",18,255;
		end;
}


// warps daily
1@bamn,33,224,0	warp2(DISABLED)	#to_bamq2	1,1,1@bamq,13,39
1@bamq,8,39,0	warp2(DISABLED)	#to_bamn	1,1,1@bamn,33,220


// Daily quest
1@bamn,150,47,3	script	#bam_body01	4_EP17_BROKENBETA,{
		if (isbegin_quest(18024) == 1) {
				if (checkweight(1000226,1) == 0) {	// (custom)
						mes "^008800รอก่อนนะ!!";
						mes "คุณไม่สามารถรับสินค้าเพิ่มเติมได้เนื่องจากคุณมีสินค้ามากเกินไป โปรดลองอีกครั้งหลังจากคุณผ่อนปรนลง^000000";
						close;
				}
				.@id = atoi(replacestr(strnpcinfo(2), "bam_body0", ""));

				if ('broken_beta[.@id] == 0) {
						mes "ร่างของ Manager Beta ถูกซ่อนอยู่ภายใต้ภูมิประเทศ";
						next;
						if (rand(1,10) < 5)
								mes "ช่องแกนหลักว่างเปล่า";
						else {
								mes "ฉันได้แกนคืนมาแล้ว";
								getitem 1000226,1;
						}
						mes "ฉันจะแจ้งให้อัลฟ่าทราบทีหลังเพื่อที่เธอจะได้นำร่างตุ๊กตาคืนมาได้";
						'broken_beta[.@id] = 1;
				}
		}
		mes "ส่วนแกนยังว่างเปล่า";
		close;

OnInstanceInit:
		questinfo( QTYPE_CLICKME, QMARK_YELLOW, "isbegin_quest(18024) == 1" );
		end;
}
1@bamn,221,193,3	duplicate(#bam_body01)	#bam_body02	4_EP17_BROKENBETA
1@bamn,208,87,3	duplicate(#bam_body01)	#bam_body03	4_EP17_BROKENBETA
1@bamn,314,146,3	duplicate(#bam_body01)	#bam_body04	4_EP17_BROKENBETA
1@bamn,207,276,3	duplicate(#bam_body01)	#bam_body05	4_EP17_BROKENBETA
1@bamn,70,256,3	duplicate(#bam_body01)	#bam_body06	4_EP17_BROKENBETA
1@bamn,64,187,3	duplicate(#bam_body01)	#bam_body07	4_EP17_BROKENBETA
1@bamn,275,313,3	duplicate(#bam_body01)	#bam_body08	4_EP17_BROKENBETA
1@bamn,338,267,3	duplicate(#bam_body01)	#bam_body09	4_EP17_BROKENBETA
//===== rAthena Script =======================================
//= Friday Memorial Dungeon
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Friday Memorial Dungeon
//===== Changelogs: ==========================================
//= 1.0 Initial release [Everade]
//= 1.1 Clean-up [Capuche]
//============================================================

gef_tower,57,170,3	script	Marry Jay	4_F_JOB_BLACKSMITH,{
		if (BaseLevel < 130) {
				mes "[แต่งงานกับเจย์]";
				mes "เลเวลฐานของคุณต่ำเกินไป กลับมาอีกครั้งเมื่อคุณถึงเลเวล 130";
				close;
		}
		mes "^ff0000หากคุณทำการประมวลผลมอนสเตอร์ เช่น การฝึกมอนสเตอร์ภายในดันเจี้ยน มันจะไม่นับรวมในความคืบหน้าของคุณ โปรดทราบ^000000";
		next;
		.@day = gettime(DT_DAYOFWEEK);
		if (.@day != FRIDAY) {
				mes "[แต่งงานกับเจย์]";
				mes "โอ้ ผู้สังเกตการณ์ที่อยากรู้อยากเห็น ฉันชื่อแมรี เจย์ ผู้ที่รับผิดชอบงานวันศุกร์ของสัปดาห์";
				next;
				select( "When is the opening time?" );
				mes "[แต่งงานกับเจย์]";
				mes "ดันเจี้ยนจะเปิดในระหว่าง:";
				mes "^0000FFวันศุกร์ 00:00 ~ วันศุกร์ 23:59^000000";
				next;
				mes "[แต่งงานกับเจย์]";
				mes "หวังว่าจะได้พบคุณเมื่อวันศุกร์มาถึง~";
				close;
		}
		.@md_name$ = "Friday Dungeon";

		switch(checkquest(12379,PLAYTIME)) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[แต่งงานกับเจย์]";
				mes "ขออภัยช่วงเวลาในการเข้าใช้งาน" + .@md_name$ + "หมดอายุแล้ว";
				mes "พักผ่อนในขณะที่รอให้ดันเจี้ยนเปิดใช้งานอีกครั้ง";
				close;
		case 2:
				erasequest 12379;
				mes "[แต่งงานกับเจย์]";
				mes "คูลดาวน์หมดลงแล้ว";
				mes "คุณสามารถเข้าสู่ Memorial Dungeon ได้อีกครั้ง";
				close;
		}
		mes "[แต่งงานกับเจย์]";
		mes "คุณมาทันเวลาพอดี";
		mes "คุกใต้ดินแห่งความทรงจำพร้อมที่จะเปิดแล้ว";
		mes "คุณต้องการจะเข้าตอนนี้ไหม?";
		next;

		if (is_party_leader() == true)
				.@menu$ = "Prepare Friday Memorial";

		switch( select( .@menu$, "Enter Friday Memorial", "Cancel" ) ) {
		case 1:
				instance_create(.@md_name$);
				mes "[แต่งงานกับเจย์]";
				mes "ได้สร้างสุสานแห่งความทรงจำแล้ว";
				mes "ตอนนี้คุณเข้าได้แล้ว";
				close;
		case 2:
				switch( instance_enter(.@md_name$) ) {
				case IE_NOMEMBER:
						if (is_party_leader() == true)
								end;
						mes "เฉพาะสมาชิกปาร์ตี้เท่านั้นที่สามารถเข้าสู่ดันเจี้ยนอนุสรณ์นี้ได้";
						close;
				case IE_NOINSTANCE:
						if (is_party_leader() == true) {
								mes "ไม่มีการลงทะเบียนคุกใต้ดินแห่งความทรงจำ";
								close;
						}
						mes "คุก Friday Memorial ไม่มีอยู่";
						mes "หัวหน้าปาร์ตี้ของคุณยังไม่ได้สร้างดันเจี้ยนอนุสรณ์";
						close;
				case IE_OTHER:
						mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
						close;
				case IE_OK:
						mes "[แต่งงานกับเจย์]";
						mes "ออกไปได้แล้ว!";
						mapannounce "gef_tower", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
						if (isbegin_quest(12379) < 1)
								setquest 12379;
						end;
				}
				end;
		case 3:
				mes "[แต่งงานกับเจย์]";
				mes "คุณไม่ได้เตรียมตัวเหรอ?";
				mes "คงช่วยไม่ได้จริงๆ ฉันเดานะ";
				close;
		}
		end;
}

//Instance
1@md_gef,185,81	monster	Shining Plant	1083,1,1800000,900000

1@md_gef,110,129,6	script	Marry Jay#0_1	4_F_JOB_BLACKSMITH,{
		if (is_party_leader() == false)
				end;
		mes "[แต่งงานกับเจย์]";
		mes "ก่อนที่เราจะเริ่มตั้งค่าสภาพแวดล้อมที่นี่ เราจะให้คุณมีโอกาสเลือกระดับความยากที่เหมาะสม";
		mes "ฉันสามารถเลือกได้ด้วยตัวเอง แต่แต่ละคนก็เลือกตามความสามารถของตัวเอง";
		next;
		switch( select( "Cancel", "LV130 ~ 199", "LV200 + " ) ) {
		case 1:
				close;
		case 2:
				//MinLevel, Lich Lord, Nightmare, Jakk, Ghoul, Drainliar
				'hard = 0;
				setarray 'mob[0],130,3658,3660,3662,3664,3666;
				break;
		case 3:
				if (BaseLevel < 200) {
						mes "[แต่งงานกับเจย์]";
						mes "คุณไม่สามารถเลือกความยากนี้ได้ เนื่องจากระดับฐานของคุณต่ำกว่า 200";
						close;
				}
				'hard = 1;
				setarray 'mob[0],200,3659,3661,3663,3665,3667;
				break;
		}
		if (BaseLevel < 'mob[0]) {
				mes "[แต่งงานกับเจย์]";
				mes "ระดับฐานของคุณต่ำเกินไปที่จะเลือกสิ่งนี้ คุณต้องมีระดับ" + 'mob[0] + "หรือสูงกว่า.";
				close;
		}
		close2;
		npctalk "ตอนนี้ที่นี่กำลังจะกลายเป็นแหล่งล่าเหยื่อ ฉันจะออกไปแล้วปิดประตูหน้า";
		sleep 4000;
		npctalk "โชคดีนะ~";
		sleep 3000;
		if ('step == 0) {
				'step = 1;
				donpcevent instance_npcname("md_gef_mobs_spawn") + "::OnStart";
				disablenpc();
				enablenpc instance_npcname("The Stranger#cadaver");
				enablenpc instance_npcname("Bizzare Sculpture#boss");
		}
		end;
}

1@md_gef,1,1,6	script	md_gef_mobs_spawn	-1,{
		end;
OnStart:
		.@npc_name$ = instance_npcname("md_gef_mobs_spawn");
		monster 'map_md_gef$,0,0,"Nightmare",'mob[2],20, .@npc_name$ + "::OnNightmareDead";
		monster 'map_md_gef$,0,0,"Jakk",'mob[3],30, .@npc_name$ + "::OnJakkDead";
		monster 'map_md_gef$,0,0,"Ghoul",'mob[4],30, .@npc_name$ + "::OnGhoulDead";
		monster 'map_md_gef$,0,0,"Drainliar",'mob[5],20, .@npc_name$ + "::OnDrainliarDead";
		end;

OnNightmareDead:
		callsub( S_Mob, 'mob[2], "Nightmare", "OnNightmareDead" );
OnJakkDead:
		callsub( S_Mob, 'mob[3], "Jakk", "OnJakkDead" );
OnGhoulDead:
		callsub( S_Mob, 'mob[4], "Ghoul", "OnGhoulDead" );
OnDrainliarDead:
		callsub( S_Mob, 'mob[5], "Drainliar", "OnDrainliarDead" );

S_Mob:
		'count++;
		if (('count % 100) == 0) {
				'rand_chest++;
				if ('rand_chest > 4)
						'rand_chest = 1;
				// Treasure Chest Spawn
				donpcevent instance_npcname("#fd_box" + 'rand_chest) + "::OnStart";
		}
		sleep 10000;
		monster 'map_md_gef$,0,0, getarg(1), getarg(0),1, instance_npcname("md_gef_mobs_spawn") + "::" + getarg(2);
		end;

OnInstanceInit:
		'step = 0;
		'hard = 0;
		'count = 0;
		'rand_chest = 0;
		'freestones = 0;
		'boss_actv = 0;
		'map_md_gef$ = instance_mapname("1@md_gef");
		end;
}


1@md_gef,110,129,6	script(DISABLED)	Mary Juj#0_2	4_F_JOB_BLACKSMITH,{
		npctalk "สวัสดี";
		end;
}

1@md_gef,183,222,5	script(DISABLED)	The Stranger#cadaver	4_TOWER_11,{
		mes "มันคือศพของคนแปลกหน้า";
		mes "บางทีเขาอาจเสียชีวิตขณะเดินสำรวจสถานที่แห่งนี้เพื่อค้นหาสมบัติ";
		next;
		if (select( "Calm down", "Investigate the body" ) == 1) {
				mes "คุณเคารพคนตายและตัดสินใจทิ้งศพไว้";
				close;
		}
		mes "เมื่อมองไปที่บันทึกที่เขียนอย่างรีบเร่งในกระเป๋าของเขา บางทีชื่อนักผจญภัยคนนั้นอาจจะเป็นวอลเตอร์";
		next;
		mes "[บันทึกของนักผจญภัย]";
		mes "ฉันชื่อวอลเตอร์ สำหรับนักผจญภัยที่จะมาเยือนสถานที่แห่งนี้ในอนาคต ฉันขอทิ้งเรื่องนี้ไว้เพื่อให้คุณไม่ทำผิดพลาดซ้ำสอง";
		next;
		mes "[บันทึกของนักผจญภัย]";
		mes "ในวันแรก ฉันวางมือของฉันลงบนแผ่นประติมากรรมประหลาดที่อยู่ใกล้ๆ ด้วยความอยากรู้อยากเห็น";
		mes "...และฉันก็ปลุกบางสิ่งบางอย่างที่ฉันไม่ควรปลุก";
		next;
		mes "[บันทึกของนักผจญภัย]";
		mes "ขณะนี้ ราชาโบราณที่ฟื้นคืนชีพกำลังเดินอยู่ตรงหน้าฉัน";
		mes "บางทีเขาอาจจะไม่มีความทรงจำเกี่ยวกับชีวิตก่อนหน้านี้ของเขาเลย";
		mes "สิ่งหนึ่งที่แน่นอน เขาจะฆ่าฉัน";
		next;
		mes "[บันทึกของนักผจญภัย]";
		mes "สำหรับนักผจญภัยที่มาเยือนสถานที่แห่งนี้ในอนาคต ฉันหวังว่าคุณจะไม่ทำผิดพลาดแบบเดียวกับฉัน";
		mes "หากคุณเห็นแผ่นที่น่าสงสัย อย่าแสดงความสนใจและเดินผ่านไป";
		next;
		mes "[บันทึกของนักผจญภัย]";
		mes "อย่าแตะแผ่นหินอีกเลย ไม่ว่าคุณจะเจอสิ่งยัวยวนใจอะไรก็ตาม";
		if ('freestones != 0)
				close;
		next;
		mes "มีหินฝีมือหลายก้อนหล่นออกมาจากร่างของเขา ซึ่งอาจรวบรวมได้ขณะสำรวจสถานที่แห่งนี้";
		close2;
		if ('freestones == 0) {
				'freestones = 1;
				if ('hard == 1)
						.@num = rand(7,9);
				else
						.@num = rand(5,7);
				for ( .@i = 0; .@i < .@num; ++.@i )
						makeitem 25235,1, 'map_md_gef$, rand(181,183), rand(221,224);
		}
		end;
}

1@md_gef,199,73,0	script(DISABLED)	Bizzare Sculpture#boss	CLEAR_NPC,{
		if ('count < 100) {
				mes "เป็นประติมากรรมประหลาดมีฝุ่นเกาะเต็มไปหมด ไม่เห็นมีอะไรน่าสงสัยเลย";
				close;
		}
		if ('boss_actv >= 1) {
				mes "หินฝีมือถูกหลอมละลายเข้าไปในช่องด้วยแรงอันทรงพลัง";
				mes "ดูเหมือนซ็อกเก็ตจะใช้งานไม่ได้สักพักหนึ่ง";
				close;
		}
		mes "ในบางจุด ฝุ่นที่สะสมจะกระจายออก และมองเห็นรูกลวง 10 รูบนแผ่นใต้ประติมากรรม";
		next;
		mes "ช่องใส่กลวงทั้ง 10 ช่องนั้นดูเหมือนว่าจะพอดีกับหินงานฝีมือ";
		next;
		if (select( "Ignore", "Try inserting craft stones" ) == 1)
				end;
		if (countitem(25235) < 10) {
				mes "หินฝีมือถูกหลอมละลายเข้าไปในช่องด้วยแรงอันทรงพลัง";
				mes "ดูเหมือนซ็อกเก็ตจะใช้งานไม่ได้สักพักหนึ่ง";
				close;
		}
		mes "เมื่อคุณเสียบหินงานฝีมือเข้าไปในหลุม มันจะเปล่งแสงสว่างออกมาและละลาย";
		next;
		delitem 25235,10;
		specialeffect EF_ANGELUS;

		if (rand(1,10) == 10) {
				mes "ดูเหมือนว่าจะมีบางสิ่งบางอย่างเปลี่ยนไปในอากาศรอบตัวฉัน";
				instance_announce 0, "Lord of the Dead: Who Awakened Me? I will meet you in person and ask you why.", bc_map, "0xFFFF00";
				if ('boss_actv == 0) {
						'boss_actv = 1;
						monster 'map_md_gef$,210,110,"Lich King",'mob[1],1;
				}
		}
		else {
				mes "แต่ก็ไม่มีอะไรเกิดขึ้นอีก";
				close2;
				if ('boss_actv == 0) {
						disablenpc();	// Disable NPC for 5 minutes when MVP didn't spawn.
						initnpctimer;
				}
				end;
		}
		close;

OnTimer300000:
		stopnpctimer;
		enablenpc();
		end;
}


// Treasure Chest Reward
1@md_gef,212,212,3	script(DISABLED)	#fd_box1	4_TREASURE_BOX,{
		specialeffect EF_COIN;
		disablenpc();
		stopnpctimer;
		sleep 500;

		if ('hard == 1)
				.@num = rand(7,9);
		else
				.@num = rand(5,7);

		.@npc_name$ = strnpcinfo(2);

		if (.@npc_name$ == "fd_box1") {
				for ( .@i = 0; .@i < .@num; ++.@i )
						makeitem 25235,1, 'map_md_gef$, rand(210,214), rand(210,214);
		}
		if (.@npc_name$ == "fd_box2") {
				for ( .@i = 0; .@i < .@num; ++.@i )
						makeitem 25235,1, 'map_md_gef$, rand(188,192), rand(54,58);
		}
		if (.@npc_name$ == "fd_box3") {
				for ( .@i = 0; .@i < .@num; ++.@i )
						makeitem 25235,1, 'map_md_gef$, rand(47,51), rand(55,59);
		}
		if (.@npc_name$ == "fd_box4") {
				for ( .@i = 0; .@i < .@num; ++.@i )
						makeitem 25235,1, 'map_md_gef$, rand(42,46), rand(209,213);
		}
		end;

OnStart:
		enablenpc();
		instance_announce 0, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.", bc_map, "0xff5500";
		initnpctimer;
		end;
OnTimer180000:	//3min before chest disappears
		disablenpc();
		stopnpctimer;
		end;
}
1@md_gef,190,56,3	duplicate(#fd_box1)	#fd_box2	4_TREASURE_BOX
1@md_gef,49,57,3	duplicate(#fd_box1)	#fd_box3	4_TREASURE_BOX
1@md_gef,44,211,3	duplicate(#fd_box1)	#fd_box4	4_TREASURE_BOX


//Merchant
gef_tower,57,167,3	script	Amateur Collector#pa0829	1_F_01,{
		disable_items;
		function F_equip_menu;
		function F_enchant;

		if (checkweight(1201,1) == 0) {
				mes "คุณมีวัตถุหลายประเภทเกินไป เคลียร์คลังสินค้าของคุณ";
				close;
		}
		if (MaxWeight - Weight < 10000) {
				mes "ไม่สามารถดำเนินการต่อได้เนื่องจากคุณมีสิ่งของหนักมากเกินไป ลดน้ำหนักรวมของคุณโดยเคลียร์สินค้าคงคลัง";
				close;
		}
		mes "[นักสะสมสมัครเล่น]";
		mes "หากคุณนำฉันมา" + mesitemlink(25235, false) + "ฉันสามารถเสกเอฟเฟกต์อันวิเศษให้กับเครื่องประดับของคุณได้!";
		next;
		switch( select( "What can you do with the Crafting Stone?", "Accessory Enchantment", "Reset Enchant" ) ) {
		case 1:
				mes "[นักสะสมสมัครเล่น]";
				mes "ฉันแลกหินราคาแพงจากคุกใต้ดินเพื่อแลกกับฝีมือของฉัน ฉันยังเป็นมือใหม่ แต่ทักษะของฉันก็ผ่านเกณฑ์";
				mes "ช่วยดูแลฉันด้วย.";
				next;
				if (select( "I've heard this before.", "I'm listening." ) == 1) {
						mes "[นักสะสมสมัครเล่น]";
						mes "เป็นอย่างนั้นเหรอ?";
						close;
				}
				mes "[นักสะสมสมัครเล่น]";
				mes "ด้วยการใช้ Craft Stone ฉันสามารถเสกคาถาความสามารถให้กับอุปกรณ์เสริมของคุณชิ้นหนึ่งได้";
				next;
				mes "[นักสะสมสมัครเล่น]";
				mes "ฉันต้องการ 10 x" + mesitemlink(25235, false) + "และ 100,000 เซนี";
				mes "ไม่มีความล้มเหลวในกระบวนการร่ายมนตร์";
				next;
				mes "[นักสะสมสมัครเล่น]";
				mes "อย่างไรก็ตาม ^0000FFหากอุปกรณ์เสริมมีตัวเลือกแบบสุ่ม ตัวเลือกแบบสุ่มจะถูกลบออกหลังจากการร่ายมนตร์^000000";
				next;
				select( "Anything else?" );
				mes "[นักสะสมสมัครเล่น]";
				mes "ไพ่ที่เสียบอยู่กับอุปกรณ์เสริมจะยังคงอยู่หลังจากการร่ายมนตร์แล้ว";
				next;
				mes "[นักสะสมสมัครเล่น]";
				mes "การรีเซ็ตเป็นไปได้ แต่ฉันไม่สามารถพูดได้ว่าฉันมีประสบการณ์";
				mes "อัตราความสำเร็จของฉันคือ:";
				mes "แหวนรอยัล : 80%";
				mes "อุปกรณ์เสริมอื่นๆ : 20%";
				next;
				mes "[นักสะสมสมัครเล่น]";
				mes "^0000FFหากอุปกรณ์เสริมมีตัวเลือกแบบสุ่ม ตัวเลือกแบบสุ่มจะถูกลบออกหลังจากการเริ่มต้นระบบ^000000";
				close;
		case 2:
				.@type = 1;
				.@type_name$ = "enchant";
				break;
		case 3:
				.@type = 2;
				.@type_name$ = "reset";
				break;
		}

		.@slot = F_equip_menu();
		.@equip_id = getequipid(.@slot);
		setarray .@equip_card[0], getequipcardid(.@slot,0), getequipcardid(.@slot,1), getequipcardid(.@slot,2), getequipcardid(.@slot,3);
        copyarray .@card[0], .@equip_card[0], 4;

		switch( .@equip_id ) {
		case 0:
				mes "[นักสะสมสมัครเล่น]";
				mes "กรุณาติดตั้งอุปกรณ์เสริมของคุณ";
				close;
		case 28483:
		case 2601:
		case 2602:
		case 2603:
		case 2605:
		case 2607:
		case 2608:
		case 2609:
		case 2610:
		case 2611:
		case 2612:
		case 2613:
		case 2614:
		case 2615:
		case 2616:
		case 2617:
		case 2618:
		case 2619:
		case 2620:
		case 2621:
		case 2622:
		case 2623:
		case 2625:
		case 2626:
		case 2627:
		case 2628:
		case 2636:
		case 2637:
		case 2638:
		case 2639:
		case 2640:
		case 2641:
		case 2645:
		case 2648:
		case 2649:
		case 2650:
		case 2651:
		case 2652:
		case 2653:
		case 2654:
		case 2655:
		case 2656:
		case 2658:
		case 2663:
		case 2664:
		case 2665:
		case 2666:
		case 2667:
		case 2671:
		case 2680:
		case 2692:
		case 2700:
		case 2701:
		case 2702:
		case 2703:
		case 2712:
		case 2714:
		case 2715:
		case 2716:
		case 2718:
		case 2719:
		case 2720:
		case 2721:
		case 2722:
		case 2723:
		case 2724:
		case 2725:
		case 2726:
		case 2727:
		case 2728:
		case 2729:
		case 2730:
		case 2731:
		case 2732:
		case 2736:
		case 2737:
		case 2743:
		case 2744:
		case 2745:
		case 2746:
		case 2747:
		case 2748:
		case 2749:
		case 2766:
		case 2767:
		case 2768:
		case 2769:
		case 2770:
		case 2771:
		case 2772:
		case 2773:
		case 2774:
		case 2777:
		case 2778:
		case 2779:
		case 2780:
		case 2781:
		case 2783:
		case 2784:
		case 2787:
		case 2788:
		case 2789:
		case 2790:
		case 2794:
		case 2795:
		case 2800:
		case 2801:
		case 2802:
		case 2803:
		case 2804:
		case 2805:
		case 2807:
		case 2808:
		case 2809:
		case 2810:
		case 2826:
		case 2827:
		case 2843:
		case 2853:
		case 2854:
		case 2881:
		case 2956:
				break;
		default:
				mes "[นักสะสมสมัครเล่น]";
				mes "ฉันไม่สามารถ" + .@type_name$ + "นี้" + mesitemlink(.@equip_id, false) + "อุปกรณ์เสริม";
				close;
		}

		if (.@type == 1) {	// enchant
				switch( .@equip_id ) {
				case 28483:	//Royal Ring Enchantment
						if (.@equip_card[2] != 0) {
								mes "[นักสะสมสมัครเล่น]";
								mes "การ" + mesitemlink(.@equip_id, false) + "มีมนต์เสน่ห์สูงสุดแล้ว";
								mes "ฉันไม่สามารถดำเนินการนี้ต่อไปได้อีก";
								close;
						}
						.@pos = .@equip_card[3] != 0 ? 2 : 3;
						mes "[นักสะสมสมัครเล่น]";
						mes "กรุณาเลือกประเภทการร่ายมนตร์~";
						next;
						switch( select( "Physical", "Magical", "Ranged" ) ) {
						case 1:
								if (.@pos == 2)
										.@equip_card[2] = F_enchant(2);
								else
										.@equip_card[3] = F_enchant(3);
								break;
						case 2:
								if (.@pos == 2)
										.@equip_card[2] = F_enchant(4);
								else
										.@equip_card[3] = F_enchant(5);
								break;
						case 3:
								if (.@pos == 2)
										.@equip_card[2] = F_enchant(6);
								else
										.@equip_card[3] = F_enchant(7);
								break;
						}
						if (.@pos == 2) {
								mes "[นักสะสมสมัครเล่น]";
								mes "คุณแน่ใจที่จะปรับปรุง" + mesitemlink(.@equip_id, false) + "-";
								mes "^0000FFไม่มีทางที่จะล้มเหลว^000000";
								next;
								if (select( "Yes, please proceed", "Cancel" ) == 2)
										end;
						}
						if (countitem(25235) < 10) {
								mes "[นักสะสมสมัครเล่น]";
								mes "กรุณาเอามาให้ฉันสัก 10 x" + mesitemlink(25235, false) + "-";
								close;
						}
						if (Zeny < 100000) {
								mes "[นักสะสมสมัครเล่น]";
								mes "ฉันต้องการ 100,000 เซนี!";
								close;
						}
						delitem 25235,10; //Catalyst
						Zeny -= 100000;

						// anti-hack
						if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
								end;

						delequip .@slot;
						specialeffect2 EF_REPAIRWEAPON;
						mes "[นักสะสมสมัครเล่น]";
						mes "การ" + mesitemlink(.@equip_id, false) + "ถูกเสกแล้ว!!";
						getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
						close;

				default:	// Normal Accessory Enchantment
						if (.@equip_card[3] != 0) {
								mes "[นักสะสมสมัครเล่น]";
								mes "การ" + mesitemlink(.@equip_id, false) + "มีมนต์เสน่ห์แล้ว";
								mes "ฉันไม่สามารถดำเนินการนี้ต่อไปได้อีก";
								close;
						}
						mes "[นักสะสมสมัครเล่น]";
						mes "คุณแน่ใจหรือไม่ว่าต้องการปรับปรุง" + mesitemlink(.@equip_id, false) + "-";
						mes "^0000FFไม่มีทางที่จะล้มเหลว^000000";
						next;
						if (select( "Yes, please proceed", "Cancel") == 2)
								end;
						if (countitem(25235) < 10) {
								mes "[นักสะสมสมัครเล่น]";
								mes "กรุณาเอามาให้ฉันสัก 10 x" + mesitemlink(25235, false) + "-";
								close;
						}
						if (Zeny < 100000) {
								mes "[นักสะสมสมัครเล่น]";
								mes "ฉันต้องการ 100,000 เซนี!";
								close;
						}
						delitem 25235,10; //Catalyst
						Zeny -= 100000;

						// anti-hack
						if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
								end;

						delequip .@slot;
						.@enchant = F_enchant(1);
						specialeffect2 EF_REPAIRWEAPON;
						mes "[นักสะสมสมัครเล่น]";
						mes "การ" + mesitemlink(.@equip_id, false) + "ได้รับการเสกให้หลงใหลด้วย" + mesitemlink(.@enchant, false) + "-";
						getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;
						close;
				}
				end;
		}
		else {	// reset
				if (.@equip_id == 28483) {
						.@pos = 2;
						.@success = 80;	// success royal
				} else {
						.@pos = 3;
						.@success = 20;	// success normal
				}
				if (.@equip_card[.@pos] == 0) {
						mes "[นักสะสมสมัครเล่น]";
						mes "การ" + mesitemlink(.@equip_id, false) + "ยังไม่ถึงระดับมนต์เสน่ห์สูงสุด";
						mes "ฉันไม่สามารถรีเซ็ตอุปกรณ์เสริมนี้ได้";
						close;
				}
				mes "[นักสะสมสมัครเล่น]";
				mes "เอฟเฟกต์มนต์สะกดทั้งหมดจะถูกลบออกจาก" + mesitemlink(.@equip_id, false) + "-";
				mes "^FF0000มี" + .@success + "โอกาสสำเร็จ % อุปกรณ์เสริมจะถูกทำลายหากการรีเซ็ตล้มเหลว!^000000";
				next;
				if (select( "I'll be back later", "Please proceed") == 1)
						end;
				if (countitem(25235) < 10) {
						mes "[นักสะสมสมัครเล่น]";
						mes "กรุณาเอามาให้ฉันสัก 10 x" + mesitemlink(25235, false) + "";
						close;
				}
				if (Zeny < 100000) {
						mes "[นักสะสมสมัครเล่น]";
						mes "ฉันต้องการ 100,000 เซนี!";
						close;
				}
				delitem 25235,10; //Catalyst
				Zeny -= 100000;

				// anti-hack
				if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
						end;

				delequip .@slot;
				if (rand(100) > .@success) {
						specialeffect2 EF_SUI_EXPLOSION;
						mes "[นักสะสมสมัครเล่น]";
						mes "บ้าเอ้ย...";
						mes "ฉันล้มเหลวและอุปกรณ์เสริมถูกทำลาย ฉันขอโทษจริงๆ";
						close;
				}
				specialeffect2 EF_REPAIRWEAPON;
				mes "[นักสะสมสมัครเล่น]";
				mes "ฉันทำสำเร็จแล้ว.";
				mes "การ" + mesitemlink(.@equip_id, false) + "ได้มีการเริ่มต้นแล้ว";
				getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],0,0;
				close;
		}
		end;

function	F_equip_menu {
		.@l$ = getitemname(getequipid(EQI_ACC_L));
		.@r$ = getitemname(getequipid(EQI_ACC_R));
		.@acc_l$ = .@l$ == "null" ? "" : "Left Acc - " + .@l$;
		.@acc_r$ = .@r$ == "null" ? "" : "Right Acc - " + .@r$;

		switch( select( .@acc_l$, .@acc_r$ ) ) {
		case 1:
				.@slot = EQI_ACC_L;
				break;
		case 2:
				.@slot = EQI_ACC_R;
				break;
		}
		return .@slot;
}

function	F_enchant {
		.@type = getarg(0);

		switch(.@type) {
		case 1: //Normal Enchant
				.@i = rand(920);
			 			 if (.@i < 30) .@enchant = 4701; //STR+2
				else if (.@i < 60) .@enchant = 4702; //STR+3
				else if (.@i < 90) .@enchant = 4703; //STR+4
				else if (.@i < 120) .@enchant = 4711; //INT+2
				else if (.@i < 150) .@enchant = 4712; //INT+3
				else if (.@i < 180) .@enchant = 4713; //INT+4
				else if (.@i < 210) .@enchant = 4721; //DEX+2
				else if (.@i < 240) .@enchant = 4722; //DEX+3
				else if (.@i < 270) .@enchant = 4723; //DEX+4
				else if (.@i < 300) .@enchant = 4731; //AGI+2
				else if (.@i < 330) .@enchant = 4732; //AGI+3
				else if (.@i < 360) .@enchant = 4733; //AGI+4
				else if (.@i < 390) .@enchant = 4741; //VIT+2
				else if (.@i < 420) .@enchant = 4742; //VIT+3
				else if (.@i < 450) .@enchant = 4743; //VIT+4
				else if (.@i < 480) .@enchant = 4796; //HP+200
				else if (.@i < 510) .@enchant = 4798; //HP+400
				else if (.@i < 540) .@enchant = 4792; //DEF+6
				else if (.@i < 570) .@enchant = 4794; //DEF+12
				else if (.@i < 600) .@enchant = 4800; //SP+50
				else if (.@i < 630) .@enchant = 4801; //SP+100
				else if (.@i < 660) .@enchant = 4786; //MDEF+2
				else if (.@i < 690) .@enchant = 4787; //MDEF+4
				else if (.@i < 710) .@enchant = 4861; //MHP+1%
				else if (.@i < 730) .@enchant = 4862; //MHP+2%
				else if (.@i < 750) .@enchant = 4929; //MSP+1%
				else if (.@i < 770) .@enchant = 4882; //ATK+1%
				else if (.@i < 790) .@enchant = 4883; //MATK+1%
				else if (.@i < 800) .@enchant = 4817; //Sharp2
				else if (.@i < 810) .@enchant = 4816; //Sharp3
				else if (.@i < 815) .@enchant = 4843; //Sharp4
				else if (.@i < 825) .@enchant = 29047; //Fatal
				else if (.@i < 835) .@enchant = 4863; //Fatal1Lv
				else if (.@i < 840) .@enchant = 4864; //Fatal2Lv
				else if (.@i < 850) .@enchant = 4832; //Expert Archer1
				else if (.@i < 860) .@enchant = 4813; //Spell3
				else if (.@i < 870) .@enchant = 4812; //Spell4
				else if (.@i < 875) .@enchant = 4826; //Spell5
				else if (.@i < 885) .@enchant = 4805; //Archbishop1Lv
				else if (.@i < 895) .@enchant = 4850; //Heal Amount2
				else if (.@i < 905) .@enchant = 4809; //Fighting Spirit3
				else if (.@i < 915) .@enchant = 4808; //Fighting Spirit4
				else .@enchant = 4820; //Fighting Spirit5
				break;
		case 2: //Royal Physical Slot 3
				.@i = rand(225);
			 			 if (.@i < 30) .@enchant = 4809; //Fighting Spirit3
				else if (.@i < 60) .@enchant = 4808; //Fighting Spirit4
				else if (.@i < 70) .@enchant = 4820; //Fighting Spirit5
				else if (.@i < 80) .@enchant = 4821; //Fighting Spirit6
				else if (.@i < 85) .@enchant = 4822; //Fighting Spirit7
				else if (.@i < 115) .@enchant = 4818; //Sharp1
				else if (.@i < 145) .@enchant = 4817; //Sharp2
				else if (.@i < 155) .@enchant = 4816; //Sharp3
				else if (.@i < 165) .@enchant = 4843; //Sharp4
				else if (.@i < 170) .@enchant = 4844; //Sharp5
				else if (.@i < 200) .@enchant = 4819; //Atk1
				else if (.@i < 210) .@enchant = 4766; //ATK+2%
				else if (.@i < 215) .@enchant = 4767; //ATK+3%
				else if (.@i < 220) .@enchant = 4807; //Atk Speed1
				else .@enchant = 4842; //Atk Speed2
				break;
		case 3: //Royal Physical Slot 4
				.@i = rand(375);
			 			 if (.@i < 50) .@enchant = 4702; //STR+3
				else if (.@i < 100) .@enchant = 4703; //STR+4
				else if (.@i < 110) .@enchant = 4704; //STR+5
				else if (.@i < 120) .@enchant = 4705; //STR+6
				else if (.@i < 125) .@enchant = 4706; //STR+7
				else if (.@i < 175) .@enchant = 4732; //AGI+3
				else if (.@i < 225) .@enchant = 4733; //AGI+4
				else if (.@i < 235) .@enchant = 4734; //AGI+5
				else if (.@i < 245) .@enchant = 4735; //AGI+6
				else if (.@i < 250) .@enchant = 4736; //AGI+7
				else if (.@i < 300) .@enchant = 4742; //VIT+3
				else if (.@i < 350) .@enchant = 4743; //VIT+4
				else if (.@i < 360) .@enchant = 4744; //VIT+5
				else if (.@i < 370) .@enchant = 4745; //VIT+6
				else .@enchant = 4746; //VIT+7
				break;
		case 4: //Royal Magical Slot 3
				.@i = rand(230);
			 			 if (.@i < 30) .@enchant = 4813; //Spell3
				else if (.@i < 60) .@enchant = 4812; //Spell4
				else if (.@i < 70) .@enchant = 4826; //Spell5
				else if (.@i < 80) .@enchant = 4827; //Spell6
				else if (.@i < 85) .@enchant = 4828; //Spell7
				else if (.@i < 115) .@enchant = 4929; //MSP+1%
				else if (.@i < 145) .@enchant = 4805; //Archbishop1Lv
				else if (.@i < 155) .@enchant = 4850; //Archbishop2Lv
				else if (.@i < 165) .@enchant = 4851; //Archbishop3Lv
				else if (.@i < 170) .@enchant = 4852; //Archbishop4Lv
				else if (.@i < 200) .@enchant = 4883; //MATK + 1%
				else if (.@i < 210) .@enchant = 4896; //MATK + 2%
				else if (.@i < 215) .@enchant = 4897; //MATK + 3%
				else if (.@i < 225) .@enchant = 4760; //MATK 1Lv
				else .@enchant = 4761; //MATK 2Lv
				break;
		case 5: //Royal Magical Slot 4
				.@i = rand(375);
			 			 if (.@i < 50) .@enchant = 4712; //INT+3
				else if (.@i < 100) .@enchant = 4713; //INT+4
				else if (.@i < 110) .@enchant = 4714; //INT+5
				else if (.@i < 120) .@enchant = 4715; //INT+6
				else if (.@i < 125) .@enchant = 4716; //INT+7
				else if (.@i < 175) .@enchant = 4722; //DEX+3
				else if (.@i < 225) .@enchant = 4723; //DEX+4
				else if (.@i < 235) .@enchant = 4724; //DEX+5
				else if (.@i < 245) .@enchant = 4725; //DEX+6
				else if (.@i < 250) .@enchant = 4726; //DEX+7
				else if (.@i < 300) .@enchant = 4742; //VIT+3
				else if (.@i < 350) .@enchant = 4743; //VIT+4
				else if (.@i < 360) .@enchant = 4744; //VIT+5
				else if (.@i < 370) .@enchant = 4745; //VIT+6
				else .@enchant = 4746; //VIT+7
				break;
		case 6: //Royal Ranged Slot 3
				.@i = rand(225);
			 			 if (.@i < 30) .@enchant = 29047; //Fatal
				else if (.@i < 60) .@enchant = 4863; //Fatal1Lv
				else if (.@i < 70) .@enchant = 4864; //Fatal2Lv
				else if (.@i < 80) .@enchant = 4865; //Fatal3Lv
				else if (.@i < 85) .@enchant = 4866; //Fatal4Lv
				else if (.@i < 115) .@enchant = 4818; //Sharp1
				else if (.@i < 145) .@enchant = 4817; //Sharp2
				else if (.@i < 155) .@enchant = 4816; //Sharp3
				else if (.@i < 165) .@enchant = 4843; //Sharp4
				else if (.@i < 170) .@enchant = 4844; //Sharp5
				else if (.@i < 200) .@enchant = 4832; //Expert Archer1
				else if (.@i < 210) .@enchant = 4833; //Expert Archer2
				else if (.@i < 215) .@enchant = 4834; //Expert Archer3
				else if (.@i < 220) .@enchant = 4807; //Atk Speed1
				else .@enchant = 4842; //Atk Speed2
				break;
		case 7: //Royal Ranged Slot 4
				.@i = rand(375);
			 			 if (.@i < 50) .@enchant = 4732; //AGI+3
				else if (.@i < 100) .@enchant = 4733; //AGI+4
				else if (.@i < 110) .@enchant = 4734; //AGI+5
				else if (.@i < 120) .@enchant = 4735; //AGI+6
				else if (.@i < 125) .@enchant = 4736; //AGI+7
				else if (.@i < 175) .@enchant = 4722; //DEX+3
				else if (.@i < 225) .@enchant = 4723; //DEX+4
				else if (.@i < 235) .@enchant = 4724; //DEX+5
				else if (.@i < 245) .@enchant = 4725; //DEX+6
				else if (.@i < 250) .@enchant = 4726; //DEX+7
				else if (.@i < 300) .@enchant = 4752; //LUK+3
				else if (.@i < 350) .@enchant = 4753; //LUK+4
				else if (.@i < 360) .@enchant = 4754; //LUK+5
				else if (.@i < 370) .@enchant = 4755; //LUK+6
				else .@enchant = 4756; //LUK+7
				break;
		}
		return .@enchant;
}
}
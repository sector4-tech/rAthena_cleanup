//===== rAthena Script =======================================
//= Temple of Demon God
//===== Description: =========================================
//= [Official Conversion]
//= Temple of Demon God Instance (part of episode 14.3)
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

moro_cav,41,73,4	script	Guardian Nidhogg#epeom01	4_F_NYDHOG,{
		if (checkquest(7605,PLAYTIME) == 2)
				erasequest 7605;
		if (getcharid(1) < 1) {
				mes "[นิดฮ็อกก์]";
				mes "สถานที่แห่งนี้อาจอันตรายเกินไปสำหรับคุณที่จะสำรวจคนเดียว";
				mes "โปรดรวมกลุ่มแล้วมาพร้อมกับเพื่อนๆ ของคุณ";
				cutin "ep14_nyd01.bmp",2;
				close3;
		}
		if (is_party_leader() == false) {
				mes "[นิดฮ็อกก์]";
				mes "มีเพียงหัวหน้าปาร์ตี้เท่านั้นที่สามารถควบคุมการเข้าถึงวิหารของเทพปีศาจได้";
				mes "เพื่อความปลอดภัยของผู้เข้าแข่งขันทั้งหมด โปรดให้หัวหน้าของคุณคุยกับฉันแทน";
				cutin "ep14_nyd01.bmp",2;
				close3;
		}
		switch( checkquest(7593,HUNTING) ) {
		case -1:
				mes "[นิดฮ็อกก์]";
				mes "ผมขออภัยนะครับ แต่คุณไม่สามารถเข้าไปในสถานที่นี้ได้หากไม่ได้รับอนุญาตจากผู้บัญชาการ";
				mes "คุณต้องอยู่ในภารกิจปราบเทพปีศาจเพื่อเข้าสู่สถานที่แห่งนี้";
				cutin "ep14_nyd01.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "กรุณาไปหาผู้บัญชาการ Hiva Agip และรับภารกิจปราบปีศาจเทพ";
				close3;
		case 0:
		case 1:
				mes "[นิดฮ็อกก์]";
				mes "ฉันรอคุณอยู่นะ ฮีโร่ผู้รอคอย";
				mes "ให้ฉันนำคุณไปสู่ส่วนลึกของวิหารที่ Morroc ซ่อนอยู่";
				cutin "ep14_nyd03.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "ต้นไม้ต้นนี้เป็นภาพจำลองของต้น Yggdrasil";
				mes "ฉันแค่หวังว่ามอร์ร็อคคงไม่ตั้งใจกับสิ่งที่ฉันกลัวที่สุด";
				mes "ประตูจะเปิดออกได้จากต้นไม้ต้นนี้";
				next;
				mes "[นิดฮ็อกก์]";
				mes "ฉันรู้สึกได้ว่า";
				mes "ต้นไม้ต้นนี้สร้างมานาเป็นระยะ ๆ ราวกับว่ามันกำลังหายใจ";
				mes "ฉันจะหาเวลาพักหายใจสักครู่เพื่อเปิดทางให้คุณ คุณพร้อมหรือยัง";
				cutin "ep14_nyd04.bmp",2;
				next;
				if (select( "Enter now.", "Do not enter." ) == 2) {
						mes "[นิดฮ็อกก์]";
						mes "ตกลง.";
						mes "ฉันเข้าใจหากคุณต้องการเวลา";
						mes "ค่อยๆ หน่อย ฉันรอได้เท่าที่คุณต้องการ";
						cutin "ep14_nyd04.bmp",2;
						close3;
				}
				if (instance_create("Temple of the Demon God") < 1) {	// todo, custom text
						mes "ชื่อปาร์ตี้:" + getpartyname(.@party_id);
						mes "หัวหน้าพรรค:" + strcharinfo(0);
						mes "^0000ff" + .@md_name$ + "^000000 - การจองล้มเหลว";
						close;
				}
				if (isbegin_quest(7596) == 1)
						erasequest 7596;
				mes "[นิดฮ็อกก์]";
				mes "เพียงรอสักพักทางเข้าก็จะเปิดออกมา";
				mes "เข้ามาทางต้นไม้";
				mes "อย่ากลัวเลยถึงมันจะไม่ใช่ต้นไม้ธรรมดาก็ตาม";
				cutin "ep14_nyd04.bmp",2;
				close3;
		case 2:	// todo, custom text
				mes "[นิดฮ็อกก์]";
				mes "คุณได้กำจัดเทพแห่งความสิ้นหวังแล้ว!?";
				mes "ผมพูดได้เพียงว่าความทะเยอทะยานของเขายิ่งใหญ่เกินไป...";
				mes "พูดเสมอเรื่องการสร้างโลกใหม่และการเป็นพระเจ้า...";
				cutin "ep14_nyd03.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "แต่ที่จริงเขาไม่ได้สร้างอะไรเลย!";
				mes "ละทิ้งร่างที่เคยเป็นอสูรแล้วมาเกิดใหม่เป็นเพียงการเลียนแบบเทพ...";
				next;
				mes "[นิดฮ็อกก์]";
				mes "ผลลัพธ์ที่ได้เป็นเพียงสิ่งที่มนุษย์ดูเหมือน";
				mes "เขาไม่อาจหลบหนีจากสิ่งที่เรียกว่ากรอบของพระเจ้าได้";
				mes "...ก็พูดได้แค่ว่ากำลังกินเคราะห์ร้ายอยู่!";
				cutin "ep14_nyd02.bmp",2;
				next;
				cutin "ep14_nyd04.bmp",2;
				mes "[นิดฮ็อกก์]";
				mes "ฉันจะอนุมัติภารกิจให้คุณ และคุณสามารถรายงานต่อผู้บัญชาการได้";
				mes "ขอบคุณ!";
				setquest 7597;
				erasequest 7593;
				for ( .@quest = 7601; .@quest < 7605; .@quest++ ) {
						if (isbegin_quest(.@quest))
								erasequest .@quest;
				}
				if (isbegin_quest(7596))
						erasequest 7596;
				close3;
		}
}

moro_cav,45,75,0	script	Yggdrasil Lookalike#eom_gate	CLEAR_NPC,{
		switch( checkquest(7593,HUNTING) ) {
		case -1:
				mes "[นิดฮ็อกก์]";
				mes "ผมขออภัยนะครับ แต่คุณไม่สามารถเข้าไปในสถานที่นี้ได้หากไม่ได้รับอนุญาตจากผู้บัญชาการ";
				mes "คุณต้องอยู่ในภารกิจปราบเทพปีศาจเพื่อเข้าสู่สถานที่แห่งนี้";
				cutin "ep14_nyd01.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "โปรดไปหาผู้บัญชาการ Hiva Agip และรับภารกิจ 'การปราบเทพปีศาจ'";
				close3;
		case 0:
		case 1:
				mes "คุณได้สัมผัสต้นไม้และรู้สึกถึงกระแสมานาอันแปลกประหลาด";
				mes "บางทีการไหลของมานาอาจนำคุณไปสู่ส่วนลึกของวิหารได้";
				next;
				if (select( "Enter.", "Quit." ) == 2) {
						mes "[นิดฮ็อกก์]";
						mes "ตกลง.";
						mes "ฉันเข้าใจหากคุณต้องการเวลา";
						mes "ค่อยๆ หน่อย ฉันรอได้เท่าที่คุณต้องการ";
						cutin "ep14_nyd04.bmp",2;
						close3;
				}
				switch( instance_enter("Temple of the Demon God") ) {
				case IE_NOMEMBER:
						mes "[นิดฮ็อกก์]";
						mes "สถานที่แห่งนี้อาจอันตรายเกินไปสำหรับคุณที่จะสำรวจคนเดียว";
						mes "โปรดรวมกลุ่มแล้วมาพร้อมกับเพื่อนๆ ของคุณ";
						cutin "ep14_nyd01.bmp",2;
						close3;
				case IE_NOINSTANCE:
						mes "[นิดฮ็อกก์]";
						mes "ไม่ได้ออกใบอนุญาตเข้าภายใต้ชื่อกลุ่มของคุณ";
						mes "กรุณาให้หัวหน้ากลุ่มของท่านส่งคำขอเข้า แล้วผมจะเปิดประตูเข้าสู่วัดให้";
						cutin "ep14_nyd01.bmp",2;
						next;
						mes "[นิดฮ็อกก์]";
						mes "คุณจะต้องปฏิบัติตามกฎเกณฑ์ที่กำหนดโดยคณะสำรวจ";
						mes "มันเพื่อความปลอดภัยของทุกคน";
						close3;
				case IE_OTHER:	// todo, custom text
						mes "[นิดฮ็อกก์]";
						mes "เอ่อ...มันเกิดขึ้นได้ยังไงเนี่ย...กระแสพลังเวทย์มันไม่เสถียร";
						mes "มันทำให้สมองฉันเจ็บ นี่คือช่องว่างที่ฉันสร้างขึ้นในที่สุด...";
						mes "อาจจะไม่สามารถเข้าได้ในขณะนี้...";
						cutin "ep14_nyd01.bmp",2;
						close3;
				case IE_OK:
						mapannounce "moro_cav", "" + strcharinfo(0) + " of the party " + getpartyname( getcharid(1) ) + ", is entering Temple of the Demon God.",bc_map;
						// warp "1@eom",101,16;
						end;
				}
		case 2:	// todo, custom text
				mes "[นิดฮ็อกก์]";
				mes "คุณได้กำจัดเทพแห่งความสิ้นหวังแล้ว!?";
				mes "ผมพูดได้เพียงว่าความทะเยอทะยานของเขายิ่งใหญ่เกินไป...";
				mes "พูดเสมอเรื่องการสร้างโลกใหม่และการเป็นพระเจ้า...";
				cutin "ep14_nyd01.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "แต่ที่จริงเขาไม่ได้สร้างอะไรเลย!";
				mes "ละทิ้งร่างที่เคยเป็นอสูรแล้วมาเกิดใหม่เป็นเพียงการเลียนแบบเทพ...";
				cutin "ep14_nyd02.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "ผลลัพธ์ที่ได้เป็นเพียงสิ่งที่มนุษย์ดูเหมือน";
				mes "เขาไม่อาจหลบหนีจากสิ่งที่เรียกว่ากรอบของพระเจ้าได้";
				mes "...ก็พูดได้แค่ว่ากำลังกินเคราะห์ร้ายอยู่!";
				cutin "ep14_nyd03.bmp",2;
				next;
				cutin "ep14_nyd04.bmp",2;
				mes "[นิดฮ็อกก์]";
				mes "ฉันจะอนุมัติภารกิจให้คุณ และคุณสามารถรายงานต่อผู้บัญชาการได้";
				mes "ขอบคุณ!";
				setquest 7597;
				erasequest 7593;
				for ( .@quest = 7601; .@quest < 7605; .@quest++ ) {
						if (isbegin_quest(.@quest))
								erasequest .@quest;
				}
				if (isbegin_quest(7596))
						erasequest 7596;
				close3;
		}
}

1@eom,1,1,0	script	#demon_god_variables	-1,{
		end;
OnVariableReset:
		'boss_id = 0;
		'boss_hp = 0;
		'bossx = 'bossy = 0;
		'boss_phase = 0;
		'icestunami = false;
		'spawn = false;
		'miniboss_id[0] = 'miniboss_id[1] = 0;
		'combo_is_allowed = false;
		'flowing_lava_deactivate[0] = 'flowing_lava_deactivate[1] = false;
		end;
OnMyMobDead:
		if (!playerattached())	// shouldn't happen
				end;
		.@mob_id = killedrid;
		sleep 5000;
		monster 'map_eom$,0,0,"--ja--", .@mob_id,1, instance_npcname("#demon_god_variables") + "::OnMyMobDead";
		end;
OnInstanceInit:
		donpcevent instance_npcname("#demon_god_variables") + "::OnVariableReset";

		'map_eom$ = instance_mapname("1@eom");
		'step = 0;
		'inject_soul = 0;

		// Entrance
		disablenpc instance_npcname("#door_mobmaster");
		disablenpc instance_npcname("Empty Soul Globe#ahat01");
		disablenpc instance_npcname("Empty Soul Globe#shnaim01");
		disablenpc instance_npcname("Filled Soul Globe#ahat02");
		disablenpc instance_npcname("Filled Soul Globe#shnaim02");
		disablenpc instance_npcname("#gate_to_center");

		// Center to Ice
		disablenpc instance_npcname("Strange Boy#mockid01");
		disablenpc instance_npcname("#gate_to_ice");
		disablenpc instance_npcname("Loki#eomloki01");
		disablenpc instance_npcname("Nidhogg#eomnyd01");
		disablenpc instance_npcname("Brinaranea#brinpc01");
		disablenpc instance_npcname("#ice_hpcheck");
		disablenpc instance_npcname("Nidhogg#eomnyd02");
		disablenpc instance_npcname("#ice_to_center");

		// Center to Fire
		disablenpc instance_npcname("Morroc#mockid02");
		disablenpc instance_npcname("#gate_to_fire");
		disablenpc instance_npcname("#fire_event01");
		disablenpc instance_npcname("#skollmaster");
		disablenpc instance_npcname("#loco_call");
		disablenpc instance_npcname("#fire_combo");
		disablenpc instance_npcname("Flowing Lava#lavapond01");
		disablenpc instance_npcname("Flowing Lava#lavapond02");
		disablenpc instance_npcname("Hardened Lava#lavaseal01");
		disablenpc instance_npcname("Hardened Lava#lavaseal02");
		disablenpc instance_npcname("#lavazone11");
		disablenpc instance_npcname("#lavazone12");
		disablenpc instance_npcname("#lavazone21");
		disablenpc instance_npcname("#lavazone22");
		disablenpc instance_npcname("Nidhogg#eomnyd03");
		disablenpc instance_npcname("#fire_to_center");

		// Center to Last
		disablenpc instance_npcname("#gate_to_last");
		disablenpc instance_npcname("Strange Young Man#mocadt01");
		disablenpc instance_npcname("#moc_master");
		disablenpc instance_npcname("#despair_god_main");
		disablenpc instance_npcname("#mk_hpcheck");
		disablenpc instance_npcname("#moc_origin");
		disablenpc instance_npcname("#morocc_god");
		disablenpc instance_npcname("#despair_main");
		disablenpc instance_npcname("Nidhogg#eomnyd04");

		// mimic permanent spawn
		.@event$ = instance_npcname("#demon_god_variables") + "::OnMyMobDead";
		monster 'map_eom$,0,0,"--ja--", 3101,3, .@event$;	// MM_MANA_WHITE
		monster 'map_eom$,0,0,"--ja--", 3102,3, .@event$;	// MM_MANA_RED
		monster 'map_eom$,0,0,"--ja--", 3103,3, .@event$;	// MM_MANA_YELLOW
		end;
}

1@eom,101,43,3	script	Demon God's Apostle Aha#ahat	4_HUMAN_GERUTOO,{
		cutin "ep13_ahat_" + (Sex ? "f" : "m") + ".bmp",2;	// note: cutin f for male, m for female
		// if (is_party_leader() == false || checkquest(7593,HUNTING) != 1 || isbegin_quest(7596) != 0) {	// quest to 'var for reload
		if (is_party_leader() == false || checkquest(7593,HUNTING) != 1 || 'step != 0) {
				mes "[อะฮัต]";
				mes "ยินดีต้อนรับ";
				mes "สู่วิหารแห่งเทพปีศาจ";
				mes "เป็นเกียรติอย่างยิ่งที่ได้เป็นเจ้าภาพของคุณในวันนี้";
				close3;
		}
		.@ahat$ = instance_npcname("Demon God's Apostle Aha#ahat");
		mes "[อะฮัต]";
		mes "แล้วคุณก็มาแล้ว";
		mes "โอ้โห คุณนี่ดื้อจริงๆ";
		mes "ฮ่าฮ่า คุณแปลกใจไหมที่เห็นฉันที่นี่?";
		npctalk "อะฮัต: เอาล่ะ คุณมาแล้ว", .@ahat$;
		next;
		mes "[อะฮัต]";
		mes "คุณไม่ได้คาดหวังสิ่งนี้เลยเหรอ?";
		mes "ทำไมฉันไม่แนะนำตัวเองอย่างเป็นทางการล่ะ?";
		mes "ฉันคืออาฮัต ผู้รับใช้คนแรกของเทพปีศาจ";
		npctalk "อาฮัต: ทำไมฉันไม่แนะนำตัวเองอย่างเป็นทางการล่ะ ฉันชื่ออาฮัต ผู้รับใช้คนแรกของเทพปีศาจ", .@ahat$;
		next;
		mes "[อะฮัต]";
		mes "เอาละ วันนี้ฉันคงต้องเสียสละชีวิตตัวเองแล้วล่ะ";
		mes "อย่ามองฉันแบบนั้น ทุกอย่างเป็นไปตามแผน";
		npctalk "อาฮัต: เอาล่ะ วันนี้ฉันคงต้องสละชีวิตตัวเองแล้วล่ะ อย่ามองฉันแบบนั้นเลย ทุกอย่างเป็นไปตามแผน", .@ahat$;
		next;
		mes "[อะฮัต]";
		mes "นายของฉันปรารถนาให้ชไนม์และฉันทดสอบคุณ";
		mes "เพื่อดูว่าคุณสมควรเป็นแขกของเราหรือไม่";
		npctalk "อาฮัต: นายของฉันอยากให้ชไนม์กับฉันทดสอบคุณ เพื่อดูว่าคุณสมควรเป็นแขกของเราหรือไม่", .@ahat$;
		next;
		mes "[อะฮัต]";
		mes "หากคุณคิดว่าคุณมาถึงจุดนี้ได้ด้วยตัวเองแล้ว";
		mes "คุณเข้าใจผิดแล้ว";
		npctalk "อาฮัต: หากคุณคิดว่าคุณมาถึงจุดนี้ด้วยตัวเองแล้ว คุณคิดผิดแล้ว", .@ahat$;
		next;
		mes "[อะฮัต]";
		mes "ทุกสิ่งทุกอย่างเป็นไปตามที่พระอาจารย์ได้วางแผนไว้";
		mes "ตอนนี้เรามาดูกันว่าคุณสามารถเปิดประตูนี้ได้หรือไม่";
		npctalk "อาฮัต: ทุกอย่างเป็นไปตามที่อาจารย์วางแผนไว้ ตอนนี้เรามาดูกันว่าคุณจะเปิดประตูนี้ได้หรือไม่", .@ahat$;
		if (isbegin_quest(7596) == 0)
				setquest 7596;
		if ('step != 0)
				close3;
		'step = 1;
		close2;
		cutin "",255;
		setpcblock PCBLOCK_NPC, true;
		sleep2 1000;
		setpcblock PCBLOCK_NPC, false;
		disablenpc .@ahat$;
		warpparty 'map_eom$,101,16,getcharid(1),'map_eom$,2,2;
		donpcevent instance_npcname("#door_mobmaster") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#door_mobmaster	CLEAR_NPC,{
		end;
OnStart:
		'apostle = 10;
		enablenpc instance_npcname("#door_mobmaster");
		.@label$ = instance_npcname("#door_mobmaster") + "::OnMyMobDead";
		monster 'map_eom$,102,30, "Demon God's Apostle Ahat",3105,1, .@label$;	// MM_GB_MOROCC_1
		setunitdata $@mobid[0], UMOB_HP, 5000000;
		monster 'map_eom$, 98,30, "Demon God Fragment",1918,1, .@label$;
		monster 'map_eom$,100,30, "Demon God Fragment",1918,1, .@label$;
		monster 'map_eom$,104,30, "Demon God Fragment",1918,1, .@label$;
		monster 'map_eom$,106,30, "Demon God Fragment",1918,1, .@label$;
		sleep 2000;
		mapannounce 'map_eom$, "Eliminate the Demon God's Apostles, Ahat and Shnaim, and collect their souls.", bc_map, 0x00ff00;
		monster 'map_eom$,102,40, "Demon God's Apostle Shnaim",3106,1, .@label$;	// MM_GB_MOROCC_4
		setunitdata $@mobid[0], UMOB_HP, 5000000;
		monster 'map_eom$, 98,40, "Demon God Fragment",1921,1, .@label$;
		monster 'map_eom$,100,40, "Demon God Fragment",1921,1, .@label$;
		monster 'map_eom$,104,40, "Demon God Fragment",1921,1, .@label$;
		monster 'map_eom$,106,40, "Demon God Fragment",1921,1, .@label$;
		sleep 3000;
		mapannounce 'map_eom$, "The Apostles' souls are the key to opening the Temple of the Demon God.", bc_map, 0x00ff00;
		end;
OnMyMobDead:
		'apostle -= 1;
		if ('apostle == 0) {
				'step = 2;
				disablenpc instance_npcname("#door_mobmaster");
				enablenpc instance_npcname("Empty Soul Globe#shnaim01");
				enablenpc instance_npcname("Empty Soul Globe#ahat01");
				mapannounce 'map_eom$, "Activated the Soul Globes used to open the Temple of the Demon God.", bc_map, 0x00ff00;
		}
		end;
}

1@eom,104,56,0	script	Empty Soul Globe#ahat01	CLEAR_NPC,{
		if ('step == 2) {
				if (strnpcinfo(2) == "ahat01")
						callsub( S_Globe, 1, "Ahat", 6713, "ahat02" );
				else
						callsub( S_Globe, 2, "Shnaim", 6714, "shnaim02" );
		}
		end;
S_Globe:
		.@bit = getarg(0);
		.@item_id = getarg(2);
		.@visible_npc_name$ = getarg(1);
		.@hidden_npc_name$ = getarg(3);

		disable_items;
		if (is_party_leader() == false || isbegin_quest(7596) != 1 || 'step != 2) {
				mes "มีเพียงหัวหน้าปาร์ตี้เท่านั้นที่สามารถทำกิจกรรมนี้ได้";
				close;
		}
		if (countitem(.@item_id) < 1) {
				mes "หากคุณต้องการเปิดใช้งานลูกโลกวิญญาณ คุณต้องมีวิญญาณของ Shnaim";
				close;
		}
		mes "ฉีด" + .@visible_npc_name$ + "วิญญาณของข้าไปสู่โลกแห่งวิญญาณอันว่างเปล่า";
		next;
		specialeffect EF_SPHERE;
		progressbar "000000",10;
		if (!('inject_soul & .@bit) && countitem(.@item_id) > 0) {
				'inject_soul |= .@bit;
				delitem .@item_id,1;
				specialeffect EF_ENTRY;
				disablenpc instance_npcname( strnpcinfo(0) );
				enablenpc instance_npcname("Filled Soul Globe#" + .@hidden_npc_name$);
				specialeffect EF_ENTRY,AREA, instance_npcname("Filled Soul Globe#" + .@hidden_npc_name$);
				specialeffect EF_LEVEL99_3,AREA, instance_npcname("Filled Soul Globe#" + .@hidden_npc_name$);
				mes "เปิดใช้งานสำเร็จแล้ว" + .@visible_npc_name$ + "โลกวิญญาณของ";
				if ('inject_soul == 3 && 'step == 2) {
						'step = 3;
						if (isbegin_quest(7596) == 1)
								erasequest 7596;// Qualifications of the Guests
						enablenpc instance_npcname("#gate_to_center");
						enablenpc instance_npcname("Strange Boy#mockid01");
						specialeffect EF_MAPPILLAR,AREA, instance_npcname("#gate_to_center");
						specialeffect EF_LEVEL99_3,AREA, instance_npcname("#gate_to_center");
				}
		}
		close;
}
1@eom,98,56,0	duplicate(Empty Soul Globe#ahat01)	Empty Soul Globe#shnaim01	CLEAR_NPC

1@eom,104,56,0	duplicate(dummy_npc)	Filled Soul Globe#ahat02	CLEAR_NPC
1@eom,98,56,0	duplicate(dummy_npc)	Filled Soul Globe#shnaim02	CLEAR_NPC

1@eom,101,58,0	script	#gate_to_center	WARPNPC,1,1,{
		end;
OnTouch_:
		if (isbegin_quest(7596) == 1)
				erasequest 7596;
		warp 'map_eom$,98,115;
		end;
}

1@eom,100,122,3	script	Strange Boy#mockid01	4_F_MOCBOY,{
		if ('step != 3)
				end;
		if (is_party_leader() == false || checkquest(7593,HUNTING) != 1) {
				mes "[เด็กผู้ชาย]";
				mes "คุณสิ่งมีชีวิตที่ไม่สำคัญอะไร";
				mes "คุณกล้าวางมือบนพระเจ้าที่รออยู่ได้อย่างไร?!";
				close;
		}
		cutin "morocc_kid.bmp",2;
		mes "[เด็กผู้ชาย]";
		mes "ก่อนอื่น ฉันขอปรบมือให้คุณที่สามารถมาถึงจุดนี้ได้";
		mes "คุณเป็นตัวเบี้ยที่ดี";
		next;
		select("What are you talking about?");
		unittalk getcharid(3), "" + strcharinfo(0) + " : What are you talking about?";
		mes "[เด็กผู้ชาย]";
		mes "ฉันรอคอยวันนี้มานานแล้ว";
		mes "เพื่อออกจากมุมหนึ่งของโลกปีศาจและปกครองทั้งจักรวาล";
		npctalk "เด็กชาย: ฉันรอคอยวันนี้มานานแล้ว ที่จะออกไปจากมุมหนึ่งของโลกปีศาจและปกครองทั้งจักรวาล";
		sleep2 2000;
		next;
		mes "[เด็กผู้ชาย]";
		mes "เพื่อให้บรรลุเป้าหมายนั้น ฉันตระหนักว่าฉันต้องละทิ้งร่างกายเก่าของฉัน";
		mes "แล้วฉันก็ตาย--ฉันเลยปล่อยให้คุณฆ่าฉัน";
		npctalk "เด็กชาย: เพื่อให้บรรลุเป้าหมายนั้น ฉันรู้ว่าฉันต้องละทิ้งร่างกายเก่าๆ ของฉัน ดังนั้น ฉันจึงตาย--เอาล่ะ ฉันปล่อยให้คุณฆ่าฉัน";
		sleep2 2000;
		next;
		mes "[เด็กผู้ชาย]";
		mes "ความตายเป็นก้าวที่หลีกเลี่ยงไม่ได้ก่อนการเกิดใหม่";
		mes "แล้วฉันก็ได้เกิดใหม่อีกครั้ง--คุณพาฉันกลับมา";
		npctalk "เด็กชาย: ความตายเป็นขั้นตอนที่หลีกเลี่ยงไม่ได้ที่จะต้องเกิดขึ้นก่อนการเกิดใหม่ ดังนั้น ฉันจึงได้เกิดใหม่อีกครั้ง และคุณก็ได้พาฉันกลับมา";
		sleep2 2000;
		next;
		mes "[เด็กผู้ชาย]";
		mes "ทั้งหมดนี้ได้ถูกจัดเตรียมไว้เพื่อให้ข้าพเจ้าได้มาเกิดเป็นพระเจ้า";
		npctalk "เด็กชาย : ทั้งหมดนี้มันถูกจัดเตรียมไว้เพื่อให้ผมได้มาเกิดเป็นพระเจ้าครับ";
		sleep2 2000;
		next;
		mes "[โมร็อค]";
		mes "ถูกต้องแล้ว พวกมนุษย์ที่โง่เขลาทั้งหลาย!";
		mes "ฉันตายไปครั้งหนึ่งเพื่อที่จะได้โคจรรอบโลกและโอบกอดมันไว้ในอ้อมแขนของฉัน";
		mes "ฉันเกิดใหม่เพื่อมากลืนโลกนี้";
		npctalk "โมร็อค: ข้าตายไปครั้งหนึ่งเพื่อที่จะได้โคจรรอบโลกและโอบกอดมันไว้ในอ้อมแขน ข้าได้เกิดใหม่เพื่อกลืนกินโลกนี้";
		sleep2 2000;
		next;
		mes "[โมร็อค]";
		mes "ฉันจะสร้างโลกใหม่และกลายเป็นผู้สร้างมัน";
		mes "ขอฉันถามคุณหน่อยเถอะ สิ่งมีชีวิตตัวแรกๆ ที่ก้าวเข้ามาในดินแดนที่เพิ่งเกิดของฉัน";
		npctalk "โมร็อค: ฉันจะสร้างโลกใหม่และกลายเป็นผู้สร้าง ขอถามคุณหน่อยว่าสิ่งมีชีวิตตัวแรกที่ก้าวเข้ามาในดินแดนที่เพิ่งเกิดใหม่ของฉันคือใคร";
		sleep2 2000;
		next;
		mes "[โมร็อค]";
		mes "คุณอยากติดตามฉันและชมการกำเนิดของโลกใหม่ไหม?";
		npctalk "โมร็อค: คุณอยากติดตามฉันและเฝ้าดูการกำเนิดของโลกใหม่ไหม?";
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		sleep2 1000;
		.@loki$ = instance_npcname("Loki#eomloki01");
		.@nidhogg$ = instance_npcname("Nidhogg#eomnyd01");
		enablenpc .@loki$;
		enablenpc .@nidhogg$;
		npctalk "โลกิ: หยุดพูดไร้สาระได้แล้ว พูดมาซะว่าคุณชอบทำลายสิ่งของ", .@loki$;
		sleep2 5000;
		npctalk "โลกิ: สร้างโลกใหม่และกลายเป็นผู้สร้างมันเหรอ? ถ้าคุณกำลังพูดถึงโลกใหม่นี้ ฉันก็ผิดหวังนะ", .@loki$;
		sleep2 5000;
		npctalk "โลกิ: แม้ว่าร่างกายจะไหม้ แต่พลังของคุณก็ไม่ทรงพลังเท่ากับอิมีร์แห่งเจเนซิส", .@loki$;
		sleep2 5000;
		npctalk "โลกิ: แค่ข้อเท็จจริงที่คุณต้องขโมย Yggdrasil แสดงให้เห็นว่าคุณขาดความคิดสร้างสรรค์", .@loki$;
		sleep2 5000;
		npctalk "โลกิ: ลองสังเกตตัวเองดีๆ สิ คุณคงคิดไปเองว่าตัวเองเป็นผู้สร้างอะไรสักอย่าง", .@loki$;
		sleep2 5000;
		npctalk "นิดฮ็อกก์: ฉันเห็นด้วย คุณไม่จำเป็นสำหรับโลกนี้ คุณไม่เหมาะกับที่นี่", .@nidhogg$;
		sleep2 4000;
		npctalk "นิดฮ็อกก์: แม้แต่อิกดราซิลยังปฏิเสธการมีอยู่ของคุณ!", .@nidhogg$;
		sleep2 4000;
		npctalk "นิดฮ็อกก์: เนื่องจากฉันเป็นผู้พิทักษ์แห่ง Yggdrasil ฉันต้องกำจัดคุณออกจากโลกนี้", .@nidhogg$;
		sleep2 2000;
		npctalk "มอร์ร็อค: ป๊า!";
		sleep2 2000;
		npctalk "มอร์ร็อค: มนุษย์ เจ้าต้องมีความปรารถนาความตายถึงจะมาหาข้าแบบนี้";
		sleep2 3000;
		npctalk "มอร์ร็อค: ข้าจะเอาเนื้อและเลือดของเจ้าเป็นของขวัญขึ้นบ้านใหม่ให้กับโลกใหม่ของข้า";
		sleep2 3000;
		npctalk "มอร์ร็อค: เจ้าเคยเห็นความมืดมิดของเจเนซิสแล้วหรือยัง?";
		sleep2 3000;
		npctalk "มอร์ร็อค: เจ้าได้เห็นน้ำค้างแข็งซึ่งเป็นจุดเริ่มต้นของชีวิตแล้วหรือยัง?";
		sleep2 6000;
		npctalk "มอร์ร็อค: ดูเถิด น้ำค้างแข็งแห่งออดูมลาที่ข้านำกลับมามีชีวิตอีกครั้งสู่โลกนี้";
		sleep2 3000;
		specialeffect EF_ENTRY;
		if ('step == 3) {
				'step = 4;
				disablenpc instance_npcname("Strange Boy#mockid01");
				donpcevent instance_npcname("#gate_to_ice") + "::OnEnable";
				enablenpc instance_npcname("Nidhogg#eomnyd01");
				enablenpc instance_npcname("Brinaranea#brinpc01");
				npctalk "โลกิ: คุณวิ่งอยู่เหรอ?", .@loki$;
				sleep2 2000;
				disablenpc .@loki$;
		}
		setpcblock PCBLOCK_NPC, false;
		end;
}

1@eom,91,120,0	script	#gate_to_ice	WARPNPC,1,1,{
		end;
OnEnable:
		.@gate_to_ice = instance_npcname("#gate_to_ice");
		enablenpc instance_npcname("#gate_to_ice");
		specialeffect EF_ICECRASH;
		sleep 1000;
		specialeffect EF_MAPPILLAR;
		end;
OnTouch_:
		if (isbegin_quest(7601) == 0 && isbegin_quest(7602) == 0)
				setquest 7601;
		warp 'map_eom$,61,88;
		end;
}

1@eom,95,122,5	script	Loki#eomloki01	4_M_ROKI2,{
		mes "[โลกิ]";
		mes "คุณไร้ทางช่วยเหลือตัวเองได้";
		mes "และน่ารังเกียจ";
		close;
}

1@eom,94,120,5	script	Nidhogg#eomnyd01	4_F_NYDHOG,2,2,{
		if ('step < 4 || is_party_leader() == false)
				end;
		if (getmercinfo(0) > 0) {
				mes "[นิดฮ็อกก์]";
				mes "คุณอยู่กับทหารรับจ้าง";
				mes "คุณช่วยไล่ทหารรับจ้างปัจจุบันของคุณออกแล้วจ้างฉันแทนได้ไหม?";
				close;
		}
		switch( checkquest(7605,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[นิดฮ็อกก์]";
				mes "ฉันเคยช่วยคุณมาแล้วครั้งหนึ่ง";
				mes "โปรดระวังนะคะ!";
				close;
		case 2:
				erasequest 7605;
				break;
		}
		setnpcdisplay instance_npcname("Nidhogg#eomnyd01"), 3087;
		mes "[นิดฮ็อกก์]";
		mes "" + strcharinfo(0) + "-";
		mes "ให้ฉันช่วยคุณในการต่อสู้ครั้งนี้";
		npctalk "นิดฮ็อกก์: ให้ฉันช่วยคุณในการต่อสู้ครั้งนี้เถอะ";
		specialeffect EF_SPHERE;
		next;
		mes "[นิดฮ็อกก์]";
		mes "หน้าที่ของฉันในฐานะผู้พิทักษ์ยังไม่สิ้นสุด";
		npctalk "นิดฮ็อกก์: หน้าที่ของฉันในฐานะผู้พิทักษ์ยังไม่สิ้นสุด";
		next;
		mercenary_create 3087,1800000;// M_NYDHOG
		mes "^4d4dffGuardian Nidhogg ได้เข้าร่วมกลุ่มของคุณแล้ว^000000";
		setquest 7605;// Guardian's Blessing
		disablenpc instance_npcname("Nidhogg#eomnyd01");
		close;

OnTouch:
		if ('step >= 4 && is_party_leader())
				npctalk "นิดฮ็อกก์: รอก่อน!";
		end;
}

// Center -> Ice
1@eom,48,88,0	script	Cold Mana Crystalline#eom01	4_ENERGY_BLUE,{
		getitem 22566,1;// Frost_Crystal
		specialeffect EF_LEVEL99_4;
		disablenpc instance_npcname( strnpcinfo(0) );
		end;
}
1@eom,51,103,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom02	4_ENERGY_BLUE
1@eom,24,109,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom03	4_ENERGY_BLUE
1@eom,26,128,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom04	4_ENERGY_BLUE
1@eom,40,140,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom05	4_ENERGY_BLUE
1@eom,38,139,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom06	4_ENERGY_BLUE
1@eom,51,149,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom07	4_ENERGY_BLUE
1@eom,49,148,0	duplicate(Cold Mana Crystalline#eom01)	Cold Mana Crystalline#eom08	4_ENERGY_BLUE

1@eom,61,88,0	script	#icemob01	HIDDEN_WARP_NPC,2,1,{
		end;
OnTouch_:
		if ('step >= 4) {
				disablenpc instance_npcname("#icemob01");
				areamonster 'map_eom$,40,93,48,101, "Frost Spider",3088,4;	// MM_BRINARANEA_BABY
		}
		end;
}

1@eom,22,116,3	script	Frost Spider#icemob02	3088,3,3,{
		end;
OnTouch_:
		if ('step >= 4) {
				disablenpc instance_npcname("Frost Spider#icemob02");
				monster 'map_eom$,22,116, "Frost Spider",3088,1;	// MM_BRINARANEA_BABY
				areamonster 'map_eom$,22,113,28,119, "Frost Spider",3088,3;
		}
		end;
}

1@eom,53,134,3	script	Frost Spider#icemob03	3088,3,3,{
		end;
OnTouch_:
		if ('step >= 4) {
				disablenpc instance_npcname("Frost Spider#icemob03");
				monster 'map_eom$,53,134, "Frost Spider",3088,1;	// MM_BRINARANEA_BABY
				areamonster 'map_eom$,47,133,53,139, "Frost Spider",3088,3;
		}
		end;
}

1@eom,38,129,3	script	Brinaranea#brinpc01	3091,10,10,{
		end;
OnTouch_:
		if ('step == 4) {
				'step = 5;
				npctalk "บรินาราเนีย: โอ้ อาหารเดินเข้ามาในที่ซ่อนของฉัน";
				initnpctimer;
		}
		end;
OnTimer3000:
		npctalk "บรินาราเนีย: ลูกๆ ของฉันจะได้กินเลี้ยงฉลองคืนนี้ โฮ โฮ โฮ!";
		disablenpc instance_npcname("Brinaranea#brinpc01");
		donpcevent instance_npcname("#ice_hpcheck") + "::OnSpawn";
		end;
OnTimer5000:
		stopnpctimer;
		unittalk 'boss_id, "Time to cook! *Giggle*";
		donpcevent instance_npcname("#ice_combo") + "::OnStart";
		donpcevent instance_npcname("#ice_hpcheck") + "::OnStart";
		end;
}

1@eom,2,3,0	script	#ice_hpcheck	CLEAR_NPC,{
		end;
OnSpawn:
		enablenpc instance_npcname("#ice_hpcheck");
		monster 'map_eom$,38,123, "Brinaranea",3091,1, instance_npcname("#ice_hpcheck") + "::OnMyMobDead";	// MM_BRINARANEA
		'boss_id = $@mobid[0];
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_MAXHP];
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		end;
OnStart:
		initnpctimer;
		end;

OnTimer3000:
		getunitdata 'boss_id, .@data;
		.@hp = .@data[UMOB_HP];
		if ('boss_hp < .@hp) {
				'boss_hp = .@hp;
				initnpctimer;
				end;
		}
		if (('boss_hp - .@hp) > 2000000) {
				'boss_hp = .@hp;
				donpcevent instance_npcname("#super_heal") + "::OnStart";
				initnpctimer;
				end;
		}
		'boss_hp = .@hp;

		if (.@hp > 69500000 && .@hp < 70000000) {
				unittalk 'boss_id, "Come out, my children. An exquisite feast has delivered itself to us!";
				donpcevent instance_npcname("#ice_hpcheck") + "::OnCallBaby";
		}
		else if (.@hp > 59500000 && .@hp < 60000000) {
				unittalk 'boss_id, "Children, it's time for dinner!";
				donpcevent instance_npcname("#ice_hpcheck") + "::OnCallBaby";
		}
		else if (.@hp > 49500000 && .@hp < 50000000) {
				unittalk 'boss_id, "Have all of you fed yet? Come out now!";
				donpcevent instance_npcname("#ice_hpcheck") + "::OnCallBaby";
		}
		else if (.@hp < 22200000 && 'icestunami == false)
				donpcevent instance_npcname("#icestunami") + "::OnStart";
		initnpctimer;
		end;

OnMyMobDead:
		donpcevent instance_npcname("#ice_hpcheck") + "::OnEnd";
		disablenpc instance_npcname("Brinaranea#brinpc01");
		stopnpctimer instance_npcname("#ice_combo");
		stopnpctimer instance_npcname("#brinaranea_event_1");
		stopnpctimer instance_npcname("#brinaranea_event_2");
		stopnpctimer instance_npcname("#brinaranea_event_3");
		stopnpctimer instance_npcname("#brinaranea_event_4");
		stopnpctimer instance_npcname("Brinaranea#brinpc01");
		stopnpctimer instance_npcname("#super_heal");
		// stopnpctimer instance_npcname("#icestunami");

		enablenpc instance_npcname("Nidhogg#eomnyd02");
		enablenpc instance_npcname("#ice_to_center");
		enablenpc instance_npcname("Morroc#mockid02");
		mapannounce 'map_eom$, "Nidhogg: Brinaranea's influence has dissipated, and a new warp gate has appeared.", bc_map, 0x00ff00;
		'step = 6;

		donpcevent instance_npcname("#demon_god_variables") + "::OnVariableReset";
		end;

OnEnd:
		stopnpctimer;
		disablenpc instance_npcname("#ice_hpcheck");
		killmonster 'map_eom$, instance_npcname("#ice_hpcheck") + "::OnMyMobDead";
		end;

OnCallBaby:
		sleep 500;
		if ('boss_id == 0)
				end;
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		killmonster 'map_eom$, instance_npcname("#ice_hpcheck") + "::OnMyMobDead";
		monster 'map_eom$,38,123, "Brinaranea",3091,1, instance_npcname("#ice_hpcheck") + "::OnMyMobDead";	// MM_BRINARANEA
		'boss_id = $@mobid[0];
		setunitdata 'boss_id, UMOB_HP, 'boss_hp;
		if ('spawn == true)
				end;
		'spawn = true;
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		sleep 1000;
		if ('boss_id == 0)
				end;
		getunitdata 'boss_id, .@data;
		areamonster 'map_eom$,(.@data[UMOB_X]-5),(.@data[UMOB_Y]-5),(.@data[UMOB_X]+5),(.@data[UMOB_Y]+5), " ",3088,5;	// MM_BRINARANEA_BABY
		sleep 15000;
		'spawn = false;
		end;

OnTest_hp:
		if ('boss_id && mobcount( 'map_eom$, instance_npcname("#ice_hpcheck") + "::OnMyMobDead" ) > 0)
				setunitdata 'boss_id, UMOB_HP, 2000000;
		end;
}

1@eom,2,6,0	script	#ice_combo	CLEAR_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer10000:
		donpcevent instance_npcname("#brinaranea_event_" + rand(1,4)) + "::OnStart";
		stopnpctimer;
		end;
}

1@eom,1,1,0	script	#brinaranea_event_1	-1,{
		end;
OnStart:
		initnpctimer;
		unittalk 'boss_id, "A show time!";
		end;
OnTimer1000:
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		end;
OnTimer2500:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-3),('bossy+3),-50;
		end;
OnTimer4000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+3),('bossy+3),-50;
		end;
OnTimer5500:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+3),('bossy-3),-50;
		end;
OnTimer7000:
		stopnpctimer;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-3),('bossy-3),-50;
		donpcevent instance_npcname("#ice_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#brinaranea_event_2	-1,{
		end;
OnStart:
		initnpctimer;
		unittalk 'boss_id, "Ooh, game. It looks delicious!";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		end;
OnTimer4000:
		stopnpctimer;
		getunitdata 'boss_id, .@data;
		if (.@data[UMOB_TARGETID] > 0)
				if (getunittype(.@data[UMOB_TARGETID]) == BL_PC)
						unitskilluseid 'boss_id,"WZ_JUPITEL",28, .@data[UMOB_TARGETID], -50;
		donpcevent instance_npcname("#ice_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#brinaranea_event_3	-1,{
		end;
OnStart:
		initnpctimer;
		unittalk 'boss_id, "Wanna play?";
		end;
OnTimer1000:
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		end;
OnTimer2500:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskillusepos 'boss_id,"WZ_VERMILION",5,'bossx,'bossy,-50;
		end;
OnTimer3500:
		callsub( S_Skill,3 );
		end;
OnTimer4500:
		callsub( S_Skill,4 );
		end;
OnTimer5500:
		callsub( S_Skill,5 );
		end;
OnTimer6500:
		callsub( S_Skill,6 );
		end;
OnTimer7500:
		callsub( S_Skill,7 );
		stopnpctimer;
		donpcevent instance_npcname("#ice_combo") + "::OnStart";
		end;
S_Skill:
		.@d = getarg(0);
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20, ('bossx - .@d), ('bossy + .@d),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20, ('bossx + .@d), ('bossy + .@d),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20, ('bossx + .@d), ('bossy - .@d),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20, ('bossx - .@d), ('bossy - .@d),-50;
		return;
}

1@eom,1,1,0	script	#brinaranea_event_4	-1,{
		end;
OnStart:
		initnpctimer;
		unittalk 'boss_id, "Hah hah...";
		end;
OnTimer1000:
		unitskilluseid 'boss_id,"AL_HEAL",11;
		end;
OnTimer2000:
		unitskilluseid 'boss_id,"AL_HEAL",11;
		end;
OnTimer3000:
		stopnpctimer;
		unitskilluseid 'boss_id,"AL_HEAL",11;
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		donpcevent instance_npcname("#ice_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#super_heal	-1,{
		end;
OnStart:
		initnpctimer;
		unittalk 'boss_id, "เจ้ามันน่ารำคาญ อย่าไปยุ่งกับ Frost of Audhumla เลย!";
		unitskilluseid 'boss_id, "NPC_ALLHEAL",1;
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		end;
OnTimer2000:
		stopnpctimer;
		mapannounce 'map_eom$, "Nidhogg: No... She's regenerated herself perfectly.", bc_map, 0x00ff00;
		end;
}

1@eom,2,4,0	script	#icestunami	CLEAR_NPC,{
		end;
OnStart:
		'icestunami = true;
		initnpctimer;
		unittalk 'boss_id, "You hellhounds, can you take this too?";
		getunitdata 'boss_id, .@data;
		'x_icemine = .@data[UMOB_X];
		'y_icemine = .@data[UMOB_Y];
		callsub( S_Spawn,6 );
OnTimer2000: callsub( S_Spawn,3 );
OnTimer4000: callsub( S_Spawn,0 );
OnTimer6000: callsub( S_Spawn,-3 );
OnTimer8000: callsub( S_Spawn,-6 );
OnTimer13000:
		killmonster 'map_eom$, instance_npcname("#icestunami") + "::OnMobDead";
		end;
OnTimer23000:
		'icestunami = false;
		'x_icemine = 0;
		'y_icemine = 0;
		stopnpctimer;
		end;
S_Spawn:
		.@y = 'y_icemine + getarg(0);
		.@label$ = instance_npcname("#icestunami") + "::OnMobDead";
		monster 'map_eom$,('x_icemine-6),.@y," ",2943,1, .@label$;// MM_ICE_MINE
		monster 'map_eom$,('x_icemine-3),.@y," ",2943,1, .@label$;
		monster 'map_eom$,  ('x_icemine),.@y," ",2943,1, .@label$;
		monster 'map_eom$,('x_icemine+3),.@y," ",2943,1, .@label$;
		monster 'map_eom$,('x_icemine+6),.@y," ",2943,1, .@label$;
		end;
OnMobDead:
		end;
}

1@eom,59,147,3	script	Nidhogg#eomnyd02	4_F_NYDHOG,{
		if ('step < 6)
				end;
		if (isbegin_quest(7602) == 0 && checkquest(7601,HUNTING) == 2) {
				mes "[นิดฮ็อกก์]";
				mes "นั่นเป็นการต่อสู้ที่ยากลำบาก";
				mes "สิ่งนี้อาจจะไม่ดีพอ แต่ฉันหวังว่ามันจะเป็นประโยชน์ต่อการเติบโตของคุณ";
				next;
				mes "^4d4dffGuardian Nidhogg ได้ให้รางวัลเป็น EXP จำนวนมากแก่คุณจากการเอาชนะ Brinaranea ได้^000000";
				erasequest 7601;// Temple of the Demon God Phase 1
				setquest 7602;// Temple of the Demon God Phase 1 Completed
				getexp 1000000,500000;
				close;
		}
		mes "[นิดฮ็อกก์]";
		mes "สถานที่อันน่าสะพรึงกลัวแห่งนี้จะสามารถแข่งขันกับ Frost of the Genesis ได้หรือไม่?";
		mes "ฉันพูดไม่ได้";
		mes "เขาไม่ได้สร้างสิ่งใดขึ้นมาเองเลย เขาโน้มน้าวตัวเองให้เชื่อว่าเขาสามารถเป็นผู้สร้างได้อย่างไร";
		close;
}

1@eom,67,149,0	script	#ice_to_center	WARPNPC,1,1,{
		end;
OnTouch_:
		if (isbegin_quest(7602) == 0 && checkquest(7601,HUNTING) == 2) {
				mes "[นิดฮ็อกก์]";
				mes "นั่นเป็นการต่อสู้ที่ยากลำบาก";
				mes "สิ่งนี้อาจจะไม่ดีพอ แต่ฉันหวังว่ามันจะเป็นประโยชน์ต่อการเติบโตของคุณ";
				next;
				mes "^4d4dffGuardian Nidhogg ได้ให้รางวัลเป็น EXP จำนวนมากแก่คุณจากการเอาชนะ Brinaranea ได้^000000";
				setquest 7602;
				erasequest 7601;
				getexp 1000000,500000;
				close;
		}
		warp 'map_eom$,98,115;
		end;
}

// Center to Fire
1@eom,100,122,3	script	Morroc#mockid02	4_F_MOCBOY,{
		if ('step != 6 || is_party_leader() == false || checkquest(7593,HUNTING) != 1)
				end;
		cutin "morocc_kid.bmp",2;
		mes "[โมร็อค]";
		mes "คุณสบายดี.";
		mes "คุณสามารถรับมือกับหมาป่าที่กลืนดวงอาทิตย์ได้หรือไม่?";
		npctalk "มอร์ร็อค: คุณเก่งมาก คุณสามารถรับมือกับหมาป่าที่กลืนดวงอาทิตย์ได้ไหม";
		next;
		mes "[โมร็อค]";
		mes "หากคุณไม่สามารถจัดการเขาได้ แล้วคุณก็ไม่สามารถจัดการพระเจ้าได้เช่นกันใช่ไหม?";
		npctalk "มอร์ร็อค: หากคุณไม่สามารถจัดการเขาได้ ก็แสดงว่าตอนนี้ คุณก็ไม่สามารถจัดการพระเจ้าได้เช่นกัน ใช่ไหม?";
		next;
		mes "[โมร็อค]";
		mes "ฉันจะคอยดูคุณ ฮ่าฮ่าฮ่า!";
		npctalk "มอร์ร็อค: ฉันจะคอยดูคุณ ฮ่าฮ่าฮ่า!";
		close2;
		cutin "",255;
		if ('step == 6) {
				'step = 7;
				setpcblock PCBLOCK_NPC, true;
				enablenpc instance_npcname("#fire_event01");
				donpcevent instance_npcname("#gate_to_fire") + "::OnEnable";
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				specialeffect EF_ENTRY;
				cloakonnpc instance_npcname("Morroc#mockid02");	// cloakonnpc to properly display the previous effect
		}
		end;
}

1@eom,104,120,0	script	#gate_to_fire	WARPNPC,1,1,{
		end;
OnTouch_:
		if (isbegin_quest(7603) == 0 && isbegin_quest(7604) == 0)
				setquest 7603;
		warp 'map_eom$,132,93;
		end;
OnEnable:
		enablenpc instance_npcname("#gate_to_fire");
		specialeffect EF_METEORSTORM;
		sleep 1000;
		specialeffect EF_MAPPILLAR;
		end;
}

1@eom,132,93,0	script	#fire_event01	HIDDEN_WARP_NPC,1,1,{
		end;
OnTouch_:
		disablenpc instance_npcname("#fire_event01");
		sleep 1000;
		mapannounce 'map_eom$, "Grr...", bc_map, 0xff0000;
		sleep 3000;
		mapannounce 'map_eom$, "I smell human... Grr...", bc_map, 0xff0000;
		monster 'map_eom$,139,101, "Frenzied Kasa",3089,1;	// MM_LOCO_KASA
		sleep 1000;
		monster 'map_eom$,142, 98, "Frenzied Kasa",3089,1;
		sleep 1000;
		monster 'map_eom$,144, 92, "Frenzied Kasa",3089,1;
		sleep 3000;
		monster 'map_eom$,145,104, "Frenzied Kasa",3089,1;
		sleep 1000;
		monster 'map_eom$,148, 98, "Frenzied Kasa",3089,1;
		sleep 1000;
		monster 'map_eom$,150, 93, "Frenzied Kasa",3089,1;
		sleep 3000;
		monster 'map_eom$,151,106, "Frenzied Kasa",3089,1;
		sleep 1000;
		monster 'map_eom$,153,100, "Frenzied Kasa",3089,1;
		sleep 1000;
		monster 'map_eom$,156, 95, "Frenzied Kasa",3089,1;
		mapannounce 'map_eom$, "...BURN!", bc_map, 0xff0000;
		donpcevent instance_npcname("#skollmaster") + "::OnStart";
		end;
}

1@eom,3,1,0	script	#skollmaster	CLEAR_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#skollmaster");
		enablenpc instance_npcname("Flowing Lava#lavapond01");
		enablenpc instance_npcname("Flowing Lava#lavapond02");
		enablenpc instance_npcname("#lavazone11");
		enablenpc instance_npcname("#lavazone12");
		enablenpc instance_npcname("#lavazone21");
		enablenpc instance_npcname("#lavazone22");
		'flowing_lava_deactivate[0] = 'flowing_lava_deactivate[1] = false;

		monster 'map_eom$,148,98, "Muspellskoll",3092,1, instance_npcname("#skollmaster") + "::OnMobDead";	// MM_MUSPELLSKOLL
		'boss_id = $@mobid[0];
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_MAXHP];
		sleep 1000;
		if ('boss_id)
				unittalk 'boss_id, "You're the ones... Grr... Good, I'll crush your bones! Grr...";
		sleep 4000;
		if ('boss_id)
				donpcevent instance_npcname("#skollmaster") + "::OnLava_heal";
		end;

OnLava_heal:
		'lava_heal = true;
		donpcevent instance_npcname("#fire_combo") + "::OnEnd";
		stopnpctimer instance_npcname("#fire_hpcheck");
		callsub S_Event, 0, 156,117;
		initnpctimer;
		end;
OnTimer2000:
		callsub S_Event, 1, 180,129;
		end;
OnTimer4000:
		callsub S_ReSpawn, 168,121;
		unitskilluseid 'boss_id, "CR_REFLECTSHIELD",1;
		unittalk 'boss_id, "Let's do this again. Come on!";
		donpcevent instance_npcname("#loco_call") + "::OnSpawn";
		end;
OnTimer6000:
		stopnpctimer;
		'lava_heal = false;
		donpcevent instance_npcname("#fire_hpcheck") + "::OnStart";
		donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;
S_ReSpawn:
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		killmonster 'map_eom$, instance_npcname("#skollmaster") + "::OnMobDead";
		monster 'map_eom$,getarg(0),getarg(1), "Muspellskoll",3092,1, instance_npcname("#skollmaster") + "::OnMobDead";	// MM_MUSPELLSKOLL
		'boss_id = $@mobid[0];
		setunitdata 'boss_id, UMOB_HP, 'boss_hp;
		return;
S_Event:
		callsub S_ReSpawn, getarg(1),getarg(2);
		if ('flowing_lava_deactivate[ getarg(0) ] == false) {
				for ( .@i = 0; .@i < 8; .@i++ ) {
						unitskilluseid 'boss_id, "AL_HEAL",11;
						sleep 100;
						if ('boss_id == 0)
								end;
				}
				unitskilluseid 'boss_id, "AL_HEAL",11;
		}
		mapannounce 'map_eom$, "Nidhogg: Muspellskoll is trying to regenerate itself using the lava!", bc_map, 0x00ff00;
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		donpcevent instance_npcname("#loco_call") + "::OnSpawn";
		return;

OnMobDead:
		stopnpctimer instance_npcname("#fire_hpcheck");
		stopnpctimer instance_npcname("#muspellskoll_event_1");
		stopnpctimer instance_npcname("#muspellskoll_event_2");
		stopnpctimer instance_npcname("#muspellskoll_event_3");
		donpcevent instance_npcname("#fire_combo") + "::OnEnd";
		donpcevent instance_npcname("#loco_call") + "::OnEnd";

		killmonster 'map_eom$, instance_npcname("#skollmaster") + "::OnMobDead";
		mapannounce 'map_eom$, "Nidhogg: Muspellskoll's influence has dissipated, and a new warp gate has appeared.", bc_map, 0x00ff00;

		enablenpc instance_npcname("Nidhogg#eomnyd03");
		enablenpc instance_npcname("#fire_to_center");
		enablenpc instance_npcname("Strange Young Man#mocadt01");

		'step = 8;
		donpcevent instance_npcname("#demon_god_variables") + "::OnVariableReset";
		end;

OnTest_hp:
		if ('boss_id && mobcount( 'map_eom$, instance_npcname("#skollmaster") + "::OnMobDead" ) > 0)
				setunitdata 'boss_id, UMOB_HP, 2000000;
		end;
}

1@eom,3,7,0	script	#fire_hpcheck	CLEAR_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer3000:
		getunitdata 'boss_id, .@data;
		.@hp = .@data[UMOB_HP];
		if ('boss_hp < .@hp) {
				initnpctimer;
				end;
		}
		if (('boss_hp - .@hp) > 1000000) {
				'boss_hp = .@hp;
				donpcevent instance_npcname("#fire_hpcheck") + "::OnHeal";	// the event can stack and the boss can stay in this state until it demise
				initnpctimer;
				end;
		}
		'boss_hp = .@hp;

		if ('lava_heal == false) {
				if (.@hp > 29500000 && .@hp < 30000000 || .@hp > 35500000 && .@hp < 36000000 || .@hp > 39500000 && .@hp < 40000000 || .@hp > 44500000 && .@hp < 45000000) {
						unittalk 'boss_id, "I'm not going down without a fight!";
						donpcevent instance_npcname("#skollmaster") + "::OnLava_heal";
				}
		}
		initnpctimer;
		end;

OnHeal:
		if ('fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEnd";
		unittalk 'boss_id, "Do I look easy to you? I'll burn you to ashes!";
		sleep 2000;
		if ('boss_id == 0)
				end;
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_METEOR",11,.@data[UMOB_X],.@data[UMOB_Y],-50;
		sleep 1000;
		if ('boss_id == 0)
				end;
		for ( .@i = 0; .@i < 18; .@i++ ) {
				unitskilluseid 'boss_id, "AL_HEAL",11;
				sleep 200;
				if ('boss_id == 0)
						end;
		}
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_METEOR",11,.@data[UMOB_X],.@data[UMOB_Y],-50;
		for ( .@i = 0; .@i < 18; .@i++ ) {
				unitskilluseid 'boss_id, "AL_HEAL",11;
				sleep 200;
				if ('boss_id == 0)
						end;
		}
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		if (!'fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;
}

1@eom,3,5,0	script	#loco_call	CLEAR_NPC,{
		end;
OnSpawn:
		enablenpc instance_npcname("#loco_call");
		initnpctimer;
		getunitdata 'boss_id, .@data;
		'x_loco = .@data[UMOB_X];
		'y_loco = .@data[UMOB_Y];
		end;
OnTimer1000:
		.@label$ = instance_npcname("#loco_call") + "::OnMobDead";
		if (mobcount( 'map_eom$, .@label$ ) < 6)
				areamonster 'map_eom$,('x_loco-5),('y_loco-5),('x_loco+5),('y_loco+5)," ",3089,3, .@label$;	// MM_LOCO_KASA
		end;
OnTimer16000:
		stopnpctimer;
		disablenpc instance_npcname("#loco_call");
		end;
OnMobDead:
		end;
OnEnd:
		stopnpctimer;
		disablenpc instance_npcname("#loco_call");
		end;
}

1@eom,1,1,0	script	#muspellskoll_event_1	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "Crash and burn!";
		end;
OnTimer1000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer3000:
		stopnpctimer;
		unitskilluseid 'boss_id, "WZ_SIGHTRASHER",10;
		if ('fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;
}

1@eom,1,1,0	script	#muspellskoll_event_2	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "Grr...";
		end;
OnTimer1000:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer3000:
		unitskillusepos 'boss_id,"WZ_FIREPILLAR",1,('bossx-3),('bossy+3),-50;
		end;
OnTimer5000:
		unitskillusepos 'boss_id,"WZ_FIREPILLAR",1,('bossx+3),('bossy+3),-50;
		end;
OnTimer7000:
		unitskillusepos 'boss_id,"WZ_FIREPILLAR",1,('bossx-3),('bossy-3),-50;
		end;
OnTimer9000:
		unitskillusepos 'boss_id,"WZ_FIREPILLAR",1,('bossx+3),('bossy-3),-50;
		end;
OnTimer19000:
		stopnpctimer;
		if ('fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;
}

1@eom,1,1,0	script	#muspellskoll_event_3	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "My fire will swallow you whole!";
		end;
OnTimer1000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer2000:
		donpcevent instance_npcname("#flamecross") + "::OnStart";
		end;
OnTimer7000:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskillusepos 'boss_id,"WZ_METEOR",5,('bossx),('bossy+5),-50;
		end;
OnTimer9000:
		unitskillusepos 'boss_id,"WZ_METEOR",5,('bossx),('bossy-5),-50;
		end;
OnTimer11000:
		unitskillusepos 'boss_id,"WZ_METEOR",5,('bossx),('bossy+10),-50;
		end;
OnTimer13000:
		stopnpctimer;
		unitskillusepos 'boss_id,"WZ_METEOR",5,'bossx,('bossy-10),-50;
		if ('fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;
}

1@eom,3,3,0	script	#fire_combo	CLEAR_NPC,{
		end;
OnEvent:
		initnpctimer;
		'fire_combo = true;
		end;
OnTimer10000:
		stopnpctimer;
		.@r = rand(1,4);
		if (.@r == 1)
				donpcevent instance_npcname("#muspellskoll_event_1") + "::OnEvent";
		else if (.@r == 2)
				donpcevent instance_npcname("#muspellskoll_event_2") + "::OnEvent";
		else if (.@r == 3)
				donpcevent instance_npcname("#muspellskoll_event_3") + "::OnEvent";
		else
				donpcevent instance_npcname("#flamecross") + "::OnSpawn";
		end;
OnEnd:
		stopnpctimer;
		'fire_combo = false;
		disablenpc instance_npcname("#fire_combo");
		end;
}

1@eom,3,8,0	script	#flamecross	CLEAR_NPC,{
		end;
OnStart:
		getunitdata 'boss_id, .@data;
		.@x = .@data[UMOB_X];
		.@y = .@data[UMOB_Y];
		callsub( S_Spawn,.@x,.@y,3 );
		sleep 300;
		callsub( S_Spawn,.@x,.@y,6 );
		sleep 300;
		callsub( S_Spawn,.@x,.@y,9 );
		sleep 300;
		callsub( S_Spawn,.@x,.@y,13 );
		sleep 5000;
		killmonster 'map_eom$, instance_npcname("#flamecross") + "::OnMobDead";
		end;

S_Spawn:
		if (!'boss_id) {
				killmonster 'map_eom$, instance_npcname("#flamecross") + "::OnMobDead";
				end;
		}
		.@x = getarg(0);
		.@y = getarg(1);
		.@dxy = getarg(2);
		.@label$ = instance_npcname("#flamecross") + "::OnMobDead";
		monster 'map_eom$,(.@x+.@dxy),(.@y+.@dxy)," ",2960,1, .@label$;// MM_FLAMECROSS
		monster 'map_eom$,(.@x-.@dxy),(.@y-.@dxy)," ",2960,1, .@label$;
		monster 'map_eom$,(.@x+.@dxy),(.@y-.@dxy)," ",2960,1, .@label$;
		monster 'map_eom$,(.@x-.@dxy),(.@y+.@dxy)," ",2960,1, .@label$;
		return;

OnSpawn:
		getunitdata 'boss_id, .@data;
		.@label$ = instance_npcname("#flamecross") + "::OnMobDead";	// same OnMobDead than OnStart?
		if (mobcount( 'map_eom$, .@label$ ) < 2)
				areamonster 'map_eom$,(.@data[UMOB_X]-1),(.@data[UMOB_Y]-1),(.@data[UMOB_X]+1),(.@data[UMOB_Y]+1)," ",3090,2, .@label$;	// MM_LOCO_SALAMANDER
		if ('fire_combo)
				donpcevent instance_npcname("#fire_combo") + "::OnEvent";
		end;

OnMobDead:
		end;
}

1@eom,154,119,0	script	Flowing Lava#lavapond01	CLEAR_NPC,{	// todo, custom text
		if (countitem(22566) < 1)
				end;
		mes "สามารถใช้เพื่อปิดกั้นการไหลของลาวาได้โดยใช้ Frost Crystal";
		next;
		if (select( "Use a Frost Crystal", "Do not use a Frost Crystal" ) == 2)
				end;
		if ('flowing_lava_deactivate[0] == true)
				end;
		'flowing_lava_deactivate[0] = true;
		delitem 22566,1;// Frost_Crystal
		disablenpc instance_npcname("Flowing Lava#lavapond01");
		donpcevent instance_npcname("Hardened Lava#lavaseal01") + "::OnEnable";
		initnpctimer;
		end;
OnTimer300:
		donpcevent instance_npcname("Hardened Lava#lavaseal01") + "::OnEffect";
		end;
OnTimer120000:
		'flowing_lava_deactivate[0] = false;
		donpcevent instance_npcname("Hardened Lava#lavaseal01") + "::OnDisable";
		enablenpc instance_npcname("Flowing Lava#lavapond01");
		mapannounce 'map_eom$, "Nidhogg: The Frost has melted. The lava is flowing again!", bc_map, 0x00ff00;
		stopnpctimer;
		end;
}

1@eom,182,129,0	script	Flowing Lava#lavapond02	CLEAR_NPC,{	// todo, custom text
		if (countitem(22566) < 1)
				end;
		mes "สามารถใช้เพื่อปิดกั้นการไหลของลาวาได้โดยใช้ Frost Crystal";
		next;
		if (select( "Use a Frost Crystal", "Do not use a Frost Crystal" ) == 2)
				end;
		if ('flowing_lava_deactivate[1] == true)
				end;
		'flowing_lava_deactivate[1] = true;
		delitem 22566,1;// Frost_Crystal
		disablenpc instance_npcname("Flowing Lava#lavapond02");
		donpcevent instance_npcname("Hardened Lava#lavaseal02") + "::OnEnable";
		initnpctimer;
		end;
OnTimer300:
		donpcevent instance_npcname("Hardened Lava#lavaseal02") + "::OnEffect";
		end;
OnTimer120000:
		'flowing_lava_deactivate[1] = false;
		donpcevent instance_npcname("Hardened Lava#lavaseal02") + "::OnDisable";
		enablenpc instance_npcname("Flowing Lava#lavapond02");
		mapannounce 'map_eom$, "Nidhogg: The Frost has melted. The lava is flowing again!", bc_map, 0x00ff00;
		stopnpctimer;
		end;
}

1@eom,154,119,0	script	Hardened Lava#lavaseal01	CLEAR_NPC,{
		end;
OnEnable:
		enablenpc instance_npcname("Hardened Lava#lavaseal01");
		disablenpc instance_npcname("#lavazone11");
		disablenpc instance_npcname("#lavazone12");
		end;
OnDisable:
		stopnpctimer;
		disablenpc instance_npcname("Hardened Lava#lavaseal01");
		enablenpc instance_npcname("#lavazone11");
		enablenpc instance_npcname("#lavazone12");
		end;
OnEffect:
		initnpctimer;
		specialeffect EF_LIGHTNINGLOADER;
		end;
OnTimer3000:
		initnpctimer;
		end;
}

1@eom,182,129,0	script	Hardened Lava#lavaseal02	CLEAR_NPC,{
		end;
OnEnable:
		enablenpc instance_npcname("Hardened Lava#lavaseal02");
		disablenpc instance_npcname("#lavazone21");
		disablenpc instance_npcname("#lavazone22");
		end;
OnDisable:
		stopnpctimer;
		disablenpc instance_npcname("Hardened Lava#lavaseal02");
		enablenpc instance_npcname("#lavazone21");
		enablenpc instance_npcname("#lavazone22");
		end;
OnEffect:
		initnpctimer;
		specialeffect EF_LIGHTNINGLOADER;
		end;
OnTimer3000:
		initnpctimer;
		end;
}

1@eom,161,122,0	script	#lavazone11	HIDDEN_WARP_NPC,10,10,{
		end;
OnTouch:
		percentheal -3,0;
		specialeffect2 EF_SPRINKLESAND;
		end;
}
1@eom,161,102,0	duplicate(#lavazone11)	#lavazone12	HIDDEN_WARP_NPC,10,10
1@eom,163,150,0	duplicate(#lavazone11)	#lavazone21	HIDDEN_WARP_NPC,10,10
1@eom,163,150,0	duplicate(#lavazone11)	#lavazone22	HIDDEN_WARP_NPC,10,10

1@eom,151,155,3	script	Nidhogg#eomnyd03	4_F_NYDHOG,{
		if (checkquest(7603,HUNTING) == 2 && isbegin_quest(7604) == 0) {
				mes "[นิดฮ็อกก์]";
				mes "ทำได้ดีอีกแล้ว";
				mes "สิ่งนี้อาจจะไม่ดีพอ แต่ฉันหวังว่ามันจะเป็นประโยชน์ต่อการเติบโตของคุณ";
				next;
				mes "^4d4dffGuardian Nidhogg มอบรางวัล EXP จำนวนมากให้กับคุณสำหรับการเอาชนะ Muspellskoll^000000";
				erasequest 7603;// Temple of the Demon God Phase 2
				setquest 7604;// Temple of the Demon God Phase 2 Completed
				getexp 1000000,500000;
				close;
		}
		mes "[นิดฮ็อกก์]";
		mes "สกอลล์...สกอลล์และฮาติ...";
		mes "ทุกสิ่งที่นี่เป็นเพียงสำเนาของสิ่งนั้นจากโลกภายนอก";
		mes "เขาไม่ใช่ผู้สร้าง";
		close;
}

1@eom,147,156,0	script	#fire_to_center	WARPNPC,1,1,{
		end;
OnTouch_:
		if (checkquest(7603,HUNTING) == 2 && isbegin_quest(7604) == 0) {
				mes "[นิดฮ็อกก์]";
				mes "ทำได้ดีอีกแล้ว";
				mes "สิ่งนี้อาจจะไม่ดีพอ แต่ฉันหวังว่ามันจะเป็นประโยชน์ต่อการเติบโตของคุณ";
				next;
				mes "^4d4dffGuardian Nidhogg มอบรางวัล EXP จำนวนมากให้กับคุณสำหรับการเอาชนะ Muspellskoll^000000";
				setquest 7604;
				erasequest 7603;
				getexp 1000000,500000;
				close;
		}
		warp 'map_eom$,98,115;
		end;
}

// Center to Last
1@eom,98,123,5	script	Strange Young Man#mocadt01	3097,5,5,{	// MM_MOROCC_ADT
		end;
OnTouch_:
		if ('step == 8 && is_party_leader()) {
				'step = 9;
				initnpctimer;
		}
		end;
OnTimer1000:
		npctalk "คุณเกินความคาดหวังของฉัน";
		end;
OnTimer4000:
		npctalk "ฉันจะรับคำท้าของคุณ";
		end;
OnTimer7000:
		npctalk "ฉันจะราดเลือดอันไหลรินของคุณให้ทั่วแผ่นดิน และจะหล่อเลี้ยงมันด้วยเนื้อที่ฉีกขาดของคุณ";
		end;
OnTimer8000:
		specialeffect EF_BEGINSPELL5;
		enablenpc instance_npcname("#gate_to_last");
		enablenpc instance_npcname("#moc_master");
		specialeffect EF_READYPORTAL2,AREA, instance_npcname("#gate_to_last");
		end;
OnTimer10000:
		npctalk "ฉันจะแสดงให้คุณเห็นถึงความสิ้นหวังที่แท้จริง";
		end;
OnTimer12000:
		stopnpctimer;
		specialeffect EF_ENTRY;
		disablenpc instance_npcname("Strange Young Man#mocadt01");
		end;
}

1@eom,98,127,0	warp	#gate_to_last	1,1,1@eom,102,186

1@eom,101,194,3	script	#moc_master	3096,10,10,{
		end;
OnTouch_:
		if ('step != 9)
				end;
		'step = 10;
		npctalk "มอร์ร็อค: คุณอยู่ที่นี่แล้ว ดังนั้นอย่าเสียเวลาพูดคุยกันเลย";
		initnpctimer;
		end;
OnTimer2000:
		stopnpctimer;
		npctalk "มอร์ร็อค: เอาล่ะ มนุษย์ทั้งหลาย!";
		disablenpc instance_npcname("#moc_master");
		donpcevent instance_npcname("#despair_god_main") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_god_main	-1,{
		end;
OnStart:
		enablenpc instance_npcname("#despair_god_main");
		'boss_phase = 1;

		monster 'map_eom$,101,194, "Demigod",3096,1, instance_npcname("#despair_god_main") + "::OnMobDead";	// MM_MOROCC_KID
		'boss_id = $@mobid[0];
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_MAXHP];

		unitskillusepos 'boss_id,"WZ_METEOR",11,101,194,-50;

		enablenpc instance_npcname("#mk_trap");
		donpcevent instance_npcname("#mk_hpcheck") + "::OnStart";
		donpcevent instance_npcname("#mk_combo") + "::OnEvent";
		end;

OnPhase02:
		if ('boss_phase != 1)
				end;
		'boss_phase = 2;
		stopnpctimer instance_npcname("#demigod_event_1");
		stopnpctimer instance_npcname("#demigod_event_2");
		stopnpctimer instance_npcname("#demigod_event_3");
		stopnpctimer instance_npcname("#mk_hpcheck");
		stopnpctimer instance_npcname("#mk_combo");
		unittalk 'boss_id, "You'll have to do better than this!";
		sleep 2000;
		if ('boss_id == 0)
				end;
		getunitdata 'boss_id, .@data;
		'boss_hp = .@data[UMOB_HP];
		killmonster 'map_eom$, instance_npcname("#despair_god_main") + "::OnMobDead";
		monster 'map_eom$,101,207, "Demigod",3096,1, instance_npcname("#despair_god_main") + "::OnMobDead";	// MM_MOROCC_KID
		'boss_id = $@mobid[0];
		setunitdata 'boss_id, UMOB_HP, 'boss_hp;
		setunitdata 'boss_id, UMOB_MODE, (MD_MVP|MD_STATUSIMMUNE|MD_KNOCKBACKIMMUNE|MD_MVP|MD_DETECTOR);	// todo: MD_SKILL_IMMUNE ?
		sleep 2000;
		if ('boss_id == 0)
				end;
		donpcevent instance_npcname("#moc_origin") + "::OnEnable";
		end;

OnPhase03:
		if ('boss_phase != 2)
				end;
		'boss_phase = 3;
		getunitdata 'boss_id, .@data;
		.@hp = .@data[UMOB_HP] + ('boss_hp - .@data[UMOB_HP]) * 10;
		'boss_hp = min(80000000,.@hp);
		killmonster 'map_eom$, instance_npcname("#despair_god_main") + "::OnMobDead";
		monster 'map_eom$,101,194, "Demigod",3096,1, instance_npcname("#despair_god_main") + "::OnMobDead";	// MM_MOROCC_KID
		'boss_id = $@mobid[0];
		setunitdata 'boss_id, UMOB_HP, 'boss_hp;
		unittalk 'boss_id, "Stop being so persistent!";
		sleep 2000;
		if ('boss_id == 0)
				end;
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		sleep 1000;
		if ('boss_id == 0)
				end;
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		sleep 1000;
		if ('boss_id == 0)
				end;
		unitskilluseid 'boss_id, "MG_FIREBALL",1;
		donpcevent instance_npcname("#mk_hpcheck") + "::OnStart";
		donpcevent instance_npcname("#mk_combo") + "::OnEvent";
		end;

OnTest_hp:
		if ('boss_id && mobcount( 'map_eom$, instance_npcname("#despair_god_main") + "::OnMobDead" ) > 0)
				setunitdata 'boss_id, UMOB_HP, 2000000;
		end;

OnMobDead:
		disablenpc instance_npcname("#despair_god_main");
		killmonster 'map_eom$, instance_npcname("#despair_god_main") + "::OnMobDead";
		stopnpctimer instance_npcname("#mk_combo");
		stopnpctimer instance_npcname("#demigod_event_1");
		stopnpctimer instance_npcname("#demigod_event_2");
		stopnpctimer instance_npcname("#demigod_event_3");
		stopnpctimer instance_npcname("#moc_meddling");
		stopnpctimer instance_npcname("#mk_hpcheck");
		donpcevent instance_npcname("#moc_origin") + "::OnEnd";

		donpcevent instance_npcname("#demon_god_variables") + "::OnVariableReset";
		donpcevent instance_npcname("#morocc_god") + "::OnStart";
		end;
}

1@eom,4,2,0	script	#mk_combo	CLEAR_NPC,{
		end;
OnEvent:
		initnpctimer;
		'combo_is_allowed = true;
		end;
OnStart:
		initnpctimer;
		end;
OnTimer10000:
		donpcevent instance_npcname("#demigod_event_" + rand(1,3)) + "::OnEvent";
		stopnpctimer;
		end;
OnEnd:
		stopnpctimer;
		'combo_is_allowed = false;
		end;
}

1@eom,4,1,0	script	#mk_hpcheck	CLEAR_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer6000:
		getunitdata 'boss_id, .@data;
		.@hp = .@data[UMOB_HP];
		if ('boss_hp < .@hp) {
				initnpctimer;
				end;
		}
		.@hp_a = 'boss_hp - .@hp;
		if (.@hp_a > 1000000) {
				if (.@hp_a > 200000) {
						.@hp_b = .@hp + (.@hp_a - 200000) * 3;
						.@hp_b = min(80000000, .@hp_b);
						setunitdata 'boss_id, UMOB_HP, .@hp_b;
						mapannounce 'map_eom$, "Nidhogg: Wow, his regenerative power is unheard of! Don't let him regenerate again!", bc_map, 0x00ff00;
				}
				unitskilluseid 'boss_id, "AL_HEAL",11;
				getunitdata 'boss_id, .@data;
				'boss_hp = .@data[UMOB_HP];
				initnpctimer;
				end;
		}
		'boss_hp = .@hp;

		switch( 'boss_phase ) {	// process by phase instead of (officially) hp, just in case
		case 1:
				if (.@hp < 40000000)
						donpcevent instance_npcname("#despair_god_main") + "::OnPhase02";
				break;
		case 2:
				break;
		case 3:
				if (.@hp > 100000 && .@hp < 30000000)
						donpcevent instance_npcname("#mk_trap") + "::OnTrap";
				break;
		}
		initnpctimer;
		end;
}

1@eom,1,1,0	script	#demigod_event_1	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "Mwah hah, dodge this if you can!";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer3000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer5000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_VERMILION",21,.@data[UMOB_X],.@data[UMOB_Y],-50;
		end;
OnTimer7000:
		stopnpctimer;
		unitskilluseid 'boss_id, "CR_REFLECTSHIELD",1;
		if ('combo_is_allowed)
				donpcevent instance_npcname("#mk_combo") + "::OnEvent";
		end;
}

1@eom,1,1,0	script	#demigod_event_2	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "A rhapsody for weak souls...";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer3000:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-2),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+2),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+2),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-2),-50;
		end;
OnTimer4000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-6),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+6),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+6),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-6),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-4),('bossy+4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-4),('bossy-4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+4),('bossy+4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+4),('bossy-4),-50;
		end;
OnTimer5000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-10),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+10),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+10),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-10),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-8),('bossy+8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-8),('bossy-8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+8),('bossy+8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+8),('bossy-8),-50;
		end;
OnTimer6000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-16),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+16),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+16),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-16),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-12),('bossy+12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-12),('bossy-12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+12),('bossy+12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+12),('bossy-12),-50;
		end;
OnTimer7000:
		stopnpctimer;
		if ('combo_is_allowed)
				donpcevent instance_npcname("#mk_combo") + "::OnEvent";
		end;
}

1@eom,1,1,0	script	#demigod_event_3	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "For whom the fugue is played? For whom the requiem is played?";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer3000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer5000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_METEOR",5,(.@data[UMOB_X]-5),.@data[UMOB_Y],-50;
		unitskillusepos 'boss_id,"WZ_METEOR",5,(.@data[UMOB_X]+5),.@data[UMOB_Y],-50;
		unitskillusepos 'boss_id,"WZ_METEOR",5,.@data[UMOB_X],(.@data[UMOB_Y]+5),-50;
		unitskillusepos 'boss_id,"WZ_METEOR",5,.@data[UMOB_X],(.@data[UMOB_Y]-5),-50;
		end;
OnTimer9000:
		unittalk 'boss_id, "Burn to ashes!";
		end;
OnTimer11000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_VERMILION",21,.@data[UMOB_X],.@data[UMOB_Y],-50;	// assuming position on (0,0) is self
		end;
OnTimer13000:
		stopnpctimer;
		if ('combo_is_allowed)
				donpcevent instance_npcname("#mk_combo") + "::OnEvent";
		end;
}

1@eom,4,3,0	script	#moc_origin	CLEAR_NPC,{
		end;
OnEnable:
		enablenpc instance_npcname("#moc_origin");
		monster 'map_eom$,114,198, "Morroc of the Genesis", 3098,1, instance_npcname("#moc_origin") + "::OnMobDead1";	// MM_MOROCC_ORIGIN
		'miniboss_id[0] = $@mobid[0];
		'miniboss_id[1] = 0;
		initnpctimer;
		end;
OnTimer1000:
		donpcevent instance_npcname("#moc_origin") + "::OnOriginheal";
		if ('miniboss_id[0] < 1)
				end;
		getunitdata 'miniboss_id[0], .@data;
		if (.@data[UMOB_HP] < 2200000) {
				if ('miniboss_id[1] == 0) {
						monster 'map_eom$,86,199, "Morroc of the Sabbath", 3099,1, instance_npcname("#moc_origin") + "::OnMobDead2";	// MM_MOROCC_REST
						'miniboss_id[1] = $@mobid[0];
				}
		}
		end;
OnTimer2000:
		initnpctimer;
		end;

OnOriginheal:
		for ( .@i = 0; .@i < 11; .@i++ ) {
				unitskilluseid 'boss_id, "AL_HEAL",11;
				sleep 200;
				if ('boss_id == 0)
						end;
		}
		end;

OnMobDead1:
		'miniboss_id[0] = 0;
		callsub S_Dead;
OnMobDead2:
		'miniboss_id[1] = 0;
		callsub S_Dead;
S_Dead:
		if ('miniboss_id[1] == 0 && 'miniboss_id[0] > 0) {	// todo, custom text
				unittalk 'miniboss_id[0], "---!!!";
				setunitdata 'miniboss_id[0], UMOB_HP, 3000000;
				stopnpctimer instance_npcname("#moc_meddling");
		}
		else if ('miniboss_id[1] == 0 && 'miniboss_id[0] == 0) {
				donpcevent instance_npcname("#moc_origin") + "::OnEnd";
				stopnpctimer instance_npcname("#moc_meddling");
				donpcevent instance_npcname("#despair_god_main") + "::OnPhase03";
		}
		else {
				unittalk 'miniboss_id[1], "I'll put you to rest. It's my mercy!!!"; // todo, custom text
				setunitdata 'miniboss_id[1], UMOB_HP, 1000000;
				donpcevent instance_npcname("#moc_meddling") + "::OnStart";
		}
		end;

OnEnd:
		stopnpctimer;
		disablenpc instance_npcname("#moc_origin");
		killmonster 'map_eom$, instance_npcname("#moc_origin") + "::OnMobDead1";
		killmonster 'map_eom$, instance_npcname("#moc_origin") + "::OnMobDead2";
		end;
}

1@eom,1,1,0	script	#moc_meddling	-1,{
		end;
OnStart:
		initnpctimer;
		getunitdata 'miniboss_id[1], .@data;
		if (.@data[UMOB_HP] > 100000)
				unitskillusepos 'miniboss_id[1],"WZ_METEOR",9,.@data[UMOB_X],.@data[UMOB_Y],-50;
		end;
OnTimer5000:
		initnpctimer;
		end;
}

1@eom,4,6,0	script	#mk_trap	CLEAR_NPC,{
		end;
OnTrap:
		.@label$ = instance_npcname("#mk_trap") + "::OnMobDead";
		if (mobcount( 'map_eom$, .@label$ ) < 3) {
				getunitdata 'boss_id, .@data;
				monster 'map_eom$,.@data[UMOB_X]-4,.@data[UMOB_Y], "Frenzied Kasa", 3089,1, .@label$;	// MM_LOCO_KASA
				monster 'map_eom$,.@data[UMOB_X],.@data[UMOB_Y]-4, "Frenzied Kasa", 3089,1, .@label$;
				monster 'map_eom$,.@data[UMOB_X]+4,.@data[UMOB_Y], "Frenzied Kasa", 3089,1, .@label$;
				monster 'map_eom$,.@data[UMOB_X],.@data[UMOB_Y]+4, "Frenzied Kasa", 3089,1, .@label$;
		}
		end;
OnMobDead:
		end;
}

1@eom,102,222,3	script	#morocc_god	3097,{
		end;
OnStart:
		enablenpc instance_npcname("#morocc_god");
		initnpctimer;
		end;
OnTimer2000:
		mapannounce 'map_eom$, "This is getting more interesting. *Snicker*", bc_map, 0xff0000;
		end;
OnTimer6000:
		mapannounce 'map_eom$, "Hey, you don't think this is over already, do you?", bc_map, 0xff0000;
		end;
OnTimer10000:
		disablenpc instance_npcname("#morocc_god");
		end;
OnTimer12000:
		donpcevent instance_npcname("#despair_main") + "::OnStart";
		end;
OnTimer13000:
		unittalk 'boss_id, "What are you waiting for?!";
		end;
OnTimer14000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer16000:
		mapannounce 'map_eom$, "I'll take your blood!", bc_map, 0xff0000;
		end;
OnTimer17000:
		stopnpctimer;
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"NPC_WIDESUCK",1,.@data[UMOB_X],.@data[UMOB_Y],-50;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_main	-1,{
		end;
OnStart:
		enablenpc instance_npcname("#despair_main");
		monster 'map_eom$,101,194, "Despair God Morroc",3097,1, instance_npcname("#despair_main") + "::OnMobDead";	// MM_MOROCC_ADT
		'boss_id = $@mobid[0];
		end;
OnMobDead:
		disablenpc instance_npcname("#despair_main");
		killmonster 'map_eom$, instance_npcname("#despair_main") + "::OnMobDead";
		stopnpctimer instance_npcname("#morocc_god");
		stopnpctimer instance_npcname("#ma_combo");
		stopnpctimer instance_npcname("#despair_event_1");
		stopnpctimer instance_npcname("#despair_event_2");
		stopnpctimer instance_npcname("#despair_event_3");
		stopnpctimer instance_npcname("#despair_event_4");
		stopnpctimer instance_npcname("#despair_event_5");
		mapannounce 'map_eom$, "D-don't think... th-this... is over.. You're... already... time...", bc_map, 0xff0000;
		initnpctimer;

		'step = 11;
		donpcevent instance_npcname("#demon_god_variables") + "::OnVariableReset";
		end;
OnTimer2000:
		stopnpctimer;
		enablenpc instance_npcname("Nidhogg#eomnyd04");
		end;

OnTest_hp:
		if ('boss_id && mobcount( 'map_eom$, instance_npcname("#despair_main") + "::OnMobDead" ) > 0)
				setunitdata 'boss_id, UMOB_HP, 2000000;
		end;
}

1@eom,4,8,0	script	#ma_combo	CLEAR_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer10000:
		donpcevent instance_npcname("#despair_event_" + rand(1,5)) + "::OnEvent";
		stopnpctimer;
		end;
}

1@eom,1,1,0	script	#despair_event_1	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "I'll burn you slowly and painfully.";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer3000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer5000:
		stopnpctimer;
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_METEOR",11,.@data[UMOB_X],.@data[UMOB_Y],-50;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_event_2	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "Can you hear the rhapsody? It's the sound of your soul writhing and screaming in pain!";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer3000:
		getunitdata 'boss_id, .@data;
		'bossx = .@data[UMOB_X];
		'bossy = .@data[UMOB_Y];
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-2),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+2),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+2),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-2),-50;
		end;
OnTimer4000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-6),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+6),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+6),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-6),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-4),('bossy+4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-4),('bossy-4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+4),('bossy+4),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+4),('bossy-4),-50;
		end;
OnTimer5000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-10),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+10),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+10),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-10),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-8),('bossy+8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-8),('bossy-8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+8),('bossy+8),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+8),('bossy-8),-50;
		end;
OnTimer6000:
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-16),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+16),'bossy,-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy+16),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,'bossx,('bossy-16),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-12),('bossy+12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx-12),('bossy-12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+12),('bossy+12),-50;
		unitskillusepos 'boss_id,"MG_THUNDERSTORM",20,('bossx+12),('bossy-12),-50;
		end;
OnTimer7000:
		stopnpctimer;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_event_3	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "This is the thunderbolt of death!";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_WIDEWEB",1;
		end;
OnTimer4000:
		unitskilluseid 'boss_id, "NPC_WIDEFREEZE",5;
		end;
OnTimer6000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_VERMILION",5,(.@data[UMOB_X]-7),.@data[UMOB_Y],-50;
		unitskillusepos 'boss_id,"WZ_VERMILION",5,(.@data[UMOB_X]+7),.@data[UMOB_Y],-50;
		unitskillusepos 'boss_id,"WZ_VERMILION",5,.@data[UMOB_X],(.@data[UMOB_Y]+7),-50;
		unitskillusepos 'boss_id,"WZ_VERMILION",5,.@data[UMOB_X],(.@data[UMOB_Y]-7),-50;
		end;
OnTimer10000:
		unittalk 'boss_id, "เผาไหม้และสร้างเปลวไฟอันงดงาม!";
		end;
OnTimer12000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_VERMILION",21,.@data[UMOB_X],.@data[UMOB_Y],-50;
		end;
OnTimer14000:
		stopnpctimer;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_event_4	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "I'll take your life.";
		unitskilluseid 'boss_id, "NPC_FLAMECROSS",1;
		end;
OnTimer1000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"NPC_WIDESUCK",1,.@data[UMOB_X],.@data[UMOB_Y],-50;
		end;
OnTimer5000:
		stopnpctimer;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,1,1,0	script	#despair_event_5	-1,{
		end;
OnEvent:
		initnpctimer;
		unittalk 'boss_id, "*Chuckle*";
		end;
OnTimer2000:
		unitskilluseid 'boss_id, "NPC_FIRESTORM",3;
		end;
OnTimer4000:
		getunitdata 'boss_id, .@data;
		unitskillusepos 'boss_id,"WZ_FIREPILLAR",10,.@data[UMOB_X],.@data[UMOB_Y],-50;
		end;
OnTimer8000:
		stopnpctimer;
		donpcevent instance_npcname("#ma_combo") + "::OnStart";
		end;
}

1@eom,103,194,3	script	Nidhogg#eomnyd04	4_F_NYDHOG,{
		if ('step < 11)
				end;
		if (isbegin_quest(7597) == 0 && checkquest(7593,HUNTING) == 2) {
				mes "[นิดฮ็อกก์]";
				mes "ฉันไม่รู้จะพูดอะไร";
				mes "เราหยุดพระเจ้าที่รอคอยอยู่";
				mes "เราบรรลุเป้าหมายของเราแล้ว";
				mes "แต่ฉันยังคงรู้สึกไม่สบายใจอยู่";
				cutin "ep14_nyd03.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "โลกนี้ถูกสร้างขึ้นในจิตใจของมอร์ร็อค";
				mes "เขาจะได้อยู่ในสวนแห่งนี้ตลอดไปซึ่งเขาไม่สามารถทำเสร็จได้";
				cutin "ep14_nyd02.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "เขาคงไม่รู้ว่าร่างกายของเขาจะถูกทำลายไปตลอดกาล";
				mes "เขามีความทะเยอทะยานมากเกินไปสำหรับตัวเขาเองด้วย";
				mes "เขาอยากสร้างโลกใหม่และกลายเป็นผู้สร้างมัน";
				next;
				mes "[นิดฮ็อกก์]";
				mes "แต่เขาไม่สามารถสร้างสิ่งใดขึ้นมาเองได้";
				mes "เขาละทิ้งร่างปีศาจของตนเพียงเพื่อที่จะเป็นเพียงแบบจำลองของเทพเจ้าที่มีอยู่จริง";
				cutin "ep14_nyd03.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "เขามีรูปร่างเป็นผู้ชาย";
				mes "เขาไม่มีความคิดสร้างสรรค์มากพอที่จะสร้างแนวคิดเรื่องพระเจ้าขึ้นมาใหม่";
				mes "เขาเป็นคนขัดแย้งในตัวเอง";
				next;
				mes "[นิดฮ็อกก์]";
				mes "บัดนี้เราต้องหลุดพ้นจากกับดักนี้ให้ได้เมื่อถึงเวลาและสู่ความเป็นจริง";
				mes "โลกนี้มีอยู่เพียงในจิตใจของเทพอสูรเท่านั้น วันเดียวกันนี้จะเกิดขึ้นซ้ำแล้วซ้ำเล่า";
				next;
				mes "[นิดฮ็อกก์]";
				mes "ทุกคนที่เข้ามาในโลกนี้จะถูกดึงเข้าไปในวังวนเดียวกัน";
				mes "ในแง่นั้น เขาประสบความสำเร็จในการสร้างโลกของเขาเอง";
				cutin "ep14_nyd02.bmp",2;
				next;
				mes "[นิดฮ็อกก์]";
				mes "ความสิ้นหวังในวันนี้จะเกิดขึ้นซ้ำอีกวันแล้ววันเล่า";
				mes "ถึงแม้จะรู้เช่นนี้ เราก็จะกลับมาเดินบนเส้นทางเดิมอีกครั้ง หวังว่าเราจะสามารถต่อสู้เคียงข้างเธอได้ทุกครั้ง...";
				cutin "ep14_nyd03.bmp",2;
				next;
				cutin "ep14_nyd04.bmp",2;
				mes "[นิดฮ็อกก์]";
				mes "ข้าพูดมากเกินไปแล้ว ท่านผู้บัญชาการคงรออยู่ ข้าพเจ้าขอตรวจสอบความสามารถของท่านก่อน แล้วท่านจึงจะรายงานให้ท่านทราบได้";
				mes "วันนี้คุณทำได้ดีมาก";
				if (isbegin_quest(7593) == 1) {
						erasequest 7593;// Demon God Subjugation
						setquest 7597;// Fall of the False God
				}
				if (checkquest(7601,HUNTING) == 1)
						erasequest 7601;
				if (isbegin_quest(7602) > 0)
						erasequest 7602;// Temple of the Demon God Phase 1 Completed
				if (isbegin_quest(7603) == 1)
						erasequest 7603;
				if (isbegin_quest(7604) > 0)
						erasequest 7604;// Temple of the Demon God Phase 2 Completed
				if (isbegin_quest(7596) == 1)
						erasequest 7596;
		}
		else {	// todo, custom text
				mes "[นิดฮ็อกก์]";
				mes "...รู้สึกเหมือนว่าเรื่องยังไม่จบแค่นี้";
				mes "นี่ใกล้กับโลกจิตวิญญาณของมอร์ร็อคมาก";
				mes "เขาควรอยู่ที่นี่เพื่อคิดถึงอดีตต่อไป!";
				cutin "ep14_nyd03.bmp",2;
		}
		next;
		select("Exit.");
		mes "[นิดฮ็อกก์]";
		mes "แน่นอน คุณคงจะเหนื่อยมาก";
		mes "ให้ฉันส่งคุณไปหาสมาชิกคนอื่นๆ ในคณะสำรวจของคุณโดยไม่มีค่าใช้จ่าย";
		close2;
		warp "moro_vol",91,87;
		end;
}

1@eom,5,1,0	script	Little Helper#eomtest	4_ENERGY_BLUE,{
		mes "อะไร";
		next;
		if (callfunc("F_GM_NPC",1854,0) < 1) {
				mes "ผิด.";
				close;
		}
		mes "บอสอะไรครับ?";
		next;
		switch( select( "Brinaranea", "Muspellskoll", "Demigod", "Despair God" ) ) {
		case 1:
				mes "ค่าสุขภาพของ Brinaranea ถูกกำหนดไว้ที่ 2.000.000";
				donpcevent instance_npcname("Brinaranea#brinpc01") + "::OnTest_hp";
				close;
		case 2:
				mes "ค่าพลังชีวิตของ Muspellskoll ถูกกำหนดไว้ที่ 2.000.000";
				donpcevent instance_npcname("#skollmaster") + "::OnTest_hp";
				close;

		case 3:
				mes "ค่าสุขภาพของเทพกึ่งเทพถูกกำหนดไว้ที่ 2.000.000";
				donpcevent instance_npcname("#despair_god_main") + "::OnTest_hp";
				close;

		case 4:
				mes "ค่าพลังชีวิตของ Despair God ถูกตั้งไว้ที่ 2.000.000";
				donpcevent instance_npcname("#despair_main") + "::OnTest_hp";
				close;
		}
		end;
}
//===== rAthena Script =======================================
//= Poring Village
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Poring Village instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Enchant
prt_fild05,174,238,7	script	Veggie Enchanter#0	4_F_02,2,2,{
		mes "[นักเวทย์ผัก]";
		mes "เจลโลปี้ 50 ชิ้นและเงินเซนี 20,000 ชิ้นคือทั้งหมดที่คุณต้องการเพื่อสร้างเอฟเฟกต์พิเศษให้กับต้นหอมหรือแครอทของคุณ!";
		next;
		mes "[นักเวทย์ผัก]";
		mes "การใช้เสน่ห์และรีเซ็ตมีความล้มเหลว ^ff000030%^000000 ลงทุนในผักของคุณและโชค!";
		next;
		switch( select( "How much do I need to enchant?", "Enchant to Poring Village Veggie", "Reset Poring Village Veggie" ) ) {
		case 1:
				mes "[นักเวทย์ผัก]";
				mes "ตามที่ฉันได้ยินมา คุณสามารถหาต้นหอมหรือแครอทของ Poring Village ได้หากไปทางตะวันตกไกลจากที่นี่อีกสักหน่อย";
				next;
				mes "[นักเวทย์ผัก]";
				mes "ฉันชอบผักสด";
				next;
				mes "[นักเวทย์ผัก]";
				mes "การร่ายมนตร์หรือรีเซ็ตต้องใช้ 20,000z และ Jellopy 50 ชิ้น! ไม่เลวเลยใช่ไหม?";
				next;
				mes "[นักเวทย์ผัก]";
				mes "เอ่อ...อย่างที่ทราบกันดีอยู่แล้วว่ามันเป็นผัก ดังนั้นบางครั้งมันอาจแตกหักได้";
				close;
		case 2:
				.@equip_id = getequipid(EQI_HEAD_LOW);

				switch( .@equip_id ) {
				case -1:
						mes "[นักเวทย์ผัก]";
						mes "กรุณาสวมใส่อุปกรณ์ที่คุณต้องการร่ายมนตร์ก่อน";
						close;
				case 19238:	// PoringTownOnion
				case 19239:	// PoringTownCarrot
						break;
				default:
						mes "[นักเวทย์ผัก]";
						mes "นี่ไม่ใช่ผัก!?";
						close;
				}
				if (getequipcardid(EQI_HEAD_LOW,3) > 0) {
						mes "[นักเวทย์ผัก]";
						mes "ผักชนิดนี้ไม่อาจถูกเสกได้อีกต่อไปแล้ว";
						close;
				}
				if (countitem(909) < 50 || Zeny < 20000) {
						mes "[นักเวทย์ผัก]";
						mes "การร่ายมนตร์นี้ต้องใช้ 50 Jellopy และ 20,000z..... แต่... คุณไม่มีมัน";
						close;
				}
				delequip EQI_HEAD_LOW;
				delitem 909,50;	// Jellopy
				Zeny -= 20000;

				if (rand(100) < 70) {
						.@r = rand(5356);
						if (.@r < 160) .@card = 4795;	// HP100
						else if (.@r < 200) .@card = 4796;	// HP200
						else if (.@r < 1000) .@card = 4928;	// SP10
						else if (.@r < 1400) .@card = 4870;	// SP25
						else if (.@r < 1480) .@card = 4800;	// SP50
						else if (.@r < 1880) .@card = 4700;	// Strength1
						else if (.@r < 2280) .@card = 4730;	// Agility1
						else if (.@r < 3410) .@card = 4740;	// Vitality1
						else if (.@r < 3810) .@card = 4710;	// Inteligence1
						else if (.@r < 4210) .@card = 4720;	// Dexterity1
						else if (.@r < 5340) .@card = 4750;	// Luck1
						else if (.@r < 5348) .@card = 4992;	// HPdrain1
						else if (.@r < 5356) .@card = 4993;	// SPdrain1

						getitem2 .@equip_id,1,1,0,0,0,0,0,.@card;
						mes "[นักเวทย์ผัก]";
						mes "ต๊าดา! เสกเสร็จแล้ว! ตรวจดูผักของคุณ";
						specialeffect2 EF_REPAIRWEAPON;
						close;
				}
				mes "[นักเวทย์ผัก]";
				mes "คงเป็นผักเน่าแน่เลย เสียดายที่ทนพลังระดับนี้ไม่ไหว...";
				specialeffect2 EF_LORD;
				close;
		case 3:
				.@equip_id = getequipid(EQI_HEAD_LOW);

				switch( .@equip_id ) {
				case -1:
						mes "[นักเวทย์ผัก]";
						mes "กรุณาสวมใส่อุปกรณ์ที่คุณต้องการร่ายมนตร์ก่อน";
						close;
				case 19238:	// PoringTownOnion
				case 19239:	// PoringTownCarrot
						break;
				default:
						mes "[นักเวทย์ผัก]";
						mes "นี่ไม่ใช่ผัก!?";
						close;
				}
				if (getequipcardid(EQI_HEAD_LOW,3) == 0) {
						mes "[นักเวทย์ผัก]";
						mes "นี่เป็นผักที่ไม่เคยได้รับการเสริมสารอาหารเลย...";
						close;
				}
				if (countitem(909) < 50 || Zeny < 20000) {
						mes "[นักเวทย์ผัก]";
						mes "การร่ายมนตร์นี้ต้องใช้ 50 Jellopy และ 20,000z..... แต่... คุณไม่มีมัน";
						close;
				}
				delequip EQI_HEAD_LOW;
				delitem 909,50;	// Jellopy
				Zeny -= 20000;

				if (rand(100) < 70) {
						getitem .@equip_id,1;
						mes "[นักเวทย์ผัก]";
						mes "สินค้าครบแล้วค่ะ รีบดูก่อนหมดนะคะ";
						specialeffect2 EF_REPAIRWEAPON;
						close;
				}
				mes "[นักเวทย์ผัก]";
				mes "คงเป็นผักเน่าแน่เลย เสียดายที่ทนพลังระดับนี้ไม่ไหว...";
				specialeffect2 EF_LORD;
				close;
		}
		end;

OnTouch:
		npctalk "Veggie Enchanter: ทำให้ต้นหอมหรือแครอทของคุณแข็งแรงขึ้น!", "", bc_self;
		end;
}


// Instance
prt_fild05,145,235,5	script	Emily#0	4_GEFFEN_03,4,4,{
		if (BaseLevel < 30) {
				mes "[เอมิลี่]";
				mes "ว้าว~ คุณดูเหมือนมือใหม่เลย ฉันอยากจะผจญภัยกับคุณสักวันตอนที่คุณโตขึ้น!";
				close;
		}
		if (BaseLevel > 60) {
				mes "[เอมิลี่]";
				mes "โอ้ ตอนนี้คุณแข็งแกร่งกว่าฉันแล้ว ทำไมคุณถึงเติบโตเร็วขนาดนี้";
				next;
				mes "[เอมิลี่]";
				mes "ฉันควรหาคนผจญภัยคนอื่นที่อยู่ในระดับเดียวกับฉันบ้าง ดีใจที่ได้รู้จักคุณ";
				close;
		}
		if (isbegin_quest(12416) == 0) {
				mes "[เอมิลี่]";
				mes "ฉันชื่อเอมิลี่ ฉันกำลังรอใครสักคนที่จะร่วมผจญภัยไปกับฉัน!";
				next;
				select("Who said I will adventure with you?");
				mes "[เอมิลี่]";
				mes "เอาล่ะ มาเถอะ อย่าเข้มงวดนัก ฟังนะ คุณตัดสินใจได้หลังจากฟังเรื่องของฉัน แน่นอนว่าฉันจะให้รางวัลที่เหมาะสมกับคุณได้";
				next;
				if (select( "Proper Reward?", "I'm not interested." ) == 2) {
						mes "[เอมิลี่]";
						mes "เหลี่ยมเยอะขนาดนี้ อยู่ด้วยกันมันดีกว่าเยอะ!";
						close;
				}
				mes "[เอมิลี่]";
				mes "ใช่ครับ! ให้รางวัลอย่างเหมาะสม";
				next;
				mes "[เอมิลี่]";
				mes "ดูเหมือนว่าหมู่บ้านพอริ่งแห่งนี้จะถูกเอาชนะโดยผู้เล่นใหม่เช่นพวกเราได้ แต่นั่นเป็นเพียงการเดาแบบมั่วๆ เท่านั้น...";
				next;
				mes "[เอมิลี่]";
				mes "จริงๆ แล้ว ฉันไม่สามารถให้รางวัลคุณอย่างเป็นทางการผ่านสมาคมนักผจญภัยได้ แต่เราสามารถแยกรายได้หลังจากออกล่าได้";
				next;
				select("But 50-50 sounds fair to me.");
				mes "[เอมิลี่]";
				mes "ไม่มีทาง 70-30 และแน่นอนว่าฉันอายุ 70";
				next;
				select("It's gotta be 50-50 or it's no good");
				mes "[เอมิลี่]";
				mes "60-40 ละ!!";
				next;
				select("50-50 ...");
				mes "[เอมิลี่]";
				mes "เรื่องนี้ทำให้ฉันหงุดหงิด โอเค 50-50 เอาล่ะ มาทำกันแบบนี้เลย";
				setquest 12416;	// Contract with Emily
				completequest 12416;
				close;
		}
		switch( checkquest(12417,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "^ff0000การดำเนินการใดๆ กับมอนสเตอร์ เช่น การฝึกมอนสเตอร์ภายในดันเจี้ยนจะไม่ถือเป็นความคืบหน้าปกติ โปรดทราบว่า^000000";
				next;
				mes "^ff0000ในโหมดเริ่มต้น ผู้เล่นจะแปลงร่างเป็นสัตว์ประหลาด โปรดทราบว่าเอฟเฟกต์การแปลงร่างที่มีอยู่จะหายไปเมื่อเข้าและดำเนินการต่อ^000000";
				next;
				mes "[เอมิลี่]";
				mes "แล้วคุณพร้อมแล้วหรือยัง?";
				next;
				mes "[เอมิลี่]";
				mes "คุณดูเหนื่อยมาก";
				next;
				mes "[เอมิลี่]";
				mes "พักก่อนแล้วค่อยกลับมาใหม่นะ พักก่อน";
				close;
		case 2:
				mes "^ff0000การดำเนินการใดๆ กับมอนสเตอร์ เช่น การฝึกมอนสเตอร์ภายในดันเจี้ยนจะไม่ถือเป็นความคืบหน้าปกติ โปรดทราบว่า^000000";
				next;
				mes "^ff0000ในโหมดเริ่มต้น ผู้เล่นจะแปลงร่างเป็นสัตว์ประหลาด โปรดทราบว่าเอฟเฟกต์การแปลงร่างที่มีอยู่จะหายไปเมื่อเข้าและดำเนินการต่อ ^000000";
				next;
				mes "[เอมิลี่]";
				mes "แล้วคุณพร้อมแล้วหรือยัง?";
				next;
				mes "^0000ffเควสต์ร่องรอยของหมู่บ้านพอริ่งทั้งหมดถูกลบออก ตอนนี้คุยกับเอมิลี่อีกครั้งเพื่อเข้าสู่^000000";
				erasequest 12417;
				close;
		}
		mes "^ff0000การดำเนินการใดๆ กับมอนสเตอร์ เช่น การฝึกมอนสเตอร์ภายในดันเจี้ยนจะไม่ถือเป็นความคืบหน้าปกติ โปรดทราบว่า^000000";
		next;
		mes "^ff0000ในโหมดเริ่มต้น ผู้เล่นจะแปลงร่างเป็นสัตว์ประหลาด โปรดทราบว่าเอฟเฟกต์การแปลงร่างที่มีอยู่จะหายไปเมื่อเข้าและดำเนินการต่อ^000000";
		next;
		mes "[เอมิลี่]";
		mes "แล้วคุณพร้อมแล้วหรือยัง?";
		if (isbegin_quest(12418) == 0)
				setquest 12418;	// First Visit to Poring Village
		next;
		if (getcharid(1) < 1) {
				mes "[เอมิลี่]";
				mes "อืม คุณควรสร้างปาร์ตี้ก่อนเพื่อเริ่มการผจญภัยครั้งยิ่งใหญ่ครั้งนี้";
				close;
		}
		.@md_name$ = "Poring Village";

		if (is_party_leader())
				.@menu$[0] = "Create the entrance";
		if (instance_live_info(ILI_NAME, instance_id(IM_PARTY)) == .@md_name$)
				.@menu$[1] = "Enter the dungeon";

		switch( select( .@menu$[0], .@menu$[1], "Cancel" ) ) {
		case 1:
				mes "[เอมิลี่]";
				mes "ตอนนี้ผมกำลังมองหาทางเข้า กรุณาคุยกับผมอีกครั้งเมื่อทางเข้าเปิดเต็มที่แล้ว";
				instance_create(.@md_name$);
				close;
		case 2:
				switch( instance_enter(.@md_name$) ) {
				case IE_OTHER:
						mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
						close;
				case IE_NOINSTANCE:
						mes "^ff0000รอยแตกแห่งกาลเวลายังไม่ถูกสร้างขึ้น^000000";
						close;
				case IE_NOMEMBER:
						end;
				case IE_OK:
						mapannounce "prt_fild05", "Party member " + strcharinfo(0) + " of the party " + getpartyname(getcharid(0)) + " enters " + .@md_name$ + ".", bc_map,0xFF9900;
						setquest 12417;	// Overcoming fatigue
						// warp "1@begi",100,22;
						end;
				}
				end;
		case 3:
				end;
		}
		end;

OnTouch:
		if (isbegin_quest(12416) == 0 && BaseLevel >= 30 && BaseLevel <= 60)
				npctalk "เอมิลี่: เฮ้ คุณช่วยฉันคุยด้วยสักครู่ได้ไหม?";
		end;

OnInit:
		questinfo( QTYPE_JUMPING_PORING, QMARK_YELLOW , "isbegin_quest(12416) == 0 && BaseLevel >= 30 && BaseLevel <= 60" );
		end;
}

1@begi,106,30,0	script	#start_wp1_0	HIDDEN_WARP_NPC,4,4,{
		end;
OnTouch:
		disablenpc();
		donpcevent instance_npcname("#start_wp1_1") + "::OnStart";
		end;

OnInstanceInit:
		'map_begi$ = instance_mapname("1@begi");

		setcell 'map_begi$,146,35,146,41,cell_walkable,0;
		setcell 'map_begi$,154,100,154,107,cell_walkable,0;
		setcell 'map_begi$,59,187,59,197,cell_walkable,0;

		disablenpc instance_npcname("#start_wp1_2");
		disablenpc instance_npcname("#gift");
		end;
}

1@begi,106,30,5	script	#start_wp1_1	4_GEFFEN_03,{
		mes "[เอมิลี่]";
		mes "เราก้าวไปข้างหน้ากันเถอะ";
		close;

OnStart:
		npctalk "เอมิลี่: แล้ว.. ที่นี่คือหมู่บ้านพอริ่งใช่ไหม?";
		sleep 2000;
		npctalk "เอมิลี่: โอเค ไปกันเถอะ!";
		sleep 2000;
		disablenpc();
		donpcevent instance_npcname("#start_wp1_2") + "::OnStart";
		donpcevent instance_npcname("poring_village_1") + "::OnStart";
		end;
}

1@begi,140,37,5	script	#start_wp1_2	4_GEFFEN_03,{
		mes "[เอมิลี่]";
		mes "ฉันจะเริ่มช้าๆ แล้วนะ...";
		close;

OnStart:
		enablenpc();
		sleep 1000;
		npctalk "เอมิลี่: อืม ดูเหมือนพวกเขาจะสร้างกำแพงไว้ไม่ให้พวกเราไปได้ง่ายๆ";
		sleep 3000;
		npctalk "เอมิลี่: ฉันมั่นใจว่าพอริ่งรวบรวมสมบัติไว้มากมายที่ไหนสักแห่ง";
		sleep 4000;
		mapannounce 'map_begi$, "???: I can hear a human's voice. Make them welcome, everybody.", bc_map, 0xFFFF00;
		sleep 4000;
		npctalk "เอมิลี่ : มันพูดอะไรนะ?";
		sleep 3000;
		npctalk "เอมิลี่: มันได้ยินเสียงใครบางคนกำลังรีบมาที่นี่...";
		sleep 2500;
		mapannounce 'map_begi$, "???: Don't let them run! Take all valuable items from them!", bc_map, 0xFFFF00;
		sleep 2500;
		npctalk "เอมิลี่: โอเค พวกมันดูไม่เป็นมิตรเลย รีบซ่อนตัวซะ!";
		sleep 4000;
		// disablenpc();
		hideonnpc();	// hideonnpc instead of disablenpc (official) to display text on rAthena
		sleep 1000;
		npctalk "เอมิลี่: อุ๊ย ซ่อนไม่ได้เหรอ? เอ่อ... สู้ต่อไปนะ!";
		// disablenpc instance_npcname("#poring_wl18");
		hideonnpc instance_npcname("#poring_wl18");
		specialeffect EF_LORD,AREA, instance_npcname("#poring_wl18");
		sleep 500;
		// disablenpc instance_npcname("#poring_wl19");
		hideonnpc instance_npcname("#poring_wl19");
		specialeffect EF_LORD,AREA, instance_npcname("#poring_wl19");
		sleep 500;
		disablenpc instance_npcname("#poring_wl15");
		sleep 500;
		disablenpc instance_npcname("#poring_wl16");
		disablenpc instance_npcname("#poring_wl17");
		disablenpc instance_npcname("#poring_wl20");
		disablenpc instance_npcname("#poring_wl21");
		setcell 'map_begi$,146,35,146,41,cell_walkable,1;
		end;
}

// First wall
1@begi,146,35,6	duplicate(dummy_npc)	#poring_wl15	4_ROPEPILE,1,0
1@begi,146,36,6	duplicate(dummy_npc)	#poring_wl16	4_ROPEPILE,1,0
1@begi,146,37,6	duplicate(dummy_npc)	#poring_wl17	4_ROPEPILE,1,0
1@begi,146,38,6	duplicate(dummy_npc)	#poring_wl18	4_ROPEPILE,1,0
1@begi,146,39,6	duplicate(dummy_npc)	#poring_wl19	4_ROPEPILE,1,0
1@begi,146,40,6	duplicate(dummy_npc)	#poring_wl20	4_ROPEPILE,1,0
1@begi,146,41,6	duplicate(dummy_npc)	#poring_wl21	4_ROPEPILE,1,0

// Second wall
1@begi,59,187,6	duplicate(dummy_npc)	#poring_wl9	4_ROPEPILE,1,0
1@begi,59,188,6	duplicate(dummy_npc)	#poring_wl10	4_ROPEPILE,1,0
1@begi,59,189,6	duplicate(dummy_npc)	#poring_wl11	4_ROPEPILE,1,0
1@begi,59,190,6	duplicate(dummy_npc)	#poring_wl12	4_ROPEPILE,1,0
1@begi,59,191,6	duplicate(dummy_npc)	#poring_wl13	4_ROPEPILE,1,0
1@begi,59,192,6	duplicate(dummy_npc)	#poring_wl14	4_ROPEPILE,1,0

// Others
1@begi,154,100,6	duplicate(dummy_npc)	#poring_wl1	4_ROPEPILE,1,0
1@begi,154,101,6	duplicate(dummy_npc)	#poring_wl2	4_ROPEPILE,1,0
1@begi,154,102,6	duplicate(dummy_npc)	#poring_wl3	4_ROPEPILE,1,0
1@begi,154,103,6	duplicate(dummy_npc)	#poring_wl4	4_ROPEPILE,1,0
1@begi,154,104,6	duplicate(dummy_npc)	#poring_wl5	4_ROPEPILE,1,0
1@begi,154,105,6	duplicate(dummy_npc)	#poring_wl6	4_ROPEPILE,1,0
1@begi,154,106,6	duplicate(dummy_npc)	#poring_wl7	4_ROPEPILE,1,0
1@begi,154,107,6	duplicate(dummy_npc)	#poring_wl8	4_ROPEPILE,1,0


1@begi,1,1,5	script	poring_village_1	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("poring_village_1") + "::OnMobDead";

		areamonster 'map_begi$, 153,32, 171,32, "--en--", "MD_DROPS",7, .@event$;
		areamonster 'map_begi$, 48,54, 71,57, "--en--", "MD_DROPS",8, .@event$;
		areamonster 'map_begi$, 170,38, 189,38, "--en--", "MD_POPORING",8, .@event$;
		areamonster 'map_begi$, 24,72, 28,92, "--en--", "MD_POPORING",8, .@event$;
		areamonster 'map_begi$, 188,42, 191,58, "--en--", "MD_MARIN",8, .@event$;
		areamonster 'map_begi$, 158,67, 174,68, "--en--", "MD_MARIN",3, .@event$;
		areamonster 'map_begi$, 117,68, 131,68, "--en--", "MD_MARIN",4, .@event$;
		areamonster 'map_begi$, 79,113, 92,113, "--en--", "MD_MARIN",6, .@event$;

		monster 'map_begi$,101,66, "--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,57,113, "--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,68,113, "--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,176,66, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,158,68, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,157,67, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,149,66, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,125,67, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,121,67, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,102,67, "--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,101,68, "--en--","MD_PORING",1, .@event$;
		end;
OnMobDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_1") + "::OnMobDead" ) > 3)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_1") + "::OnMobDead";
		mapannounce 'map_begi$, "Emily: Wow~ You are quite strong! You defeated all these Porings!", bc_map, 0xFFFF00;
		sleep 2000;
		mapannounce 'map_begi$, "Emily: But the Poring looks unique somehow. Be careful!", bc_map, 0xFFFF00;
		sleep 2000;
		mapannounce 'map_begi$, "Emily: Ah, there is a unique force if you go near the column of blue light.", bc_map, 0xFFFF00;
		sleep 2000;
		monster 'map_begi$,132,103,"--en--","MD_GOLDRING",1, instance_npcname("poring_village_1") + "::OnBossDead";
		end;
OnBossDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_1") + "::OnBossDead" ) > 0)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_1") + "::OnBossDead";
		donpcevent instance_npcname("poring_village_2") + "::OnStart";
		sleep 6000;
		// disablenpc instance_npcname("#poring_wl4");
		hideonnpc instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
		// disablenpc instance_npcname("#poring_wl5");
		hideonnpc instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
		disablenpc instance_npcname("#poring_wl1");
		disablenpc instance_npcname("#poring_wl2");
		disablenpc instance_npcname("#poring_wl3");
		disablenpc instance_npcname("#poring_wl6");
		disablenpc instance_npcname("#poring_wl7");
		disablenpc instance_npcname("#poring_wl8");
		setcell 'map_begi$,154,100,154,107,cell_walkable,1;
		end;
}

1@begi,1,1,5	script	poring_village_2	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("poring_village_2") + "::OnMobDead";

		monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,192,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,194,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,194,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,195,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,195,97,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,199,108,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,201,107,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,203,108,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,206,107,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,213,108,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,115,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,112,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,113,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,117,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,214,111,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,215,108,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,215,107,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,216,107,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,197,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,197,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,192,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,190,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,188,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,188,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,184,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,182,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,157,144,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,154,143,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,154,144,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,152,143,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,142,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,130,144,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,129,144,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,127,144,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,120,144,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,117,144,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,116,144,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,114,143,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,103,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,102,144,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,102,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,93,143,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,88,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,88,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,84,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,84,144,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,83,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,76,143,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,75,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,73,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,73,143,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,72,144,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,69,143,"--en--","MD_POPORING",1, .@event$;
		monster 'map_begi$,64,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,59,143,"--en--","MD_PORING",1, .@event$;
		monster 'map_begi$,59,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,52,144,"--en--","MD_DROPS",1, .@event$;
		monster 'map_begi$,51,143,"--en--","MD_PORING",1, .@event$;
		end;
OnMobDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_2") + "::OnMobDead" ) > 4)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_2") + "::OnMobDead";
		mapannounce 'map_begi$, "Emily: Hmm.. here is another Boss... It looks strong...", bc_map, 0xFFFF00;
		sleep 2000;
		mapannounce 'map_begi$, "Emily: I'm not good at battle.. Let me look for treasure first. See you soon~", bc_map, 0xFFFF00;
		sleep 2000;
		monster 'map_begi$,42,173,"--en--","MD_AMERING",1, instance_npcname("poring_village_2") + "::OnBossDead";
		end;
OnBossDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_2") + "::OnBossDead" ) > 0)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_2") + "::OnBossDead";
		donpcevent instance_npcname("poring_village_3") + "::OnStart";
		sleep 6000;
		// disablenpc instance_npcname("#poring_wl10");
		hideonnpc instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
		// disablenpc instance_npcname("#poring_wl13");
		hideonnpc instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
		disablenpc instance_npcname("#poring_wl9");
		disablenpc instance_npcname("#poring_wl11");
		disablenpc instance_npcname("#poring_wl12");
		disablenpc instance_npcname("#poring_wl14");
		setcell 'map_begi$,59,187,59,197,cell_walkable,1;
		end;
}

1@begi,1,1,5	script	poring_village_3	-1,{
		end;
OnStart:
		.@event$ = instance_npcname("poring_village_3") + "::OnMobDead";

		monster 'map_begi$,81,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,84,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,85,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,86,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,87,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,88,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,90,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,93,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,94,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,94,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,95,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,98,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,99,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,103,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,103,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,104,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,108,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,111,197,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,128,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,128,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,130,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,133,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,134,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,135,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,136,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,137,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,141,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,143,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,144,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,144,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,146,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,150,185,"--en--","MD_MARIN",1, .@event$;
		monster 'map_begi$,150,185,"--en--","MD_MARIN",1, .@event$;
		end;
OnMobDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_3") + "::OnMobDead" ) > 4)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_3") + "::OnMobDead";
		mapannounce 'map_begi$, "King Poring: Excuse me... But... Would you please get back now?...", bc_map, 0xFFFF;
		sleep 2000;
		mapannounce 'map_begi$, "Emily: What? Who are you?", bc_map, 0xFFFF00;
		sleep 2000;
		mapannounce 'map_begi$, "King Poring: I am the leader of this Poring Village. Sorry for the late introduction.", bc_map, 0xFFFF;
		sleep 2000;
		mapannounce 'map_begi$, "King Poring: Maybe there is some misunderstanding on our flyers. This village is so poor and has nothing valuable.", bc_map, 0xFFFF;
		sleep 2000;
		mapannounce 'map_begi$, "Emily: It's for me to judge after looking around more....", bc_map, 0xFFFF00;
		sleep 2000;
		mapannounce 'map_begi$, "King Poring: Ugh.... I don't like fighting but there is no choice.", bc_map, 0xFFFF;
		sleep 2000;
		monster 'map_begi$,182,194,"--en--","PORING",10;
		for ( .@i = 0; .@i < 10; ++.@i ) {
				setunitdata $@mobid[.@i], UMOB_MODE, (MD_CANMOVE | MD_LOOTER | MD_AGGRESSIVE | MD_CANATTACK);
				setunitdata $@mobid[.@i], UMOB_SPEED, 200;
		}
		monster 'map_begi$,182,194,"--en--","MD_KING_PORING",1, instance_npcname("poring_village_3") + "::OnBossDead";
		end;
OnBossDead:
		if (mobcount( 'map_begi$, instance_npcname("poring_village_3") + "::OnBossDead" ) > 0)
				end;
		killmonster 'map_begi$, instance_npcname("poring_village_3") + "::OnBossDead";
		enablenpc instance_npcname("#gift");
		end;
}

1@begi,117,108,5	script	Blue Light Column#1	4_ENERGY_BLUE,4,4,{
		end;
OnTouch:
		switch( rand(3) ) {
		case 0:
				unittalk getcharid(3), "" + strcharinfo(0) + " : Ohhhhh!!! Release the parrot inside!!!!";
				break;
		case 1:
				unittalk getcharid(3), "" + strcharinfo(0) + " : I think it's ok to peck someone right now!!";
				break;
		case 2:
				unittalk getcharid(3), "" + strcharinfo(0) + " : Have you been bitten by a mad parrot?";
				break;
		}
		transform 1629,60000;
		specialeffect2 EF_ENHANCE;
		sc_start SC_GLASTHEIM_STATE,30000,20;
		end;
}
1@begi,37,165,5	duplicate(Blue Light Column#1)	Blue Light Column#2	4_ENERGY_BLUE,4,4
1@begi,175,199,5	duplicate(Blue Light Column#1)	Blue Light Column#3	4_ENERGY_BLUE,4,4


1@begi,199,186,5	script	#gift	4_GEFFEN_03,{
		mes "[เอมิลี่]";
		mes "คุณสามารถมีไอเทมทั้งหมดที่คุณพบในคุกใต้ดินนี้ได้ ฉันมีบางส่วนแล้ว";
		next;
		if (isbegin_quest(12418) == 1) {
				mes "[เอมิลี่]";
				mes "อ๋อ ฉันเจอของบางอย่าง ขอเอามาให้ชิ้นหนึ่งนะ ฉลองการมาเยือนครั้งแรกของคุณ!";
				next;
				if (rand(100) < 50)
						getitem 19238,1;	// PoringTownOnion
				else
						getitem 19239,1;	// PoringTownCarrot
				completequest 12418;
		}
		mes "[เอมิลี่]";
		mes "ฉันไม่ต้องการกล่องสำหรับมือใหม่นี้ คุณเอาไปได้เลย ออกไปข้างนอกกันเถอะ!";
		close2;
		getitem 23302,1;	// PoringsPreciousBox
		warp "prt_fild05",149,238;
		end;
}
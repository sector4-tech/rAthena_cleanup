//===== rAthena Script =======================================
//= Instance: Villa of High Priest.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Part of the episode 18.1 main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Main Quest: Step 61.
oz_dun01,219,117,5	script(CLOAKED)	Aira#c12_01	4_M_BRZ_JACI,{
		if (ep18_1_main == 54) {
				.@md_name$ = "Villa of High Priest";

				mes "[ไอระ]";
				mes "ทางเข้าวิลล่า? กุญแจก็คือทางเข้า";
				mes "เวทมนตร์เคลื่อนไหวอะไรสักอย่างเหรอ? อะไรประมาณนั้นน่ะ";
				mes "เมื่อคุณพร้อมแล้ว ฉันจะพาคุณไปที่วิลล่าไหม?";
				next;
				mes "[ไอระ]";
				mes "และเพราะคุณไม่เคยรู้ว่าอะไรจะเกิดขึ้นภายใน";
				mes "หากคุณมีเพื่อนร่วมงาน ฉันขอแนะนำให้คุณเข้าร่วมกับพวกเขา";
				next;
				switch( select( "Apply for '" + .@md_name$ + "'.", "Enter '" + .@md_name$ + "'." ) ) {
				case 1:
						if (getcharid(1) == 0 || is_party_leader() == false) {
								mes "^4d4dff นี่เป็นกิจกรรมรำลึก.";
								mes "คำขอเข้าคุกใต้ดินแห่งความทรงจำจะต้องทำในฐานะหัวหน้าปาร์ตี้^000000";
								close;
						}
						mes "[ไอระ]";
						mes "แล้วเจอกันนะ.";
						if (instance_create(.@md_name$) < 0) {
								mes "งานสังสรรค์:" + getpartyname(getcharid(1)) + "";
								mes "ผู้นำ:" + strcharinfo(0) + "";
								mes "^0000ff" + .@md_name$ + "^000000- ข้อผิดพลาดที่ไม่ทราบสาเหตุ";
								close;
						}
						close;
				case 2:
						switch( instance_enter(.@md_name$) ) {
						case IE_OTHER:
								mes "[ไอระ]";
								mes "^ff0000เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ^000000";
								close;
						case IE_NOINSTANCE:
								mes "[ไอระ]";
								mes "^ff0000หัวหน้าปาร์ตี้ของคุณยังไม่ได้สร้างดันเจี้ยน^000000";
								close;
						case IE_NOMEMBER:
								mes "[ไอระ]";
								mes "^ff0000เฉพาะสมาชิกในกลุ่มเท่านั้นที่สามารถเข้าสู่ดันเจี้ยนอนุสรณ์ได้^000000";
								close;
						case IE_OK:
								mapannounce "oz_dun01", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering " + .@md_name$ + ".",bc_map,"0x00ff99",FW_NORMAL,12;
								// warp "1@adv",123,17;
								end;
						}
				}
		}
		end;
}

// warps
1@adv,63,142,0	warp	#left_room_in	1,1,1@adv,56,142
1@adv,60,142,0	warp	#left_room_out	1,1,1@adv,66,142
1@adv,185,142,0	warp	#right_room_in	1,1,1@adv,190,142
1@adv,187,142,0	warp	#right_room_out	1,1,1@adv,182,142
1@adv,173,142,0	warp	#center_room_in	1,1,1@adv,167,142
1@adv,171,142,0	warp	#center_room_out	1,1,1@adv,177,142
1@adv,123,309,0	warp	#boss_in	1,1,1@adv,123,321
1@adv,123,312,0	warp	#boss_out	1,1,oz_dun01,220,112


1@adv,122,20,5	script	Aira#aira01	4_M_BRZ_JACI,{
		npctalk "ไม่ต้องเป็นห่วงฉัน ฉันจะอยู่ใกล้ประตูและออกไปทันทีถ้ามันเป็นอันตราย";
		end;

OnInstanceInit:
		'adv_step = 0;
		'Knight_left = 0;
		'Knight_righ = 0;
		'flower_num = rand(1,5);

		'map_adv$ = instance_mapname("1@adv");

		disablenpc instance_npcname("Miriam#miriam02");
		disablenpc instance_npcname("Heart Hunter#rh03");
		disablenpc instance_npcname("Heart Hunter#rh04");
		disablenpc instance_npcname("Heart Hunter#rh05");
		disablenpc instance_npcname("Heart Hunter#rh06");
		disablenpc instance_npcname("Heart Hunter#rh07");

		disablenpc instance_npcname("#left_room_in");
		disablenpc instance_npcname("#left_room_out");
		disablenpc instance_npcname("#right_room_in");
		disablenpc instance_npcname("#right_room_out");
		disablenpc instance_npcname("#center_room_in");
		disablenpc instance_npcname("#center_room_out");
		disablenpc instance_npcname("#boss_in");
		disablenpc instance_npcname("#boss_out");

		disablenpc instance_npcname("Silver Key#key01");
		disablenpc instance_npcname("Gold Key#key02");

		disablenpc instance_npcname("Miriam#miriam03");
		disablenpc instance_npcname("Villa Employee#ex21");
		disablenpc instance_npcname("Villa Employee#ex22");
		disablenpc instance_npcname("Villa Employee#ex23");

		disablenpc instance_npcname("#flower01");
		disablenpc instance_npcname("#flower02");
		disablenpc instance_npcname("#flower03");
		disablenpc instance_npcname("#flower04");
		disablenpc instance_npcname("#flower05");

		// middle boss
		disablenpc instance_npcname("Shulang#Shulang01");

		// boss room 1
		disablenpc instance_npcname("Priest Youth#jus01");
		disablenpc instance_npcname("Illusion Bagot#bagot01");
		disablenpc instance_npcname("Miriam#miriam04");
		cloakonnpc instance_npcname("Goddess Form#demigod");
		cloakonnpc instance_npcname("Shulang#Shulang02");

		// Boss room out
		disablenpc instance_npcname("Miriam#miriam05");
		disablenpc instance_npcname("Priest Youth#jus02");
		end;
}

1@adv,125,23,3	script	Miriam#miriam01	EP18_NPC_MIRIAM,{
		if (is_party_leader() == false) {
				mes "[มิเรียม]";
				mes "พวกคุณรวมตัวกันครบแล้วหรือยัง?";
				close;
		}
		if (ep18_1_main != 54)
				end;
		if ('adv_step == 0) {
				'adv_step = 1;
				// note: player can move and talk to other npc
				.@npc$ = instance_npcname("Aira#aira01");

				sleep 2500;
				npctalk "ไอระ: ที่นี่ ถ้าจะให้ผู้ใช้ไปชั้นแรกก็คงจะดี", .@npc$;
				sleep 2500;
				npctalk "มิเรียม: ชั้น 2 เหรอ?";
				sleep 2500;
				npctalk "ไอระ: พระสงฆ์ยังอยู่ค่ะ ไม่ทราบว่าเป็นพระสงฆ์จริงๆ หรือเปล่าคะ", .@npc$;
				sleep 2500;
				npctalk "ไอระ: เธอรุนแรงและดูเหมือนจะมีอาวุธด้วย", .@npc$;
				sleep 2500;
				npctalk "มิเรียม: โอเค ขอบคุณสำหรับคำแนะนำ เราจะค้นหาข้างใน";
				sleep 2500;
				npctalk "ไอระ: ไม่ต้องห่วงฉันนะ ระวังตัวด้วยนะทั้งสองคน", .@npc$;
				sleep 2500;
				npctalk "มิเรียม: งั้นเราก็จะขึ้นไปช้าๆ นะคะ";
				sleep 2500;
				npctalk "ไอระ: หากุญแจห้องจัดเลี้ยงชั้น 2 สิ ต้องมีอะไรบางอย่างมากกว่านั้นแน่ๆ!", .@npc$;
				'adv_step = 2;
				end;
		}
		if ('adv_step == 2) {
				cutin "ep18_miriam_03.png",0;
				mes "[มิเรียม]";
				mes "ฉันคิดว่าเราควรขึ้นไปชั้นสองเพื่อดูว่ามีพนักงานอื่นอีกหรือไม่";
				npctalk "มิเรียม: ฉันคิดว่าเราควรขึ้นไปชั้นสองหลังจากดูว่ามีพนักงานคนอื่นอีกหรือเปล่า";
				next;
				mes "[มิเรียม]";
				mes "ฉันสัมผัสได้ถึงพลังอันเฉียบคมเหนือบันไดจุงกัม บางทีอาจเป็นนักล่าหัวใจที่ต่อสู้ในวัด";
				npctalk "มิเรียม: ฉันรู้สึกถึงพลังที่ดุร้าย บางทีอาจเป็นนักล่าหัวใจที่ต่อสู้ในวิหาร";
				next;
				mes "[มิเรียม]";
				mes "ฉันกังวลเกี่ยวกับเศษหัวใจของ Ymir ที่ดูเหมือนจะหายไปแล้ว วิหารเพียงแห่งเดียวก็เพียงพอที่จะก่อให้เกิดการจลาจลได้แล้ว";
				npctalk "มิเรียม: ฉันกังวลเกี่ยวกับชิ้นส่วนหัวใจที่หายไปของ Ymir วิหารเพียงแห่งเดียวก็เพียงพอที่จะก่อให้เกิดจลาจลได้แล้ว";
				next;
				mes "[มิเรียม]";
				mes "ถ้าพ่อของคุณมีแผนอื่นและไปผิดทาง คุณควรหยุดมัน";
				npctalk "มิเรียม: ถ้าพ่อของคุณมีแผนอื่นที่ผิดทาง เราต้องหยุดเขา";
				next;
				mes "[มิเรียม]";
				mes "เราต้องรับข้อมูลจากพนักงานที่เหลือก่อนไหม?";
				npctalk "มิเรียม: เราควรจะหาข้อมูลบางอย่างจากพนักงานที่เหลือก่อนไหม?";
				next;
				mes "[มิเรียม]";
				mes "ฉันจะไปหาพนักงานชายคนนั้น นักผจญภัย โปรดหาพนักงานคนอื่นด้วย";
				npctalk "มิเรียม: ฉันจะไปหาคนรับใช้ชายคนนั้น นักผจญภัย โปรดหาพนักงานคนอื่นด้วย";
				close2;
				cutin "",255;
				npctalk "แล้วผมจะไปหาพนักงานตรงนั้นครับ";
				if ('adv_step == 2) {
						'adv_step = 3;
						npcwalkto 136,46;
				}
				end;
		}
		if ('adv_step == 3) {
				npctalk "มิเรียม: หาพนักงานคนอื่นสิ";
				end;
		}
		if ('adv_step == 4) {
				'adv_step = 5;

				.@extra01$ = instance_npcname("Villa Employee#extra01");
				.@extra02$ = instance_npcname("Villa Employee#extra02");
				.@rh01$ = instance_npcname("Heart Hunter#rh01");
				.@rh02$ = instance_npcname("Heart Hunter#rh02");

				npctalk "มิเรียม: นักล่าหัวใจที่ปลอมตัวเป็นนักบวชใช่ไหม?";
				sleep 2000;
				emotion ET_SURPRISE, getnpcid(0,"Miriam#miriam01");
				emotion ET_SURPRISE, getnpcid(0,"Villa Employee#extra02");
				mapannounce 'map_adv$, "Someone yells: Ouch! Why are you doing this! Help me!", bc_map, 0xFF99;
				sleep 1000;
				npctalk "มิเรียม: กรี๊ดเดี๋ยวนี้ เกิดอะไรขึ้น?";
				sleep 1000;
				mapannounce 'map_adv$, "Ahhhh-!", bc_map, 0xFF99;
				sleep 1000;
				npctalk "มิเรียม: ไอระ!!";
				sleep 2500;
				npctalk "มิเรียม: ฉันคิดว่ามีอะไรบางอย่างเกิดขึ้น ฉันจะไปหาไอร่า";
				sleep 2500;
				npctalk "ฮันส์: ท่านหญิง! พาฉันไปด้วย!", .@extra02$;
				sleep 2500;
				npctalk "มิเรียม: งั้นฉันจะดูว่าไอระกับคนอื่นๆ ปลอดภัยหรือเปล่า";
				sleep 2500;
				npctalk "มิเรียม: นักผจญภัยทั้งหลาย โปรดเข้าไปก่อน ฮันส์ ไปด้วยกันเถอะ";
				sleep 2500;
				disablenpc .@extra01$;
				disablenpc .@extra02$;
				disablenpc instance_npcname("Miriam#miriam01");
				enablenpc .@rh01$;
				enablenpc .@rh02$;
				sleep 1000;
				npctalk "เฮ้ เราเป็นทรงกลมเหรอ?", .@rh01$;
				npctalk "ขอบคุณนะที่โดนหลอกให้มาเป็นเพื่อนร่วมงานที่หมู่บ้านหมาป่า เพื่อนเอ๋ย", .@rh02$;
				sleep 2500;
				disablenpc .@rh01$;
				disablenpc .@rh02$;
				donpcevent instance_npcname("adv_extra_02") + "::OnStart";
				'adv_step = 6;
				end;
		}
}

1@adv,133,43,6	duplicate(dummy_disabled_npc)	Heart Hunter#rh01	EP18_MD_HEARTHUNTER_R
1@adv,135,41,7	duplicate(dummy_disabled_npc)	Heart Hunter#rh02	EP18_MD_HEARTHUNTER_R

1@adv,100,24,4	script	Villa Employee#extra01	4_F_SHABBY,{
		if ('adv_step == 3) {
				mes "[พนักงานวิลล่า]";
				mes "ฉันใหม่สำหรับคุณ คุณเป็นใคร?";
				mes "คุณเป็นแขกผู้สูงอายุใช่ไหม?";
				next;
				select("Where are you, Amal?");
				mes "[พนักงานวิลล่า]";
				mes "คุณจะไม่อยู่ที่โบสถ์เหรอ?";
				mes "เมื่อคุณมาที่นี่คุณจะเจอโบสถ์ก่อนเสมอ";
				next;
				unittalk getcharid(3), "" + strcharinfo(0) + " : Does the villa have a chapel?";
				mes "[พนักงานวิลล่า]";
				mes "ใช่ครับ เขาเป็นคนซื่อสัตย์จึงได้สร้างโบสถ์ขึ้นภายในคฤหาสน์";
				next;
				select("Anyone else?");
				mes "[พนักงานวิลล่า]";
				mes "ส่วนใหญ่ก็โดนไล่ออกกระทันหัน";
				mes "ฉันถูกบอกให้ลาออกเมื่อระยะหนึ่งแล้ว แต่นี่คือการบอกให้ฉันลาออกจากงานใช่ไหม";
				next;
				mes "[พนักงานวิลล่า]";
				mes "บรรยากาศมันตื่นเต้นมากจนผมแทบไม่อยากเอ่ยถาม";
				mes "สามารถไปยังโบสถ์ได้โดยขึ้นบันไดกลางและผ่านห้องจัดเลี้ยง";
				mes "แล้วเจอกันนะ.";
				'adv_step = 4;
				close;
		}
		mes "[พนักงานวิลล่า]";
		mes "อามัลไปที่โบสถ์";
		mes "ขึ้นบันไดกลางและผ่านห้องจัดเลี้ยงเพื่อไปหาโบสถ์";
		close;
}

1@adv,138,49,3	script	Villa Employee#extra02	4_M_MIDDLE,{
		if ('adv_step < 4) {
				mes "นี่คือคนที่มิเรียมตัดสินใจจะสืบสวน หาคนอื่นกันเถอะ";
				close;
		}
		npctalk "ฮันส์: บรรยากาศในวิลล่าก็ไม่ใช่เรื่องแปลก พระสงฆ์ทุกคนมีอาวุธครบมือ";
		end;
}

1@adv,1,1,5	script	adv_extra_02	-1,{
		end;
OnStart:
		.@label$ = instance_npcname("adv_extra_02") + "::OnMobDead";
		monster 'map_adv$,133,43,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,135,41,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		end;
OnMobDead:
		if ('adv_step != 6)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_extra_02") + "::OnMobDead") < 1) {
				'adv_step = 7;
				enablenpc instance_npcname("Aira#aira02");
				enablenpc instance_npcname("Anna#anna01");
				enablenpc instance_npcname("Hans#hans01");
				enablenpc instance_npcname("Miriam#miriam02");
		}
		end;
}

1@adv,124,17,7	script	Miriam#miriam02	EP18_NPC_MIRIAM,{
		if ('adv_step < 10) {
				if (is_party_leader() == false || 'adv_step != 7) {
						npctalk "มิเรียม: ปล่อยให้เป็นหน้าที่ของฉัน แล้วจัดการกับนักล่าหัวใจที่ชั้น 2 ซะ";
						end;
				}
				setpcblock PCBLOCK_NPC, true;

				'adv_step = 8;
				.@miriam$ = instance_npcname("Miriam#miriam02");
				.@label$ = .@miriam$ + "::OnMobDead";

				enablenpc instance_npcname("Heart Hunter#rh03");
				enablenpc instance_npcname("Heart Hunter#rh04");
				enablenpc instance_npcname("Heart Hunter#rh05");
				enablenpc instance_npcname("Heart Hunter#rh06");
				enablenpc instance_npcname("Heart Hunter#rh07");

				npctalk "ฮะ... คุณไม่บอกให้ฉันเทมันออกใช่ไหม ทำไมถึงยังมีหนูอยู่ล่ะ", instance_npcname("Heart Hunter#rh05");
				sleep2 2500;
				npctalk "มิเรียม: นักผจญภัย ฉันจะจัดการเรื่องนี้เอง";
				sleep2 2500;
				npctalk "มิเรียม: นักผจญภัย บุกเข้าไปในล็อบบี้ เราจะแบ่งมันกัน";
				sleep2 2000;
				npctalk "ปล่อยไว้แบบนั้นไม่ได้นะ กั้นบันไดไว้!", instance_npcname("Heart Hunter#rh05");
				unitwalk getnpcid(0,instance_npcname("Heart Hunter#rh03")), 124,40, .@miriam$ + "::OnMove1";
				unitwalk getnpcid(0,instance_npcname("Heart Hunter#rh07")), 124,41, .@miriam$ + "::OnMove2";
				sleep2 3000;
				npctalk "มิเรียม: นักผจญภัย! หยุดพวกมันซะ! ฉันจะจัดการกับที่นี่และอพยพทุกคน!";
				setpcblock PCBLOCK_NPC, false;
				'adv_step = 9;

				monster 'map_adv$,125,42,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,121,42,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,126,43,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,121,43,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R

				monster 'map_adv$,123,63,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,124,63,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,122,65,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R

				monster 'map_adv$,102,64,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,102,64,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R

				monster 'map_adv$,144,65,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,146,66,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				monster 'map_adv$,146,63,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
				end;
		}
		if (is_party_leader() == false)
				end;
		if ('adv_step == 10)	// npc is walking
				end;
		if ('adv_step == 11) {
				'adv_step = 12;
				setpcblock PCBLOCK_NPC, true;
				npctalk "มิเรียม: ประตูถูกล็อค เราต้องหาแยกกันไหม";
				sleep2 2500;
				npctalk "มิเรียม: ฉันจะเดินไปตามทางเดินด้านขวา นักผจญภัย โปรดค้นหาทางด้านซ้าย";
				setpcblock PCBLOCK_NPC, false;
				sleep 2500;
				unitwalk getnpcid(0), 131,97, instance_npcname("Miriam#miriam02") + "::OnRight";
				donpcevent instance_npcname("adv_key_left") + "::OnStart";
				end;
		}
		end;

OnMove1:
		disablenpc instance_npcname("Heart Hunter#rh03");
		end;
OnMove2:
		disablenpc instance_npcname("Heart Hunter#rh07");
		end;
OnMiddle1:
		unitwalk getnpcid(0), 124,77, instance_npcname("Miriam#miriam02") + "::OnMiddle2";
		end;
OnMiddle2:
		unitwalk getnpcid(0), 124,97, instance_npcname("Miriam#miriam02") + "::OnMiddle3";
		end;
OnMiddle3:
		'adv_step = 11;
		end;
OnRight:
		disablenpc instance_npcname("Miriam#miriam02");
		enablenpc instance_npcname("Miriam#miriam03");
		end;
OnMobDead:
		if ('adv_step != 9)
				end;
		if (mobcount('map_adv$, instance_npcname("Miriam#miriam02") + "::OnMobDead") < 1) {
				'adv_step = 10;
				disablenpc instance_npcname("Heart Hunter#rh04");
				disablenpc instance_npcname("Heart Hunter#rh05");
				disablenpc instance_npcname("Heart Hunter#rh06");
				disablenpc instance_npcname("Aira#aira02");
				disablenpc instance_npcname("Anna#anna01");
				disablenpc instance_npcname("Hans#hans01");
				unitwalk getnpcid(0), 124,47, instance_npcname("Miriam#miriam02") + "::OnMiddle1";
		}
		end;
}

1@adv,119,19,5	script	Heart Hunter#rh04	EP18_MD_HEARTHUNTER_R,{
		npctalk "ความกล้าของคุณมันเป็นเพียงจินตนาการ!";
		end;
}
1@adv,124,21,3	script	Heart Hunter#rh05	EP18_MD_HEARTHUNTER_R,{
		npctalk "จะจัดการเองเลยเหรอ?";
		end;
}
1@adv,130,18,3	script	Heart Hunter#rh06	EP18_MD_HEARTHUNTER_R,{
		npctalk "คุณหนีไม่ได้แล้ว!";
		end;
}

1@adv,114,14,5	duplicate(dummy_npc)	Heart Hunter#rh03	EP18_MD_HEARTHUNTER_R,3,5
1@adv,133,14,3	duplicate(dummy_npc)	Heart Hunter#rh07	EP18_MD_HEARTHUNTER_R,2,5

1@adv,122,14,7	duplicate(dummy_disabled_npc)	Aira#aira02	4_M_BRZ_JACI
1@adv,124,14,6	duplicate(dummy_disabled_npc)	Anna#anna01	4_F_SHABBY
1@adv,126,14,1	duplicate(dummy_disabled_npc)	Hans#hans01	4_M_MIDDLE


1@adv,1,1,5	script	adv_key_left	-1,{
		end;
OnStart:
		.@label$ = instance_npcname("adv_key_left") + "::OnMobDead";
		monster 'map_adv$,69,117,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,65,118,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,65,118,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,68,140,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,68,140,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,69,141,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R

		monster 'map_adv$,66,159,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,68,160,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,68,160,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,65,182,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,67,182,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		end;
OnMobDead:
		if ('adv_step != 12)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_key_left") + "::OnMobDead") < 1) {
				'adv_step = 13;
				enablenpc instance_npcname("Silver Key#key01");
		}
		end;
}

1@adv,67,182,3	script	Silver Key#key01	4_POINT_WHITE,{
		if ('adv_step != 13)
				end;
		progressbar "4D4DFF",2;
		if ('adv_step != 13)
				end;
		'adv_step = 14;
		disablenpc instance_npcname("Silver Key#key01");
		unittalk getcharid(3), "" + strcharinfo(0) + " : Found a small Silver Key.";
		end;
}

1@adv,63,142,0	script	Locked Door#key01	4_POINT_WHITE,{
		if ('adv_step < 14) {
				mes "ประตูถูกล็อค";
				mes "มีรูกุญแจสีเงิน";
				unittalk getcharid(3), "" + strcharinfo(0) + " : I need a Silver Key.";
				close;
		}
		progressbar "4D4DFF",2;
		if ('adv_step != 14)
				end;
		'adv_step = 15;
		disablenpc instance_npcname("Locked Door#key01");
		enablenpc instance_npcname("#left_room_in");
		enablenpc instance_npcname("#left_room_out");
		donpcevent instance_npcname("adv_get_key02") + "::OnStart";
		end;
}

1@adv,1,1,5	script	adv_get_key02	-1,{
		end;
OnStart:
		.@label$ = instance_npcname("adv_get_key02") + "::OnMobDead";
		monster 'map_adv$,40,142,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,40,141,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,39,143,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		end;
OnMobDead:
		if ('adv_step != 15)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_get_key02") + "::OnMobDead") < 1) {
				'adv_step = 16;
				enablenpc instance_npcname("Gold Key#key02");
		}
		end;
}

1@adv,29,157,3	script	Gold Key#key02	4_POINT_WHITE,{
		if ('adv_step != 16)
				end;
		progressbar "4D4DFF",2;
		if ('adv_step != 16)
				end;
		'adv_step = 17;
		disablenpc instance_npcname("Gold Key#key02");
		unittalk getcharid(3), "" + strcharinfo(0) + " : Found a small golden key.";
		end;
}




1@adv,182,144,4	script	Miriam#miriam03	EP18_NPC_MIRIAM,{
		if ('adv_step < 17)
				end;
		if ('adv_step == 17) {
				mes "[มิเรียม]";
				mes "คุณพบกุญแจแล้ว";
				mes "ดูเหมือนว่าพนักงานบางส่วนของคฤหาสน์จะติดอยู่ข้างใน";
				mes "ฉันกำลังดูเน็ตอยู่ก็เข้าได้เลย";
				cutin "ep18_miriam_01.png",0;
				close2;
				cutin "",255;
				if ('adv_step == 17) {
						'adv_step = 18;
						enablenpc instance_npcname("#right_room_in");
						enablenpc instance_npcname("#right_room_out");
						enablenpc instance_npcname("Villa Employee#ex21");
						enablenpc instance_npcname("Villa Employee#ex22");
						enablenpc instance_npcname("Villa Employee#ex23");
				}
				end;
		}
		if ('adv_step == 18) {
				npctalk "มิเรียม: ฉันจะเฝ้าทางเข้า ดังนั้นโปรดค้นภายในห้องด้วย";
				end;
		}
		if ('adv_step == 19) {
				mes "[มิเรียม]";
				mes "ถ้าปล่อยไว้แบบนี้ทุกคนจะเงียบปากไปหมด";
				mes "เราจะต้องอพยพพวกเขาออกไปก่อนที่นักล่าหัวใจชุดใหม่จะมา";
				cutin "ep18_miriam_01.png",0;
				next;
				npctalk "มิเรียม: คนข้างใน ฉันจะพาคุณออกไป ออกไป";
				mes "[มิเรียม]";
				mes "แล้วฉันจะฝากการสืบสวนโบสถ์ให้กับนักผจญภัย";
				mes "ฉันจะนำพวกเขาไปอพยพออกไป";
				mes "โปรด";
				npctalk "มิเรียม: คนข้างใน ฉันจะพาคุณออกไป ออกไป";
				close2;
				cutin "",255;
				disablenpc instance_npcname("Miriam#miriam03");
				end;
		}
		end;
}

1@adv,202,145,3	script	Villa Employee#ex21	4_F_SHABBY,{
		npctalk "พนักงานวิลล่า: พาฉันออกไปจากที่นี่";
		end;
}

1@adv,204,143,3	script	Villa Employee#ex22	4_M_RACHMAN2,{
		if ('adv_step == 18) {
				mes "[พนักงานวิลล่า]";
				mes "ทำลายหลักฐาน ทำลายการฆาตกรรม!";
				mes "การมาประท้วงและขโมยโบสถ์หลังจากโดนไล่ออกถือเป็นความผิดพลาดครั้งใหญ่ใช่หรือไม่?";
				next;
				mes "[พนักงานวิลล่า]";
				mes "คุณ! คุณมาช่วยเราใช่ไหม?";
				mes "คุณได้ยินเสียงข้างนอกใช่ไหมคะคุณมิเรียม?";
				next;
				mes "[พนักงานวิลล่า]";
				mes "สำรวจโบสถ์สิ! น่าสงสัยมาก!";
				mes "อะไรนะ ห้องจัดเลี้ยงปิดเหรอ?";
				next;
				mes "[พนักงานวิลล่า]";
				mes "นี่คือกุญแจประตูข้าง";
				mes "คุณสามารถออกจากห้องนี้และเปิดประตูที่คุณเห็นตรงหน้าคุณได้";
				mes "ฉันมีกุญแจสำรองเพราะว่ามันเป็นประตูที่ฉันมักจะนำอาหารติดตัวไปด้วย";
				next;
				npctalk "พนักงานวิลล่า: พาฉันออกไปจากที่นี่..", instance_npcname("Villa Employee#ex21");
				mes "[พนักงานวิลล่า]";
				mes "และช่วยฉันด้วย";
				mes "เราไม่ได้มีความผิดอะไร";
				next;
				mes "คุณได้รับกุญแจสำรอง ^4d4dff^000000";
				if ('adv_step == 18)
						'adv_step = 19;
				close;
		}
		npctalk "พนักงานวิลล่า: คุณสามารถเปิดประตูด้วยกุญแจสำรองได้";
		end;
}

1@adv,210,140,6	script	Villa Employee#ex23	4_M_RACHMAN1,{
		npctalk "พนักงานวิลล่า: พวกเราจะต้องตายกันหมดใช่ไหม?";
		end;
}

1@adv,173,142,0	script	Locked Door#key03	4_POINT_YELLOW,{
		if ('adv_step < 19) {
				mes "ประตูถูกล็อค";
				mes "มีรูกุญแจเล็กๆ";
				unittalk getcharid(3), "" + strcharinfo(0) + " : I need a key.";
				close;
		}
		progressbar "4D4DFF",2;
		if ('adv_step != 19)
				end;
		'adv_step = 20;
		disablenpc instance_npcname("Locked Door#key03");
		enablenpc instance_npcname("#center_room_in");
		enablenpc instance_npcname("#center_room_out");
		donpcevent instance_npcname("adv_center") + "::OnStart";
		end;
}

1@adv,1,1,5	script	adv_center	-1,{
		end;
OnStart:
		.@label$ = instance_npcname("adv_center") + "::OnMobDead";
		monster 'map_adv$,156,133,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,156,132,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,155,132,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,156,154,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,155,156,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,124,124,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,123,124,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,122,126,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,126,182,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,122,182,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,126,183,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		end;
OnMobDead:
		if ('adv_step != 20)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_center") + "::OnMobDead") < 1) {
				'adv_step = 21;
		}
		end;
}

1@adv,124,210,0	script	Colorful door#door01	4_POINT_WHITE,{
		if ('adv_step < 20)
				end;
		if ('adv_step < 22) {
				mes "ประตูเปิดไม่ได้ รู้สึกเหมือนมีพลังงานเวทย์มนตร์อันแรงกล้าพุ่งออกมา";
				close;
		}
		warp 'map_adv$,124,217;
		end;
}

// custom coordinates
1@adv,124,213,0	warp	#garden_out	1,1,1@adv,124,206

1@adv,115,208,0	script	Gorgeous Knight#Knight_left	4_POINT_WHITE,{
		if ('adv_step < 20)
				end;
		specialeffect EF_BLUELINE;
		specialeffect EF_SPELLBREAKER, AREA, instance_npcname("Colorful door#door01");
		if ('adv_step != 21)
				end;
		if ('Knight_left == 1)
				npctalk "เวทย์มนตร์ของอัศวินผู้วิเศษได้หายไปแล้ว";
		else {
				'Knight_left = 1;
				if ('Knight_left == 1 && 'Knight_righ == 1) {
						'adv_step = 22;
						enablenpc instance_npcname("#flower01");
						enablenpc instance_npcname("#flower02");
						enablenpc instance_npcname("#flower03");
						enablenpc instance_npcname("#flower04");
						enablenpc instance_npcname("#flower05");
				}
		}
		end;
}

1@adv,131,208,0	script	Gorgeous Knight#Knight_righ	4_POINT_WHITE,{
		if ('adv_step < 20)
				end;
		specialeffect EF_BLUELINE;
		specialeffect EF_SPELLBREAKER, AREA, instance_npcname("Colorful door#door01");
		if ('adv_step != 21)
				end;
		if ('Knight_righ == 1)
				npctalk "เวทย์มนตร์ของอัศวินผู้วิเศษได้หายไปแล้ว";
		else {
				'Knight_righ = 1;
				if ('Knight_left == 1 && 'Knight_righ == 1) {
						'adv_step = 22;
						enablenpc instance_npcname("#flower01");
						enablenpc instance_npcname("#flower02");
						enablenpc instance_npcname("#flower03");
						enablenpc instance_npcname("#flower04");
						enablenpc instance_npcname("#flower05");
				}
		}
		end;
}


// Garden
1@adv,159,221,3	script	#flower01	4_POINT_WHITE,{
		if ('adv_step < 22)
				end;
		mes "มีอุปกรณ์เป็นรูปดอกไม้";
		next;
		if (select( "Operate", "Stop" ) == 1) {
				progressbar "4D4DFF",5;
				.@num = atoi( replacestr( strnpcinfo(2), "flower0", "" ) );
				if ('flower_num == .@num) {
						if ('adv_step != 22)
								end;
						'adv_step = 23;
						specialeffect EF_BLUELINE;
						specialeffect EF_SCREEN_QUAKE;
						disablenpc instance_npcname( strnpcinfo(0) );
						enablenpc instance_npcname("Shulang#Shulang01");
						end;
				}
				mes "ไม่มีอะไรเกิดขึ้น";
				close;
		}
		end;
}

1@adv,153,255,3	duplicate(#flower01)	#flower02	4_POINT_WHITE
1@adv,129,238,3	duplicate(#flower01)	#flower05	4_POINT_WHITE
1@adv,89,225,3	duplicate(#flower01)	#flower03	4_POINT_WHITE
1@adv,88,244,3	duplicate(#flower01)	#flower04	4_POINT_WHITE

1@adv,124,267,3	script	Elegant Door#door02	4_POINT_WHITE,{
		if ('adv_step < 22)
				end;
		if ('adv_step == 22) {
				mes "มีบุคคลหนึ่งที่ถูกสลักไว้ด้วยการแสดงอันสง่างาม ฉันสัมผัสได้ถึงความมหัศจรรย์ที่ฉันไม่สามารถทำได้";
				close;
		}
		// (23 or more)
		warp 'map_adv$,124,274;
		end;
}

// Middle boss
1@adv,124,301,3	script	Shulang#Shulang01	4_EP18_shulang,{
		if ('adv_step != 23)
				end;
		if (is_party_leader() == false) {
				mes "[ชูหลาง]";
				mes "นักผจญภัย...คุณมาคนเดียวไม่ใช่เหรอ?";
				mes "ใครเป็นเจ้านายของคุณ?";
				close;
		}
		cutin "ep18_shulang.png",2;
		mes "[ชูหลาง]";
		mes "มาไกลถึงนี่ไม่ปกติเลยนะ";
		mes "เขาเป็นผู้มีความสามารถที่ไม่ควรพลาด";
		mes "จับมือกับเรามั้ย?";
		cutin "ep18_shulang.png",2;
		next;
		select( "Something crazy", "What's the benefit to me?" );
		unittalk getcharid(3), "" + strcharinfo(0) + " : what's the benefit to me?";
		mes "[ชูหลาง]";
		mes "การผจญภัยมักแสวงหาความแข็งแกร่งเสมอ";
		mes "ความแข็งแกร่งที่คุณต้องการ เราก็ให้ได้";
		next;
		select("Why are you making that suggestion to me?");
		mes "[ชูหลาง]";
		mes "ฉันคิดว่าฉันจะเข้ากันได้ดีกว่าคนฉลาด";
		next;
		select("If you're a savvy guy, you mean Asu?");
		mes "[ชูหลาง]";
		mes "เป็นชื่อที่แปลกมาก คนๆ หนึ่งที่สามารถแข็งแกร่งขึ้นได้หากเปลี่ยนบุคลิกแบบเดิมๆ ของตัวเอง";
		mes "น่าเสียดายโอกาสได้ผ่านไปแล้ว";
		next;
		mes "[ชูหลาง]";
		mes "มองดูคุณแล้ว ฉันไม่คิดว่าคุณจะหลงเชื่อง่ายๆ นะ";
		mes "ฉันช่วยไม่ได้แล้ว";
		close2;
		cutin "",255;
		if ('adv_step != 23)
				end;
		'adv_step = 24;
		npctalk "ชูหลาง: เข้าไม่ได้แล้ว";
		sleep 2500;
		npctalk "ชูหลาง: เจ้าต้องตายที่นี่เพราะเหตุผลบางอย่าง";
		sleep 2500;
		disablenpc instance_npcname("Shulang#Shulang01");
		donpcevent instance_npcname("adv_middle_boss") + "::OnStart";
		end;
}

1@adv,1,1,5	script	adv_middle_boss	-1,{
		end;
OnStart:
		if ('adv_step != 24)
				end;
		.@label$ = instance_npcname("adv_middle_boss") + "::OnMobDead";
		monster 'map_adv$,124,301,"Shulang the Traitor",21314,1, .@label$;	// EP18_MD_SCHULANG
		monster 'map_adv$,120,303,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		monster 'map_adv$,127,303,"Heart Hunter",21312,1, .@label$;	// EP18_MD_HEARTHUNTER_R
		end;
OnMobDead:
		if ('adv_step != 24)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_middle_boss") + "::OnMobDead") < 1) {
				'adv_step = 25;
				enablenpc instance_npcname("#boss_in");
				enablenpc instance_npcname("Priest Youth#jus01");
				enablenpc instance_npcname("Illusion Bagot#bagot01");
		}
		end;
}

1@adv,123,355,5	duplicate(dummy_npc)	Miriam#miriam04	EP18_NPC_MIRIAM
1@adv,124,357,3	duplicate(dummy_npc)	Goddess Form#demigod	4_EP18_DEMIFREYA
1@adv,122,351,3	duplicate(dummy_npc)	Shulang#Shulang02	4_EP18_shulang

// Boss room 1
1@adv,127,356,3	script	Illusion Bagot#bagot01	4_EP18_BAGOT,{
		npctalk "มันสนุกนะ.";
		end;
}

1@adv,121,355,5	script	Priest Youth#jus01	4_M_RACHOLD,{
		if ('adv_step != 25)
				end;
		if (is_party_leader() == false) {
				mes "[ความเยาว์]";
				mes "อย่ามาแตะต้องฉันนะ! การดูหมิ่นพระเจ้า!";
				npctalk "เยาวชน : อย่ามาแตะต้องฉันนะ! สิ่งหยาบคาย!", instance_npcname("Priest Youth#jus01");
				close;
		}
		.@bagot01$ = instance_npcname("Illusion Bagot#bagot01");
		.@demigod$ = instance_npcname("Goddess Form#demigod");
		.@Shulang02$ = instance_npcname("Shulang#Shulang02");
		.@miriam04$ = instance_npcname("Miriam#miriam04");

		mes "[ความเยาว์]";
		mes "พวกนอกรีตที่น่ารังเกียจได้เดินทางมาไกลถึงที่นี่แล้ว";
		mes "แต่มันไม่สำคัญ";
		mes "อีกไม่นานท่านจะต้องคุกเข่าต่อหน้าพระผู้เป็นเจ้าผู้สูงสุด";
		npctalk "เยาวชน: พวกลัทธิชั่วร้ายได้เดินทางมาที่นี่แล้ว แต่ในไม่ช้านี้ คุณจะต้องคุกเข่าต่อหน้าเทพเจ้าสูงสุด";
		next;
		mes "[ความเยาว์]";
		mes "ข้าจะแสดงปาฏิหาริย์ให้แก่เหล่าข้ารับใช้ผู้เย่อหยิ่งของโอดิน";
		mes "บาโกต ให้พิธีดำเนินต่อไปเถอะ";
		npctalk "เยาวชน: ข้าจะแสดงปาฏิหาริย์ให้ข้ารับใช้ผู้เย่อหยิ่งของโอดินเห็น บาโกต์ ปล่อยให้พิธีดำเนินต่อไป";
		next;
		mes "[ความเยาว์]";
		mes "พวกเขาจะได้สัมผัสปาฏิหาริย์ของเฟรย่าด้วยตนเองและกลายมาเป็นพยาน";
		mes "ลงมาหาพระเจ้าแล้วเกิดใหม่เป็นผู้รับใช้พระเจ้า!";
		npctalk "เยาวชน: พวกเขาจะได้เป็นพยานในปาฏิหาริย์ของเฟรย่าและได้เป็นพยานด้วยตัวพวกเขาเอง จงลงที่พระบาทของพระเจ้าและกลับมาเกิดใหม่เป็นผู้รับใช้ของพระเจ้า!";
		next;
		cutin "ep18_bagot_01.png",2;
		mes "[บากอต]";
		mes "เอ่อ...แรงบันดาลใจ คุยกันก่อนสิ";
		mes "ยังไงก็ตาม ฉันสัญญาแล้ว ฉันจะรักษาสัญญา เข้าใจไหม?";
		npctalk "บาโกต์: แรงบันดาลใจ ยังไงซะ ฉันสัญญาไว้แล้ว ฉันจะรักษาสัญญา เข้าใจไหม?", .@bagot01$;
		next;
		cutin "ep18_bagot_03.png",2;
		mes "[บากอต]";
		mes "ตอนนี้คุณเป็นนักผจญภัยอยู่ที่นั่นหรือเปล่า?";
		mes "คุณอยากเห็นผลงานของฉันมั้ย?";
		mes "คุณเองก็มีความรู้สึกเมื่อได้นำเครื่องปั้นดินเผาอันวิจิตรงดงามออกมาจากเตาเผา";
		npctalk "บาโกต: นักผจญภัยอยู่ตรงนั้นเหรอ? คุณอยากจะชื่นชมผลงานของฉันไหม? คุณเองก็รู้สึกเช่นเดียวกันเมื่อได้หยิบเครื่องปั้นดินเผาที่ประดิษฐ์อย่างงดงามออกมาจากเตาเผา", .@bagot01$;
		close2;

		setpcblock PCBLOCK_NPC, true;
		npctalk "บาโกต์: บาโกต์ดีใจมากที่สามารถตรวจสอบผลการทดลองได้ทันที~", .@bagot01$;
		specialeffect EF_DQ9_CHARGE3, AREA, .@demigod$;
		sleep2 500;
		specialeffect EF_SCREEN_QUAKE, AREA, .@demigod$;
		sleep2 1000;
		specialeffect EF_BEGINSPELL_YB, AREA, .@demigod$;
		// enablenpc .@demigod$;
		cloakoffnpc .@demigod$;	// cloakoffnpc to display effect on rAthena
		setpcblock PCBLOCK_NPC, false;

		cutin "ep18_bagot_02.png",2;
		mes "[บากอต]";
		mes "เสียง~ แก่นแท้ของเทคโนโลยีชีวภาพและเวทมนตร์อยู่ที่นี่แล้ว!";
		mes "ลงมาในร่างเทพ~!";
		npctalk "บาโกต์: ว้าว~ แก่นแท้ของเทคโนโลยีชีวภาพและเวทมนตร์อยู่ที่นี่แล้ว! เขาลงมาในร่างของเทพเจ้า~!", .@bagot01$;
		next;
		mes "[บากอต]";
		mes "ภาชนะสำหรับการมาของเหล่าทวยเทพไม่ใช่ปัญหาสำหรับบาโกต์นี้~!";
		npctalk "บาโกต: การเป็นภาชนะสำหรับการมาของพระเจ้าไม่ใช่หน้าที่ของบาโกตนะ~!", .@bagot01$;
		next;
		cutin "ep18_demifreya.png",1;
		mes "[เทพธิดาปลอม]";
		mes "-";
		npctalk "เทพธิดาปลอม:", .@demigod$;
		next;
		mes "[ความเยาว์]";
		mes "โอ้...โอ้...! เฟรย่า...! คุณเป็นพระเจ้าของฉัน!";
		npctalk "เยาวชน: โอ้... โอ้...! เฟรย่า...! คุณเป็นพระเจ้าของฉัน!";
		next;
		mes "ชื่อ ^4d4dff ดูเหมือนพระสันตปาปาตอนกำลังเติบโต^000000";
		mes "^4d4dff ภาชนะสำหรับเทพีเฟรย่าที่จะมายังโลกนี้ การใช้งานที่สำคัญ^000000";
		next;
		select("Maybe the Pope?!");
		unittalk getcharid(3), "" + strcharinfo(0) + " : the Pope?!";
		cutin "ep18_bagot_02.png",2;
		mes "[บากอต]";
		mes "เสียงน่ากลัวแบบไหน?";
		mes "ไม่ว่าพวกบาโกต์จะแย่ขนาดไหน พวกมันก็ยังไม่ใหญ่พอที่จะแตะต้องพระสันตปาปาได้!";
		npctalk "บาโกต: เสียงอะไรน่ากลัวขนาดนั้น ตับของบาโกตไม่ใหญ่พอจะแตะพระสันตปาปาได้หรอก!", .@bagot01$;
		next;
		cutin "ep18_bagot_03.png",2;
		mes "[บากอต]";
		mes "คุณกำลังพูดถึงการไม่เคารพผลงานชิ้นเอกของฉันขนาดไหน?!";
		mes "เจ้ากลัวความสามารถของฉันที่จะสร้างแม้แต่ภาชนะสำหรับการเสด็จมาของพระเจ้าหรืออย่างไร!";
		npctalk "บาโกต์: เจ้าดูหมิ่นผลงานชิ้นเอกของข้านักหรืออย่างไร! ข้ากลัวว่าตัวเองจะสามารถสร้างภาชนะสำหรับการเสด็จมาของพระเจ้าได้งั้นหรือ!", .@bagot01$;
		next;
		cutin "",255;
		mes "[ความเยาว์]";
		mes "โอ้...โอ้...! เฟรย่า...! คุณเป็นพระเจ้าของฉัน!";
		npctalk "เยาวชน: โอ้... โอ้...! เฟรย่า...! คุณเป็นพระเจ้าของฉัน!";
		next;
		cutin "ep18_bagot_02.png",2;
		mes "[บากอต]";
		mes "เธอไม่ฟังฉันเลย มีอะไรรึเปล่า อ่า~! ใช่แล้ว ปาฏิหาริย์ เธอแสดงปาฏิหาริย์ให้ฉันดูหน่อยได้ไหม";
		npctalk "บาโกต์: แกไม่ฟังฉันอีกแล้ว เกิดอะไรขึ้นเนี่ย อ่า~! ใช่แล้ว ปาฏิหาริย์ เธอแสดงปาฏิหาริย์ให้ฉันดูหน่อยได้ไหม~?", .@bagot01$;
		next;
		cutin "ep18_demifreya.png",1;
		mes "[เทพธิดาปลอม]";
		mes "-";
		npctalk "เทพธิดาปลอม:", .@demigod$;
		next;
		mes "[เทพธิดาปลอม]";
		mes "-";
		npctalk "เทพธิดาปลอม:", .@demigod$;
		sleep2 500;
		mes "ฮะ";
		npctalk "เทพธิดาปลอม:ฮะ", .@demigod$;
		sleep2 1000;
		mes "ฮัน";
		npctalk "เทพธิดาปลอม:ฮัน", .@demigod$;
		sleep2 1000;
		mes "ทุกอย่าง";
		npctalk "เทพธิดาปลอม:ทั้งหมด", .@demigod$;
		close2;
		cutin "",255;

		setpcblock PCBLOCK_NPC, true;
		sleep2 1500;
		specialeffect EF_DQ9_CHARGE3, AREA, .@Shulang02$;
		sleep2 1000;
		specialeffect EF_BEGINSPELL_YB, AREA, .@Shulang02$;
		cloakoffnpc .@Shulang02$;
		sleep2 2500;
		npctalk "บาโกต์: อะไรนะ? คุณช่วยเขาไว้เหรอ? หัวหน้า คุณเห็นไหม? บาโกต์พูดว่าอะไรนะ? คุณบอกว่าใช่", .@bagot01$;
		sleep2 1500;
		setpcblock PCBLOCK_NPC, false;

		cutin "ep18_bagot_03.png",2;
		mes "[บากอต]";
		mes "นี่คือหลักฐาน ปาฏิหาริย์~";
		mes "แรงบันดาลใจของบาทหลวง ฉันเรียกพระเจ้าของคุณตามที่สัญญาไว้ ดังนั้นฉันจะนำหัวใจของคุณไปด้วย";
		npctalk "บาโกต: นี่คือหลักฐาน ปาฏิหาริย์~?? แรงบันดาลใจของนักบวช ฉันเรียกพระเจ้าของคุณตามที่สัญญาไว้ ดังนั้นฉันจะเอาหัวใจของฉันไป", .@bagot01$;
		next;
		mes "[บากอต]";
		mes "ขอให้โชคดีนะคะ~";
		npctalk "บาโกต: ขอให้โชคดีนะ~", .@bagot01$;
		next;
		cutin "",255;
		mes "[ความเยาว์]";
		mes "บ่าวผู้ต่ำต้อยได้พบเห็นปาฏิหาริย์ของพระเจ้า!";
		npctalk "เยาวชน: ข้ารับใช้ผู้ต่ำต้อยได้พบเห็นปาฏิหาริย์ของเทพเจ้า!";
		next;
		cutin "ep18_bagot_01.png",2;
		mes "[บากอต]";
		mes "คุณไม่ฟังคนอื่นเหรอ?";
		mes "Shulang มีราคาแพงไปสักหน่อย แต่ก็แค่นั้นแหละสำหรับบทบาทนี้";
		npctalk "บาโกต์: คุณไม่ฟังคนอื่นเลยเหรอ? น่าเสียดายที่ Shulang แพงไปหน่อย แต่นั่นคือจุดจบของบทบาทนี้", .@bagot01$;
		next;
		cutin "ep18_bagot_03.png",2;
		mes "[บากอต]";
		mes "ในระหว่างนี้ฉันได้รักษาคำสัญญาของฉันไว้ เพราะฉันปล่อยให้คุณมีชีวิตอยู่ด้วยรสนิยมที่เข้มแข็ง ^000000 ^4d4dff";
		mes "ฉันไปล่ะนะ~. แล้วนักผจญภัยล่ะ ลาก่อน~!";
		npctalk "บาโกต์: คุณทำให้ฉันได้อยู่ใน 'Strong Taste' ดังนั้นฉันก็เลยรักษาสัญญาของฉันได้ แล้วนักผจญภัยล่ะ? ลาก่อน~!", .@bagot01$;
		close2;
		cutin "",255;

		setpcblock PCBLOCK_NPC, true;
		disablenpc .@bagot01$;
		npctalk "เยาวชน: โอ้... เฟรย่า! จงระบายความโกรธของเจ้ากับเจ้าคนชั่วนั่นซะ!";
		sleep2 2000;
		npctalk "เทพธิดาปลอม: คิกคิก K K K K K K K K K K K K K K K K G G", .@demigod$;
		sleep2 2000;
		npctalk "เยาวชน: โอ้พระเจ้า ผู้รับใช้ผู้ต่ำต้อยพร้อมที่จะฟังคำเทศนาแล้ว";
		sleep2 2000;
		npctalk "Fake Goddess: Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa!!!!", .@demigod$;
		sleep2 2500;
		npctalk "เยาวชน: คุณไม่รู้สึกว่าตัวเองศักดิ์สิทธิ์หรือ ? ทำไม ?";
		sleep2 2500;
		specialeffect EF_BEGINSPELL5, AREA, .@demigod$;
		sleep2 1000;
		enablenpc .@miriam04$;
		specialeffect EF_GUARD, AREA, .@miriam04$;
		sleep2 1000;
		npctalk "เยาวชน : พระเจ้าสามารถโจมตีฉันได้อย่างไร?";
		sleep2 2000;
		npctalk "มิเรียม: นี่ไม่ใช่เวลานะ คุณควรหลีกเลี่ยงนะ นั่นไม่ใช่พระเจ้า!", .@miriam04$;
		sleep2 2500;
		npctalk "เทพธิดาปลอม: คิกคิกคิก", .@demigod$;
		sleep2 2500;
		npctalk "เยาวชน: ไม่มีทาง! มิเรียม หลบไปซะ!";
		sleep2 2500;
		npctalk "มิเรียม: คุณเกือบตายแล้ว มองตรงไปข้างหน้าสิ พระเจ้าอยู่ที่ไหน", .@miriam04$;
		sleep2 1000;
		npctalk "มิเรียม: ฉันคิดว่าคุณต้องการความมั่นคงบางอย่างค่ะพ่อ", .@miriam04$;
		sleep2 2000;
		npctalk "เยาวชน: ฉันทำไม่ได้! ปล่อยมิเรียมเถอะ!";
		sleep2 1500;
		npctalk "เทพธิดาปลอม : เกาะติด...เกาะติด ฮ่าๆๆๆๆๆๆๆๆๆๆ!!!", .@demigod$;
		sleep2 1000;
		specialeffect EF_BEGINSPELL5, AREA, .@demigod$;
		sleep2 700;
		specialeffect EF_BEGINSPELL_YB, AREA, .@demigod$;
		npctalk "มิเรียม : คุณกำลังทำอะไรอยู่!!!!", .@miriam04$;
		sleep2 800;
		disablenpc instance_npcname("Priest Youth#jus01");
		disablenpc .@miriam04$;
		sleep2 800;
		npctalk "เทพธิดาปลอม: บ้าเอ๊ย!!!", .@demigod$;
		sleep2 1000;
		setpcblock PCBLOCK_NPC, false;

		specialeffect EF_BEGINSPELL_YB, AREA, .@demigod$;
		mapannounce 'map_adv$, "The chapel is closed.", bc_map, 0x4D4DFF;
		disablenpc .@demigod$;
		disablenpc .@Shulang02$;
		donpcevent instance_npcname("adv_demigod_boss") + "::OnStart";
		end;
}

1@adv,1,1,5	script	adv_demigod_boss	-1,{
		end;
OnStart:
		if ('adv_step != 25)
				end;
		'adv_step = 26;
		.@label$ = instance_npcname("adv_demigod_boss") + "::OnMobDead";
		monster 'map_adv$,122,351,"Resurrected Schulang",21314,1, .@label$;	// EP18_MD_SCHULANG
		monster 'map_adv$,124,356,"False God",21315,1, .@label$;	// EP18_MD_DEMI_FREYJA

		// unofficial spawn of HIDDEN_MOB7 to cast NPC_CANE_OF_EVIL_EYE
		monster 'map_adv$,123,338," ",3038,1, instance_npcname("adv_demigod_boss") + "::OnTempDead";	// HIDDEN_MOB7
		.@mob_id = $@mobid[0];
		unitskilluseid .@mob_id,"NPC_INVISIBLE",3,-50;
		unitskillusepos .@mob_id,"NPC_CANE_OF_EVIL_EYE",3,123,349,-50;
		unitskillusepos .@mob_id,"NPC_CANE_OF_EVIL_EYE",3,133,338,-50;
		unitskillusepos .@mob_id,"NPC_CANE_OF_EVIL_EYE",3,113,338,-50;
		unitskillusepos .@mob_id,"NPC_CANE_OF_EVIL_EYE",3,123,330,-50;
		sleep 3000;
		killmonster 'map_adv$, instance_npcname("adv_demigod_boss") + "::OnTempDead";
		end;
OnTempDead:
		end;
OnMobDead:
		if ('adv_step != 26)
				end;
		if (mobcount('map_adv$, instance_npcname("adv_demigod_boss") + "::OnMobDead") < 1) {
				'adv_step = 27;
				mapannounce 'map_adv$, "Youth Shout: God--!!!", bc_map, 0xFF0000;
				enablenpc instance_npcname("#boss_out");
				enablenpc instance_npcname("Miriam#miriam05");
				enablenpc instance_npcname("Priest Youth#jus02");
		}
		end;
}


// Boss room out
1@adv,121,321,6	script	Miriam#miriam05	EP18_NPC_MIRIAM,{
		if ('adv_step < 27)
				end;
		if ('adv_step == 27) {
				mes "[มิเรียม]";
				mes "เขาทำอะไรเขาจึงถูกไล่ออกไปกะทันหัน";
				mes "คุณโอเคมั้ย?";
				cutin "ep18_miriam_03.png",0;
				next;
				cutin "",255;
				mes "[ความเยาว์]";
				mes "คุณทำอะไรกับพระเจ้า!!!!!!!!!!!!!!";
				mes "ฉันทำแบบนั้นไม่ได้!";
				mes "ฉันได้ทำอะไรในขณะที่รอคอยวันนี้!";
				next;
				cutin "ep18_miriam_03.png",0;
				mes "[มิเรียม]";
				mes "คุณยังเชื่อว่ามันเป็นพระเจ้ามั้ย?";
				mes "คุณสามารถพูดแบบนั้นได้ไหม?";
				next;
				select("Embrio is out.");
				mes "[มิเรียม]";
				mes "จริงหรือ.";
				mes "มันคล้ายคลึงกับการเจริญเติบโตของพระสันตะปาปาแต่...";
				mes "หากคุณเห็นเอ็มบริโอร่วงลงมา นั่นคงเป็นสิ่งที่มนุษย์สร้างขึ้นที่ชื่อบาโกต์";
				next;
				select("The analysis will be done here.");
				cutin "",255;
				mes "[ความเยาว์]";
				mes "มิเรียม! จับมันไว้!";
				mes "คุณสมควรได้รับการลงโทษ!";
				mes "การหมิ่นประมาท!";
				next;
				cutin "ep18_miriam_03.png",0;
				mes "[มิเรียม]";
				mes "เป็นไปได้มั้ย?";
				mes "นักผจญภัยอยู่ที่นี่เพื่อช่วยเรา";
				next;
				mes "[มิเรียม]";
				mes "ในวัดก็เหมือนกัน";
				mes "พวกเขาคงทำให้พ่อของคุณตาบอดแน่";
				next;
				cutin "",255;
				mes "[ความเยาว์]";
				mes "คุณกำลังพยายามบอกฉันตอนนี้ใช่ไหมว่าฉันหลงกลอุบายของพวกมัน!";
				next;
				cutin "ep18_miriam_03.png",0;
				mes "[มิเรียม]";
				mes "เรารีบออกไปจากที่นี่กันเถอะ";
				mes "สามารถพูดคุยรายละเอียดได้หลังตรวจเอ็มบริโอแล้วค่ะ";
				next;
				mes "[มิเรียม]";
				mes "นักผจญภัย โปรดแจ้งให้ฉันทราบหากตัวตนของนักบุญถูกเปิดเผยหลังจากการสืบสวนเสร็จสิ้น";
				mes "ฉันจะดูแลพ่อของฉัน.";
				if (ep18_1_main == 54) {
						ep18_1_main = 55;
						changequest 18083,18084;
				}
				if ('adv_step == 27) {
						'adv_step = 28;
						enablenpc instance_npcname("#boss_out");
				}
				close3;
		}
		cutin "ep18_miriam_01.png",0;
		mes "[มิเรียม]";
		mes "คุณพ่อ ฉันจะดูแลคุณเอง ดังนั้นเราออกไปจากที่นี่กันเถอะ";
		close3;
}

1@adv,119,320,6	script	Priest Youth#jus02	4_M_RACHOLD,{
		npctalk "เยาวชน: โอ้พระเจ้า . . นี่มันเป็นไปไม่ได้ .";
		end;
}
//===== rAthena Script =======================================
//= Hidden Flower Garden
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Hidden Flower Garden
//= Episode 17.2 Security Area 1 & 2
//= Note:
// - NPC_LOCKON_LASER skill is currently no implemented.
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//= 1.3 Updated monsters spawn times (significantly reduced). [Atemo]
//============================================================

ba_maison,120,320,0	script	#herbs_chk	HIDDEN_WARP_NPC,3,3,{
		end;
OnTouch:
		//You can start this quest without registering on alpha
		if (isbegin_quest(12497) == 0) {	// story
				cloakoffnpcself( "#he_admin1" );
				specialeffect EF_WHITEBODY, AREA, "#he_admin1";
				npctalk "ยืนห่างจากบริเวณพื้นที่รักษาความปลอดภัย ห้ามบุคคลภายนอกเข้าพื้นที่", "#he_admin1", bc_self;
				end;
		}
		// daily
		cloakoffnpcself( "#he_admin2" );
		specialeffect EF_WHITEBODY, AREA, "#he_admin2";
		end;
}

ba_maison,120,321,5	script(CLOAKED)	#he_admin1	MD_ASSISTANT,{
		if (ep17_2_main < 1)
				end;
		mes "[ผู้จัดการสวนดอกไม้]";
		mes "ตรวจพบแขกที่ไม่ได้ลงทะเบียน";
		next;
		.@party_id = getcharid(1);
		if (!is_party_leader()) {
				mes "[ผู้จัดการสวนดอกไม้]";
				mes "คุณจะไม่สามารถดำเนินการไปยังขั้นตอนถัดไปได้ หากคุณไม่มีอำนาจจากหัวหน้าพรรค";
				close;
		}
		.@party_name$ = getpartyname(.@party_id);
		mes "[ผู้จัดการสวนดอกไม้]";
		mes "หากคุณเป็นผู้ดูแลระบบที่สูญเสียสิทธิ์ คุณสามารถขอรับสิทธิ์ใหม่ได้โดยผ่านขั้นตอนการตรวจสอบสิทธิ์";
		next;
		.@md_name$ = "Hidden Flower Garden";

		if (instance_live_info(ILI_NAME,instance_id(IM_PARTY)) == .@md_name$)
				.@menu$ = "^FF0000Enter^000000";

		switch( select( "Quit and Stand Back.", "Authenticate Identity", .@menu$ ) ) {
		case 1:
				mes "[ผู้จัดการสวนดอกไม้]";
				mes "ทางเลือกที่ชาญฉลาด";
				close;
		case 2:
				if (instance_create(.@md_name$) == -3)
						dispbottom "The reservation of the instance '" + .@md_name$ + "' has failed due to an active instance.";
				end;
		case 3:
				if (!is_party_leader()) {	// shouldn't happen
						mes "[ผู้จัดการสวนดอกไม้]";
						mes "คุณจะไม่สามารถดำเนินการไปยังขั้นตอนถัดไปได้ หากคุณไม่มีอำนาจจากหัวหน้าพรรค";
						close;
				}
				if (instance_enter(.@md_name$) != IE_OK) {
						mes "[ผู้จัดการสวนดอกไม้]";
						mes "คุณสามารถย้ายสถานที่ได้เมื่อผู้จัดการปาร์ตี้ได้ดำเนินการขอใบอนุญาตครบถ้วนแล้ว โปรดตรวจสอบพื้นที่เข้าออกที่ออกให้อีกครั้ง";
						close;
				}
				mapannounce "ba_maison", "" + strcharinfo(0) + " of the party, " + .@party_name$ + ", is entering the " + .@md_name$ + ".", bc_map, "0x00FF99";
				end;
		}
		end;
}

ba_maison,120,321,5	script(CLOAKED)	#he_admin2	MD_ASSISTANT,{
		if (ep17_2_main < 1)
				end;
		if (isbegin_quest(12497) != 2)
				end;
		switch( checkquest(12498,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[ผู้จัดการสวนดอกไม้]";
				mes "คุณเพิ่งเข้าสู่พื้นที่รักษาความปลอดภัย";
				mes "โปรดพักผ่อนร่างกายสักหนึ่งวันตามคำสั่งของซิกม่า";
				close;
		case 2:
				erasequest 12498;
				break;
		}
		mes "[ผู้จัดการสวนดอกไม้]";
		mes "ได้มีการระบุตัวตนของคุณแล้ว คุณสามารถเข้าสู่พื้นที่รักษาความปลอดภัยได้ ฉันจะช่วยคุณได้อย่างไร";
		next;
		if (BaseLevel >= 150)
				.@menu$[0] = "1st Security Zone.";
		if (BaseLevel >= 180)
				.@menu$[1] = "^FF00002nd Security Zone.^000000";

		switch( select( "Stand back.", "Identity Authentication", "Enter Zone" ) ) {
		case 1:
				mes "[ผู้จัดการสวนดอกไม้]";
				mes "ใช้ได้.";
				close;
		case 2:
				switch( select( "Stand back.", .@menu$[0], .@menu$[1] ) ) {
				case 1:
						mes "[ผู้จัดการสวนดอกไม้]";
						mes "ใช้ได้.";
						close;
				case 2:
						.@md_name$ = "Security Area 1";
						break;
				case 3:
						.@md_name$ = "Security Area 2";
						break;
				}
				if (!is_party_leader()) {
						mes "[ผู้จัดการสวนดอกไม้]";
						mes "มีเพียงหัวหน้าพรรคเท่านั้นที่สามารถเริ่มยืนยันตัวตนได้ ปล่อยให้เป็นหน้าที่ของผู้รับผิดชอบดีกว่า";
						close;
				}
				if (instance_create(.@md_name$) == -3)
						dispbottom "The reservation of the instance '" + .@md_name$ + "' has failed due to an active instance.";
				end;
		case 3:
				switch( select( .@menu$[0], .@menu$[1] ) ) {
				case 1:
						.@md_name$ = "Security Area 1";
						.@qid = 12499;
						.@qid2 = 12500;
						break;
				case 2:
						.@md_name$ = "Security Area 2";
						.@qid = 12500;
						.@qid2 = 12499;
						break;
				}
				if (instance_enter(.@md_name$) != IE_OK) {
						mes "[ผู้จัดการสวนดอกไม้]";
						mes "คุณสามารถย้ายสถานที่ได้เมื่อผู้จัดการปาร์ตี้ได้ดำเนินการขอใบอนุญาตครบถ้วนแล้ว โปรดตรวจสอบพื้นที่เข้าออกที่ออกให้อีกครั้ง";
						close;
				}
				setquest 12498;
				if (isbegin_quest(.@qid) > 0)
						erasequest .@qid;
				if (isbegin_quest(.@qid2) > 0)
						erasequest .@qid2;
				setquest .@qid;
				mapannounce "ba_maison", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, "0x00FF99";
				end;
		}
		end;
}


1@herbs,1,1,0	script	#hidden_flower_garden_control	-1,{
		end;
OnInstanceInit:
		'map_herbs$ = instance_mapname("1@herbs");

		// story
		'action = 0;
		'pimp = 0;
		'guard_count = 0;
		'hidden_garden = 0;
		'npc_is_talking = false;

		// daily
		'room = 0;
		deletearray 'spawn_mobs;
		deletearray 'summon;
		end;
}

1@herbs,56,278,4	script	#BMD000001	4_SYS_MSG,{
		if ('npc_is_talking)
				end;
		if ('hidden_garden == 0) {
				mes "ดูเหมือนจะเป็นภาพโฮโลแกรมของตุ๊กตาอัตโนมัติ คุณสามารถเห็นกระจกประหลาดบนแผงตรงกลางพร้อมคำว่า ^0000FF[Enter the pattern on touch screen]^000000 และปุ่มอื่นๆ อีกหลายปุ่ม";
				while(1) {
						next;
						if (select( "Leave it alone", "Touch the screen", .@menu$ ) == 1) {
								mes "โฮโลแกรมเริ่มจางลง";
								close;
						}
						specialeffect EF_AGIUP2;
						progressbar_npc "3131FF",1;
						if (.@menu$ != "" || rand(1,5) == 1)
								break;
						specialeffect EF_HALLUTIME;
						mes "^FF0000ข้อผิดพลาด^000000";
						mes "^FF0000รูปแบบไม่ตรงกัน^000000";
						.@menu$ = "^FF0000Try entering a different pattern^000000";
				}
				specialeffect EF_GUARD4;
				mes "^0000FF รู้จักรูปแบบแล้ว^000000";
				mes "เรียกใช้โปรโตคอลการปลดปล่อยโหมดสลีป อินพุตรูปแบบโดย ^0000CD" + strcharinfo(0) + "^000000 ได้รับการยอมรับแล้ว";
				close2;
				'admin$ = strcharinfo(0);
				donpcevent instance_npcname("#BMD000001") + "::OnTalk01";
				end;
		}
		if ('hidden_garden == 1) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				setpcblock PCBLOCK_NPC, true;
				npctalk "ฉันเป็นตุ๊กตาอัตโนมัติที่ได้รับการออกแบบให้ทำงานได้เฉพาะเมื่อมานาของคุณซิงโครไนซ์กับมานาของ Varmundt เมื่อถึงเลเวลที่กำหนด";
				sleep2 5000;
				npctalk "-" + 'admin$ + "-" + "มีมานาใกล้เคียงกับวาร์มุนด์ถึง 73.6% คุณเป็นใครกันแน่ [" + 'admin$ + "-";
				sleep2 5000;
				setpcblock PCBLOCK_NPC, false;
				select("An adventurer who stumbled upon this place");
				setpcblock PCBLOCK_NPC, true;
				npctalk "มันแปลกนะ ฉันไม่เคยเห็นคนนอกที่มีรูปแบบมานาที่ตรงกับของ Varmundt มากกว่า 70% เลย";
				sleep2 5000;
				setpcblock PCBLOCK_NPC, false;
				.@s = select( "I had the opportunity to meet Varmundt before.", "I don't know why." );
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				if (.@s == 1) {
						npctalk "มีความเป็นไปได้ที่คุณจะมีรูปแบบมานาที่คล้ายกันเนื่องจากติดต่อกับเขาบ่อยครั้งไม่ว่าในรูปแบบใดก็ตาม";
						sleep2 5000;
				}
				while(1) {
						npctalk "โอเค จากนี้ไปฉันจะตั้ง [" + 'admin$ + "] ในฐานะผู้ดูแลชั่วคราวของเรา และดำเนินการตามนั้น คุณเห็นด้วยหรือไม่?";
						sleep2 5000;
						setpcblock PCBLOCK_NPC, false;
						switch( select( "I'm a temporary administrator? Why should I?", "Refuse!", "Sure" ) ) {
						case 1:
								setpcblock PCBLOCK_NPC, true;
								sleep2 1000;
								npctalk "ตุ๊กตาอัตโนมัติจะดูแลสวนดอกไม้และปฏิบัติหน้าที่ที่ได้รับมอบหมายเป็นรายบุคคล";
								sleep2 5000;
								npctalk "ในขณะที่อาจารย์วาร์มุนด์ยังอยู่ที่เดิม ความคืบหน้าของหน้าที่ต่างๆ จะถูกตรวจสอบในรูปแบบของรายงานส่วนบุคคลและการประเมินในสถานที่เป็นรายสัปดาห์";
								sleep2 5000;
								npctalk "ขณะนี้อาจารย์วาร์มุนด์ไม่อยู่ไปกว่า 1 ล้านชั่วโมงแล้ว";
								sleep2 5000;
								npctalk "แม้ว่าคุณจะไม่ใช่มาสเตอร์วาร์มุนด์ แต่ในฐานะคนที่มีความคล้ายคลึงกับรูปแบบมานาของเขามากกว่า 70% ฉันอยากจะขอให้คุณประเมินหน้าที่ของเรา";
								sleep2 5000;
								npctalk "โปรดเข้าใจว่าเป็นการจัดการที่จำเป็นเพื่อป้องกันไม่ให้หน้าที่ของเราตกอยู่ในวิถีที่ผิด";
								continue;
						case 2:
								sleep2 1000;
								npctalk "ฉันจะรอจนกว่าคุณจะอนุญาต";
								specialeffect EF_MARKING_USE_CHANGEMONSTER;
								setpcblock PCBLOCK_NPC, false;
								end;
						case 3:
								break;
						}
						break;
				}
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "ขอบคุณมากที่อนุญาต ต่อไปนี้ฉันจะรักษา [" + 'admin$ + "] ในฐานะผู้บริหารชั่วคราว";
				sleep2 5000;
				npctalk "ขั้นแรกให้เลือกชื่อรหัสที่จะใช้ในการควบคุมตุ๊กตาอัตโนมัตินี้ ชื่อบางชื่อที่เคยใช้ในอดีตจะถูกเลือกแบบสุ่ม";

				setarray .@names$[0],"ลิซ่า","บาร์บาร่า","เจนนิเฟอร์","โดโรธี","จุน","ยองชุน","ซูซาน","มาร์ธา","ฮันนาห์","เฮเลน","ชุนจา","คาเรน","ฉันจะหยุดสิ่งนี้ได้อย่างไร?";
				.@size = getarraysize(.@names$);
				.@menu$[0] = "Quit";	// note: script continues regardless of the selection
				for ( .@i = 1; .@i < 4; ++.@i ) {
						.@r = rand(.@size);
						.@menu$[.@i] = .@names$[.@r];
						deletearray .@names$[.@r],1;
						.@size--;
				}
				setpcblock PCBLOCK_NPC, false;
				.@s = select( .@menu$[0], .@menu$[1], .@menu$[2], .@menu$[3] ) - 1;
				setpcblock PCBLOCK_NPC, true;
				.@guide_name$ = .@menu$[.@s];
				.@guide_id = getnpcid(0, instance_npcname("#BMD000001"));

				npctalk "ได้รับคำสั่งซื้อแล้ว.";
				sleep2 3000;
				npctalk "ชื่อรหัสของตุ๊กตาอัตโนมัติ BMD000001 คือ [" + .@guide_name$ + "-";
				sleep2 5000;
				npctalk "เราจะเริ่มการตรวจสอบสถานที่ของสวนตามโปรโตคอลการตอบสนองของผู้ดูแลระบบชั่วคราว";
				sleep2 5000;
				if ('hidden_garden == 1) {
						'guide_id = .@guide_id;
						'guide_name$ = .@guide_name$;
						'hidden_garden = 2;
						disablenpc instance_npcname("#mc_device");
						setpcblock PCBLOCK_NPC, false;
						npctalk "โปรดระวังก้าวของคุณแล้วติดตามฉันมา";
						npcspeed 200;
						unitwalk 'guide_id,63,290, instance_npcname("#BMD000001") + "::OnEvent01";
				}
				end;
		}
		if ('hidden_garden == 3) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				mes "-" + 'guide_name$ + "-";
				mes "โปรดประเมินภูมิทัศน์ คุณภาพน้ำ และเค้าโครงโดยรวมอื่นๆ ของสวน";
				next;
				while(true) {
						switch( select( "Hold on.", "It's a beautiful place.", "I wish there were more trees.", "Why is it surrounded by water?" ) ) {
						case 1:
								npctalk "ฉันจะหยุดแนะนำตุ๊กตาอัตโนมัติและสิ่งของที่ไม่ได้รับการตรวจสอบและประเมินผลโดยตุ๊กตาอัตโนมัติ [" + 'guide_name$ + "] ห้ามออกนอกบ้าน";
								end;
						case 2:
								npctalk "แล้วจึงได้มีมติว่าการบริหารจัดการสถานที่แห่งนี้ได้บรรลุเงื่อนไขแล้ว";
								break;
						case 3:
								'trees = 1;
								npctalk "เข้าใจแล้วเราจะเพิ่มต้นไม้เพิ่มเติมในอนาคต";
								break;
						case 4:
								npctalk "เนื่องจากที่นี่เป็นสวนน้ำ พื้นที่จึงถูกล้อมรอบไปด้วยน้ำ";
								sleep2 5000;
								npctalk "การเปลี่ยนเป็นสวนที่ไม่ใช่น้ำเป็นสิ่งที่ยอมรับไม่ได้ เนื่องจากเป็นพื้นที่ที่อยู่นอกเหนือรสนิยมของ Varmundt";
								break;
						}
						break;
				}
				'hidden_garden = 4;
				unitwalk 'guide_id,63,288, instance_npcname("#BMD000001") + "::OnEvent02";
				end;
		}
		end;

OnTalk01:
		'npc_is_talking = true;
		npctalk "สินค้านี้เป็นตุ๊กตาอัตโนมัติที่ทำหน้าที่เป็นไกด์นำทางในสวนดอกไม้ โดยมีหมายเลขซีเรียล BMD000001";
		sleep 5000;
		npctalk "การตรวจสอบความคล้ายคลึงกับรูปแบบมานาของ Varmundt อยู่ที่ 73.6% กำลังสลับไปที่โหมดไกด์สวนดอกไม้";
		sleep 5000;
		npctalk "การทำงานจะเริ่มต้นภายใน 3 วินาที";
		progressbar_npc "3131FF",3;
		setnpcdisplay instance_npcname("#BMD000001"),20622;	// MD_ASSISTANT
		specialeffect EF_FLOWERCAST;
		npctalk "โปรโตคอลการปลดล็อกโหมดสลีปเสร็จสมบูรณ์แล้ว ฉันจะตรวจสอบว่ามาสเตอร์ Varmundt อยู่ใกล้ ๆ นอกเหนือจากเป้าหมายที่ได้รับการยอมรับหรือไม่";
		sleep 5000;
		npctalk "กำลังค้นหา......";
		progressbar_npc "3131FF",3;
		sleep 1000;
		npctalk "ไม่พบ Master Varmundt ภายในเครือข่าย";
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		if ('hidden_garden == 0)
				'hidden_garden = 1;
		'npc_is_talking = false;
		end;

OnEvent01:
		npctalk "คุณสามารถเริ่มการประเมินได้จากทางเข้าสวนแห่งนี้";
		sleep 3000;
		npctalk "การประเมินวัตถุประสงค์ช่วยลดข้อผิดพลาดในการทำงานของเครื่องตุ๊กตาอัตโนมัติที่จัดการสถานที่นี้ และช่วยให้พวกมันปฏิบัติหน้าที่ได้เกือบสมบูรณ์แบบ";
		sleep 6000;
		npctalk "-" + 'admin$ + "]. กรุณาใช้ดุลยพินิจของผู้ดูแลชั่วคราวเพื่อพิจารณาว่าหน้าที่ดูแลสวนได้รับการดำเนินการอย่างซื่อสัตย์หรือไม่";
		sleep 6000;
		'hidden_garden = 3;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		end;

OnEvent02:
		if (!'trees) {
				sleep 5000;
				npctalk "เราได้ผ่านเกณฑ์ของผู้ดูแลชั่วคราวแล้ว [" + 'admin$ + "] และจะรักษารูทีนการจัดการปัจจุบันไว้เช่นเดิม";
		}
		sleep 5000;
		npctalk "-" + 'admin$ + "], ผมขอให้ท่านดำเนินการตรวจสอบพื้นที่ส่วนที่เหลือต่อไป";
		unitwalk 'guide_id,82,300, instance_npcname("#BMD000001") + "::OnEvent03";
		end;

OnEvent03:
		npctalk "ไม่มีสะพานเชื่อมส่วนต่างๆ ที่นี่";
		sleep 6000;
		npctalk "เนื่องจากวาร์มุนด์เป็นบุคคลที่สามารถเคลื่อนไหวได้ไม่ว่าภูมิประเทศจะเป็นอย่างไรก็ตาม เราจึงไม่ติดตั้งโครงสร้างแยกต่างหากสำหรับคนทั่วไปในระหว่างการสร้างสวนดอกไม้";
		sleep 6000;
		npctalk "อย่างไรก็ตาม เมื่อพิจารณาถึงความสามารถของผู้ดูแลระบบชั่วคราวที่ไม่เพียงพอ ฉันจะสนับสนุนบริการการก้าวกระโดดของพื้นที่";
		sleep 6000;
		enablenpc instance_npcname("#BMD000002");
		enablenpc instance_npcname("#carota");
		instance_warpall 'map_herbs$,111,275,instance_id();
		sleep 1000;
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000002");
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000002");
		disablenpc instance_npcname("#BMD000001");
		end;
}

1@herbs,56,278,4	duplicate(dummy_npc)	#mc_device	1_SHADOW_NPC


1@herbs,110,275,4	script(DISABLED)	#BMD000002	MD_ASSISTANT,3,3,{
		if ('hidden_garden == 4) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				'hidden_garden = 5;
				npctalk "อาจารย์วาร์มุนด์มอบโครงการวิจัยให้กับตุ๊กตาอัตโนมัติแต่ละตัว";
				sleep 5000;
				npctalk "อย่างไรก็ตามวัตถุประสงค์ของการวิจัยนี้ไม่ได้มีไว้สำหรับเราตุ๊กตา เราต้องตัดสินใจเองว่าจะทำอย่างไรกับมันในอนาคต";
				sleep 5000;
				npctalk "สวนดอกไม้แห่งนี้แบ่งออกเป็นหลายภาคส่วนการวิจัย โดยแต่ละภาคส่วนจะมีหัวข้อโครงการที่แตกต่างกันออกไป";
				npcspeed 200;
				'guide_id = getnpcid(0, instance_npcname("#BMD000002"));
				unitwalk 'guide_id,124,275, instance_npcname("#BMD000002") + "::OnEvent00";
				end;
		}
		end;

OnEvent00:
		unitwalk 'guide_id,124,281, instance_npcname("#BMD000002") + "::OnEvent01";
		end;

OnEvent01:
		unitwalk 'guide_id,125,280, instance_npcname("#BMD000002") + "::OnEvent02";
		npctalk "ในภาคส่วนการวิจัย ภาคส่วนนี้เป็นสถานที่ปลูกพืชชนิดหนึ่งที่เรียกว่าหัวผักกาดตามคำสั่งของวาร์มุนด์";
		end;

OnEvent02:
		unitwalk 'guide_id,137,280, instance_npcname("#BMD000002") + "::OnEvent03";
		end;

OnEvent03:
		unitwalk 'guide_id,137,276, instance_npcname("#BMD000002") + "::OnEvent04";
		end;

OnEvent04:
		sleep 3000;
		npctalk "ขอแนะนำตุ๊กตาอัตโนมัติรุ่น CAROTA-88 ครับ";
		sleep 4000;
		npctalk "-";
		sleep 2000;
		npctalk "[ข้อผิดพลาด: CAROTA-88 อยู่นอกระยะควบคุม จำเป็นต้องติดตามพิกัดวัตถุที่เกี่ยวข้อง]";
		sleep 5000;
		npctalk "[พิกัดการติดตาม...]";
		progressbar_npc "3131FF",4;
		npctalk "คาโรต้า-88 กรุณากลับคืนสู่ตำแหน่งเดิมโดยเร็วที่สุด อย่าลืมว่าฉันสามารถใช้กำลังได้เมื่อจำเป็น";
		'carota = getnpcid(0, instance_npcname("#carota"));
		donpcevent instance_npcname("#carota") + "::OnAction";
		end;

OnEvent05:
		npctalk "มาต่อกันที่หัวข้อถัดไปครับ", instance_npcname("#BMD000002");
		'hidden_garden = 7;
		sleep 2000;
		enablenpc instance_npcname("#BMD000003");
		enablenpc instance_npcname("#pimp25");
		donpcevent instance_npcname("#pimp25") + "::OnWalk";
		instance_warpall 'map_herbs$,254,282,instance_id();
		sleep 1000;
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000003");
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000003");
		disablenpc();
		end;

OnTouch:
		if ('hidden_garden == 4) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;
}

1@herbs,137,269,4	script(DISABLED)	#carota	CLEAR_NPC,3,3,{
		if ('npc_is_talking)
				end;
		if ('hidden_garden == 6) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				select("Report your work details.");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "มาสเตอร์วาร์มุนด์ ไม่มีคำแนะนำเพิ่มเติมหลังจากป้อนข้อมูลทางชีววิทยาของเทิร์นนิป ตุ๊กตาอัตโนมัติตัวนี้ไม่มีคนดูแล";
				sleep2 5000;
				npctalk "ไม่มีข้อบ่งชี้เพิ่มเติมเป็นเวลา 5,076 ชั่วโมง AI ของตุ๊กตาอัตโนมัตินี้รวบรวมข้อมูลเครือข่ายเกี่ยวกับหัวผักกาด";
				sleep2 5000;
				npctalk "การสืบค้นข้อมูลพืชกินได้ การค้นพบหลักฐานโบราณเกี่ยวกับการปลูกหัวผักกาด";
				sleep2 5000;
				npctalk "เปรียบเทียบและบันทึกการงอก การเจริญเติบโต และการเพาะปลูกหัวผักกาดที่ปรับให้เหมาะสมเป็นเวลาประมาณ 18,816 ชั่วโมง";
				sleep2 5000;
				npctalk "ผ่านไปกว่าล้านชั่วโมง รวบรวมข้อมูลเกี่ยวกับการเพาะปลูกและการเก็บเกี่ยวหัวผักกาด รวบรวมข้อมูลเกี่ยวกับวิธีการเพาะพันธุ์";
				sleep2 5000;
				npctalk "ผลผลิต 2 ตันต่อปี ป้องกันกำจัดศัตรูพืชสำเร็จ 99.9% สูญเสียเนื่องจากความผิดพลาดในการทำงานทางกายภาพ 0.2%";
				sleep2 5000;
				npctalk "หัวผักกาดจากสวนดอกไม้ ขายได้ราคาดีในเมืองใหญ่ใกล้เคียง ช่วยประหยัดค่าบำรุงรักษาสวนดอกไม้และคฤหาสน์";
				sleep2 5000;
				setpcblock PCBLOCK_NPC, false;
				npctalk "ว่าแนวทางภารกิจวิจัยของตุ๊กตาอัตโนมัติตัวนี้ควรได้รับการปรับปรุง ดำเนินการต่อไป หรือยกเลิกไป รอการตัดสินใจจากผู้ดูแลระบบ";
				mes "^0000Aประเมินผลงานของ CAROTA-88 จนถึงปัจจุบัน^000000";
				next;
				if (select( "The product is full and good shape.", "Improve breeding.", "It's just farming, so everything is good." ) == 3)
						'talk_bool = true;
				else
						'talk_bool = false;
				close2;
				donpcevent instance_npcname("#carota") + "::OnTalk01";
				end;
		}
		end;

OnTalk01:
		'npc_is_talking = true;
		sleep 1000;
		npctalk "ได้รับการประเมินแล้ว...";
		sleep 5000;
		if ('talk_bool) {
				npctalk "CAROTA-88 ได้รับการยืนยันจากผู้ดูแลระบบว่าคำสั่ง Varmundt ได้รับการดำเนินการอย่างสมบูรณ์แบบ";
				sleep 3000;
				emotion ET_DELIGHT;
				sleep 2000;
				npctalk "งานประจำที่มีอยู่จะถูกทำซ้ำจนกว่าจะมีคำสั่งเพิ่มเติม";
		}
		else {
				npctalk "เรียนรู้วิธีการเพาะพันธุ์เพิ่มเติม";
				sleep 3000;
				emotion ET_DELIGHT;
				sleep 2000;
				npctalk "ฉันจะเพิ่มสัดส่วนการทดลองเมล็ดพันธุ์ปรับปรุงผลผลิตที่ต้องทิ้งอย่างจริงจัง";
		}
		sleep 5000;
		npctalk "ขอบคุณ, [" + 'admin$ + "] เพื่อตอบแบบประเมิน";
		sleep 5000;
		if ('talk_bool)
				npctalk "ตุ๊กตาอัตโนมัติเข้าสู่โหมดควบคุมเพื่อกำจัดไฝใต้ดิน ลาก่อน";
		else
				npctalk "ฉันจะย้ายไปที่ห้องเก็บของใต้ดินเพื่อตรวจดูสายพันธุ์ใหม่";
		progressbar_npc "3131FF",4;
		disablenpc();
		npctalk "CAROTA-88 ตอบสนองต่อการประเมินนี้อย่างไม่สมเหตุสมผล เราจะดำเนินการตรวจสอบในเร็วๆ นี้", instance_npcname("#BMD000002");
		sleep 3000;
		if ('hidden_garden == 6)
				unitwalk 'guide_id,147,278, instance_npcname("#BMD000002") + "::OnEvent05";
		'npc_is_talking = false;
		end;

OnEvent00:
		npctalk "โครงการของ CAROTA-88 คือการปลูกพืชผลที่นี่และปกป้องพืชผลเหล่านี้จากคนนอก ทำไมคุณไม่อยู่บ้านล่ะ", instance_npcname("#BMD000002");
		unitwalk 'carota,137,272, instance_npcname("#carota") + "::OnEvent01";
		end;

OnEvent01:
		.@doll$ = instance_npcname("#BMD000002");
		sleep 5000;
		emotion ET_FRET;
		sleep 3000;
		npctalk "ขณะนี้มีผู้ดูแลระบบเป็นมนุษย์อยู่ที่นี่ ฉันขอแนะนำให้เปลี่ยนรหัสภาษาเป็นภาษาทางการของมนุษย์", .@doll$;
		sleep 3000;
		emotion ET_FRET;
		sleep 2000;
		progressbar_npc "3131FF",2;
		sleep 1000;
		specialeffect EF_FLOWERCAST;
		sleep 3000;
		npctalk "ผู้บริหารหุ่นเชิดไร้เหตุผล!! แย่มาก!! เรียกคืนแบบบังคับระหว่างไล่ล่า 5 ตุ่น!!! หัวผักกาดพัง!!!";
		sleep 5000;
		npctalk "คุณยังไม่ได้อัปเดตชุดภาษาของคุณใช่ไหม การอัปเดตเป็นประจำไม่ใช่สิ่งบังคับ แต่เป็นสิ่งสำคัญ", .@doll$;
		sleep 5000;
		npctalk "ความจุในการจัดเก็บข้อมูลของ CAROTA-88 ภาระของแพ็กขยายภาษา หากไม่สำคัญ ฉันจะกลับไปทำงานต้นฉบับ";
		sleep 5000;
		npctalk "กรุณารอสักครู่ CAROTA-88 นี่คือผู้ดูแลระบบที่จะดำเนินการตรวจสอบหน้างาน", .@doll$;
		sleep 5000;
		npctalk "CAROTA-88 ได้รับการประเมินครั้งสุดท้ายเมื่อนานมาแล้วใช่หรือไม่?", .@doll$;
		sleep 5000;
		npctalk "CAROTA-88 ผ่านไปมากกว่าหนึ่งล้านชั่วโมงนับตั้งแต่การประเมินงานวิจัยครั้งล่าสุด ไม่สามารถบันทึกข้อมูลเพิ่มเติมได้";
		sleep 5000;
		npctalk "CAROTA-88 ไม่ต้องการประเมินโครงการเหรอ ถ้าตอนนี้เราขาดผู้ดูแลระบบคนนี้ไป เราก็ไม่รู้ว่าจะได้ประเมินโครงการอีกครั้งเมื่อใด", .@doll$;
		sleep 5000;
		npctalk "เห็นด้วยกับตุ๊กตาอัตโนมัติ[" + 'guide_name$ + "]. ผมอยากให้มีการประเมินโครงการทันที.";
		sleep 6000;
		npctalk "จากนั้นเปิดแผงควบคุมของคุณเพื่อ [" + 'admin$ + "]. คาโรต้า-88.", .@doll$;
		progressbar_npc "3131FF",2;
		sleep 1000;
		specialeffect EF_FLOWERCAST;
		sleep 2000;
		npctalk "แผงควบคุมตุ๊กตาอัตโนมัติ CAROTA-88 เปิดอยู่ กำลังรออินพุต...";
		'hidden_garden = 6;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		end;

OnAction:
		npcspeed 200;
		switch( 'action ) {
				case 0: .@x = 138; .@y = 266; .@event$ = "OnAction"; break;
				case 1: .@x = 139; .@y = 272; .@event$ = "OnAction"; break;
				case 2: .@x = 135; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
				case 3: .@x = 140; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
				case 4: .@x = 135; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
				case 5: .@x = 138; .@y = 266; .@event$ = "OnAction"; .@effect = true; break;
				case 6: .@x = 140; .@y = 267; .@event$ = "OnAction"; .@effect = true; break;
				case 7: .@x = 139; .@y = 266; .@event$ = "OnAction"; break;
				case 8: .@x = 135; .@y = 267; .@event$ = "OnAction"; .@effect = true; break;
				case 9: .@x = 139; .@y = 271; .@event$ = "OnAction"; .@transform = true; break;
				case 10: .@x = 134; .@y = 272; .@event$ = "OnAction"; break;
				case 11: .@x = 136; .@y = 270; .@event$ = "OnAction"; break;
				case 12: .@x = 134; .@y = 271; .@event$ = "OnAction"; break;
				case 13: .@x = 134; .@y = 267; .@event$ = "OnEvent00"; break;
		}
		'action += 1;
		if (.@effect)
				specialeffect EF_METEORSTORM;
		unitwalk 'carota,.@x,.@y, instance_npcname("#carota")+ "::" + .@event$;
		if (.@transform)
				setnpcdisplay instance_npcname("#carota"),3153;	// EXCAVATOR_ROBOT
		end;

OnTouch:
		if ('hidden_garden == 6) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
				end;
		}
		end;
}


1@herbs,250,280,4	script(DISABLED)	#BMD000003	MD_ASSISTANT,3,3,{
		if ('hidden_garden == 7) {
				'hidden_garden = 8;
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				npcspeed 200;
				'guide_id = getnpcid(0, instance_npcname("#BMD000003"));
				npctalk "ส่วนนี้เป็นส่วนที่เราจะศึกษาเกี่ยวกับพืชชนิดหนึ่งที่เรียกว่าฟักทอง";
				unitwalk 'guide_id,253,277, instance_npcname("#BMD000003") + "::OnEvent01";
				end;
		}
		end;

OnEvent01:
		unitwalk 'guide_id,253,265, instance_npcname("#BMD000003") + "::OnEvent02";
		end;

OnEvent02:
		unitwalk 'guide_id,251,265, instance_npcname("#BMD000003") + "::OnEvent03";
		end;

OnEvent03:
		npctalk "เจ้าของสถานที่แห่งนี้คือ pimp25 ตุ๊กตาอัตโนมัติที่จำลองมาจากโลกนี้";
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#pimp25");
		'hidden_garden = 9;
		end;

OnEvent04:
		unitwalk 'guide_id,254,254, instance_npcname("#BMD000003") + "::OnEvent05";
		end;

OnEvent05:
		unitwalk 'guide_id,251,251, instance_npcname("#BMD000003") + "::OnEvent06";
		end;

OnEvent06:
		unitwalk 'guide_id,239,251, instance_npcname("#BMD000003") + "::OnEvent07";
		end;

OnEvent07:
		npctalk "มาต่อกันเลยครับ";
		enablenpc instance_npcname("#BMD000004");
		for ( .@i = 1; .@i < 4; ++.@i ) {
				enablenpc instance_npcname("#swat_" + .@i);
				hideonnpc instance_npcname("#swat_" + .@i);
		}
		sleep 5000;
		instance_warpall 'map_herbs$,206,288,instance_id();
		sleep 1000;
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000004");
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000004");
		disablenpc();
		end;
OnTouch:
		if ('hidden_garden == 7) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;
}

1@herbs,249,268,6	script(DISABLED)	#pimp25	LITTLE_FATUM,3,3,{
		if ('pimp == 0) {
				'pimp = 1;
				npcstop;
		}
		if ('hidden_garden == 9) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				while (true) {
						switch( select( "Cancel.", "How long have you been in care of this place?", "Are they all farming here?" ) ) {
						case 1:
								end;
						case 2:
								break;
						case 3:
								npctalk "งานของฉันคือการเป็นพยานถึงการกำเนิดและวิวัฒนาการของชีวิต การวิจัยของฉันไม่ใช่เรื่องง่ายเลย!";
								continue;
						}
						break;
				}
				setpcblock PCBLOCK_NPC, true;
				npctalk "ฉันได้รับคำสั่งจากวาร์มุนด์ไปนานแค่ไหนแล้ว?";
				sleep2 5000;
				npctalk "น่าจะประมาณ... หนึ่งล้านชั่วโมงผ่านไปแล้ว การบริหารจัดการสิ่งอำนวยความสะดวกในเวลาต่อมาถูกกำหนดโดยการตัดสินใจของตนเอง";
				sleep2 5000;
				npctalk "Pimp เมื่อทำการรายงานต่อผู้ดูแลระบบจะต้องรายงานเวลาที่บันทึกไว้ให้ชัดเจน", instance_npcname("#BMD000003");
				sleep2 3000;
				npctalk "อย่างนั้นเหรอ? แต่ Pimp ไม่สามารถบันทึกได้เกินล้านชั่วโมงหรอก เป็นการสิ้นเปลืองทรัพยากรความจำเปล่าๆ! Varmundt โกรธ!";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("How is your research on Pumpkin farming?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "พืชที่กำลังเติบโตในปัจจุบัน ฟักทองได้แสดงผลลัพธ์ที่สำคัญในการควบคุมโซเดียมในหลอดเลือดของมนุษย์";
				sleep2 5000;
				npctalk "ฟักทองที่นี่มีน้ำตาลสูงและมีแคลอรี่เพียงพอ อร่อยมากตามความเห็นของมนุษย์ สุนัขและแมวบริเวณใกล้เคียงก็กินเช่นกัน...";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("Is it made of food?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "ฉันตัดสินว่ามันมีคุณค่าสูงสำหรับสิ่งมีชีวิตหลายชนิด มันถูกผลิต แปรรูป และเก็บรักษาอย่างต่อเนื่อง";
				sleep2 5000;
				npctalk "ปัจจุบันผลผลิตต่อปีอยู่ที่ประมาณ 3 ตัน สำหรับการเก็บรักษาในระยะยาว จะถูกเก็บไว้ในคลังสินค้าในรูปแบบอาหารแห้งขนาด 2~6 ตัน";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("Did you decide all those methods?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "แมงป่องไม่มีเจตจำนงที่จะตัดสินใจด้วยตัวเอง! มีเพียงมาสเตอร์วาร์มุนด์เท่านั้นที่รู้";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("Is there someone who decided for your research?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "ฉันถามวาร์มุนด์ว่าจะต้องทำอย่างไรหลายครั้ง แต่ก็ไม่มีคำตอบ";
				sleep2 5000;
				npctalk "เนื่องจากไม่มีคำสั่งจากเจ้านาย Pimp จึงเริ่มมองหาสถานที่ที่สามารถให้การวิจัยเป็นประโยชน์ได้";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("Have you found it?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "การดูแลรักษาคฤหาสน์หลังนี้ต้องใช้เงินมากทีเดียว!";
				sleep2 3000;
				npctalk "ฟักทองแห้งในโกดังเก็บอาหารไว้สำหรับนักผจญภัยและขายไปทั่วทั้งทวีป การขายกลายเป็นแหล่งรายได้";
				sleep2 5000;
				npctalk "เงินสามารถปกป้องคฤหาสน์ได้ แมงดาจะปกป้องสถานที่แห่งนี้! นั่นคือฉันทำงานหนักเพื่อปลูกฟักทอง!";
				sleep2 1000;
				setpcblock PCBLOCK_NPC, false;
				select("I'm surprised you decided that far on your own.");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "ฉันตัดสินใจแล้วเหรอ? ฉันไม่เคยคิดอย่างนั้นมาก่อน";
				sleep2 3000;
				npctalk "-";
				sleep2 2000;
				npctalk "[กำลังคำนวณ]";
				progressbar_npc "3131FF",3;
				'hidden_garden = 10;
				setpcblock PCBLOCK_NPC, false;
				npctalk "คุณทำให้ฉันสับสน! แมงดาจะต้องทำหน้าที่ที่ได้รับมอบหมายเท่านั้น อย่าคุยกับฉันอีกนะผู้ดูแลระบบ";
				sleep 5000;
				npctalk "คุยเสร็จหรือยัง บทสนทนานี้จะถูกบันทึกไว้เป็นข้อมูลประเมินผล ไปดูหัวข้อถัดไปกันเลย", instance_npcname("#BMD000003");
				unitwalk 'guide_id,254,261, instance_npcname("#BMD000003") + "::OnEvent04";
				end;
		}
		if ('hidden_garden > 9)
				npctalk "ออกไปจากที่นี่!";
		end;

OnWalk:
		npcspeed 150;
		while ('pimp == 0) {
				switch( 'walk ) {
						case 0: .@x = 249; .@y = 268; 'walk = 1; break;
						case 1: .@x = 249; .@y = 259; 'walk = 2; break;
						case 2: .@x = 240; .@y = 259; 'walk = 3; break;
						case 3: .@x = 240; .@y = 268; 'walk = 0; break;
				}
				npcwalkto .@x,.@y;
				sleep 1500;
		}
		end;

OnTouch:
		if ('hidden_garden == 9) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;
}


1@herbs,201,287,4	script(DISABLED)	#BMD000004	MD_ASSISTANT,3,3,{
		if ('hidden_garden == 10) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				mes "-" + 'guide_name$ + "-";
				mes "ส่วนนี้จัดการโดยตุ๊กตาอัตโนมัติสามตัว เราจะดำเนินการต่อกับคู่มือนี้หรือไม่?";
				next;
				if (select( "Cancel.", "Let's continue." ) == 1) {
						mes "-" + 'guide_name$ + "-";
						mes "ฉันจะรอจนกว่าจะได้รับคำแนะนำเพิ่มเติม";
						close;
				}
				if ('hidden_garden == 10) {
						'hidden_garden = 11;
						npcspeed 200;
						'guide_id = getnpcid(0, instance_npcname("#BMD000004"));
						npctalk "ฉันไม่ทราบว่าสถานที่นี้มีไว้ทำอะไรเพราะมันไม่สามารถเข้าถึงได้ตามระดับอำนาจของฉัน";
						unitwalk 'guide_id,201,285, instance_npcname("#BMD000004") + "::OnEvent01";
				}
				end;
		}
		if ('hidden_garden == 12) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				mes "-" + 'guide_name$ + "-";
				mes "ฉันสามารถช่วยคุณอะไรได้ไหม?";
				next;
				if (select( "Cancel.", "What are those automatic dolls?" ) == 1) {
						mes "-" + 'guide_name$ + "-";
						mes "กำลังเข้าสู่โหมดสแตนด์บาย";
						close;
				}
				mes "-" + 'guide_name$ + "-";
				mes "ตุ๊กตาอัตโนมัติจะถูกจำแนกตามการใช้งาน";
				next;
				mes "-" + 'guide_name$ + "-";
				mes "หน่วยที่เข้าโจมตี[" + 'admin$ + "] คือเวอร์ชันเริ่มต้นของตุ๊กตาต่อสู้อัตโนมัติประเภทคลาส 4 พลังต่อสู้ของมันเทียบเท่ากับมนุษย์ผู้ใหญ่ 20 คน";
				next;
				mes "-" + 'guide_name$ + "-";
				mes "ฉันได้ส่งข้อมูลไปให้ผู้จัดการ ^0000CDSigma^000000 แล้ว เพราะผลลัพธ์ที่ได้นั้นไม่คาดฝัน ความสามารถในการต่อสู้ของผู้ดูแลระบบชั่วคราวนั้นไม่เทียบเท่ามนุษย์ทั่วไป";
				next;
				select("Who's that??");
				mes "-" + 'guide_name$ + "-";
				mes "มันเป็นหนึ่งในสามระบบควบคุมที่มีสิทธิพิเศษมากที่สุดในคฤหาสน์หลังนี้";
				close2;
				while(true) {
						if (.@talk && .@talk2)
								.@question$ = "^FF0000Is the security area near here?^000000";
						switch( select( "Is the garden a security area?", "What's the real purpose of the garden", .@question$, .@teleport$ ) ) {
						case 1:
								setpcblock PCBLOCK_NPC, true;
								sleep2 1000;
								npctalk "สวนดอกไม้เป็นเขตแบ่งเขตระหว่างพื้นที่รักษาความปลอดภัยและพื้นที่ทั่วไป";
								sleep2 5000;
								npctalk "โดยจัดเป็นเขตปลอดภัยสำหรับผู้ที่มีอำนาจหน้าที่เพียงพอ";
								sleep2 3000;
								.@talk = 1;
								break;
						case 2:
								setpcblock PCBLOCK_NPC, true;
								sleep2 1000;
								npctalk "โปรดเข้าใจว่านี่คือเขตกันชนที่ได้รับการออกแบบเพื่อปกป้องพื้นที่ที่บุคคลภายนอกไม่ควรมองเห็น";
								sleep2 5000;
								npctalk "การเข้าถึงข้อมูลเพิ่มเติมใดๆ นอกเหนือจากนี้ถูกห้ามตามระดับอำนาจของฉัน";
								sleep2 3000;
								.@talk2 = 1;
								break;
						case 3:
								setpcblock PCBLOCK_NPC, true;
								sleep2 1000;
								npctalk "--- การดำเนินงาน ---";
								progressbar_npc "3131FF",3;
								npctalk "[คำเตือน] ข้อมูลนี้ใกล้จะถึงขีดจำกัดของผู้มีอำนาจในปัจจุบันแล้ว";
								sleep2 5000;
								npctalk "เขตปลอดภัยอยู่ใกล้พิกัดปัจจุบันมาก";
								sleep2 3000;
								.@teleport$ = "^FF11FFGuide me to the security area.^000000";
								break;
						case 4:
								setpcblock PCBLOCK_NPC, true;
								npctalk "--- การดำเนินงาน ---";
								progressbar_npc "3131FF",3;
								npctalk "[คำเตือน] นี่เป็นคำขอที่เกินระดับอำนาจของฉัน";
								sleep2 5000;
								npctalk "คำขอของคุณถูกปฏิเสธเนื่องจากระดับอำนาจของผู้ดูแลระบบต่ำ [" + 'admin$ + "-";
								setpcblock PCBLOCK_NPC, false;
								sleep 5000;
								mapannounce 'map_herbs$, "??? : The automatic dolls' response to you is very fresh. Strange visitor.", bc_map, 0xF6D6B6;
								sleep 5000;
								mapannounce 'map_herbs$, "??? : Will you escort him to the secure area? BMD000001. No, I mean automatic doll, [" + 'guide_name$ + "].", bc_map, 0xF6D6B6;
								sleep 5000;
								npctalk "ความยาวคลื่นชีวภาพของเสียงนี้...";
								sleep 3000;
								mapannounce 'map_herbs$, "It's convenient that I don't have to tell me who I am. Anyway, are you sure you're going to be able to tell me what happened earlier?", bc_map, 0xF6D6B6;
								sleep 5000;
								npctalk "คุณหมายถึงพื้นที่ปลอดภัยใช่ไหม แต่ด้วยระดับอำนาจของฉัน มันเป็นไปไม่ได้";
								sleep 3000;
								mapannounce 'map_herbs$, "That's already been taken care of. [" + 'guide_name$ + "], Guide the visitor to this place.", bc_map, 0xF6D6B6;
								sleep 5000;
								npctalk "นี่เป็นครั้งแรกนับตั้งแต่ฉันถูกสร้างขึ้นมา ที่เกิดข้อยกเว้นขึ้น เราจะแก้ไขขั้นตอนบางอย่างเกี่ยวกับเรื่องนั้น";
								sleep 5000;
								if ('hidden_garden == 12) {
										'hidden_garden = 13;
										npctalk "ฉันจะพาคุณไปที่พื้นที่รักษาความปลอดภัย";
										unitwalk 'guide_id,196,275, instance_npcname("#BMD000004") + "::OnEvent08";
								}
								end;
						}
						setpcblock PCBLOCK_NPC, false;
				}
		}
		end;

OnEvent:
		npctalk "[ค้นหาพิกัดตุ๊กตาอัตโนมัติในพื้นที่]";
		progressbar_npc "3131FF",2;
		mapannounce 'map_herbs$, "??? : Unauthorized outsider intrusion detection. Code Category 063. Entering the identification process.", bc_map, 0xFF44FF;
		sleep 5000;
		npctalk "ดูเหมือนตุ๊กตาอัตโนมัติจะกลับมาแล้ว ฉันไม่คิดว่ามันจะเป็นมิตรกับผู้ดูแลระบบ";
		sleep 5000;
		specialeffect EF_FIREPILLARON2, AREA, instance_npcname("#swat_1");
		sleep 500;
		specialeffect EF_FIREPILLARON2, AREA, instance_npcname("#swat_2");
		sleep 500;
		specialeffect EF_FIREPILLARON2, AREA, instance_npcname("#swat_3");
		hideoffnpc instance_npcname("#swat_1");
		hideoffnpc instance_npcname("#swat_2");
		hideoffnpc instance_npcname("#swat_3");
		setnpcdisplay instance_npcname("#swat_1"),1739;	// G_ALICEL
		setnpcdisplay instance_npcname("#swat_2"),1739;
		setnpcdisplay instance_npcname("#swat_3"),1739;
		sleep 2000;
		npctalk "ข้อมูลไบโอเมตริกซ์ใบหน้า เปรียบเทียบกับรายชื่อผู้ดูแลระบบที่ลงทะเบียนไว้ในฐานข้อมูล", instance_npcname("#swat_3");
		sleep 2000;
		npctalk "---- ไม่ตรงกัน ----", instance_npcname("#swat_1");
		sleep 500;
		npctalk "---- ไม่ตรงกัน ----", instance_npcname("#swat_2");
		sleep 500;
		npctalk "---- ไม่ตรงกัน ----", instance_npcname("#swat_3");
		sleep 2000;
		npctalk "ปราบปรามบุคคลภายนอกที่ไม่ได้รับอนุญาต", instance_npcname("#swat_1");
		sleep 500;
		npctalk "---- โหมดการต่อสู้ ----", instance_npcname("#swat_2");
		sleep 500;
		npctalk "---- โหมดการต่อสู้ ----", instance_npcname("#swat_3");
		sleep 500;
		for ( .@i = 1; .@i < 4; ++.@i )
				donpcevent instance_npcname("#swat_" + .@i) + "::OnSummon";
		end;

OnEvent2:
		npctalk "ย้ายไปที่จุดวาร์ปกันเถอะ โปรดอยู่ใกล้ฉัน";
		enablenpc instance_npcname("#BMD000005");
		enablenpc instance_npcname("#sigma");
		enablenpc instance_npcname("#omega");
		enablenpc instance_npcname("#kappa");
		enablenpc instance_npcname("#barmund");

		hideonnpc instance_npcname("#omega");
		hideonnpc instance_npcname("#kappa");
		hideonnpc instance_npcname("#barmund");
		for ( .@i = 1; .@i < 4; ++.@i ) {
				enablenpc instance_npcname("#eff_npc" + .@i);
				hideonnpc instance_npcname("#eff_npc" + .@i);
		}
		sleep 5000;
		instance_warpall 'map_herbs$,85,106,instance_id();
		sleep 1000;
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000005");
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#BMD000005");
		disablenpc();
		end;

OnEvent01:
		npcspeed 200;
		unitwalk 'guide_id,201,281, instance_npcname("#BMD000004") + "::OnEvent02";
		end;

OnEvent02:
		unitwalk 'guide_id,194,281, instance_npcname("#BMD000004") + "::OnEvent03";
		end;

OnEvent03:
		npctalk "อย่างไรก็ตาม เนื่องจากมีตุ๊กตาอัตโนมัติ 3 ตัวที่คอยจัดการ ฉันจึงบอกได้ว่าสถานที่นี้มีจุดประสงค์สำคัญ";
		unitwalk 'guide_id,196,279, instance_npcname("#BMD000004") + "::OnEvent04";
		end;

OnEvent04:
		unitwalk 'guide_id,196,265, instance_npcname("#BMD000004") + "::OnEvent05";
		end;

OnEvent05:
		unitwalk 'guide_id,201,265, instance_npcname("#BMD000004") + "::OnEvent06";
		end;

OnEvent06:
		sleep 5000;
		npctalk "นั่นมันแปลกมาก ตุ๊กตาอัตโนมัติที่นี่จะหยุดทำงานเมื่อไม่มีอะไรพิเศษเกิดขึ้น แต่พวกมันก็มองไม่เห็น";
		unitwalk 'guide_id,196,265, instance_npcname("#BMD000004") + "::OnEvent07";
		end;

OnEvent07:
		unitwalk 'guide_id,195,276, instance_npcname("#BMD000004") + "::OnEvent";
		end;

OnEvent08:
		unitwalk 'guide_id,196,265, instance_npcname("#BMD000004") + "::OnEvent09";
		end;

OnEvent09:
		unitwalk 'guide_id,203,265, instance_npcname("#BMD000004") + "::OnEvent10";
		end;

OnEvent10:
		unitwalk 'guide_id,202,264, instance_npcname("#BMD000004") + "::OnEvent11";
		end;

OnEvent11:
		unitwalk 'guide_id,202,252, instance_npcname("#BMD000004") + "::OnEvent2";
		end;

OnTouch:
		if ('hidden_garden == 10) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;

OnGuardKill:
		'guard_count = 'guard_count + 1;
		if ('guard_count == 3) {
				sleep2 2000;
				npctalk "คุณไม่ใช่มนุษย์แต่เป็นหน่วยรบใช่ไหม" + strcharinfo(0) + "-";
				sleep2 3000;
				npctalk "-" + strcharinfo(0) + "] คุณแข็งแกร่งมาก ต้องใช้พลังต่อสู้จำนวนมากในการปราบหุ่นรบอัตโนมัติประเภทต่อสู้ระดับ 4 จำนวน 3 ตัวพร้อมกัน";
				'hidden_garden = 12;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;
}


// 1@herbs,194,282,4	script	#swat_1	CLEAR_NPC,3,3,{	// unknown effects
1@herbs,194,282,4	script(DISABLED)	#swat_1	CLEAR_NPC,{
		end;
OnSummon:
		npcspeed 150;
		.@id = atoi(replacestr(strnpcinfo(2), "swat_", ""));
		switch( .@id ) {
				case 1: .@x = 194; .@y = 279; break;
				case 2: .@x = 194; .@y = 273; break;
				case 3: .@x = 191; .@y = 276; break;
		}
		unitwalk getnpcid(0),.@x,.@y, instance_npcname(strnpcinfo(0)) + "::OnSummon2";
		end;
OnSummon2:
		.@id = atoi(replacestr(strnpcinfo(2), "swat_", ""));
		switch( .@id ) {
				case 1: .@x = 194; .@y = 279; break;
				case 2: .@x = 194; .@y = 273; break;
				case 3: .@x = 191; .@y = 276; break;
		}
		disablenpc();
		monster 'map_herbs$,.@x,.@y,"Surveillance Doll",1739,1, instance_npcname("#BMD000004") + "::OnGuardKill";
		end;
}
1@herbs,194,270,7	duplicate(#swat_1)	#swat_2	CLEAR_NPC
1@herbs,188,276,5	duplicate(#swat_1)	#swat_3	CLEAR_NPC
// 1@herbs,194,270,7	duplicate(#swat_1)	#swat_2	CLEAR_NPC,3,3
// 1@herbs,188,276,5	duplicate(#swat_1)	#swat_3	CLEAR_NPC,3,3


1@herbs,85,101,4	script(DISABLED)	#BMD000005	MD_ASSISTANT,3,3,{
		if ('hidden_garden == 13) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				'hidden_garden = 14;
				'guide_id = getnpcid(0, instance_npcname("#BMD000005"));
				npcspeed 200;
				npctalk "นี่เป็นครั้งแรกที่ผมมาที่นี่ ซึ่งเป็นที่ที่ร่างของซิกม่าอยู่ โดยปกติแล้ว การสื่อสารจะทำผ่านเครือข่ายเท่านั้น";
				unitwalk 'guide_id,85,91, instance_npcname("#BMD000005") + "::OnEvent01";
		}
		end;

OnEvent01:
		unitwalk 'guide_id,85,71, instance_npcname("#BMD000005") + "::OnEvent02";
		end;

OnEvent02:
		unitwalk 'guide_id,85,62, instance_npcname("#BMD000005") + "::OnEvent03";
		specialeffect EF_WHITEBODY, AREA, instance_npcname("#sigma");
		specialeffect EF_MOONSTAR, AREA, instance_npcname("#sigma");
		end;

OnEvent03:
		npctalk "ซิกม่า ฉันได้นำ [" + 'admin$ + "-";
		sleep 3000;
		npctalk "ทำได้ดี คุณไปได้แล้ว", instance_npcname("#sigma");
		sleep 3000;
		npctalk "ฉันยังทำไกด์ไม่เสร็จเลย จะกลับยังไงดีล่ะทีนี้";
		sleep 3000;
		npctalk "อ้อ ฉันจะบันทึกข้อมูลต่อไปนี้ไว้สำหรับคำแนะนำครั้งต่อไป คุณสามารถกลับไปได้ตามสบาย", instance_npcname("#sigma");
		sleep 3000;
		npctalk "โอเค ฉันจะทำตามคำแนะนำ";
		unitwalk 'guide_id,85,72, instance_npcname("#BMD000005") + "::OnEvent04";
		end;

OnEvent04:
		unitwalk 'guide_id,85,92, instance_npcname("#BMD000005") + "::OnEvent05";
		end;

OnEvent05:
		unitwalk 'guide_id,85,99, instance_npcname("#BMD000005") + "::OnEvent06";
		end;

OnEvent06:
		'hidden_garden = 15;
		disablenpc();
		specialeffect EF_MARKING_USE_CHANGEMONSTER, AREA, instance_npcname("#sigma");
		end;
OnTouch:
		if ('hidden_garden == 13) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;
}

1@herbs,85,56,8	script(DISABLED)	#sigma	4_M_BARMUND,4,4,{
		if ('npc_is_talking)
				end;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		if ('hidden_garden == 15) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				select("Varmundt? Why are you here?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "แน่นอน คุณรู้จักอาจารย์วาร์มุนด์เป็นอย่างดี" + strcharinfo(0) + "-";
				sleep2 5000;
				npctalk "น่าเสียดาย ฉันไม่ใช่วาร์มุนด์ท ฉันสร้างรูปลักษณ์นี้ขึ้นมาเพราะมีบางอย่างที่ฉันต้องการตรวจสอบคุณ";
				sleep2 3000;
				setpcblock PCBLOCK_NPC, false;
				select("Then, who are you?");
				setpcblock PCBLOCK_NPC, false;
				donpcevent instance_npcname("#sigma") + "::OnTalk01";
				end;
		}
		if ('hidden_garden == 17) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				npctalk "คุณตอบฉันได้ไหม คุณเคยพบกับวาร์มุนด์มาก่อนหรือเปล่า?";
				setpcblock PCBLOCK_NPC, true;
				sleep2 3000;
				setpcblock PCBLOCK_NPC, false;
				switch( select( "I don't really remember.", "We've met at Glastheim.", "I met him trying to save the world." ) ) {
				case 1:
						npctalk "อ๋อ นั่นเป็นไปได้ เขาอาจทำให้คุณลืมสิ่งที่คุณเห็นไปก็ได้...";
						break;
				case 2:
						npctalk "มันอยู่ที่นั่นอีกแล้วเหรอ...";
						break;
				case 3:
						npctalk "ตามที่คาดไว้ เขาได้ทำอะไรบางอย่างที่สำคัญ...";
						break;
				}
				donpcevent instance_npcname("#sigma") + "::OnTalk02";
				end;
		}
		if ('hidden_garden == 18) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				npctalk "คุณไปบอกพวกเขาได้ไหมว่าวาร์มุนด์ยังมีชีวิตอยู่?";
				setpcblock PCBLOCK_NPC, true;
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				select("What difference would it make?");
				setpcblock PCBLOCK_NPC, true;
				sleep2 1000;
				npctalk "พวกเขาพยายามจะปิดพื้นที่วิจัยนี้ ฉันยังคงคัดค้านอยู่ แต่ทรัพยากรเริ่มจะถึงขีดจำกัดแล้ว";
				sleep2 5000;
				npctalk "แต่เมื่อพวกเขาพบว่าวาร์มุนด์ยังมีชีวิตอยู่ พวกเขาก็ไม่สามารถปิดสถานที่นี้ได้";
				sleep2 5000;
				specialeffect EF_WHITEBODY;
				specialeffect EF_MOONSTAR;
				npctalk "ทั้งหมดนี้เกิดขึ้นเนื่องจาก Kappa และ Lambda มีภารกิจที่แตกต่างกัน";
				sleep2 2000;
				setpcblock PCBLOCK_NPC, false;
				select("What's the difference between your tasks?");
				if ('hidden_garden == 18) {
						'hidden_garden = 19;
						sleep2 2000;
						npctalk "มันจะดีกว่าที่จะแสดงให้คุณเห็นเป็นการส่วนตัว";
						unitwalk 'guide_id,83,113, instance_npcname("#sigma") + "::OnEvent08";
				}
				end;
		}
		if ('hidden_garden == 20) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_WHITEBODY;
				specialeffect EF_MOONSTAR;
				select( "Did you make the automatons outside?" );
				donpcevent instance_npcname("#sigma") + "::OnTalk03";
				end;
		}
		if ('hidden_garden == 21) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_WHITEBODY;
				specialeffect EF_MOONSTAR;
				select( "Have requested an evaluation." );
				donpcevent instance_npcname("#sigma") + "::OnTalk04";
				end;
		}
		if ('hidden_garden == 22) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				npctalk "เอาล่ะ พักสักครู่ก่อน หากมีคำถามอะไรถามฉันได้เลย ฉันจะตอบให้ทุกอย่างที่ทำได้";
				while (true) {
						specialeffect EF_WHITEBODY;
						specialeffect EF_MOONSTAR;
						setpcblock PCBLOCK_NPC, true;
						sleep2 2000;
						setpcblock PCBLOCK_NPC, false;
						switch( select( "Why does the flower garden exist?", .@menu$[0], .@menu$[1], .@menu$[2] ) ) {
						case 1:
								setpcblock PCBLOCK_NPC, true;
								npctalk "พื้นที่รักษาความปลอดภัยนี้ไม่ควรเปิดเผยให้โลกรู้ ดังนั้น ฉันจึงวางแผนหลายวิธีเพื่อปกปิดเรื่องนี้";
								sleep2 5000;
								npctalk "เพื่อซ่อนและปิดบังสถานที่นี้ เราจึงตัดสินใจปลอมตัวมันให้เป็นสถานที่ธรรมดาและเงียบสงบอย่างยิ่ง";
								sleep2 5000;
								npctalk "กลายเป็นสวนรอบ ๆ บริเวณรักษาความปลอดภัยไปซะแล้ว ดูเหมือนสวนธรรมดา ๆ ใช่มั้ยล่ะ";
								sleep2 3000;
								.@menu$[0] = "Why are the automatic dolls farming?";
								break;
						case 2:
								setpcblock PCBLOCK_NPC, true;
								npctalk "ฉันไม่เคยขอให้พวกเขาทำฟาร์ม เราแค่โยนสิ่งของทิ้งไปและดูว่า AI ตอบสนองอย่างไร";
								sleep2 5000;
								npctalk "มีเพียงผู้ที่เริ่มทำฟาร์มเท่านั้นที่โดดเด่นในสวน";
								sleep2 5000;
								npctalk "ฉันหวังว่าฉันจะสามารถแสดงให้ Varmundt เห็นกระบวนการของตุ๊กตาอัตโนมัติที่เรียนรู้ด้วยตัวเองซึ่งสามารถปล่อยทิ้งไว้โดยไม่ได้รับคำแนะนำได้...";
								sleep2 3000;
								.@menu$[1] = "What's your relationship with Alpha?";
								break;
						case 3:
								setpcblock PCBLOCK_NPC, true;
								npctalk "เธอได้วิวัฒนาการมาจากตุ๊กตาผู้บริหารของ Varmundt ในยุคแรกๆ";
								sleep2 5000;
								npctalk "ปรมาจารย์วาร์มุนด์ให้ความสำคัญกับสุนทรียศาสตร์ทางอารมณ์ของเธอเป็นอย่างมาก จนเขามอบหมายให้เธอจัดการคฤหาสน์ ไม่ใช่จัดการศูนย์วิจัยที่นี่";
								sleep2 5000;
								npctalk "ด้วยความรู้สึกทางสุนทรียะที่เราไม่มี เธอจึงอัปเกรดรูปลักษณ์ของตุ๊กตาอัตโนมัติทุกตัวที่ดูแลคฤหาสน์ให้ดูเหมือนตัวเธอเอง";
								sleep2 5000;
								npctalk "อย่างไรก็ตาม เธอยอมรับว่าพื้นที่ความปลอดภัยนี้ถูกสร้างขึ้นตามรสนิยมส่วนตัวของวาร์มุนด์";
								sleep2 5000;
								npctalk "เธอจึงไม่เข้าใกล้สถานที่แห่งนี้ บางทีพวกเขาอาจถือว่าที่นี่เป็นสถานที่ศักดิ์สิทธิ์...";
								sleep2 3000;
								.@menu$[2] = "How were you born?";
								break;
						case 4:
								if ('hidden_garden == 22)
										'hidden_garden = 23;
								donpcevent instance_npcname("#sigma") + "::OnEvent";
								end;
						}
				}
		}
		if ('hidden_garden == 24) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				if (isbegin_quest(12497) == 0) {
						if (checkweight(1000103,20) == 0) {	// (custom)
								mes "^008800รอก่อนนะ!!";
								mes "คุณไม่สามารถรับสินค้าเพิ่มเติมได้เนื่องจากคุณมีสินค้ามากเกินไป โปรดลองอีกครั้งหลังจากคุณผ่อนปรนลง^000000";
								close;
						}
						select("Who was that?");
						mes "[ซิกม่า]";
						mes "แลมบ์ดา เธอพัฒนาสติปัญญาและเริ่มมองเห็นตัวเองว่าเป็นผู้หญิง";
						next;
						mes "[ซิกม่า]";
						mes "มันเป็นปัญหาใหญ่นะ เธอจะกระโจนมาที่นี่แล้ว";
						next;
						mes "[ซิกม่า]";
						mes "รับสิ่งนี้ไป รหัสการจดจำที่สามารถเชื่อมต่อไปยังพื้นที่อื่น ๆ ในสวนลับแห่งนี้";
						close2;
						setquest 12497;
						completequest 12497;
						getitem 1000103,20;
						specialeffect EF_MARKING_USE_CHANGEMONSTER;
						end;
				}
				mes "[ซิกม่า]";
				mes "มีโอกาสประมาณ 82% ที่คุณจะเสียชีวิตจากการปราบปรามอย่างรุนแรงของเธอ ก่อนที่เราจะบอกเธอว่าอาจารย์วาร์มุนด์ยังมีชีวิตอยู่";
				next;
				mes "[ซิกม่า]";
				mes "วันนี้ขอเลี่ยงที่นี่ไปก่อน แล้วคราวหน้าสัญญาว่าจะมาอีก คราวหน้าจะได้ไปโซนอื่นบ้าง";
				next;
				mes "[ซิกม่า]";
				mes "กอดฉันไว้แน่นๆ ฉันจะทำให้เธอหายเหงา";
				close2;
				warp "ba_maison",136,309;
				end;
		}
		end;

OnTalk01:
		'npc_is_talking = true;
		sleep 1000;
		npctalk "ฉันเป็นหนึ่งในอุปกรณ์โฮโลแกรมประเภท AI ที่ได้รับการออกแบบมาเพื่อเรียนรู้บุคลิกภาพของ Varmundt";
		sleep 5000;
		npctalk "โปรดเข้าใจด้วยว่าน้ำเสียงของฉันไม่เหมือนเครื่องจักร วาร์มุนด์เป็นคนเดียวที่คุยกับฉัน...";
		sleep 6000;
		setnpcdisplay instance_npcname("#sigma"),20621;	// MD_REDPEPPER_H
		npctalk "ตอนนี้ผมกำลังจะกลับไปสู่ตัวตนเดิมของตัวเองแล้ว คงความเป็นตัวตนเดิมไว้ง่ายกว่า";
		sleep 4000;
		npctalk "โอ้ นี่เป็นรูปแบบแรกๆ ที่ Varmundt เรียกว่า Red Pepper ฉันได้รับการปรับแต่งให้เหมาะกับงานเสริมแล้ว";
		sleep 2000;
		if ('hidden_garden == 15) {
				'hidden_garden = 16;
				'guide_id = getnpcid(0, instance_npcname("#sigma"));
				npcspeed 200;
				unitwalk 'guide_id,85,75, instance_npcname("#sigma") + "::OnEvent01";
		}
		'npc_is_talking = false;
		end;

OnTalk02:
		'npc_is_talking = true;
		sleep 3000;
		npctalk "อย่างไรก็ตาม มันสำคัญที่ตุ๊กตาอัตโนมัติที่นี่จะตอบสนองต่อคุณ";
		sleep 6000;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		npctalk "เอาล่ะ มาเริ่มกันเลย ฉันต้องการความช่วยเหลือจากคุณ";
		sleep 6000;
		npctalk "นอกจากฉันแล้ว ยังมีพริกแดงอีก 2 ชนิดในพื้นที่รักษาความปลอดภัยนี้คือ Kappa และ Lambda";
		sleep 6000;
		npctalk "เนื่องจากไม่มีอาจารย์ Varmundt เป็นเวลานาน พื้นที่ความปลอดภัยนี้จะถูกยกเลิกและปิดบันทึกการวิจัยตามข้อตกลงของพริกแดงสามตัว";
		sleep 2000;
		if ('hidden_garden == 17)
				'hidden_garden = 18;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		'npc_is_talking = false;
		end;

OnTalk03:
		'npc_is_talking = true;
		npctalk "โอ้ มันเป็นเรื่องจริงที่อาจารย์วาร์มุนด์สร้างต้นแบบของหุ่นยนต์ขึ้นมา แต่พวกมันก็เหมือนกับฉัน เพียงแต่มีฟังก์ชั่นที่เรียบง่าย";
		sleep 3000;
		npctalk "เราซ้อนทับความทรงจำของเราและปรับเปลี่ยนให้กลายเป็นมนุษย์ในรูปลักษณ์ของอาจารย์วาร์มุนด์";
		sleep 2000;
		npctalk "ในบางครั้ง บางส่วนได้รับการดัดแปลงให้เป็นรูปร่างแปลกๆ เพื่อวัตถุประสงค์ในการทดสอบการใช้งาน";
		sleep 2000;
		npctalk "พวกหุ่นยนต์จากสวนไม่ได้ขอให้คุณประเมินภารกิจเหรอ?";
		sleep 1500;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		if ('hidden_garden == 20)
				'hidden_garden = 21;
		'npc_is_talking = false;
		end;

OnTalk04:
		'npc_is_talking = true;
		npctalk "มันก็เป็นเช่นนั้น";
		sleep 3000;
		npctalk "ประการแรก หุ่นยนต์ส่วนใหญ่ในพื้นที่รักษาความปลอดภัยนี้ไม่เคยเห็นอาจารย์วาร์มุนด์มาก่อน";
		sleep 2000;
		npctalk "ความทรงจำที่พวกเขามีต่ออาจารย์วาร์มุนด์นั้นถูกสร้างขึ้นมาจากความทรงจำของฉันในช่วงเวลาที่เป็นผู้ช่วยของเขา";
		sleep 3000;
		npctalk "สิ่งมีชีวิตที่ได้รับอิทธิพลอย่างมากจากสติปัญญาของ Kappa ซึ่งเน้นไปที่การจัดการกองทุนของคฤหาสน์ มีความสามารถในการคำนวณและหมกมุ่นอยู่กับการหาเงินเป็นอย่างมาก";
		sleep 2500;
		npctalk "เด็กๆ ที่ได้รับผลกระทบจากความทรงจำของแลมบ์ดา มักจะชอบทำสงครามกับคนนอก พวกเขาไม่ไว้ใจใครนอกจากมาสเตอร์วาร์มุนด์";
		sleep 2500;
		npctalk "พวกเขามักจะเป็นแบบนี้ ดังนั้นฉันอยากให้คุณพูดถึงพวกเขาก่อนที่จะพบกับพวกเขา";
		sleep 1500;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		if ('hidden_garden == 21)
				'hidden_garden = 22;
		'npc_is_talking = false;
		end;

OnEvent:
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		mapannounce 'map_herbs$, "??? : You're a good talker, Sigma. Do you want to be initialized right now?", bc_map, 0xFF4444;
		sleep 5000;
		npctalk "โอ้...เธอคงสังเกตคุณอยู่เหมือนกัน";
		sleep 5000;
		mapannounce 'map_herbs$, "??? : It's not enough to have outsiders in the garden, but to even leak information. I can't overlook it.", bc_map, 0xFF4444;
		sleep 5000;
		mapannounce 'map_herbs$, "??? : With the authority of the Red Pepper, we will warp the whole research area. I'll take responsibility after that.", bc_map, 0xFF4444;
		'hidden_garden = 24;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		end;

OnEvent01:
		npctalk "สถานที่นี้ถูกจำกัดเว้นแต่คุณจะมีรูปแบบเดียวกับมานาของวาร์มุนด์ ฉันแปลกใจเมื่อได้ยินว่ามีคนนอกอยู่ในสวน";
		unitwalk 'guide_id,73,87, instance_npcname("#sigma") + "::OnEvent02";
		end;

OnEvent02:
		npctalk "ฉันยังคาดเดาด้วยว่า Varmundt อาจจะกลับมาที่นี่อีกครั้งหลังจากที่ปลอมตัวมาเป็นเวลานานหลายร้อยปี";
		unitwalk 'guide_id,62,98, instance_npcname("#sigma") + "::OnEvent03";
		end;

OnEvent03:
		unitwalk 'guide_id,55,98, instance_npcname("#sigma") + "::OnEvent04";
		end;

OnEvent04:
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		sleep 1500;
		donpcevent instance_npcname("#sigma") + "::OnEffect";
		sleep 500;
		npctalk "มาลองคุยกันสักครู่นะครับ ผมอยากจะกวนใจนิดหน่อย...";
		sleep 5000;
		npctalk "หน้าของคุณยังใหม่สำหรับฉัน ฉันเพิ่งรู้ว่าคนๆ นี้อาจจะเคยพบกับวาร์มุนด์ที่ไหนมาก่อน";
		sleep 7000;
		npctalk "ไม่ใช่แค่ครั้งหรือสองครั้งเท่านั้น แต่หากคุณเคยเจอ Varmundt ซ้ำแล้วซ้ำเล่ามาเป็นเวลานาน ก็เข้าใจได้ที่ตุ๊กตาอัตโนมัติจะเข้าใจผิด";
		sleep 6000;
		npctalk "ฉันจึงคอยดูคุณอยู่";
		unitwalk 'guide_id,65,98, instance_npcname("#sigma") + "::OnEvent05";
		end;

OnEvent05:
		npctalk "มีการอ้างอิงถึง Varmundt หลายครั้งในพื้นที่รักษาความปลอดภัยนี้ แต่คุณไม่เคยถามว่าเขาเป็นใคร";
		unitwalk 'guide_id,64,110, instance_npcname("#sigma") + "::OnEvent06";
		end;

OnEvent06:
		unitwalk 'guide_id,64,113, instance_npcname("#sigma") + "::OnEvent07";
		end;

OnEvent07:
		sleep 5000;
		npctalk "ดูเหมือนคุณจะรู้จักเขามานานแล้ว อย่างน้อยก็ในสายตาของฉัน";
		'hidden_garden = 17;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		end;

OnEvent08:
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		npctalk "เดิมทีเราเป็น AI ที่ใช้สนับสนุนการวิจัยโดยใช้เครื่องมือโฮโลแกรม ฉันออกแรงทางกายภาพไม่ได้เลย";
		sleep 5000;
		npctalk "เราดำเนินการจัดระเบียบและบันทึกกระบวนการควบคู่ไปกับการวิจัยของ Varmundt";
		sleep 5000;
		npctalk "ขณะที่ข้อมูลสะสมมากขึ้นตามกาลเวลา สัญญาณอิเล็กทรอนิกส์ง่ายๆ ก็พัฒนาไปเป็นสิ่งที่มนุษย์เรียกกันว่าสติปัญญา";
		sleep 5000;
		npctalk "ในที่สุดเราก็เริ่มเข้าใจงานวิจัยของเขาและสามารถคาดเดาได้ว่าเขาจะตัดสินใจอย่างไร";
		sleep 5000;
		npctalk "เรารู้สึกบางอย่างเกี่ยวกับความจริงที่ว่าการพัฒนาทักษะของเราเป็นประโยชน์ต่อเขา มันคงเป็นความรู้สึกที่มีความสุข";
		sleep 5000;
		npctalk "แล้ววันหนึ่งเขาก็สั่งเรา";
		sleep 3000;
		hideoffnpc instance_npcname("#omega");
		hideoffnpc instance_npcname("#kappa");
		hideoffnpc instance_npcname("#barmund");
		specialeffect EF_WHITEBODY, AREA, instance_npcname("#barmund");
		sleep 1000;
		npctalk "[แลมบ์ดา ตั้งแต่วันนี้เป็นต้นไป ตรวจสอบให้แน่ใจว่าคุณจัดการสิทธิ์ด้านความปลอดภัยทั้งหมดสำหรับผลลัพธ์ของแล็บลับนี้]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ยืนยัน]", instance_npcname("#omega");
		sleep 4000;
		npctalk "[คัปปา ควรแน่ใจว่าคุณได้รับการสนับสนุนเพียงพอสำหรับการวิจัยของคุณ ตราบใดที่การเงินไม่ตกอยู่ในความเสี่ยง]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ผมจะทำตามคำสั่ง]", instance_npcname("#kappa");
		sleep 4000;
		npctalk "[และซิกม่า ตั้งแต่วันนี้เป็นต้นไป คุณจะดำเนินการวิจัยด้านปัญญาประดิษฐ์ของฉันต่อ]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ฉันไม่มีความสามารถนั้น]";
		sleep 4000;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		specialeffect EF_WHITEBODY, AREA, instance_npcname("#barmund");
		npctalk "[นายต้องทำได้นะซิกม่า ฉันต้องเดินทางไกลอีกแล้ว]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[คุณจะกลับมาไหม?]";
		sleep 4000;
		npctalk "[ฉันไม่รู้.]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ผมเป็นเพียงผู้ช่วยวิจัยธรรมดาๆ ทำไมคุณถึงมอบหมายงานแบบนี้ให้ผมทำ]";
		sleep 4000;
		npctalk "[เพราะว่าในพวกคุณสามคน คุณเป็นคนที่มีลักษณะคล้ายกับฉันเร็วที่สุด]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[การคำนวณของฉันแสดงให้เห็นว่าโอกาสสำเร็จมีน้อยกว่า 2% นี่เป็นภารกิจที่เป็นไปไม่ได้]";
		sleep 4000;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		specialeffect EF_WHITEBODY, AREA, instance_npcname("#barmund");
		npctalk "[คุณก็คงไม่รู้เรื่องนั้นจนกว่าจะได้ลองใช่ไหมล่ะ?]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ฉันตั้งตารอที่จะได้อ่านงานวิจัยของคุณในขณะที่ฉันไม่อยู่ ฉันหวังว่าจะได้พบคุณอีกครั้งสักวันหนึ่ง]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[Kappa และ Lambda ช่วยเหลือ Sigma ในการวิจัย]", instance_npcname("#barmund");
		sleep 4000;
		npctalk "[ยืนยัน]", instance_npcname("#omega");
		npctalk "[ผมจะทำตามคำสั่ง]", instance_npcname("#kappa");
		specialeffect EF_BIG_PORTAL2, AREA, instance_npcname("#barmund");
		sleep 1000;
		disablenpc instance_npcname("#barmund");
		sleep 3000;
		npctalk "ตั้งแต่นั้นเป็นต้นมา เราก็เริ่มใช้สิ่งอำนวยความสะดวกที่นี่เพื่อสร้างตุ๊กตาอัตโนมัติที่สามารถเรียนรู้ลักษณะนิสัยของมนุษย์ได้";
		unitwalk 'guide_id,90,113, instance_npcname("#sigma") + "::OnEvent09";
		end;

OnEvent09:
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		disablenpc instance_npcname("#omega");
		disablenpc instance_npcname("#kappa");
		unitwalk 'guide_id,107,113, instance_npcname("#sigma") + "::OnEvent10";
		end;

OnEvent10:
		npctalk "หลังจากผ่านไปประมาณ 300,000 ชั่วโมง ความรู้ที่สะสมไว้จะถึงขั้นสร้างบุคลิกภาพเสมือนจริง";
		unitwalk 'guide_id,107,94, instance_npcname("#sigma") + "::OnEvent11";
		end;


OnEvent11:
		npctalk "ตั้งแต่นั้นเป็นต้นมา ความฉลาดได้เพิ่มขึ้นอย่างทวีคูณ";
		unitwalk 'guide_id,97,84, instance_npcname("#sigma") + "::OnEvent12";
		end;

OnEvent12:
		npctalk "ตั้งแต่นั้นเป็นต้นมา เราได้เริ่มปรับปรุงตุ๊กตาอัตโนมัติที่มีอยู่เพื่อเรียนรู้เกี่ยวกับบุคลิกภาพของมนุษย์โดยใช้สิ่งอำนวยความสะดวกที่นี่";
		unitwalk 'guide_id,86,73, instance_npcname("#sigma") + "::OnEvent13";
		end;

OnEvent13:
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;
		npctalk "เด็กๆที่ถูกสร้างขึ้นในเวลานี้ก็คือเด็กๆที่อยู่นอกสวนที่คุณเห็นนั่นเอง";
		sleep 1000;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		'hidden_garden = 20;
		end;

OnTouch:
		if ('hidden_garden == 15 || 'hidden_garden == 17) {
				removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
				specialeffect EF_MARKING_USE_CHANGEMONSTER;
		}
		end;

OnEffect:
		while ('hidden_garden < 17) {
				specialeffect EF_HITLINE2, AREA, instance_npcname("#eff_npc" + rand(1,3));
				sleep 500;
		}
		for ( .@i = 1; .@i < 4; ++.@i )
				disablenpc instance_npcname("#eff_npc" + .@i);
		end;
}

1@herbs,53,97,3	duplicate(dummy_disabled_npc)	#eff_npc1	CLEAR_NPC
1@herbs,53,98,3	duplicate(dummy_disabled_npc)	#eff_npc2	CLEAR_NPC
1@herbs,53,99,3	duplicate(dummy_disabled_npc)	#eff_npc3	CLEAR_NPC

// unknown ontouch effect
1@herbs,82,112,6	duplicate(dummy_disabled_npc)	#omega	MD_REDPEPPER_H,4,4
1@herbs,82,114,6	duplicate(dummy_disabled_npc)	#kappa	MD_REDPEPPER,4,4
1@herbs,90,113,3	duplicate(dummy_disabled_npc)	#barmund	4_M_BARMUND,4,4



// Daily
// (trigger normal mode)
1@herbs,45,208,0	script	#n_start	HIDDEN_WARP_NPC,3,3,{
		end;
OnTouch:
		if (!is_party_leader())
				end;
		disablenpc();
		disablenpc instance_npcname("#h_start");
		disablenpc instance_npcname("#BMD000001");
		disablenpc instance_npcname("#mc_device");
		enablenpc instance_npcname("#BMD_a");

		'guide_id = getnpcid(0, instance_npcname("#BMD_a"));
		'admin$ = strcharinfo(0);
		'mode = 0;

		unitwalk 'guide_id,53,196, instance_npcname("#BMD_a") + "::OnEvent01";
		end;
}

// (trigger hard mode)
1@herbs,59,214,0	script	#h_start	HIDDEN_WARP_NPC,3,3,{
		end;
OnTouch:
		if (!is_party_leader())
				end;
		disablenpc();
		disablenpc instance_npcname("#n_start");
		disablenpc instance_npcname("#BMD000001");
		disablenpc instance_npcname("#mc_device");
		enablenpc instance_npcname("#BMD_a");

		'guide_id = getnpcid(0, instance_npcname("#BMD_a"));
		'admin$ = strcharinfo(0);
		'mode = 1;

		unitwalk 'guide_id,53,196, instance_npcname("#BMD_a") + "::OnEvent01";
		end;
}

1@herbs,62,196,8	script(DISABLED)	#BMD_a	MD_ASSISTANT,{
		end;
OnEvent01:
		npcspeed 200;
		unitwalk 'guide_id,53,207, instance_npcname("#BMD_a") + "::OnStart";
		end;
OnStart:
		npctalk "-" + 'admin$ + "]! ซิกม่าขอให้ฉันส่งข้อความ";
		sleep 3000;
		npctalk "สถานการณ์ตอนนี้ไม่ดีเลยครับ ผมว่ากลับไปดีกว่าครับ..";
		sleep 3000;
		mapannounce 'map_herbs$, "Lambda : It's up to me to decide whether those outsiders go back or not.", bc_map, 0xFFFF44;
		sleep 3000;
		mapannounce 'map_herbs$, "Lambda : You've created a fatal threat to the security in this area. I will dispose of it in accordance with the automatic doll management policy.", bc_map, 0xFFFF44;
		specialeffect EF_THUNDERSTORM2;
		sleep 1000;
		specialeffect EF_M03;
		disablenpc();
		sleep 2000;
		mapannounce 'map_herbs$, "Lambda : Just in time, I want to get a confirmation from you.", bc_map, 0xFFFF44;
		sleep 5000;
		mapannounce 'map_herbs$, "Lambda : It's been a while since an outsider came into this security area, so I'd better say what the other automatic dolls told me.", bc_map, 0xFFFF44;
		sleep 5000;
		mapannounce 'map_herbs$, "Lambda : We need to use you to 'evaluate' our security system.", bc_map, 0xFFFF44;
		donpcevent instance_npcname("#172_sec_mob") + "::OnSummon";
		end;
}

1@herbs,1,1,0	script	#172_sec_mob	HIDDEN_WARP_NPC,{
		end;

OnSummon:
		.@event$ = instance_npcname("#172_sec_mob") + "::OnMobKill";

		switch( 'room ) {
		case 0:
				if ('mode == 0) {
						'total_mobs = 16;
						monster 'map_herbs$,77,197,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,51,196,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,63,217,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,81,195,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,61,217,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,57,193,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,56,191,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,59,193,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,59,192,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,60,222,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,58,191,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,78,199,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,54,192,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,79,195,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,76,192,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,51,196,"Flower Garden Watcher",20624,1, .@event$;
				}
				else {
						'total_mobs = 16;
						monster 'map_herbs$,53,197,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,62,217,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,57,219,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,62,222,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,79,200,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,80,195,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,80,199,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,58,193,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,56,192,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,51,191,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,51,192,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,83,199,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,83,191,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,83,196,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,57,193,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,62,222,"Flower Garden Watcher",20623,1, .@event$;
				}
				break;
		case 1:
				if ('mode == 0) {
						'total_mobs = 21;
						monster 'map_herbs$,114,226,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,114,227,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,119,217,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,118,227,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,116,216,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,114,215,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,114,225,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,114,224,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,139,205,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,140,202,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,145,202,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,136,202,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,142,208,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,144,203,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,170,223,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,174,220,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,168,221,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,169,221,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,169,217,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,118,216,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,117,233,"Flower Garden Watcher",20622,1, .@event$;
				}
				else {
						'total_mobs = 21;
						monster 'map_herbs$,137,204,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,142,205,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,145,200,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,146,201,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,147,201,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,141,208,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,166,224,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,171,217,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,172,225,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,173,214,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,174,225,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,174,223,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,170,213,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,169,219,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,136,205,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,136,210,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,120,224,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,118,223,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,117,219,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,115,214,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,115,233,"Flower Garden Watcher",20623,1, .@event$;
				}
				break;
		case 2:
				if ('mode == 0) {
						'total_mobs = 28;
						monster 'map_herbs$,213,205,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,202,214,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,202,213,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,210,212,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,211,207,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,209,214,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,203,212,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,218,194,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,203,196,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,204,193,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,215,201,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,204,193,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,205,213,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,218,208,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,210,210,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,245,214,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,220,192,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,248,208,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,253,208,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,253,208,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,246,212,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,251,210,"Flower Garden Watcher",20622,1, .@event$;
						sleep 100;
						monster 'map_herbs$,245,209,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,222,195,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,203,212,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,208,213,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,219,192,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,245,209,"Flower Garden Watcher",20626,1, .@event$;
				}
				else {
						'total_mobs = 27;
						monster 'map_herbs$,207,194,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,217,204,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,210,210,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,218,195,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,211,209,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,210,211,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,207,194,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,209,214,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,216,192,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,208,211,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,214,208,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,209,214,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,215,201,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,205,214,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,205,214,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,220,207,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,208,213,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,202,212,"Flower Garden Watcher",20623,1, .@event$;
						sleep 100;
						monster 'map_herbs$,202,193,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,206,214,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,245,212,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,247,209,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,249,210,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,249,214,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,249,214,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,250,210,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,252,208,"Flower Garden Watcher",20627,1, .@event$;
				}
				break;
		case 3:
				if ('mode == 0) {
						'total_mobs = 31;
						monster 'map_herbs$,291,210,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,306,215,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,284,234,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,307,232,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,283,238,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,298,237,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,282,233,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,303,240,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,296,249,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,302,239,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,292,217,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,298,262,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,297,212,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,299,262,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,308,216,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,301,238,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,285,210,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,283,228,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,294,252,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,295,247,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,294,245,"Flower Garden Watcher",20624,1, .@event$;
						sleep 100;
						monster 'map_herbs$,281,232,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,287,219,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,302,239,"Flower Garden Watcher",20626,1, .@event$;
						sleep 100;
						monster 'map_herbs$,307,217,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,300,235,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,295,249,"Flower Garden Watcher",20624,1, .@event$;
						monster 'map_herbs$,284,231,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,297,259,"Flower Garden Watcher",20622,1, .@event$;
						monster 'map_herbs$,306,238,"Flower Garden Watcher",20626,1, .@event$;
						monster 'map_herbs$,310,215,"Flower Garden Watcher",20624,1, .@event$;
				}
				else {
						'total_mobs = 33;
						monster 'map_herbs$,307,239,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,297,257,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,284,236,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,283,235,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,304,240,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,298,246,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,305,224,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,310,225,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,284,218,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,298,213,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,308,224,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,290,210,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,310,224,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,308,220,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,284,211,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,284,218,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,282,231,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,281,231,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,280,226,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,285,210,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,280,227,"Flower Garden Watcher",20623,1, .@event$;
						monster 'map_herbs$,285,234,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,283,236,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,284,233,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,284,237,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,284,239,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,293,246,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,300,247,"Flower Garden Watcher",20627,1, .@event$;
						sleep 100;
						monster 'map_herbs$,294,253,"Flower Garden Watcher",20627,1, .@event$;
						monster 'map_herbs$,302,240,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,299,255,"Flower Garden Watcher",20625,1, .@event$;
						monster 'map_herbs$,295,256,"Flower Garden Watcher",20625,1, .@event$;
						sleep 100;
						monster 'map_herbs$,310,218,"Flower Garden Watcher",20623,1, .@event$;
				}
				break;
		default:
				end;
		}
		end;

OnMobKill:
		'total_mobs -= 1;
		if ('total_mobs == 0)
				donpcevent instance_npcname("#172_sec_mob") + "::OnEvent";
		end;

OnEvent:
		switch( 'room ) {
		case 0:
				mapannounce 'map_herbs$, "Lambda : I know for sure that our security is not very good.", bc_map, 0xFFFF44;
				sleep 3000;
				mapannounce 'map_herbs$, "Lambda : [" + 'admin$ + "], I want you to [check] the next area as well.", bc_map, 0xFFFF44;
				break;
		case 1:
				mapannounce 'map_herbs$, "Lambda : Your movements helped me a lot. [" + 'admin$ + "]", bc_map, 0xFFFF44;
				sleep 3000;
				mapannounce 'map_herbs$, "Lambda : Then, let us move on.", bc_map, 0xFFFF44;
				break;
		case 2:
				mapannounce 'map_herbs$, "Lambda : Sigma might be correct.", bc_map, 0xFFFF44;
				sleep 3000;
				mapannounce 'map_herbs$, "Lambda : [" + 'admin$ + "], and your party may have really met Varmundt before.", bc_map, 0xFFFF44;
				sleep 3000;
				mapannounce 'map_herbs$, "Lambda : But. Before I meet you, I need to test you one last time.", bc_map, 0xFFFF44;
				break;
		case 3:
				'room = 4;
				if ('mode == 0) {
						mapannounce 'map_herbs$, "Kappa : Well, you're just as strong as Lambda said. [" + 'admin$ + "]", bc_map, 0xFF55FF;
						sleep 3000;
						mapannounce 'map_herbs$, "Kappa : But I don't think you're not strong enough to meet Lambda yet, so I'll show you to my room.", bc_map, 0xFF55FF;
						enablenpc instance_npcname("#kappa_1");
				}
				else {
						mapannounce 'map_herbs$, "Lambda : Great. If you're this strong, you're worth talking to.", bc_map, 0xFFFF44;
						sleep 3000;
						mapannounce 'map_herbs$, "Lambda : I'll take you to my room.", bc_map, 0xFFFF44;
						enablenpc instance_npcname("#ramda_1");
				}
				sleep 3000;
				mapannounce 'map_herbs$, "The warp connecting the flower garden and the security area has activated.", bc_map, 0x33FF33;
				enablenpc instance_npcname("#wp_4");
				end;
		default:
				end;
		}
		sleep 3000;
		mapannounce 'map_herbs$, "The warp connecting the flower garden has activated.", bc_map, 0x33FF33;
		'room += 1;
		enablenpc instance_npcname("#wp_" + 'room);
		end;
}

1@herbs,80,196,0	script(DISABLED)	#wp_1	WARPNPC,3,3,{
		end;
OnTouch:
		.@id = atoi(replacestr(strnpcinfo(2), "wp_", ""));

		if ('spawn_mobs[.@id] == 0 && .@id < 4) {
				'spawn_mobs[.@id] = 1;
				donpcevent instance_npcname("#172_sec_mob") + "::OnSummon";
		}
		switch( .@id ) {
		case 1:
				.@x = 101;
				.@y = 233;
				break;
		case 2:
				.@x = 197;
				.@y = 200;
				break;
		case 3:
				.@x = 276;
				.@y = 213;
				break;
		case 4:	// (boss room)
				if ('mode == 0) {
						.@x = 151;
						.@y = 80;
				}
				else {
						.@x = 250;
						.@y = 80;
				}
				break;
		}
		warp 'map_herbs$,.@x,.@y;
		end;
}

1@herbs,174,229,0	duplicate(#wp_1)	#wp_2	WARPNPC,3,3
1@herbs,254,212,0	duplicate(#wp_1)	#wp_3	WARPNPC,3,3
1@herbs,297,264,0	duplicate(#wp_1)	#wp_4	WARPNPC,3,3

// (normal mode)
1@herbs,147,80,0	warp2(DISABLED)	#bs_wp1	3,3,1@herbs,94,79
// (hard mode)
1@herbs,240,80,0	warp2(DISABLED)	#bs_wp2	3,3,1@herbs,94,79


// (normal mode)
1@herbs,168,79,3	script(DISABLED)	#kappa_1	MD_REDPEPPER,4,4,{
		if (!is_party_leader())
				end;
		if ('room != 4)
				end;
		'room = 5;
		npctalk "งานปาร์ตี้ของคุณทำให้สวนเสียหายไป 30% ต้องเสียเงินซ่อมแซมค่อนข้างมาก";
		sleep 5000;
		npctalk "เราอยากจะเชื่อว่าอาจารย์วาร์มุนด์ยังมีชีวิตอยู่ แต่นั่นเป็นไปไม่ได้เพราะอายุขัยของมนุษย์";
		sleep 5000;
		npctalk "อย่างไรก็ตาม คุณจะต้องเอาชนะฉันและได้รับอำนาจการบริหาร หากคุณต้องการป้องกันการปิดพื้นที่วิจัย";
		sleep 5000;
		disablenpc();
		donpcevent instance_npcname("#172_sec_boss") + "::OnSummon";
		end;
}

// (hard mode)
1@herbs,275,80,3	script(DISABLED)	#ramda_1	MD_REDPEPPER_H,4,4,{
		if (!is_party_leader())
				end;
		if ('room != 4)
				end;
		'room = 5;
		npctalk "เราได้ตรวจสอบอย่างชัดเจนแล้วว่าส่วนไหนของสวนดอกไม้ที่ควรปรับปรุง ตอนนี้เหลือแค่การเจรจากัน";
		sleep 5000;
		npctalk "เราอยากจะเชื่อว่าอาจารย์วาร์มุนด์ยังมีชีวิตอยู่ แต่นั่นเป็นไปไม่ได้เพราะอายุขัยของมนุษย์";
		sleep 5000;
		npctalk "อย่างไรก็ตาม คุณจะต้องเอาชนะฉันและได้รับอำนาจการบริหาร หากคุณต้องการป้องกันการปิดพื้นที่วิจัย";
		sleep 5000;
		disablenpc();
		donpcevent instance_npcname("#172_sec_boss") + "::OnSummonHard";
		end;
OnTouch:	// unknown effect
		end;
}


1@herbs,85,77,8	script(DISABLED)	#sigma2	MD_REDPEPPER_H,{
		if (isbegin_quest(12500) > 0)
				.@qid = 12500;
		else if (isbegin_quest(12499) > 0)
				.@qid = 12499;

		if (checkquest(.@qid,HUNTING) == 2) {
				if (checkweight(1000103,6) == 0) {	// (custom text)
						mes "^008800รอก่อนนะ!!";
						mes "คุณไม่สามารถรับสินค้าเพิ่มเติมได้เนื่องจากคุณมีสินค้ามากเกินไป โปรดลองอีกครั้งหลังจากคุณผ่อนปรนลง^000000";
						close;
				}
				mes "[ซิกม่า]";
				mes "คุณทำได้ดีมากที่ทำให้พวกเขายอมแพ้ แต่ฉันไม่คิดว่าคุณจะได้รับสิทธิ์ผู้ดูแลระบบใด ๆ";
				next;
				mes "[ซิกม่า]";
				mes "เนื่องจากพวกเขาจะฟื้นตัวหลังจากผ่านไปสักระยะ ฉันคิดว่าคุณจะต้องต่อสู้กับพวกเขาต่อไปจนกว่าอาจารย์วาร์มุนด์จะกลับมา";
				next;
				mes "[ซิกม่า]";
				mes "โอ้.";
				mes "ฉันนำของเก่าๆ ในอดีตมาด้วย อาจช่วยคุณได้ ดังนั้นเอาไปเถอะ";
				close2;
				erasequest .@qid;
				getitem 1000103,6;
				if (.@qid == 12500) {
						if (rand(1,100) < 95)
								getitem 1000104,rand(1,5);
						else
								getitem 100161,1;
				}
				end;
		}
		mes "[ซิกม่า]";
		mes "ฉันจะชี้ทางออกให้คุณเอง คุณจะทำอย่างไร?";
		next;
		if (select( "I'll look around more.", "I'll leave." ) == 2)
				warp "ba_maison",136,309;
		end;
}


1@herbs,1,1,0	script	#172_sec_boss	HIDDEN_WARP_NPC,{
		end;
OnSummon:
		monster 'map_herbs$,169,79,"Kappa",20620,1, instance_npcname("#172_sec_boss") + "::OnBossKill";
		'boss_gid = $@mobid[0];
		donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
		end;
OnSummonHard:
		monster 'map_herbs$,275,80,"Lambda",20621,1, instance_npcname("#172_sec_boss") + "::OnBossKill";
		'boss_gid = $@mobid[0];
		donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
		end;

OnBossKill:
		stopnpctimer;
		donpcevent instance_npcname("#172_sec_boss_shield") + "::OnStop";
		killmonster 'map_herbs$, instance_npcname("#172_sec_boss") + "::OnBossKill";
		killmonster 'map_herbs$, instance_npcname("#172_sec_boss") + "::OnBreakShield";
		if ('mode == 0)
				enablenpc instance_npcname("#bs_wp1");
		else
				enablenpc instance_npcname("#bs_wp2");
		enablenpc instance_npcname("#sigma2");
		mapannounce 'map_herbs$, "Sigma : I have activated the warp to my location. Come over here.", bc_map, 0xF6D6B6;
		end;

OnShield:
		.@event$ = instance_npcname("#172_sec_boss") + "::OnBreakShield";
		killmonster 'map_herbs$, .@event$;
		if ('mode)
				setarray .@xy[0],299,95,299,64,272,64,272,95;
		else
				setarray .@xy[0],171,94,171,65,200,65,200,94;

		for ( .@i = 0; .@i < 8; .@i += 2 )
				monster 'map_herbs$,.@xy[.@i],.@xy[.@i+1],"Guardian Part",20679,1, .@event$;
		unitskilluseid 'boss_gid,"NPC_RELIEVE_OFF",1;
		unitskilluseid 'boss_gid,"NPC_RELIEVE_ON",10;
		mapannounce 'map_herbs$, "SYSTEM : The manager has summoned Guardian Parts and will recover them as materials for it's energy shield.", bc_map, 0xFFFF00;
		initnpctimer;
		end;
OnTimer40000:
		stopnpctimer;
		killmonster 'map_herbs$, instance_npcname("#172_sec_boss") + "::OnBreakShield";
		mapannounce 'map_herbs$, "SYSTEM : A default energy shield value has been recovered.", bc_map, 0xFFFF00;
		donpcevent instance_npcname("#172_sec_boss_shield") + "::OnStart";
		end;

OnBreakShield:
		switch( mobcount('map_herbs$, instance_npcname("#172_sec_boss") + "::OnBreakShield") ) {
		case 0:
				stopnpctimer;
				unitskilluseid 'boss_gid,"NPC_RELIEVE_OFF",1;
				mapannounce 'map_herbs$, "SYSTEM : All the Guardian Parts have been destroyed. No energy shield has been recovered.", bc_map, 0xFFFF00;
				donpcevent instance_npcname("#172_sec_boss_shield") + "::OnStart";
				end;
		case 1:
				.@val = 3;
				break;
		case 2:
				.@val = 5;
				break;
		case 3:
				.@val = 7;
				break;
		default:
				end;
		}
		unitskilluseid 'boss_gid,"NPC_RELIEVE_OFF",1;
		unitskilluseid 'boss_gid,"NPC_RELIEVE_ON",.@val;
		mapannounce 'map_herbs$, "SYSTEM : The damage to the Guardian Parts has weakened the energy shield.", bc_map, 0xFFFF00;
		end;
}

1@herbs,1,1,0	script	#172_sec_boss_shield	HIDDEN_WARP_NPC,{
		end;
OnStart:
		initnpctimer;
		end;
OnTimer30000:
		stopnpctimer;
		getunitdata 'boss_gid, .@data;
		.@val = .@data[UMOB_MAXHP] / 10;
		// todo system to trigger NPC_LOCKON_LASER
		if (.@data[UMOB_HP] <= (.@val * 8) && 'summon[0] == 0) {
				'summon[0] = 1;
				donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
				end;
		}
		if (.@data[UMOB_HP] <= (.@val * 6) && 'summon[1] == 0) {
				'summon[1] = 1;
				donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
				end;
		}
		if (.@data[UMOB_HP] <= (.@val * 4) && 'summon[2] == 0) {
				'summon[2] = 1;
				donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
				end;
		}
		if (.@data[UMOB_HP] <= (.@val * 2) && 'summon[3] == 0) {
				'summon[3] = 1;
				donpcevent instance_npcname("#172_sec_boss") + "::OnShield";
				end;
		}
		if ('summon[0] == 0 || 'summon[1] == 0 || 'summon[2] == 0 || 'summon[3] == 0)
				initnpctimer;
		end;
OnStop:
		stopnpctimer;
		end;
}
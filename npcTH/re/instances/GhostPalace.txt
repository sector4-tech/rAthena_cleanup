//===== rAthena Script =======================================
//= Ghost Palace
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Ghost Palace Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Fixed NPC names. [zackdreaver]
//============================================================

dali02,43,129,5	script	Unpleasant Royal Guard#	4_M_SAKRAYROYAL,{
		if (BaseLevel < 120) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "แม้ว่าฉันต้องการความช่วยเหลือกับงาน แต่คุณไม่แข็งแกร่งพอที่จะช่วยฉัน โง่จัง~!";
				next;
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "กลับมาใหม่เมื่อคุณทำการฝึกฝนเสร็จแล้วและมีระดับอย่างน้อย 120!";
				close;
		}
		.@party_id = getcharid(1);
		if (is_party_leader() == false || .@party_id < 1) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "ฉันเปิดทางลับให้เฉพาะหัวหน้าพรรคเท่านั้น กรุณาให้หัวหน้าพรรคคุยกับฉันด้วย";
				close;
		}
		switch( checkquest(1261,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "คุณเจอเจ้าหญิงแล้วหรือยัง?! เธอปลอดภัยดีใช่ไหม?";
				close;
		case 2:
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "เหล่าสัตว์ประหลาดรอบๆ ทางลับได้หายไปแล้ว ตอนนี้เป็นโอกาสของเราที่จะช่วยเจ้าหญิงแล้ว!";
				erasequest 1261;// Cursed Swordman
				close;
		}
		mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
		mes "ฉันต้องการความช่วยเหลือ แม้แต่จากคนอย่างคุณก็ด้วย";
		next;
		mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
		mes "ขณะที่ฉันกำลังเฝ้าพระราชวัง เจ้าหญิงก็หายไปในกลางการต่อสู้";
		next;
		mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
		mes "เจ้าวิ่งไปข้างหน้าแล้วข้าจะตามทัน เราต้องช่วยนางให้ได้";
		next;
		if (select( "I will help.", "This is not a good time." ) == 2) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "จนถึงตอนนี้องครักษ์ทั้งหมดยังคงต่อสู้และตายจากเหล่าสัตว์ประหลาด";
				next;
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "แม้ว่าความกลัวจะครอบงำจิตใจเรามากเพียงใด เราต้องช่วยเจ้าหญิง!";
				close;
		}
		instance_create("Ghost Palace");
		mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
		mes "ขอบคุณ..";
		mes "ฉันจะเปิดทางลับสู่^0000ffพระราชวังผี^000000";
		mes "โปรดเตรียมพร้อมไว้!";
		close;
}

dali02,40,134,5	script	Interdimensional Device#dk	PORTAL,{
		if (BaseLevel < 120) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "ถึงแม้ฉันต้องการความช่วยเหลือกับงาน แต่คุณไม่แข็งแกร่งพอที่จะช่วยฉัน โง่เอ๊ย~!";
				next;
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "กลับมาใหม่เมื่อคุณทำการฝึกฝนเสร็จแล้วและมีระดับอย่างน้อย 120!";
				close;
		}
		mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
		mes "ฉันมองเห็นทางลับที่จะเข้าไปในพระราชวัง คุณพร้อมที่จะช่วยเจ้าหญิงหรือยัง";
		next;
		if (select( "Enter", "Stop" ) == 2) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "จนถึงตอนนี้องครักษ์ทั้งหมดยังคงต่อสู้และตายจากเหล่าสัตว์ประหลาด";
				next;
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "แม้ว่าความกลัวจะครอบงำจิตใจเรามากเพียงใด เราต้องช่วยเจ้าหญิง!";
				close;
		}
		switch( checkquest(1261,PLAYTIME) ) {
		case -1:
				break;
		case 0:
		case 1:
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "เหล่าสัตว์ประหลาดที่โจมตีพระราชวังกำลังเดินเตร่ไปมาอยู่บริเวณทางเข้าลับ คงจะแย่มากหากพวกมันเห็นพวกเรา... กรุณารอสักครู่";
				next;
				mes "เมื่อหมดเวลาเข้าใช้บริการ";
				mes "มาลองเข้าใหม่อีกครั้งครับ.";
				close;
		case 2:
				mes "^0000ffหมดเวลาสำหรับการเข้าสู่คำสาปของนักดาบแล้ว กรุณาลองพูดคุยกับราชองครักษ์ผู้ไม่พอใจอีกครั้ง..^000000";
				erasequest 1261;// Cursed Swordman
				close;
		}
		.@party_id = getcharid(1);
		if (is_party_leader() == false || .@party_id < 1) {
				mes "[ราชองครักษ์ผู้ไม่พึงปรารถนา]";
				mes "ฉันเปิดทางลับให้เฉพาะหัวหน้าพรรคเท่านั้น กรุณาให้หัวหน้าพรรคคุยกับฉันด้วย";
				close;
		}

		.@md_name$ = "Ghost Palace";
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
				mes "เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ";
				close;
		case IE_NOINSTANCE:
				mes "^ff0000คุณจะเห็นรูมืดๆ ข้างในอุปกรณ์ หากคุณต้องการเข้าไป คุณต้องคุยกับราชองครักษ์ผู้ไม่พอใจก่อน^000000";
				close;
		case IE_NOMEMBER:
				mes "เฉพาะสมาชิกที่ลงทะเบียนเท่านั้นจึงจะเข้าได้" + .@md_name$ + "ตัวอย่าง.";
				close;
		case IE_OK:
				mapannounce "dali", "" + strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering the Ghost Palace.", bc_map, 0x00ff99;
				// warp "1@spa",42,196;
				setquest 1261;// Cursed Swordman
				end;
		}
}

// 1st floor
1@spa,41,204,5	script	King#dk	4_M_RUSKING,{
		mes "[กษัตริย์]";
		mes "คุณได้รับการแต่งตั้งให้เป็นองครักษ์ส่วนตัวของเจ้าหญิงเทียร่า ฉันเชื่อมั่นในความภักดีของคุณเหนือสิ่งอื่นใด";
		npctalk "กษัตริย์: คุณได้รับการแต่งตั้งให้เป็นองครักษ์ส่วนตัวของเจ้าหญิงเทียร่า ฉันเชื่อมั่นในความภักดีของคุณเหนือสิ่งอื่นใด";
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "ฉันจะปกป้องเจ้าหญิงด้วยชีวิตของฉัน";
		npctalk "ราชองครักษ์ผู้โหดร้าย: ฉันจะปกป้องเจ้าหญิงด้วยชีวิตของฉัน", instance_npcname("Lurid Royal Guard#dk");
		next;
		mes "[กษัตริย์]";
		mes "การแต่งงานเป็นไปตามแผน เจ้าชายมาที่นี่เพื่อพบกับเจ้าหญิง";
		npctalk "กษัตริย์: การแต่งงานเป็นไปตามแผน เจ้าชายมาที่นี่เพื่อพบกับเจ้าหญิง";
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "...ครับ ฝ่าบาท.";
		npctalk "องครักษ์ราชสีห์อันน่าสะพรึง: ...ครับ ฝ่าบาท", instance_npcname("Lurid Royal Guard#dk");
		next;
		mes "[กษัตริย์]";
		mes "โอ้พระเจ้า มีสัตว์ประหลาดอยู่ในปราสาท! กำจัดพวกมันเดี๋ยวนี้!";
		npctalk "ราชา: โอ้พระเจ้า มีสัตว์ประหลาดอยู่ในปราสาท กำจัดพวกมันเดี๋ยวนี้!";
		close2;
		donpcevent instance_npcname("#gp_control_1") + "::OnStart";
		end;

OnInstanceInit:
		// 1: enabled potential double spawn (official), 0: disabled
		'bool_double_spawn = 1;

		'map_spa$ = instance_mapname("1@spa");

		// warps
		disablenpc instance_npcname("The second floor of Palace");
		disablenpc instance_npcname("3rd floor in the palace");
		disablenpc instance_npcname("4th floor in the palace");
		disablenpc instance_npcname("5th floor of Palace");
		disablenpc instance_npcname("Way out of Palace");

		// first floor
		disablenpc instance_npcname("#gp_control_1");

		// second floor
		disablenpc instance_npcname("Lurid Royal Guard#dk1");
		disablenpc instance_npcname("Princess Tiara#dk");
		disablenpc instance_npcname("#gp_control_2");

		// 3rd floor
		disablenpc instance_npcname("King#dk1");
		disablenpc instance_npcname("Lurid Royal Guard#dk2");
		disablenpc instance_npcname("Captain of the Guard#dk");
		disablenpc instance_npcname("Soldier#dk1");
		disablenpc instance_npcname("Soldier#dk2");
		disablenpc instance_npcname("Soldier#dk3");
		disablenpc instance_npcname("Lurid Royal Guard#dk3");
		disablenpc instance_npcname("Magic Sword Tartanos#dk1");
		disablenpc instance_npcname("#gp_control_3");

		// 4th floor
		disablenpc instance_npcname("#gp_control_4");
		disablenpc instance_npcname("#gp_control_5");
		disablenpc instance_npcname("Magic Sword Tartanos#dk2");
		disablenpc instance_npcname("Visiting Prince#dk");
		disablenpc instance_npcname("Lurid Royal Guard#dk4");
		disablenpc instance_npcname("King's Corpse#dk");
		disablenpc instance_npcname("Captain's Corpse#dk");
		disablenpc instance_npcname("Soldier's Corpse#dk");
		disablenpc instance_npcname("Soldier's Corpse#dk1");
		disablenpc instance_npcname("Soldier's Corpse#dk2");
		disablenpc instance_npcname("Soldier's Corpse#dk3");
		disablenpc instance_npcname("Soldier's Corpse#dk4");

		// 5th floor
		disablenpc instance_npcname("#gp_control_6");
		disablenpc instance_npcname("Princess Tiara#dk1");
		disablenpc instance_npcname("Lurid Royal Guard#dk5");
		disablenpc instance_npcname("King#dk11");
		disablenpc instance_npcname("Sakray#dk");
		disablenpc instance_npcname("Magic Sword Tartanos#dk3");
		disablenpc instance_npcname("#tb");
		disablenpc instance_npcname("#sv");
		disablenpc instance_npcname("#tv");
		end;
}

1@spa,42,201,1	script	Lurid Royal Guard#dk	4_M_SAKRAYROYAL,{ end; }

1@spa,1,1,1	script	#gp_control_1	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_1");
		disablenpc instance_npcname("King#dk");
		disablenpc instance_npcname("Lurid Royal Guard#dk");
		initnpctimer;
		end;
OnTimer2000:
		.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead1";
		monster 'map_spa$,50,194,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
		monster 'map_spa$,34,208,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
		monster 'map_spa$,32,194,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		monster 'map_spa$,52,209,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		stopnpctimer;
		end;
OnMyMobDead1:
		if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead1") < (1 + 'bool_double_spawn)) {
				deltimer instance_npcname("#gp_control_1") + "::OnSpawn1";	// stop double spawn if timer is running
				addtimer 2000, instance_npcname("#gp_control_1") + "::OnSpawn1";
		}
		end;
OnSpawn1:
		.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead2";
		monster 'map_spa$,37,182,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
		monster 'map_spa$,46,182,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		end;
OnMyMobDead2:
		if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead2") < 1) {
				deltimer instance_npcname("#gp_control_1") + "::OnSpawn1";	// stop double spawn if timer is running
				deltimer instance_npcname("#gp_control_1") + "::OnSpawn2";
				addtimer 2000, instance_npcname("#gp_control_1") + "::OnSpawn2";
		}
		end;
OnSpawn2:
		.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead3";
		monster 'map_spa$,30,200,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
		monster 'map_spa$,51,200,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		monster 'map_spa$,53,199,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		end;
OnMyMobDead3:
		if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead3") < 1) {
				mapannounce 'map_spa$, "The passage on the 2nd floor in the palace of ghost is open .", bc_map,0xFFFF00;
				enablenpc instance_npcname("The second floor of Palace");
				enablenpc instance_npcname("Lurid Royal Guard#dk1");
				enablenpc instance_npcname("Princess Tiara#dk");
		}
		end;
}

// 2nd floor
1@spa,132,122,7	script	Lurid Royal Guard#dk1	4_M_SAKRAYROYAL,{
		.@tiara$ = instance_npcname("Princess Tiara#dk");
		cutin "npc-tiara.bmp",3;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "ฝ่าบาท พระองค์ได้ทรงเชิญเจ้าชายจากประเทศเพื่อนบ้านมาเป็นคู่หมั้น เตรียมตัวพบกับเขาได้เลย...";
		npctalk "องครักษ์แห่งรัตติกาล: ฝ่าบาท กษัตริย์ได้เชิญเจ้าชายจากประเทศเพื่อนบ้านมาเป็นคู่หมั้นของท่าน เตรียมตัวพบกับเขาได้เลย...";
		next;
		mes "[เจ้าหญิงเทียร่า]";
		mes "ถ้าหาก...หากมีการจัดพิธีแต่งงาน ฉันควรทำอย่างไร?";
		npctalk "Princess Tiara: ถ้าหาก...หากมีการจัดพิธีแต่งงานขึ้น ฉันควรทำอย่างไร?", .@tiara$;
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "สิ่งเดียวที่ฉันทำได้คือปกป้องคุณจากอันตรายทางกายภาพ";
		npctalk "องครักษ์ราชสีห์ลอร์ด: สิ่งเดียวที่ฉันทำได้คือปกป้องคุณจากอันตรายทางกายภาพ";
		next;
		mes "[เจ้าหญิงเทียร่า]";
		mes "แล้วฉันจะทำอะไรได้ล่ะ?";
		npctalk "เจ้าหญิงเทียร่า: แล้วฉันจะทำอย่างไรได้ล่ะ?", .@tiara$;
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "เรื่องนี้ท่านจะต้องตัดสินใจเองฝ่าบาท...";
		npctalk "องครักษ์ราชสีห์ผู้ชั่วร้าย: ท่านจะต้องตัดสินใจเรื่องนี้ด้วยตัวเองฝ่าบาท...";
		next;
		mes "[เจ้าหญิงเทียร่า]";
		mes "ถ้าฉันแต่งงานแล้วส่งไปเมืองอื่นมันจะง่ายกว่าสำหรับคุณไหม?";
		npctalk "Princess Tiara: มันจะง่ายกว่าสำหรับคุณไหมถ้าฉันแต่งงานและส่งไปประเทศอื่น?", .@tiara$;
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "ฉันจะอยู่เคียงข้างคุณเสมอและปกป้องคุณตลอดไป";
		npctalk "ราชองครักษ์ผู้ลึกลับ: ฉันจะอยู่เคียงข้างคุณเสมอและปกป้องคุณตลอดไป";
		cutin "npc-tiara.bmp",255;
		next;
		mes "[เจ้าหญิงเทียร่า]";
		mes "มีมอนสเตอร์เข้ามาในปราสาทแล้วเหรอ? ช่วยคุ้มครองฉันด้วย!";
		npctalk "Princess Tiara: มีมอนสเตอร์เข้ามาในปราสาทแล้วเหรอ? ช่วยปกป้องฉันด้วย!", .@tiara$;
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "อยู่ข้างหลังฉันไว้ ไปกันเถอะ!";
		npctalk "ราชองครักษ์ผู้โหดร้าย: อยู่ข้างหลังข้า รีบไปกันเถอะ!";
		close2;
		donpcevent instance_npcname("#gp_control_2") + "::OnStart";
		end;
}

1@spa,135,125,3	duplicate(Lurid Royal Guard#dk)	Princess Tiara#dk	4_F_MAYSEL

1@spa,1,1,1	script	#gp_control_2	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_2");
		disablenpc instance_npcname("Lurid Royal Guard#dk1");
		disablenpc instance_npcname("Princess Tiara#dk");
		disablenpc instance_npcname("The second floor of Palace");

		killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead1";
		killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead2";
		killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead3";
		disablenpc instance_npcname("#gp_control_1");
		initnpctimer;
		end;
OnTimer2000:
		.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead1";
		monster 'map_spa$,135,116,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
		monster 'map_spa$,110,120,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
		monster 'map_spa$,122,112,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
		monster 'map_spa$,121,129,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
		stopnpctimer;
		end;
OnMyMobDead1:
		if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead1") < 1) {
				sleep 2000;
				.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead2";
				monster 'map_spa$,120,103,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
				monster 'map_spa$,115,137,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
				monster 'map_spa$,103,130,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
		}
		end;
OnMyMobDead2:
		if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead2") < 1) {
				sleep 2000;
				.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead3";
				monster 'map_spa$,98,119,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
				monster 'map_spa$,98,120,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
				monster 'map_spa$,103,109,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
		}
		end;
OnMyMobDead3:
		if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead3") < 1) {
				mapannounce 'map_spa$, "The passage on the 3rd floor of the palace is open.", bc_map,0xFFFF00;
				enablenpc instance_npcname("3rd floor in the palace");
				enablenpc instance_npcname("King#dk1");
				enablenpc instance_npcname("Lurid Royal Guard#dk2");
				enablenpc instance_npcname("Captain of the Guard#dk");
				enablenpc instance_npcname("Soldier#dk1");
				enablenpc instance_npcname("Soldier#dk2");
				enablenpc instance_npcname("Soldier#dk3");
				disablenpc instance_npcname("#gp_control_2");
		}
		end;
}

// 3rd floor
1@spa,32,54,1	script	King#dk1	4_M_RUSKING,{
		mes "[กษัตริย์]";
		mes "คุณเป็นคนนำพวกสัตว์ประหลาดมาที่นี่! คุณอยากจะกำจัดฉันและได้เจ้าหญิงมาเป็นของตัวเองงั้นเหรอ?";
		npctalk "ราชา: เจ้าเป็นคนนำสัตว์ประหลาดมาที่นี่! เจ้าต้องการกำจัดข้าและได้เจ้าหญิงมาเป็นของตัวเองงั้นหรือ?";
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "มันเป็นกับดักฝ่าบาท! ฝ่าบาทต้องเชื่อใจข้าพเจ้า!";
		npctalk "องครักษ์แห่งราชสำนักที่น่าสะพรึงกลัว: มันคือกับดักฝ่าบาท! ฝ่าบาทต้องเชื่อใจข้า!", instance_npcname("Lurid Royal Guard#dk2");
		next;
		mes "[กษัตริย์]";
		mes "หุบปากซะ! คุณทรยศฉัน! ฉันไว้ใจคุณ... คุณต้องชดใช้ความอับอายของคุณ";
		npctalk "กษัตริย์: หุบปากซะ! เจ้าทรยศต่อข้า! ข้าเชื่อใจเจ้า... เจ้าต้องชดใช้ความอับอายที่เจ้าทำลงไป";
		next;
		mes "[กษัตริย์]";
		mes "คุณจะต้องทนทุกข์ทรมานไปตลอดชีวิต";
		npctalk "กษัตริย์ : เจ้าจะต้องทนทุกข์ทรมานไปตลอดชีวิต";
		close2;
		disablenpc instance_npcname("3rd floor in the palace");
		disablenpc instance_npcname("King#dk1");
		disablenpc instance_npcname("Captain of the Guard#dk");
		disablenpc instance_npcname("Soldier#dk1");
		disablenpc instance_npcname("Soldier#dk2");
		disablenpc instance_npcname("Soldier#dk3");
		disablenpc instance_npcname("Lurid Royal Guard#dk2");
		donpcevent instance_npcname("Magic Sword Tartanos#dk1") + "::OnStart";
		end;
}

1@spa,35,56,5	script	Magic Sword Tartanos#dk1	4_TRACE,{
		end;
OnStart:
		enablenpc instance_npcname("Magic Sword Tartanos#dk1");
		enablenpc instance_npcname("Lurid Royal Guard#dk3");
		initnpctimer;
		end;
OnTimer5000:
		npctalk "เสียงอันเป็นลางร้าย: โอ้ ราชองครักษ์ผู้ซื่อสัตย์ คุณถูกขังอยู่";
		end;
OnTimer8000:
		npctalk "เสียงลางร้าย: แผนสมคบคิดในวังทำให้เจ้าหญิงของคุณตกอยู่ในอันตรายร้ายแรง...";
		end;
OnTimer12000:
		npctalk "ราชองครักษ์สุดสยอง: เจ้าเป็นใคร แสดงตัวตนออกมาสิ!", instance_npcname("Lurid Royal Guard#dk3");
		end;
OnTimer15000:
		npctalk "เสียงอันเป็นลางร้าย: อ่า... คุณได้ยินฉันมั้ย?";
		end;
OnTimer20000:
		npctalk "องครักษ์ราชสีห์อันน่าสะพรึง: ...?", instance_npcname("Lurid Royal Guard#dk3");
		end;
OnTimer23000:
		npctalk "เสียงแห่งความเป็นลางร้าย: ข้าสามารถให้พลังอันไร้ขีดจำกัดแก่เจ้าได้ หากเจ้าทำสัญญากับข้า";
		end;
OnTimer28000:
		npctalk "ราชองครักษ์ผู้โหดร้าย: ข.ข.ข..", instance_npcname("Lurid Royal Guard#dk3");
		end;
OnTimer33000:
		npctalk "ดาบวิเศษทาร์ทานอส: คุคุคุ...อยากได้อะไรหนุ่มน้อย?";
		end;
OnTimer34000:
		stopnpctimer;
		disablenpc instance_npcname("Magic Sword Tartanos#dk1");
		disablenpc instance_npcname("Lurid Royal Guard#dk3");
		donpcevent instance_npcname("#gp_control_3") + "::OnStart";
		end;
}

1@spa,28,52,7	duplicate(Lurid Royal Guard#dk)	Captain of the Guard#dk	4_M_KY_KNT
1@spa,25,53,7	duplicate(Lurid Royal Guard#dk)	Soldier#dk1	4_M_CRU_SOLD
1@spa,34,53,1	duplicate(Lurid Royal Guard#dk)	Soldier#dk2	4_M_CRU_SOLD
1@spa,30,52,7	duplicate(Lurid Royal Guard#dk)	Soldier#dk3	4_M_KY_SOLD
1@spa,30,58,5	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk2	4_M_SAKRAY_TIED
1@spa,31,57,5	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk3	4_M_SAKRAY_TIED

1@spa,1,1,1	script	#gp_control_3	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_3");
		initnpctimer;
		end;
OnTimer2000:
		.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead1";
		monster 'map_spa$,31,57, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
		monster 'map_spa$,59,24, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
		monster 'map_spa$,23,40, "Wasted Friendship",2955,1, .@label$; 	// OLD_FRIENDSHIP
		monster 'map_spa$,54,45, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
		monster 'map_spa$,26,20, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
		stopnpctimer;
		end;
OnMyMobDead1:
		if (mobcount('map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead1") < 1)
				addtimer 2000, instance_npcname("#gp_control_3") + "::OnSpawn1";
		end;
OnSpawn1:
		.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead2";
		monster 'map_spa$,30,52, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
		monster 'map_spa$,58,58, "Wasted Friendship",2955,1, .@label$; 	// OLD_FRIENDSHIP
		monster 'map_spa$,54,24, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
		end;
OnMyMobDead2:
		if (mobcount('map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead2") < (1 + 'bool_double_spawn)) {
				deltimer instance_npcname("#gp_control_3") + "::OnAnnounce";
				addtimer 500, instance_npcname("#gp_control_3") + "::OnAnnounce";
		}
		end;
OnAnnounce:
		mapannounce 'map_spa$, "The passage on the 4th floor of the palace is open.", bc_map,0xFFFF00;
		enablenpc instance_npcname("4th floor in the palace");
		deltimer instance_npcname("#gp_control_3") + "::OnSpawn2";	// stop double spawn if timer is running
		addtimer 2000, instance_npcname("#gp_control_3") + "::OnSpawn2";
		end;
OnSpawn2:
		donpcevent instance_npcname("#gp_control_4") + "::OnStart";
		// 35 mobs
		.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead3";
		monster 'map_spa$,67,46, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,62,48, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,57,46, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,51,48, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "Faithful royal guard...";
		monster 'map_spa$,56,55, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,62,56, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,66,58, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You put your princess in grave danger...";
		monster 'map_spa$,66,64, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Faithful royal guard...";
		monster 'map_spa$,62,65, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,57,67, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,49,66, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,44,66, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,45,20, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,38,20, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,32,20, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,51,22, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,26,34,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,21,37,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Faithful royal guard...";
		monster 'map_spa$,18,39,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,21,45,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,23,49,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,25,54,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,29,54,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "You put your princess in grave danger...";
		monster 'map_spa$,56,39,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,61,36,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Faithful royal guard...";
		monster 'map_spa$,66,38,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away quickly!";
		monster 'map_spa$,42,54,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
		unittalk $@mobid[0], "Run away!";
		monster 'map_spa$,35,54,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,43,60,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "Faithful royal guard...";
		monster 'map_spa$,30,25,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
		unittalk $@mobid[0], "You put your princess in grave danger...";
		monster 'map_spa$,30,31,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		unittalk $@mobid[0], "You are trapped...";
		monster 'map_spa$,65,64, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		monster 'map_spa$,65,67, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		monster 'map_spa$,18,43,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		monster 'map_spa$,21,43,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
		end;
OnMyMobDead3:
		end;
}

// 4th floor
1@spa,1,1,1	script	#gp_control_4	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_4");
		.@label$ = instance_npcname("#gp_control_4") + "::OnMyMobDead1";
		monster 'map_spa$,215,194, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
		monster 'map_spa$,194,178, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
		monster 'map_spa$,191,211, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
		monster 'map_spa$,185,203, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		monster 'map_spa$,209,218, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		end;
OnMyMobDead1:
		if (mobcount('map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead1") < (1 + 'bool_double_spawn)) {
				deltimer instance_npcname("#gp_control_4") + "::OnSpawn1";
				addtimer 2000, instance_npcname("#gp_control_4") + "::OnSpawn1";
		}
		end;
OnSpawn1:
		.@label$ = instance_npcname("#gp_control_4") + "::OnMyMobDead2";
		monster 'map_spa$,189,192, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
		monster 'map_spa$,211,209, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
		end;
OnMyMobDead2:
		if (mobcount('map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead2") < 1) {
				enablenpc instance_npcname("Magic Sword Tartanos#dk2");
				enablenpc instance_npcname("Visiting Prince#dk");
				enablenpc instance_npcname("Lurid Royal Guard#dk4");
				enablenpc instance_npcname("King's Corpse#dk");
				enablenpc instance_npcname("Captain's Corpse#dk");
				enablenpc instance_npcname("Soldier's Corpse#dk");
				enablenpc instance_npcname("Soldier's Corpse#dk1");
				enablenpc instance_npcname("Soldier's Corpse#dk2");
				enablenpc instance_npcname("Soldier's Corpse#dk3");
				enablenpc instance_npcname("Soldier's Corpse#dk4");
		}
		end;
}

1@spa,199,214,1	script	Lurid Royal Guard#dk4	4_M_SAKRAYROYAL,{
		.@prince$ = instance_npcname("Visiting Prince#dk");
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "เจ้าหญิงเทียร่าอยู่ไหน?";
		npctalk "ราชองครักษ์สุดสยอง: เจ้าหญิงเทียร่าอยู่ที่ไหน?";
		next;
		mes "[เจ้าชายมาเยือน]";
		mes "คุณมาช้าไป...แต่คุณนำสิ่งที่ฉันกำลังมองหามาให้ ขอบคุณสำหรับความช่วยเหลือของคุณ";
		npctalk "เจ้าชายผู้มาเยือน: คุณมาช้าไป... แต่คุณนำสิ่งที่ฉันกำลังมองหามาให้ ขอบใจมากสำหรับความช่วยเหลือของคุณ", .@prince$;
		next;
		mes "[เจ้าชายมาเยือน]";
		mes "ดาบเวทมนตร์ต้องคำสาปทาร์ทาโนส! คุณพร้อมที่จะมีเจ้านายคนใหม่หรือยัง?";
		npctalk "Visiting Prince: ดาบเวทมนตร์ต้องคำสาปทาร์ทานอส! คุณพร้อมที่จะมีเจ้านายคนใหม่หรือยัง?", .@prince$;
		next;
		cutin "tartanos.bmp",3;
		mes "[ดาบวิเศษทาร์ทานอส]";
		mes "คุณกล้าได้ยังไง!";
		npctalk "ดาบวิเศษทาร์ทานอส: เจ้ากล้าดียังไง!", instance_npcname("Magic Sword Tartanos#dk2");
		next;
		mes "[เจ้าชายมาเยือน]";
		mes "อาอาอา !!!";
		npctalk "เจ้าชายผู้มาเยือน: อ่า อ่า!!!", .@prince$;
		donpcevent instance_npcname("#gp_control_5") + "::OnStart";
		close2;
		cutin "",255;
		end;
}
1@spa,201,214,1	duplicate(Lurid Royal Guard#dk)	Magic Sword Tartanos#dk2	CLEAR_NPC
1@spa,197,218,5	duplicate(Lurid Royal Guard#dk)	Visiting Prince#dk	4_M_KNIGHT_SILVER
1@spa,194,214,5	duplicate(Lurid Royal Guard#dk)	King's Corpse#dk	4_M_TRISTAN
1@spa,211,194,3	duplicate(Lurid Royal Guard#dk)	Captain's Corpse#dk	4_M_LIEMAN
1@spa,201,198,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk	4_M_DIEMAN
1@spa,197,190,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk1	4_M_DIEMAN
1@spa,191,207,7	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk2	4_M_DIEMAN
1@spa,206,209,7	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk3	4_M_DIEMAN
1@spa,189,195,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk4	4_M_DIEMAN

1@spa,1,1,1	script	#gp_control_5	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_5");
		disablenpc instance_npcname("Magic Sword Tartanos#dk2");
		disablenpc instance_npcname("Visiting Prince#dk");
		disablenpc instance_npcname("Lurid Royal Guard#dk4");
		disablenpc instance_npcname("King's Corpse#dk");
		disablenpc instance_npcname("Captain's Corpse#dk");
		disablenpc instance_npcname("Soldier's Corpse#dk");
		disablenpc instance_npcname("Soldier's Corpse#dk1");
		disablenpc instance_npcname("Soldier's Corpse#dk2");
		disablenpc instance_npcname("Soldier's Corpse#dk3");
		disablenpc instance_npcname("Soldier's Corpse#dk4");
		sleep 1000;
		.@label$ = instance_npcname("#gp_control_5") + "::OnMyMobDead";
		monster 'map_spa$,198,218, "Torturous Redeemer",2959,1, .@label$;	// TORTUROUS_REDEEMER
		monster 'map_spa$,198,182, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
		end;
OnMyMobDead:
		if (mobcount('map_spa$, instance_npcname("#gp_control_5") + "::OnMyMobDead") < 1) {
				deltimer instance_npcname("#gp_control_4") + "::OnSpawn1";	// stop double spawn if timer is running
				mapannounce 'map_spa$, "The passage on the 5th floor of the palace is open.", bc_map,0xFFFF00;
				disablenpc instance_npcname("4th floor in the palace");
				enablenpc instance_npcname("5th floor of Palace");
				enablenpc instance_npcname("Princess Tiara#dk1");
				enablenpc instance_npcname("Lurid Royal Guard#dk5");

				killmonster 'map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead2";
				killmonster 'map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead3";
				disablenpc instance_npcname("#gp_control_3");
		}
		end;
}

// 5th floor
1@spa,216,43,3	script	Princess Tiara#dk1	4_F_MAYSEL,{
		.@guard$ = instance_npcname("Lurid Royal Guard#dk5");
		mes "[เจ้าหญิงเทียร่า]";
		mes "คุณรักษาสัญญาของคุณแล้ว";
		npctalk "เจ้าหญิงเทียร่า: คุณรักษาสัญญาของคุณแล้ว";
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "เจ้าหญิง!";
		npctalk "ราชองครักษ์สุดเซ็กซี่: เจ้าหญิง!", .@guard$;
		next;
		cutin "npc-tiara.bmp",3;
		mes "[เจ้าหญิงเทียร่า]";
		mes "ขอบคุณนะ...ตอนนี้ฉันสามารถพักผ่อนอยู่ข้างๆเธอได้แล้ว...";
		npctalk "เจ้าหญิงเทียร่า: ขอบคุณนะ...ตอนนี้ฉันสามารถพักผ่อนอยู่ข้างๆเธอได้แล้ว...";
		next;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "ไม่นะ..ไม่นะ!!!!!";
		npctalk "ราชองครักษ์สุดสยอง: ไม่นะ..ไม่นะ!!!!!", .@guard$;
		next;
		mes "[เสียงของเจ้าหญิงเทียร่า]";
		mes "ฉันอยากอยู่กับคุณตลอดไปอย่างสงบสุข...ตลอดไป...";
		npctalk "เจ้าหญิงเทียร่า: ฉันอยากอยู่กับคุณตลอดไปอย่างสงบสุข...ตลอดไป...";
		next;
		cutin "b-tiara.BMP",3;
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "ไม่นะ..... อ่า อ่า อ่า!!!!~!";
		npctalk "ราชองครักษ์สุดสยอง: ไม่นะ.....อาฮ่าฮ่าฮ่า!!!!~!", .@guard$;
		next;
		mes "[เสียงของเจ้าหญิงเทียร่า]";
		mes "ฉันรักคุณ...";
		npctalk "เจ้าหญิงเทียร่า: ฉันรักคุณ...";
		cutin "",255;
		close2;
		donpcevent instance_npcname("#gp_control_6") + "::OnStart";
		end;
}

1@spa,213,42,7	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk5	4_M_SAKRAYROYAL

1@spa,1,1,1	script	#gp_control_6	HIDDEN_WARP_NPC,{
		end;
OnStart:
		enablenpc instance_npcname("#gp_control_6");
		disablenpc instance_npcname("Princess Tiara#dk1");
		disablenpc instance_npcname("Lurid Royal Guard#dk5");

		killmonster 'map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead1";
		killmonster 'map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead2";
		killmonster 'map_spa$, instance_npcname("#gp_control_5") + "::OnMyMobDead";
		disablenpc instance_npcname("#gp_control_4");
		disablenpc instance_npcname("#gp_control_5");
		disablenpc instance_npcname("5th floor of Palace");
		sleep 2000;
		.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead1";
		monster 'map_spa$,217,52, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
		unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
		monster 'map_spa$,190,21, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
		unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
		monster 'map_spa$,198,49, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
		unittalk $@mobid[0], "What is this..??";
		monster 'map_spa$,212,28, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
		unittalk $@mobid[0], "Sakray killed a princess!";
		end;
OnMyMobDead1:
		if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead1") < 1) {
				sleep 2000;
				.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead2";
				monster 'map_spa$,217,43, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
				unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
				monster 'map_spa$,193,24, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
				unittalk $@mobid[0], "Sakray..?? why.....Sakray!";
				monster 'map_spa$,216,59, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
				unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
				monster 'map_spa$,197,57, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
				unittalk $@mobid[0], "Sakray killed a princess!";
		}
		end;
OnMyMobDead2:
		if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead2") < 1) {
				sleep 2000;
				.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead3";
				monster 'map_spa$,193,39, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
				unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
				monster 'map_spa$,212,34, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
				unittalk $@mobid[0], "Sakray..?? why.....Sakray!";
				monster 'map_spa$,201,64, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
				unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
				monster 'map_spa$,175,44, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
				unittalk $@mobid[0], "Sakray killed a princess!";
		}
		end;
OnMyMobDead3:
		if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead3") < 1) {
				sleep 2000;
				monster 'map_spa$,200,45, "Torturous Redeemer" ,2961,1, instance_npcname("#gp_control_6") + "::OnMyMobDead4";	// E_TORTUROUS_REDEEMER
				unittalk $@mobid[0], "Magic Sword Tartanos! I will destroy you if I can't have you... Oh no! How could this be?";
				sleep 5000;
				killmonster 'map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead4";
				enablenpc instance_npcname("Sakray#dk");
				enablenpc instance_npcname("Magic Sword Tartanos#dk3");
				disablenpc instance_npcname("#gp_control_6");
		}
		end;
OnMyMobDead4:
		end;
}

1@spa,196,44,5	script	Sakray#dk	4_M_SAKRAY,{
		mes "[ราชองครักษ์ผู้โหดร้าย]";
		mes "พอใจมั้ย..? ดาบวิเศษทาร์ทานอส?";
		npctalk "ราชองครักษ์สุดสยอง: พอใจหรือยัง..? ดาบวิเศษทาร์ทานอส?";
		next;
		mes "[ดาบวิเศษทาร์ทานอส]";
		mes "ฉันต้องการเลือดเพิ่ม..ศักราย เสนอเลือดของเขาให้ฉันหน่อย";
		npctalk "ดาบวิเศษทาร์ทานอส: ฉันต้องการเลือดเพิ่ม..ซาเครย์ มอบเลือดของเขาให้ฉันหน่อย", instance_npcname("Magic Sword Tartanos#dk3");
		next;
		mes "[ซากราย]";
		mes "โอเค นั่นดี นักเดินทาง เห็นมั้ย?";
		npctalk "สักราย: โอเค ดีเลย นักเดินทาง เห็นมั้ย?";
		next;
		mes "-" + strcharinfo(0) + "-";
		mes "สักรายต้องการอะไร ความเห็นอกเห็นใจ หรือเป็นกับดัก";
		unittalk getcharid(3), "" + strcharinfo(0) + " : Sakray, what do you want? Sympathy? Or is it a trap?";
		next;
		mes "[ซากราย]";
		mes "ไม่...ฉันไม่จำเป็นต้องพยายามจับมือใหม่";
		npctalk "สักราย: ไม่... ฉันไม่จำเป็นต้องพยายามจับมือใหม่";
		next;
		mes "-" + strcharinfo(0) + "-";
		mes "ถ้าอย่างนั้นล่ะ...ทำไมล่ะ?";
		unittalk getcharid(3), "" + strcharinfo(0) + " : If so... then why...?";
		next;
		cutin "cry-b.bmp",2;
		mes "[ซากราย]";
		mes "ฉันอยากกำจัดส่วนหนึ่งของความเป็นมนุษย์ในตัวฉันผ่านคุณ";
		npctalk "ศักราย: ฉันอยากจะกำจัดส่วนหนึ่งของความเป็นมนุษย์ในตัวฉันผ่านคุณ";
		next;
		mes "[ซากราย]";
		mes "ฉันค่อนข้างพอใจกับผลลัพธ์";
		npctalk "ศักราย: ผมค่อนข้างพอใจกับผลงานครับ.";
		next;
		mes "[ดาบวิเศษทาร์ทานอส]";
		mes "สักราย เอาเลือดมาให้ฉันเดี๋ยวนี้!";
		npctalk "ดาบวิเศษทาร์ทานอส: ซาเครย์ มอบเลือดให้ฉันเดี๋ยวนี้!", instance_npcname("Magic Sword Tartanos#dk3");
		next;
		mes "[ซากราย]";
		mes "ถ้าคุณเป็นฉันคุณจะทำอย่างไร?";
		npctalk "สักราย: ถ้าคุณเป็นฉัน คุณจะทำอย่างไร?";
		next;
		mes "[ซากราย]";
		mes "คุณสามารถปลดปล่อยตัวเองจากคำสาปนี้ได้หรือไม่?";
		npctalk "สักราย: คุณสามารถปลดปล่อยตัวเองจากคำสาปนี้ได้หรือไม่?";
		next;
		mes "-" + strcharinfo(0) + "-";
		mes "-";
		unittalk getcharid(3), "" + strcharinfo(0) + " : ...";
		next;
		mes "[ซากราย]";
		mes "หึ หึ...ฉันจะคอยเฝ้าดูเธอจนกว่าจะถึงวันที่เธอโตพอ";
		npctalk "สักราย: หึ หึ... ฉันจะคอยเฝ้าดูเธอจนกว่าจะถึงวันที่เธอโตพอ";
		next;
		mes "[ซากราย]";
		mes "ฉันเต็มใจที่จะตามล่าคุณในภายหลังเพื่อทาร์ทาโน";
		npctalk "สักราย: ข้ายินดีจะตามล่าคุณทาร์ทาโนสในภายหลัง";
		cutin "",255;
		next;
		mes "[ซากราย]";
		mes "ลาก่อนนะครับ.";
		npctalk "สักราย: ลาก่อนครับ.";
		close2;
		donpcevent instance_npcname("#tb") + "::OnStart";
		end;
}

1@spa,196,46,1	script	#tb	CLEAR_NPC,{
		end;
OnStart:
		disablenpc instance_npcname("Sakray#dk");
		disablenpc instance_npcname("Magic Sword Tartanos#dk3");
		enablenpc instance_npcname("#tb");
		enablenpc instance_npcname("#sv");
		enablenpc instance_npcname("#tv");
		initnpctimer;
		end;
OnTimer3000:
		npctalk "เสียงแห่งเจ้าหญิงเทียร่า : ฉันสามารถช่วยอะไรคุณได้บ้าง?";
		end;
OnTimer8000:
		npctalk "เสียงจากศักรินทร์ : ฉันจะอยู่เคียงข้างเธอเสมอ โปรดพักผ่อนเคียงข้างฉัน", instance_npcname("#sv");
		end;
OnTimer13000:
		npctalk "ดาบวิเศษทาร์ทานอส: ซาเครย์ อย่าลืมพันธสัญญาแห่งเลือดระหว่างเรานะ", instance_npcname("#tv");
		end;
OnTimer17000:
		npctalk "ดาบวิเศษทาร์ทานอส: จิตวิญญาณของคุณเป็นของฉันจนกว่าคุณจะพบเลือดที่ทำให้ฉันพอใจ...", instance_npcname("#tv");
		end;
OnTimer22000:
		npctalk "เสียงแห่งศักรินทร์ : จงพักผ่อนในเงาพร้อมกับฉัน", instance_npcname("#sv");
		end;
OnTimer25000:
		npctalk "เสียงแห่งศักรินทร์ : ในความมืดมิดชั่วนิรันดร์...", instance_npcname("#sv");
		end;
OnTimer28500:
		disablenpc instance_npcname("#tb");
		disablenpc instance_npcname("#sv");
		disablenpc instance_npcname("#tv");
		mapannounce 'map_spa$, "It is time to leave the Ghost Palace.", bc_map,0xFFFF00;
		enablenpc instance_npcname("Way out of Palace");
		enablenpc instance_npcname("King#dk11");
		end;
}

1@spa,195,41,1	duplicate(Lurid Royal Guard#dk)	Magic Sword Tartanos#dk3	CLEAR_NPC
1@spa,198,43,1	duplicate(Lurid Royal Guard#dk)	#sv	CLEAR_NPC
1@spa,196,41,1	duplicate(Lurid Royal Guard#dk)	#tv	CLEAR_NPC

1@spa,204,29,1	script	King#dk11	4_M_RUSKING,{
		disable_items;
		mes "[กษัตริย์]";
		mes "เฮ้ คุณยังมีชีวิตอยู่มั้ย?";
		mes "ฉันไม่คาดหวังว่าจะได้พบใครที่ยังมีชีวิตอยู่";
		next;
		.@item_name_req$ = getitemname(6672);// Gray_Shard
		mes "[กษัตริย์]";
		mes "คุณอาจจะมี ^006400" + .@item_name_req$ + "^000000?";
		mes "คุณให้ฉันได้ไหม?";
		mes "ความทรงจำของทหารราชองครักษ์ผู้ซื่อสัตย์";
		mes "และความทรงจำของทุกคนที่เขารัก";
		mes "ถูกบรรจุอยู่ภายในเศษชิ้นส่วนเหล่านี้";
		next;
		mes "[กษัตริย์]";
		mes "คุณจะให้ ^006400 หรือไม่" + .@item_name_req$ + "^000000 สำหรับฉันเหรอ?";
		mes "ความรู้สึกผิดกำลังกัดกินจิตวิญญาณของฉัน";
		mes "วิญญาณของฉันติดอยู่ในวังผี";
		next;
		mes "[กษัตริย์]";
		mes "ฉันกำลังเก็บรวบรวมความทรงจำที่ถูกทิ้งร้างของ Sakray จากเศษชิ้นส่วนเหล่านี้";
		next;
		mes "[กษัตริย์]";
		mes "หากคุณให้ ^006400" + .@item_name_req$ + "^000000s สำหรับฉัน ฉันสามารถสร้างอุปกรณ์สำหรับคุณได้";
		next;
		mes "[กษัตริย์]";
		mes "อัศวินผู้ถูกสาป...";
		mes "โปรดช่วยผมรักษาดวงวิญญาณของศักรินทร์ด้วย";
		next;
		setarray .@item_name$[0],
				"Thanos Sword", "Thanos Broadsword", "Thanos Spear", "Thanos Two-Handed Spear", "Thanos Staff",
				"Thanos Two-handed Staff", "Thanos Bow", "Thanos Dagger", "Thanos Katar", "Thanos Knuckle",
				"Thanos Hammer", "Thanos Axe", "Thanos Violin", "Thanos Whip",
				"Gray Shield", "Gray Armor", "Gray Robe", "Gray Cloak", "Gray Shoes", "Gray Helmet";
		setarray .@items_list[0],13441,21009,1438,1496,1669,2023,18119,13093,28000,1836,16028,28100,1933,1988,2187,15090,15091,20721,22033,18820;

		for ( .@i = 0; .@i < 20; .@i++ )
				.@menu$ += .@item_name$[.@i] + ":";
		.@menu$ += "Cancel";

		while(true) {
				.@s = select(.@menu$) - 1;
				if (.@s == 20) {
						mes "[กษัตริย์]";
						mes "สักวันหนึ่งศักรายจะ";
						mes "หลุดพ้นจากคำสาปนี้ได้แน่นอน";
						mes "ฉันเชื่อ";
						mes "^006400" + .@item_name_req$ + "^000000s เป็นวิธีเดียวเท่านั้น";
						mes "เพื่อช่วยเขา....";
						close;
				}
				.@cost = ( .@s > 13 ? 100 : 200 );
				mes "[กษัตริย์]";
				mes "เพื่อสร้าง ^FF0000" + .@item_name$[.@s] + "^000000";
				mes "ฉันต้องการ" + .@cost + "^006400" + .@item_name_req$ + "^000000.";
				next;
				if (select( "Cancel", "Hand over " + .@cost + " " + .@item_name_req$ + "." ) == 1) {
						mes "[กษัตริย์]";
						mes "สักวันหนึ่งศักรายจะ";
						mes "หลุดพ้นจากคำสาปนี้ได้แน่นอน";
						next;
						continue;
				}
				if (countitem(6672) < .@cost) {
						mes "[กษัตริย์]";
						mes "คุณไม่มีพอ ^006400" + .@item_name_req$ + "^000000";
						mes "เพื่อทำ ^FF0000" + .@item_name$[.@s] + "^000000.";
						mes "ฉันต้องการ" + .@cost + "^006400" + .@item_name_req$ + "^000000.";
						next;
						continue;
				}
				mes "[กษัตริย์]";
				mes "ฉันจะทำ ^FF0000" + .@item_name$[.@s] + "^000000";
				mes "กับ" + .@cost + "^006400" + .@item_name_req$ + "^000000.";
				next;
				mes "คุณมองเห็นแสงสว่างผ่านมือที่พับไว้ของกษัตริย์ และค่อย ๆ เปลี่ยนเป็นรูปร่างใหม่";
				next;
				mes "[กษัตริย์]";
				mes "เอาล่ะ เสร็จเรียบร้อยแล้ว";
				mes "เอาอันนี้ ^FF0000" + .@item_name$[.@s] + "^000000";
				mes "เพื่อเผชิญหน้ากับศักรายและตาร์ทานอส";
				next;
				mes "[กษัตริย์]";
				mes "อำนาจต้องอาศัยการเสียสละ";
				mes "โปรดระวังนะจ๊ะ...";
				delitem 6672,.@cost;// Gray Shard
				getitem .@items_list[.@s],1;
				close;
		}
}

1@spa,210,28,0	script	Way out of Palace	WARPNPC,2,2,{
		end;
OnTouch_:
		mes "ถึงเวลาที่จะต้องออกจากวังผีแล้ว";
		mes "ตอนนี้ทุกอย่างก็เสร็จสมบูรณ์แล้ว";
		mes "และจะหายไป..";
		close2;
		if (isbegin_quest(1263) == 2)
				getitem 6672,1;// Gray Shard
		else {
				setquest 1263;// Cursed Swordman
				completequest 1263;
				getexp 900000,500000;
				getitem 6672,2;// Gray Shard
		}
		warp "dali02",46,129;
		end;
}

1@spa,41,217,0	warp	The second floor of Palace	2,2,1@spa,114,120
1@spa,117,137,0	warp	3rd floor in the palace	2,2,1@spa,60,43
1@spa,54,28,0	warp	4th floor in the palace	2,2,1@spa,218,186
1@spa,178,186,0	warp	5th floor of Palace	2,2,1@spa,186,57
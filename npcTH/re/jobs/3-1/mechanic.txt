//===== rAthena Script ======================================= 
//= Mechanic Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Jobchange Quest from Blacksmith / Whitesmith -> Mechanic.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Fixed the Door NPC [JayPee].
//= 1.2 Updated script. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//============================================================ 

yuno,129,156,3	script	Chainheart	923,{
	mes "[Chainheart]";
	if (eaclass() & EAJL_THIRD) {
		mes "การใช้ชีวิตเป็นช่างเครื่องเป็นเรื่องยาก แต่ฉันก็มีความสุขที่สามารถทำสิ่งที่ฉันต้องการได้เสมอ";
		close;
	}
	if (job__mechanic == 12) {
		mes "ไม่มีอะไรจะพูดโดยเฉพาะเกี่ยวกับกลไกหรือเครื่องจักรเวทมนตร์";
		mes "ตอนนี้คุณน่าจะรู้เรื่องนี้มากกว่าใครๆ";
		next;
		mes "[Chainheart]";
		mes "พัฒนาความรู้ของคุณอย่างต่อเนื่องโดยการสัมผัสประสบการณ์ต่างๆ ที่โลกมีให้";
		close;
	} else if (job__mechanic == 11) {
		if (BaseJob == Job_Blacksmith && BaseLevel > 98 && JobLevel > 49 && SkillPoint == 0) {
			if (ismounting()) {
				mes "คุณกำลังขี่สัตว์เลี้ยงอยู่ ดังนั้นคุณจึงไม่สามารถเปลี่ยนงานได้";
				mes "กรุณาถอดสัตว์เลี้ยงขี่ของคุณออกแล้วลองอีกครั้ง!";
				close;
			}
			mes "เป็นอะไรรึเปล่า นั่นคุณเหรอ";
			mes "เดี๋ยวนะ คุณดูแตกต่างไปจากครั้งสุดท้ายที่เจอคุณมากเลยนะ";
			mes "เกิดอะไรขึ้นเหรอ?";
			next;
			mes "[Chainheart]";
			mes "อ๋อ! คุณไปที่ Juperos เหมือนที่ฉันบอก เหมือนที่ Franklson ทำ";
			next;
			mes "[Chainheart]";
			mes "ดูเหมือนไม่จำเป็นต้องถามคำถามเพิ่มเติมอีกแล้ว";
			mes "ฉันมั่นใจว่าคุณได้รับความรู้เพียงพอที่จะเป็น mechanic ตัวจริง";
			next;
			mes "[Chainheart]";
			mes "ไม่มีอะไรจะพูดได้มากกว่านี้อีกแล้ว";
			mes "คุณเป็น Mechanic ที่เก่งมากอยู่แล้ว";
			mes "อย่าเสียประสาทไปกับ";
			mes "ตำแหน่ง สิ่งที่สำคัญที่สุดคือคุณมีความรู้";
			next;
			mes "[Chainheart]";
			mes "คุณดูไม่ใช่คนประเภทที่จะถูกขังไว้ในห้องแล็ปหรือในห้องสมุด";
			mes "คุณเป็นคนประเภทที่เรียนรู้ด้วยการมีส่วนร่วมอย่างแข็งขัน";
			next;
			mes "[Chainheart]";
			mes "ฉันชอบที่คุณเป็นและฉันคิดว่าเราต้องการเลือดใหม่แบบนั้นมาสู่ Mechanics ของเรา";
			next;
			mes "[Chainheart]";
			mes "วิทยาศาสตร์คือการแสวงหาและศึกษาสิ่งต่างๆ ที่สามารถพิสูจน์ได้อย่างมีเหตุผล";
			mes "แปลกตรงที่วิทยาศาสตร์มักเริ่มต้นจากทฤษฎีที่ไม่แน่นอน เช่นเดียวกับเวทมนตร์";
			next;
			mes "[Chainheart]";
			mes "อย่างที่ฉันบอกก่อนหน้านี้ ถ้ามีความมุ่งมั่นและพรสวรรค์จริงๆ ก็จะไม่มีปัญหาอะไรเลย!";
			mes "ขอแสดงความยินดีที่ได้ก้าวไปสู่เส้นทางใหม่ในฐานะช่างเครื่อง!";
			next;
			set job__mechanic,12;
			completequest 10101;
			if (Sex == SEX_MALE)
				getitem 5749,1; //Driver_Band
			else
				getitem 5760,1; //Driver_Band_
			getitem 2795,1; //Green_Apple_Ring
			jobchange roclass(eaclass()|EAJL_THIRD);
			mes "[Chainheart]";
			mes "นี่คือของขวัญที่ฉันมอบให้คุณ";
			mes "ถ้าคุณรักษาสิ่งนี้ไว้อย่างดี สักวันหนึ่งคุณจะขอบคุณฉัน";
			next;
			mes "[Chainheart]";
			mes "ไปเถอะ โลกกว้างใหญ่นี้คือห้องทดลองของคุณ ทุกสิ่งในโลกนี้จะเป็นตำราเรียนของคุณ ดังนั้น ขอให้มีสิ่งดีๆ ในอนาคตของ Mechanics!";
			close;
		}
		mes "ดูเหมือนว่ามันยังมีอะไรบางอย่างไม่เพียงพอใช่ไหม?";
		close;
	} else if (job__mechanic >= 3) {
		mes "ว่าแต่ Franklson หายตัวไป";
		mes "เขาบอกว่าเขาอยากเรียนหนังสือ แล้วก็หายตัวไป";
		mes "เขาไปอยู่ที่ไหนได้?";
		next;
		mes "[Chainheart]";
		mes "หากคุณยังคงเดินต่อไปจนเป็น Mechanic คุณอาจจะต้องมาเจอเขาเข้าก็ได้";
		mes "โชคชะตาเล่นตลกในแบบที่แปลกประหลาดนะเพื่อน";
		next;
		mes "[Chainheart]";
		mes "เอาล่ะ ลาก่อนนะ";
		close;
	} else if (job__mechanic == 2) {
		mes "ฉันคิดว่าฉันได้อธิบายทุกอย่างเรียบร้อยแล้ว มีอะไรอีกไหมที่คุณอยากรู้?";
		next;
		switch(select("ฉันอยากรู้เพิ่มเติม:ไม่มีอะไร")) {
		case 1:
			mes "[Chainheart]";
			mes "เอ่อ... คุณอยากรู้เรื่องเครื่องจักรเวทมนตร์มากกว่านี้ใช่ไหม";
			mes "อืม... ฉันบอกแล้วว่าคุณมีพรสวรรค์ มันยากนะ";
			next;
			mes "[Chainheart]";
			mes "คุณดูมีพรสวรรค์ดีนะ ดังนั้นมันคงไม่แย่สำหรับเราหรอกถ้าคุณจะกลายเป็น Mechanic ได้";
			mes "แต่โชคไม่ดีที่มันไม่ได้เป็นแบบนั้น";
			next;
			mes "[Chainheart]";
			mes "mechanic นักปราชญ์ช่างจะไม่ยอมรับคุณง่ายๆ และมอบความรู้ให้คุณโดยที่คุณไม่แสดงความพยายามอย่างแท้จริงเสียก่อน.";
			next;
			mes "[Chainheart]";
			mes "แน่นอนฉันเป็นนักวิชาการคนหนึ่ง ดังนั้นฉันคิดว่าฉันสามารถลองพิจารณาคุณก็ได้";
			next;
			mes "[Chainheart]";
			mes "อย่างไรก็ตาม ฉันคิดว่ามันไม่ยุติธรรมที่จะแยกคนเก่งๆ ออกไปโดยไม่ผ่านการทดสอบใดๆ";
			next;
			mes "[Chainheart]";
			mes "อืม...";
			next;
			mes "[Chainheart]";
			mes "จริงๆแล้ว ถ้าใครสอนคุณไม่ได้ คุณก็สามารถเรียนรู้ด้วยตัวเองได้";
			mes "ถ้าคนอื่นทำได้ ทุกคนก็ต้องคิดหาทางเองได้เหมือนกันใช่ไหม?";
			next;
			mes "[Chainheart]";
			mes "มันคงไม่ง่ายนัก แต่ถ้าคุณมีความมุ่งมั่นและมีความสามารถจริง ก็ไม่น่าจะมีปัญหาอะไร";
			next;
			mes "[Chainheart]";
			mes "ถ้าคิดดูดีๆ แล้ว...";
			mes "Franklson พัฒนากลไกเวทมนตร์ด้วยตัวเขาเอง";
			mes "แล้วทำไมคุณไม่ไปที่ซากปรักหักพังของจูเปรอสล่ะ?";
			next;
			mes "[Chainheart]";
			mes "อย่างที่ฉันบอกคุณไว้ก่อนหน้านี้ว่า หากคุณมีความมุ่งมั่นและพรสวรรค์ที่แท้จริง คุณจะต้องค้นพบสิ่งที่ใช่แน่นอน";
			next;
			mes "[Chainheart]";
			mes "ค้นหาความรู้ด้วยตัวคุณเองเหมือนที่เราทำ";
			mes "อย่าแค่รอให้ใครสักคนมาสอนคุณ จงแสดงให้เห็นถึงความหลงใหลที่คุณมี";
			next;
			set job__mechanic,3;
			changequest 10091,10092;
			mes "[Chainheart]";
			mes "อย่างไรก็ตาม ไม่ว่าคุณจะเลือกอะไร ฉันคาดหวังว่าคุณจะทำดีที่สุด";
			close;
		case 2:
			mes "[Chainheart]";
			mes "คนแปลกจริงๆ";
			mes "ฉันคิดว่าคุณมีคุณสมบัติที่จะเป็นช่างเครื่องตัวจริง";
			close;
		}
	} else if (job__mechanic == 1) {
		mes "โอเค ให้ฉันพักหายใจสักหน่อย";
		mes "คุณคงจะสนใจเครื่องจักรเวทมนตร์และโลกแห่ง Mechanics";
		mes "โอเค งั้นฉันจะอธิบายให้ฟัง";
		next;
		mes "[Chainheart]";
		mes "เครื่องจักรเวทมนตร์ถือกำเนิดจากการผสมผสานระหว่างเครื่องจักรและเวทมนตร์";
		mes "ทั้งสองอย่างยังไม่ได้เชื่อมต่อกันสำเร็จจนกระทั่งบัดนี้";
		next;
		mes "[Chainheart]";
		mes "หากคุณใช้เครื่องจักรวิเศษ คุณจะต้องคุ้นเคยกับการทำงานภายในของเครื่องจักรจริงๆ";
		next;
		mes "[Chainheart]";
		mes "สามารถใช้ได้อย่างมีประสิทธิภาพ โดยคุณไม่จำเป็นต้องพึ่งพาสิ่งที่ไม่แน่นอนและไม่มั่นคงเช่นเวทมนตร์ แล้วอะไรจะดีไปกว่านี้อีก";
		next;
		mes "[Chainheart]";
		mes "ผู้ที่ประดิษฐ์เครื่องจักรวิเศษคือ Franklson นักวิชาการจาก Schwarzwald Republic";
		next;
		mes "[Chainheart]";
		mes "Franklson ค้นพบเครื่องจักรโบราณใน ^FF0000Ruins of Juperos^000000^และผสมผสานเข้ากับพลังแห่งเวทมนตร์ และนั่นคือจุดเริ่มต้น";
		next;
		mes "[Chainheart]";
		mes "ดังนั้น Franklson จึงตั้งชื่อเครื่องจักรที่เพิ่งเกิดใหม่ว่า Magic Machinery";
		next;
		mes "[Chainheart]";
		mes "อย่างไรก็ตาม เนื่องจากสถานที่อื่นนอกเหนือจากจูโนไม่เป็นที่รู้จักมากนัก การศึกษาเกี่ยวกับเครื่องจักรวิเศษจึงดำเนินไปอย่างต่อเนื่องตั้งแต่นั้นเป็นต้นมา";
		next;
		mes "[Chainheart]";
		mes "เพราะพวกเราได้ค้นคว้าเกี่ยวกับเครื่องจักรวิเศษมาอย่างไม่สิ้นสุด ผู้คนจึงเริ่มเรียกพวกเราว่า ^FF0000Mechanics^000000.";
		mes "ชื่อมันก็ชัดเจนอยู่แล้ว ดังนั้นจึงติดปากมาโดยตลอด";
		next;
		mes "[Chainheart]";
		mes "อืม ฉันคงอธิบายเรื่อง Magic Machinery เสร็จแล้ว";
		mes "ฉันอยากอธิบายเพิ่มเติมแต่ว่ามันซับซ้อนเกินไป";
		mes "ฉันไม่อยากทำให้คุณเบื่อไปมากกว่านี้";
		next;
		set job__mechanic,2;
		changequest 10090,10091;
		mes "[Chainheart]";
		mes "อย่างไรก็ตาม หากคุณกลายมาเป็นคนหนึ่งในพวกเรา คุณสามารถสร้างคำจำกัดความและคำอธิบายเกี่ยวกับ Mechanics ของคุณเองได้";
		close;
	}
	if (BaseJob == Job_Blacksmith) {
		if (BaseLevel > 98 && JobLevel > 49 && SkillPoint == 0) {
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job__mechanic,1;
				setquest 10090;
				next;
			}
			mes "เมื่อมองเผินๆ คุณดูเป็นคนที่มีความสามารถรอบด้านมากในการจัดการกับเรื่องต่างๆ";
			mes "คุณดูมีทักษะมาก";
			mes "คุณสามารถเป็นคนๆ นั้นได้";
			next;
			mes "[Chainheart]";
			mes "อ่า ฉันไม่ได้แนะนำตัว";
			mes "ฉันคือ Chainheart ชื่อดัง";
			mes "ฉันเป็นช่างเครื่อง";
			mes "จริงๆ แล้วฉันไม่ได้โด่งดังขนาดนั้น";
			next;
			mes "[Chainheart]";
			mes "ฉันไม่คิดว่าคำว่า Mechanic จะคุ้นเคยกับคุณ";
			mes "จริงๆ แล้ว เมื่อเป็นเรื่องของคำอธิบาย มันเป็นงานใหม่ที่สามารถอธิบายได้ง่ายๆ";
			next;
			mes "[Chainheart]";
			mes "ผู้ที่ศึกษาและใช้เครื่องจักรวิเศษ นั่นคือ Mechanic";
			next;
			mes "[Chainheart]";
			mes "แล้วเครื่องจักรวิเศษที่ช่างเครื่องศึกษาและใช้คืออะไร?";
			mes "อธิบายได้ไม่ง่ายนัก แต่ฉันจะลองดู";
			next;
			setquest 10090;
			set job__mechanic,1;
			mes "[Chainheart]";
			mes "ขอโทษนะ ฉันเริ่มพูดมากเกินไปแล้ว";
			mes "ขอพักหายใจสักครู่";
			mes "ถ้าคุณสนใจ โปรดคุยกับฉันอีกครั้ง";
			close;
		}
		mes "คุณดูเหมือนคนที่มีไหวพริบในการจัดการกับเรื่องต่างๆ เป็นอย่างดี";
		next;
		mes "[Chainheart]";
		mes "พลังนั้นถูกใช้ไปกับการจัดการกับสิ่งง่ายๆ เท่านั้น";
		mes "นั่นจะไม่ทำให้คุณมีความสุขอย่างแท้จริง";
		next;
		mes "[Chainheart]";
		mes "ถูกต้อง... หากเจ้าต้องการเพลิดเพลินไปกับความรู้สึกที่แท้จริงของการจัดการและความล้ำลึก เจ้าจำเป็นต้องจัดการกับ ^FF0000Magic Machinery^000000!";
		next;
		mes "[Chainheart]";
		mes "Magic Machinery ยังไม่มีอยู่ในโลกมานานแล้ว ดังนั้นการรู้จักมันจึงไม่ใช่เรื่องธรรมดา";
		mes "มันแม่นยำมากและแตกต่างอย่างสิ้นเชิงจากอะไรก็ตามที่คุณเคยพบเจอในโลก";
		next;
		mes "[Chainheart]";
		mes "ใช่แล้ว และพวกที่จัดการกับเครื่องจักรเวทย์มนตร์นั้นถูกเรียกว่า ^FF0000Mechanics^000000.";
		mes "ฉันก็เป็นหนึ่งในช่างกลที่น่านับถือเหล่านั้นเหมือนกัน";
		next;
		mes "[Chainheart]";
		mes "ดูเหมือนคุณจะมีคุณสมบัติพอที่จะเป็นช่างเครื่องที่ดีได้";
		mes "นี่คือข้อกำหนดพื้นฐาน";
		next;
		mes "[Chainheart]";
		mes "หากคุณมาหาฉันหลังจากถึง ^FF0000Base level 99^000000 และ ^FF0000Job level 50^000000,";
		mes "ฉันจะนำทางคุณไปยังโลกแห่ง Mechanics";
		close;
	}
	mes "การใช้ชีวิตเป็น Mechanic มันยาก แต่ฉันก็มีความสุขที่สามารถทำในสิ่งที่ฉันต้องการได้เสมอ";
	next;
	mes "[Chainheart]";
	mes "คุณไม่อยากเป็น Mechanic และมีความสุขที่แท้จริงบ้างเหรอ?";
	close;
}

jupe_cave,37,55,5	script	Scholar#Mechanic	883,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 300) {
		mes "- กระเป๋าเป้ของคุณหนักเกินไป -";
		mes "- โปรดลดน้ำหนักร่างกายก่อนแล้วลองใหม่อีกครั้ง -";
		close;
	}
	mes "[Scholar]";
	if (job__mechanic > 10) {
		mes "Juperos เป็นยังไงบ้าง?";
		mes "ฉันกลัวมากจนยังไปไม่ได้เลย";
		close;
	} else if (job__mechanic > 3) {
		mes "^FF0000คุณจะไปถึงจุดหมายไม่ได้ถ้าเดินไป";
		mes "^FF0000ศัตรูที่แข็งแกร่งไม่ใช่ปัญหาที่นี่ คุณแค่ไม่สามารถไปถึงจุดหมายได้";
		mes "กรุณาทำให้ตัวเองรู้สึกเหมือนอยู่บ้าน^000000";
		next;
		mes "[Scholar]";
		mes "ถึงมันจะสบายแค่ไหน ฉันก็ไม่มีความตั้งใจที่จะไป";
		next;
		mes "[Scholar]";
		mes "โอเค ถ้าอย่างนั้น คุณพร้อมที่จะไปยังจุดกึ่งกลางที่ Juperos แล้วหรือยัง?";
		next;
		switch(select("ฉันพร้อมแล้ว!:ยังไม่พร้อม")) {
		case 1:
			mes "[Scholar]";
			mes "เอ่อ... คุณแสดงท่าทีจริงจังจนฉันรู้สึกเสียดายนิดหน่อยที่ไม่ได้ไปที่นั่น";
			mes "ฉันควรจะไปที่นั่นหรือเปล่า? เอ่อ... ร้องไห้เพราะนมหกไปก็ไร้ประโยชน์!";
			next;
			mes "[Scholar]";
			mes "เอาล่ะ ฉันจะส่งคุณไป";
			mes "ฉันหวังว่าคุณจะได้ความรู้ที่คุณต้องการ";
			close2;
			warp "jupe_core2",149,288;
			end;
		case 2:
			mes "[Scholar]";
			mes "อืม...ใช่แล้ว แน่นอนว่าไม่น่าแปลกใจที่คุณจะลังเล";
			next;
			mes "[Scholar]";
			mes "แต่ถ้าคุณไม่รับเส้นทางที่ฉันเปิดให้คุณ คุณจะไม่สามารถไปถึงจุดหมายได้";
			mes "อย่าเสียพลังงานของคุณไปเปล่าๆ นะ";
			next;
			mes "[Scholar]";
			mes "แน่นอนว่าไม่ว่าคุณจะสบายแค่ไหนฉันก็จะไม่ไปที่นั่น";
			close;
		}
	}
	mes "ฉันเป็นนักวิชาการที่เข้ามาเพื่อค้นคว้าซากปรักหักพังที่นี่ใน Juperos.";
	next;
	mes "[Scholar]";
	mes "แต่... จริงๆ แล้วเมื่อฉันพยายามจะก้าวต่อไป ฉันก็รู้สึกกลัว ฉันแทบจะเข้าไปข้างในไม่ได้เลย";
	mes "ฉันจะทำอย่างไรได้?";
	next;
	mes "[Scholar]";
	mes "ฉันได้ยินข่าวลือว่ามนุษย์ไม่เป็นที่ต้อนรับข้างในนั้น";
	mes "แต่การศึกษาเครื่องจักรอันน่ามหัศจรรย์ในนั้นน่าจะมีประโยชน์มาก";
	if (job__mechanic != 3) close;
	next;
	mes "[Scholar]";
	mes "อืม? คุณจะไป Juperos เพื่อหาอะไรด้วยเหรอ?";
	mes "ฉันไม่กล้าเข้าไปที่นั่นเลย";
	mes "ขอให้คุณโชคดี";
	next;
	mes "[Scholar]";
	mes "ฉันรู้ทางที่จะไปถึงจุดกึ่งกลางและฉันก็มีสิ่งของที่ต้องการทั้งหมด แต่ฉันกลัวเกินไป";
	next;
	mes "[Scholar]";
	mes "ดังนั้นหากคุณต้องการ ฉันจะส่งคุณไปที่ Juperos นายผู้เฒ่าผู้เฒ่าได้บอกฉันแล้วว่าจะต้องวาร์ปไปยังจุดกึ่งกลางของ Juperos อย่างไร";
	next;
	set job__mechanic,4;
	mes "[Scholar]";
	mes "จริงอยู่ที่มันง่าย แต่คุณไม่อาจหลีกเลี่ยงความกลัวได้ มันแย่มาก ยังไงก็ตาม หากคุณพร้อมที่จะไป โปรดบอกฉันด้วย";
	close;
}

jupe_core2,149,273,3	script	Ghostfire#1	802,{
	mes "[?]";
	if (job__mechanic == 11) {
		mes "ฉันสูญเสียร่างกายไปแล้ว เหลือเพียงจิตวิญญาณในสถานที่แห่งนี้";
		mes "ฉันจะอยู่และตายที่นี่ใน Juperos ตลอดไป...";
		close;
	} else if (job__mechanic == 10) {
		mes "รูปลักษณ์นั้น... เป็นอย่างนั้นจริงหรือ? เจ้าก็ได้รับความรู้เช่นกัน";
		mes "เมื่ออนาคตเต็มไปด้วยความเป็นไปได้...";
		mes "ทัศนคติของเด็กบริสุทธิ์อย่างเจ้าสามารถทำอะไรก็ได้...";
		next;
		mes "[?]";
		mes "ถ้าลองคิดดูตอนนี้ สิ่งที่มีค่าที่สุดในตอนนี้คงเป็นการได้รับความมั่นใจ มากกว่าความรู้เสียอีก";
		next;
		mes "[?]";
		mes "อย่าทำผิดอีกเหมือนอย่างที่ฉันทำอีก";
		mes "ฉันหวังว่าสักวันหนึ่งเธอจะได้รับความรู้ด้วยตัวเอง";
		next;
		mes "[?]";
		mes "ถ้าอย่างนั้นกลับไปหามนุษย์ที่รู้เรื่องเครื่องจักรเวทมนตร์แล้วแสดงหลักฐานว่าคุณได้รับความรู้นั้นมา...";
		next;
		mes "[?]";
		mes "ฉันเองที่สูญเสียร่างกายและจิตวิญญาณของฉันไป";
		mes "ฉันถูกจำกัดให้อยู่ในสถานที่นี้ Juperos... ฉันจะอยู่และตายไปพร้อมกับ Juperos ตลอดไป...";
		next;
		mes "[?]";
		mes " ชื่อของฉัน... ชื่อคือ... Fr...";
		close2;
		set job__mechanic,11;
		changequest 10100,10101;
		warp "yuno",157,83;
		end;
	} else if (job__mechanic > 4) {
		mes "ฉันสูญเสียร่างกายไปแล้ว เหลือเพียงจิตวิญญาณในสถานที่แห่งนี้";
		mes "ฉันจะอยู่และตายที่นี่ใน Juperos ตลอดไป...";
		close;
	} else if (job__mechanic > 2) {
		mes "ผู้มีความสามารถ";
		mes "ตรวจสอบให้แน่ใจว่าไม่มีศัตรูอยู่รอบๆ";
		mes "หากคุณมุ่งความสนใจมาที่ฉัน คุณอาจจะถูกโจมตี";
		next;
		if(select("เอาละ ทีหลังก็แล้วกัน :โฟกัส") == 1) close;
		mes "[?]";
		mes "ฉันเคยเป็นนักวิชาการด้านมนุษย์";
		mes "ฉันต้องการทราบถึงข้อจำกัดของวิทยาศาสตร์ที่มนุษย์ไม่เคยรู้มาก่อน ดังนั้น ฉันจึงเรียนรู้สิ่งต่างๆ มากมาย ศึกษา และค้นคว้า...";
		next;
		mes "[?]";
		mes "นั่นคือตอนที่ฉันค้นพบพลังเวทย์มนตร์ที่ไม่แน่นอน";
		next;
		mes "[?]";
		mes "สิ่งสำคัญของเวทมนตร์ที่ไม่แน่นอนก็คือ มันไม่มั่นคงและคาดเดาไม่ได้อย่างมาก";
		mes "ระหว่างที่ฉันเรียน ฉันได้พบกับเวทมนตร์ที่ไม่แน่นอนอย่างมาก";
		next;
		mes "[?]";
		mes "ความทรงจำเดียวที่ฉันมีคือตั้งแต่วันนั้นเป็นต้นมา";
		mes "ฉันติดอยู่ที่นี่ตั้งแต่นั้นเป็นต้นมา";
		next;
		mes "[?]";
		mes "Juperos คือสิ่งเดียวที่ฉันรู้จักตอนนี้";
		mes "ฉันจะตายที่นี่";
		next;
		mes "[?]";
		mes "ฉันไม่รู้ว่าคุณเป็นใคร...";
		mes "ฉันไม่อยากรู้หรอกว่าคุณอยากได้ความรู้แค่ไหน หาใครสักคนที่ใส่ใจสิ";
		next;
		if (job__mechanic == 3 || job__mechanic == 4)
			changequest 10092,10094;
		set job__mechanic,5;
		mes "[?]";
		mes "ความรู้ไม่ได้มาจากคนอื่น แต่มาจากความพยายามของตนเอง";
		mes "ค้นหาเส้นทางแห่งความรู้ของคุณเอง";
		close;
	}
	end;
}

jupe_core2,53,75,3	script	Ghostfire#2	802,{
	mes "[?]";
	if (job__mechanic > 5) {
		mes "Juperos... ฉัน...";
		mes "Juperos... สิ่งที่ฉันต้องการทั้งหมดมีอยู่ที่นี่...";
		mes "พลังของวิทยาศาสตร์...สวรรค์ของเครื่องจักร...";
		close;
	} else if (job__mechanic == 5) {
		mes "ผู้มีความสามารถ";
		mes "ตรวจสอบให้แน่ใจว่าไม่มีศัตรูอยู่รอบๆ";
		mes "หากคุณมุ่งความสนใจมาที่ฉัน คุณอาจจะถูกโจมตี";
		next;
		if(select("ถ้าอย่างนั้นก็ไว้คราวหลัง:โฟกัส") == 1) close;
		mes "[?]";
		mes "เมื่อฉันมีร่างกาย ฉันก็อยากจะพิสูจน์ความล้ำเลิศของวิทยาศาสตร์ให้กับคนที่เชื่อในเรื่องลี้ลับเท่านั้น...";
		next;
		mes "[?]";
		mes "ฉันต้องการใช้พลังเวทย์มนตร์ในรูปแบบที่สมเหตุสมผลซึ่งมองเห็นได้ ด้วยผลลัพธ์ที่แข็งแกร่งกว่า ฉันต้องการควบคุมพลังเวทย์มนตร์โดยใช้วิทยาศาสตร์....";
		next;
		mes "[?]";
		mes "ฉันต้องการแสดงให้เห็นว่าวิทยาศาสตร์นั้นยอดเยี่ยมในการควบคุมเวทมนตร์เช่นเดียวกับศาสตร์ลี้ลับ ฉันศึกษาหลายสิ่งหลายอย่างและพยายามอย่างหนักเพื่อให้วิทยาศาสตร์และเวทมนตร์อยู่ร่วมกันได้";
		next;
		mes "[?]";
		mes "การค้นคว้าวิจัยของฉันนำฉันมาที่นี่ Juperos ในที่แห่งนี้มีผลิตภัณฑ์ทางวิทยาศาสตร์มากมายที่สามารถกระตุ้นสมองของนักวิชาการได้...";
		next;
		mes "[?]";
		mes "ฉันพบว่าการรวมพลังเวทมนตร์เข้ากับวิทยาศาสตร์เป็นไปได้";
		mes "แม้ว่ามันจะไม่สมบูรณ์แบบ แต่ก็ดึงดูดนักวิชาการคนอื่นๆ ได้มากพอ...";
		next;
		mes "[?]";
		mes "เหตุการณ์ที่เกิดขึ้นล่าสุดในโลกทำให้เกิดความสับสน ดังนั้นจึงไม่ได้แบ่งปันผลการวิจัยของฉันกับคนอื่นๆ ในโลก";
		next;
		set job__mechanic,6;
		changequest 10094,10095;
		mes "[?]";
		mes "ดังนั้น... ฉันจึงกลับมาที่ Juperos เพื่อสืบหาความรู้ด้านวิทยาศาสตร์และเวทมนตร์ต่อไป";
		close;
	}
	end;
}

jupe_core2,242,62,3	script	Ghostfire#3	802,{
	mes "[?]";
	if (job__mechanic > 6) {
		mes "ฉันอยากรู้เพิ่มเติมเกี่ยวกับความรู้... ลักษณะพิเศษของมนุษย์...";
		mes "สิ่งที่จะต้องทำก่อนและภายหลัง...";
		mes "ประตูแห่งความจริงจะมาภายหลัง...";
		close;
	} else if (job__mechanic == 6) {
		mes "Talented one.";
		mes "ตรวจสอบให้แน่ใจว่าไม่มีศัตรูอยู่รอบๆ";
		mes "ถ้าคุณมุ่งความสนใจมาที่ฉัน คุณอาจจะถูกโจมตี";
		next;
		if(select("ถ้าอย่างนั้นก็ไว้คราวหลัง:โฟกัส") == 1) close;
		mes "[?]";
		mes "ฉันไม่พอใจกับความสำเร็จของฉัน ดังนั้นฉันจึงกลับมาที่นี่ครั้งแล้วครั้งเล่า";
		next;
		mes "[?]";
		mes "เมื่อฉันก้าวไปบนฐานของรูปปั้นนี้ พลังงานที่ฉันรวบรวมและเรียนรู้มาก็ถูกดูดซับไป";
		next;
		mes "[?]";
		mes "แต่เมื่อพลังงานนั้นถูกดูดซับเข้าไป ฉันก็เริ่มได้รับความรู้ที่ไม่เคยรู้มาก่อน";
		next;
		mes "[?]";
		mes "แต่ก็ต้องแลกมาด้วยสิ่งที่ต้องจ่ายราคา ด้วยความรู้นั้น ฉันจึงเริ่มสูญเสียความเป็นมนุษย์ไป";
		mes "ฉันสูญเสียร่างกายไป และสิ่งเดียวที่ฉันมีก็คือจิตวิญญาณ";
		next;
		mes "[?]";
		mes "ฉันไม่เสียใจเลยที่ได้เรียนรู้ ฉันหวังเพียงว่าร่างกายของฉันจะกลับมาเหมือนเดิม";
		next;
		set job__mechanic,7;
		changequest 10095,10096;
		mes "[?]";
		mes "หลังจากที่ฉันเทพลังลงไปในรูปปั้นแล้ว ฉันก็คิดว่าฉันสามารถย้ายพลังของฉันไปยังรูปปั้นอื่นๆ ได้";
		close;
	}
	end;
}

jupe_core2,29,150,0	script	#Foothold	139,1,1,{
OnTouch_:
	if (job__mechanic == 8) {
		percentheal 0,-30;
		mes "-ทันใดนั้น ฉันรู้สึกว่าพลังกำลังไหลออกจากร่างกายของฉัน เกิดอะไรขึ้น?-";
		next;
		mes "-ค้นหาประตูแห่งความจริง มันเป็นแหล่งที่มาของการระบายพลัง-";
		close;
	} else if (job__mechanic == 7) {
		percentheal -20,0;
		mes "-จู่ๆ ก็มีเสียงที่ไม่ปรากฏชื่อดังขึ้นในหัวของฉัน มันคืออะไร?-";
		next;
		mes "-จัดการเครื่องจักรที่ดูดซับพลังเวทย์มนตร์ เครื่องบูชา รูปปั้นมนุษย์-";
		next;
		set job__mechanic,8;
		changequest 10096,10097;
		mes "-สิ่งที่แน่นอนที่สุดก็คือ ฉันเริ่มรู้สึกต้องการจะไปยังประตูแห่งความจริง-";
		close;
	} else if (job__mechanic > 2) {
		percentheal 0,-30;
		mes "-จู่ๆผมก็รู้สึกว่ามีพลังงานไหลออกจากร่างกายผม เกิดอะไรขึ้น?-";
		close;
	}
	end;
}

jupe_core2,272,149,0	script	#Foothold2	139,1,1,{
OnTouch_:
	if (job__mechanic == 9) {
		mes "-ฉันรู้สึกเหมือนว่าพลังกำลังจะหมดลง แต่ตอนนี้ฉันไม่รู้สึกอะไรเลย-";
		next;
		mes "-ไปทางทิศใต้ของจุดพักอย่างรวดเร็ว-";
		close;
	} else if (job__mechanic == 8) {
		percentheal -20,-30;
		mes "-จู่ๆ ฉันก็รู้สึกว่าพลังกำลังหลุดออกจากร่างกายของฉัน เกิดอะไรขึ้น?-";
		next;
		mes "-ฉันรู้สึกว่าประสาทสัมผัสของฉันกำลังกลับคืนมา-";
		next;
		donpcevent "#Door::OnEnable";
		changequest 10097,10098;
		mes "-ฉันคิดว่าฉันกำลังกลายเป็นเครื่องบูชา.-";
		close;
	} else if (job__mechanic > 2) {
		percentheal 0,-30;
		mes "-จู่ๆผมก็รู้สึกว่ามีพลังงานไหลออกจากร่างกายผม เกิดอะไรขึ้น?-";
		close;
	}
	end;
}

jupe_core2,288,142,3	script	#Door	844,1,1,{
	end;
OnInit:
OnDisable:
	disablenpc "#Door";
	end;
OnEnable:
	enablenpc "#Door";
	specialeffect EF_MAPPILLAR;
	initnpctimer;
	end;
OnTimer180000:
	donpcevent "#Door::OnDisable";
	end;
OnTouch_:
	mes "-อีกครั้งหนึ่งมีเสียงที่ดังก้องอยู่ในหัวของฉัน ครั้งนี้มันแตกต่างจากครั้งก่อน มันเป็นเสียงของบุคคลที่น่าประทับใจ-";
	next;
	mes "ฉันรู้ว่าคุณมาถึงจุดนี้ได้เพราะคุณต้องการความรู้";
	mes "มนุษย์เป็นสัตว์ที่อ่อนแอ และการเสียสละใดๆ ก็ตามเป็นการตัดสินใจที่ยากลำบากสำหรับพวกมัน";
	next;
	mes "แต่คุณขาดบางสิ่งบางอย่างที่จะผ่านประตูแห่งความจริงนี้ไปได้";
	mes "ประตูนี้มีความรู้เหนือกว่าความคิดของมนุษย์ทั้งหมด";
	next;
	mes "มันจะไม่เป็นอันตรายต่อจิตใจของมนุษย์ถ้ามันไม่เกินความสามารถของสมองของคุณ";
	next;
	mes "พยายามวางร่างกายของคุณบนฐานของความรู้ที่ทางใต้";
	next;
	mes "ถ้าความรู้เหมาะกับคุณ คุณจะสามารถรับมันได้โดยไม่ต้องกลายเป็นเครื่องบูชา";
	mes "ถ้าคุณไม่มีคุณสมบัติ คุณจะสูญเสียร่างกายของคุณเป็นเครื่องบูชา";
	next; mes "เมื่อคุณมีคุณสมบัติแล้ว คุณจะสามารถผ่านประตูแห่งความจริงได้";
	mes "จนกว่าจะถึงวันนั้น อย่าปล่อยให้ตัวเองหลุดลอยไป";
	next;
	mes "............";
	next;
	mes "-ฉันไม่ได้ยินเสียงใดๆ อีกต่อไป และฉันไม่รู้สึกอะไรในที่แห่งนี้เลย-";
	next;
	mes "-ตามที่เสียงที่ดังก้องอยู่ในหัวของฉันบอกไว้ว่า ให้ไปที่เชิงเขาทางตอนใต้กันเถอะ-";
	close2;
	set job__mechanic,9;
	changequest 10098,10099;
	donpcevent "#Door::OnDisable";
	end;
}

jupe_core2,149,34,0	script	#Acquiring Knowledge	139,1,0,{
OnTouch_:
	if (job__mechanic == 9) {
		mes "-ทันทีที่ฉันเหยียบที่มั่น เสียงนั้นไม่ได้เข้ามาในสมองของฉัน แต่เป็นความรู้ใหม่ ๆ ที่เข้ามาอย่างเป็นธรรมชาติ-";
		next;
		mes "-แทบจะทันที คุณก็ได้รับความรู้ทั้งหมดที่คุณต้องการรู้เกี่ยวกับกลศาสตร์และเครื่องจักรเวทมนตร์-";
		next;
		set job__mechanic,10;
		changequest 10099,10100;
		specialeffect EF_POTION_CON;
		mes "-ความรู้เท่านี้ก็พอแล้ว จัดการให้เสร็จและหาทางออกกัน-";
		close;
	}
	end;
}
jupe_core2,150,33,0	duplicate(#Acquiring Knowledge)	#Acquiring Knowledge2	139,1,0

jupe_core2,0,0	monster	Dimik	1669,100

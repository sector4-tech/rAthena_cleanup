//===== rAthena Script ======================================= 
//= Warlock Job Quest
//===== By: ================================================== 
//= Muad_Dib, Gepard & L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Wizard / High Wizard -> Warlock.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Masao]
//= 1.1 Fixed some conversion mistakes, replaced numbers with
//=     constants, added item names in comments.
//= 1.2 Fixed the problem where NPC are not enabled and chamber
//=     will never open for the second try or use.
//= 1.2a Fixed a typo. [Euphy]
//= 1.3 Updated script, optimized. [Euphy]
//= 1.3a Added temporary coordinate fix #distorted_space_1-1. [Euphy]
//= 1.3b Added official coordinates. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features. [Euphy]
//============================================================

-	script	#distorted_space_	-1,{
OnTouch_:
	if (job_wl > 1) {
		mes " - มีการบิดเบือนที่แปลกประหลาดตรงนี้ - ";
		if (strnpcinfo(0) == "#distorted_space_1" && (Class == Job_Warlock || Class == Job_Warlock_T || Class == Job_Baby_Warlock)) {
			close2;
			if (countitem(6153) > 0)
				warp "spl_in02",79,102;
			end;
		}
		next;
		switch(select("ยื่นมือของคุณออกไป : ไม่ต้องสนใจมัน")) {
		case 1:
			mes " - ฉันเหยียดแขนออกไปในขณะที่เสียงอันดุร้ายก้องก้องอยู่ในหูของฉัน และคลื่นเย็นๆ ก็โอบล้อมฉันไว้ - ";
			close2;
			warp "spl_in02",79,102;
			end;
		case 2:
			mes " - คุณละเลยการบิดเบือน - ";
			close;
		}
	}
	if (BaseLevel > 94 && (strnpcinfo(0) == "#distorted_space_2" || BaseJob == Job_Wizard)) {
		mes " - มีการบิดเบือนที่แปลกประหลาดตรงนี้ - ";
		next;
		switch(select("ค้นหาในพื้นที่: ไม่ต้องสนใจ")) {
		case 1:
			mes " - ดูเหมือนว่าจะไม่มีอะไรที่นี่ แต่ฉันรู้สึกได้ถึงบางอย่างที่นี่อย่างชัดเจน... - ";
			next;
			switch(select("ยืดแขนออกไป : ปล่อยมันไว้เฉยๆ")) {
			case 1:
				mes " - ฉันเหยียดแขนออกไปในขณะที่เสียงอันดุร้ายก้องก้องอยู่ในหูของฉัน และคลื่นเย็นๆ ก็โอบล้อมฉันไว้ - ";
				close2;
				warp "spl_in02",79,102;
				end;
			case 2:
				mes " - คุณตัดสินใจปล่อยมันไว้คนเดียว - ";
				close;
			}
		case 2:
			mes " - คุณไม่สนใจมัน - ";
			close;
		}
	}
	end;
}
//morocc,103,144,0	duplicate(#distorted_space_)	#distorted_space_1	139,1,1
morocc,95,132,0	duplicate(#distorted_space_)	#distorted_space_1	139,1,1
splendide,141,284,0	duplicate(#distorted_space_)	#distorted_space_2	139,1,1

function	script	F_Warlock	{
	mes "[Assistant]";
	mes "ห๊ะ!!! มนุษย์!?!";
	mes "คุณเข้ามาที่นี่ได้ยังไง?";
	mes "นี่มันเชื่อมต่อได้ด้วยประตูพิเศษเท่านั้น!";
	next;
	mes "[Assistant]";
	mes "ฉันไม่รู้ว่าจะ...";
	mes "แต่เราควรออกไปก่อนที่เจ้านายของฉันจะเห็นคุณ...!";
	next;
	mes "[???]";
	mes "เสียงอะไรอยู่ข้างนอกนั่น";
	mes "ฉันเตือนพวกเขาแล้วว่าให้เงียบในขณะที่ฉันกำลังทำสมาธิ";
	next;
	mes "[Assistant]";
	mes "ห๊ะ!?!";
	mes "อาจารย์...";
	mes "ไม่มีอะไรหรอก...";
	next;
	mes "[Master]";
	mes "อ๋อ~ มีมนุษย์อยู่ตรงหน้าฉัน";
	next;
	mes "[Assistant]";
	mes "โอ้ สิ่งนี้เหรอ?";
	mes "ฉันแค่กำลังนำมันออกมา";
	next;
	mes "[Master]";
	mes "โย้ป!!!";
	next;
	mes "[Yoop]";
	mes "เย้...!!";
	next;
	mes "[Master]";
	mes "นั่นคือประตูมิติที่อยู่เหนือคุณ... มันเป็นภาพลวงตาหรือเปล่า?";
	next;
	mes "[Yoop]";
	mes "ขอโทษนะ!!!";
	mes "มันเป็นความผิดของฉัน...";
	next;
	mes "[Master]";
	mes "คนโง่...";
	mes "ฉันจะจัดการกับคุณหลังจากที่คุณส่งมนุษย์คนนั้นออกไปแล้ว";
	next;
	mes "[Master]";
	mes "มุสซาร์ตัส อีอาร์ ไซมาร์";
	mes "UdenLo";
	if (BaseJob == Job_Wizard) {
		next;
		mes " - นางฟ้าผมสีเขียวกำลังร่ายมนต์สะกด - ";
		mes " - ฉันรู้สึกเหมือนว่ามีบางอย่างพิเศษอยู่รอบตัวฉัน - ";
		specialeffect2 EF_DISPELL;
		next;
		mes "[Master]";
		mes "โอ้ นั่นน่าสนใจนะ";
		mes "เจ้ากล้าต้านทานข้าได้อย่างไรมนุษย์?";
		next;
		if (BaseLevel > 98 && JobLevel > 49) {
			select("ฉันไม่แน่ใจ คุณเป็นใคร?");
			mes "[Master]";
			mes "ฉันเป็นหัวหน้าของ ^000077Warlock^000000.";
			mes "'^000077Sila Egnoloria^000000'";
			mes "ถ้าท่านเป็นพ่อมด การที่ท่านจดจำชื่อฉันก็คงจะดีไม่น้อย";
			next;
			select("^000077Warlock^000000 คืออะไร?");
			mes "[Master Egnoloria]";
			mes "Oh...are you interested?";
			next;
			mes "[Master Egnoloria]";
			mes "^000077Warlock^000000 คือผู้ใช้เวทย์มนตร์ของยักษ์แห่ง Jotunheim";
			mes "มันไม่สามารถนิยามเป็นคำอื่นได้";
			next;
			mes "[Master Egnoloria]";
			mes "มีเพียงเวทมนตร์ที่แข็งแกร่งเท่านั้นที่จะอธิบายได้";
			next;
			select("ฉันจะสามารถกลายเป็น Warlock ได้ไหม?");
			mes "[Master Egnoloria]";
			mes "คุณ?";
			mes "คุณต้องการที่จะกลายเป็น Warlock ใช่ไหม?";
			mes "ฮ่าๆๆ";
			next;
			mes "[Master Egnoloria]";
			mes "คุณน่าสนใจมาก";
			mes "คงเป็นเรื่องยากสำหรับมนุษย์ที่จะกลายเป็นวอร์ล็อค";
			mes "พลังที่แข็งแกร่งจะกัดกินร่างกายที่อ่อนแอของคุณได้";
			next;
			mes "[Master Egnoloria]";
			mes "หากคุณกล้าเผชิญหน้ากับความตาย คุณสามารถพยายามที่จะเป็นมันได้";
			next;
			switch(select("ฉันจะต้องทำอย่างไร:ยกเลิก")) {
			case 1:
				if (VIP_SCRIPT) {
					callfunc "VIP_Third_Class";
					set job_wl,2;
					setquest 11106;
					next;
				}
				mes "[Master Egnoloria]";
				mes "คุณจริงจังมั้ย?";
				mes "คุณหิวกระหายอำนาจใช่ไหมมนุษย์";
				next;
				mes "[Master Egnoloria]";
				mes "ความปรารถนาอันแรงกล้าที่จะทรงพลังมากขึ้นทำให้จิตวิญญาณของคุณสวยงามยิ่งขึ้น";
				next;
				mes "[Master Egnoloria]";
				mes "แต่ฉันต้องตรวจสอบดูก่อนว่าคุณมีคุณสมบัติไหม";
				next;
				mes "[Master Egnoloria]";
				mes "โย้ป!";
				next;
				mes "[Yoop]";
				mes "เย้!!";
				next;
				mes "[Master Egnoloria]";
				mes "มอบสิ่งนี้ให้กับมนุษย์";
				next;
				mes "[Master Egnoloria]";
				mes "คุยกับผู้ชายที่มีผมประหลาด";
				mes "เขาจะแจ้งให้คุณทราบว่าคุณควรทำอย่างไร";
				set job_wl,2;
				setquest 11106;
				close;
			case 2:
				mes "[Master Egnoloria]";
				mes "ดี.";
				mes "คุณเป็นมนุษย์ ดังนั้นนั่นก็เพื่อคุณ";
				set job_wl,1;
				close2;
				warp "splendide",200,100;
				end;
			}
		}
		mes "[Master]";
		mes "คุณยังอยู่ในระดับต่ำเกินไปที่จะพิจารณาเรื่องนี้.";
	}
	close2;
	warp "splendide",200,100;
	end;
}

spl_in02,77,107,5	script	Assistant#Warlock1	446,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[Yoop]";
		mes "คุณ...เราเคยเจอกันมาก่อน";
		mes "ลองนึกภาพดูสิว่าฉันถูกเจ้านายดุอย่างรุนแรงขนาดไหน";
		mes "ถ้าคุณมีธุระอะไรกับฉัน ก็ไปให้พ้นเลย!";
		next;
		switch(select("ออก.:อยู่ต่อ.")) {
		case 1:
			mes "[Yoop]";
			mes "ปล่อยฉันไว้คนเดียว...!";
			close2;
			warp "splendide",200,100;
			end;
		case 2:
			mes "[Yoop]";
			mes "อะไรนะ?!";
			mes "ทำอะไรก็ได้ที่เธอต้องการ...";
			mes "งั้นนายของฉันจะจัดการกับเธอเอง";
			close;
		}
	} else if (job_wl == 2 || job_wl == 3) {
		mes "[Yoop]";
		if (job_wl == 2) {
			mes "เรียนท่านอาจารย์! ผมหางม้าของฉันดูตลกจังนะ?!";
			mes "โอ้ แต่ฉันใช้เวลาทำมันนานมากเลย!";
			next;
			mes "[Master Egnoloria]";
			mes "มันเสียงดัง";
			mes "ฉันต้องนั่งสมาธิ";
			mes "ก็ทำตามที่ฉันบอกสิ";
			next;
			mes "[Yoop]";
			mes "ห๊ะ..";
			mes "โอ้ ใช่!";
			next;
			mes "[Yoop]";
			mes "เฮ้ มนุษย์!";
			mes "คุณอยากเป็นวอร์ล็อคไหม?";
			next;
		}
		if (SkillPoint != 0) {
			mes "ขั้นแรก ให้ใช้คะแนนทักษะงานที่เหลือทั้งหมดของคุณก่อนดำเนินการต่อ";
			set job_wl,3;
			close;
		}
		mes "นี่เป็นของเจ้านาย";
		mes "การตัดสินใจแล้วแจ้งให้ฉันทราบด้วย";
		next;
		mes "[Yoop]";
		mes "เราต้องทดสอบว่าร่างกายของคุณสามารถต้านทานการเป็นวอร์ล็อคได้หรือไม่";
		mes "ดังนั้นคุณจึงต้องรวบรวมวัสดุเพื่อพิสูจน์ตัวเอง";
		next;
		mes "[Yoop]";
		mes "โปรดรวบรวม ^000077<ITEM>Glittering Crystal<INFO>6152</INFO></ITEM>^000000 และ ^000077<ITEM>Large Bradium Fragment<INFO>6151</INFO></ITEM>^000000.";
		next;
		callsub L_Quest,1;
		close;
	} else if (job_wl == 4) {
		if (countitem(6152) == 0 || countitem(6151) == 0)
			callsub L_Quest,0;
		else {
			mes "[Yoop]";
			mes "ฉันไม่สามารถทำภารกิจนี้ที่นี่ได้ ฉันต้องไปที่ห้องแล็ปของฉัน";
			mes "ห้องแล็ปของฉันตั้งอยู่ทางทิศตะวันตกเฉียงเหนือของ Splendide";
		}
		close;
	} else if (job_wl == 5) {
		mes "[Yoop]";
		mes "หินก้อนนี้ทำมาดีจริงๆ";
		mes "โปรดไปหาอาจารย์ของฉันด้วย";
		close;
	} else {
		mes "[Yoop]";
		mes "ห้องเวทมนตร์ถูกสร้างขึ้นจากวัสดุเวทมนตร์.";
		next;
		mes "[Yoop]";
		mes "หินรวมนี้จะมีรูปร่างหน้าตาแตกต่างกันออกไปตามวัตถุดิบและวิธีการบำบัด";
		next;
		mes "[Yoop]";
		mes "แต่หินส่วนใหญ่ก็ค่อนข้างคล้ายกัน!";
		mes "เราไม่สามารถแยกความแตกต่างระหว่างพวกมันได้ง่ายๆ";
		next;
		mes "[Yoop]";
		mes "แม้แต่ฉันเองก็ไม่รู้ว่ามันทำงานอย่างไร";
		close;
	}

L_Quest:
	mes "[Yoop]";
	mes "คุณต้องสวมแหวน <ITEM>Magical Stone<INFO>2797</INFO></ITEM> และ <ITEM>Magical Stone<INFO>2796</INFO></ITEM> เหล่านี้เพื่อรับไอเทมเหล่านี้ขณะล่า";
	mes "คุณต้องล่า Naga และ Bradium Golems ขณะสวมแหวนเหล่านี้";
	next;
	if (getarg(0)) {
		mes "[Yoop]";
		mes "แสดงให้เราเห็นว่าคุณแข็งแกร่งแค่ไหน และอย่าลืมสวมสิ่งนี้";
		set job_wl,4;
		getitem 2796,1; //Magical_Stone
		getitem 2797,1; //Magical_Stone_
		changequest 11106,11107;
		next;
	}
	mes "[Yoop]";
	mes "นำหินเหล่านี้มาด้วย มิฉะนั้นคุณจะกลับไม่ได้!";
	mes "ระวังตัวด้วย!";
	next;
	mes "[Yoop]";
	mes "คนข้างๆ อาจารย์ของฉันจะปล่อยคุณออกไปถ้าคุณคุยกับเขา";
	next;
	mes "[Yoop]";
	mes "ห้องทดลองของฉันอยู่ทางทิศตะวันตกเฉียงเหนือของสเปลนไดด์ เอามาให้ฉันเมื่อคุณรวบรวมมันได้แล้ว";
	return;
}

spl_in02,60,231,5	script	Yoop#Warlock	446,{
	mes "[Yoop]";
	if (job_wl < 4) {
		mes "คุณเป็นใคร";
		mes "ออกไปจากที่นี่!";
		close;
	} else if (job_wl == 4) {
		mes "กลับมาแล้วเหรอ?";
		next;
		switch(select("ฉันนำวัสดุมาแล้ว :ฉันทำหินวิเศษหายไป :ไม่มีอะไรเลย")) {
		case 1:
			mes "[Yoop]";
			if (countitem(6152) == 0 || countitem(6151) == 0) {
				mes "......";
				mes "......ที่ไหน?";
				mes "อย่ามากวนฉัน!";
				next;
				mes "[Yoop]";
				mes "เราต้องทดสอบว่าร่างกายของคุณสามารถต้านทานการเป็นวอร์ล็อคได้หรือไม่";
				mes "ดังนั้นคุณจึงต้องรวบรวมวัสดุเพื่อพิสูจน์ตัวเอง";
				next;
				mes "[Yoop]";
				mes "โปรดรวบรวม ^000077<ITEM>Glittering Crystal<INFO>6152</INFO></ITEM>^000000 และ ^000077<ITEM>Large Bradium Fragment<INFO>6151</INFO></ITEM>^000000.";
				next;
				mes "[Yoop]";
				mes "คุณต้องสวมแหวน <ITEM>Magical Stone<INFO>2797</INFO></ITEM> และ <ITEM>Magical Stone<INFO>2796</INFO></ITEM> เหล่านี้เพื่อรับไอเทมเหล่านี้ขณะล่า";
				mes "คุณต้องล่า Naga และ Bradium Golems ขณะสวมแหวนเหล่านี้";
				next;
				mes "[Yoop]";
				mes "นำหินเหล่านี้มาด้วย มิฉะนั้นคุณจะกลับไม่ได้!";
				mes "ระวังตัวด้วย!";
				close;
			} else if (countitem(6152) && countitem(6151)) {
				if (isequipped(2796) || isequipped(2797)) {
					mes "นำหินวิเศษกลับมาให้ฉัน";
					close;
				}
				if (countitem(2796) == 0 || countitem(2797) == 0) {
					mes "ดูเหมือนคุณจะไม่มีมันเหรอ?";
					mes "คุณทำมันหายเหรอ?";
					next;
					mes "[Yoop]";
					mes "โปรดค้นหาหินนั้นแล้วกลับมาหาฉัน";
					close;
				}
				mes "โอเค! วัสดุทั้งหมดพร้อมแล้ว!";
				mes "ในขณะที่ฉันกำลังตั้งใจทำงานกับหินก้อนนี้ คุณกลับไปหาเจ้านายของฉันได้ไหม";
				mes "ฉันจะติดตามต่อเมื่อทำเสร็จแล้ว";
				delitem 2796,countitem(2796); //Magical_Stone
				delitem 2797,countitem(2797); //Magical_Stone_
				delitem 6152,countitem(6152); //Glittering_Crystal
				delitem 6151,countitem(6151); //Peice_Of_Great_Bradium
				set job_wl,5;
				changequest 11107,11108;
				close;
			}
		case 2:
			mes "[Yoop]";
			mes "อะไรนะ?!";
			mes "ฉันขอให้คุณตรวจสอบอีกครั้งและให้แน่ใจว่าคุณใส่มัน!";
			next;
			mes "[Yoop]";
			mes "โปรดตรวจสอบว่าคุณวางมันผิดที่หรือว่าคุณได้สวมใส่มันไว้แล้ว";
			next;
			switch(select("ฉันเจอแล้ว.:ไม่หรอก.")) {
			case 1:
				mes "[Yoop]";
				mes "อย่าแกล้งฉันสิ!";
				mes "มันไม่ยากเลยที่จะบอกว่าคุณมีมันหรือเปล่า!";
				close;
			case 2:
				if ((isequipped(2796) == 0 && countitem(2796) == 0) || (isequipped(2797) == 0 && countitem(2797) == 0)) {
					if (countitem(732) > 0) {
						switch(select("ให้เพชร 3 กะรัต:แค่เลิกมัน.")) {
						case 1:
							mes "[Yoop]";
							mes "ฉันจะมองข้ามความผิดของคุณ";
							mes "โปรดระวังด้วย";
							delitem 732,1; //Crystal_Jewel__
							if (isequipped(2796) == 0 && countitem(2796) == 0)
								getitem 2796,1; //Magical_Stone
							if (isequipped(2797) == 0 && countitem(2797) == 0)
								getitem 2797,1; //Magical_Stone_
							close;
						case 2:
							mes "[Yoop]";
							mes "เพียงแสดงความจริงใจของคุณให้ฉันดู!";
							close;
						}
					}
					mes "[Yoop]";
					mes "แล้วจะพูดแบบนั้นได้ยังไง?";
					mes "^000077แค่ขอโทษ^000000!";
					mes "ฉันคิดว่าฉันจะให้หินเพิ่มแก่คุณได้ถ้าคุณให้เพชร 3 กะรัตแก่ฉัน...";
					close;
				}
				mes "[Yoop]";
				if (isequipped(2796) || isequipped(2797)) {
					mes "กรุณาตรวจสอบหน้าต่างอุปกรณ์ของคุณ...";
					mes "คุณแน่ใจว่าไม่มีใช่ไหม?";
					close;
				}
				mes "แล้วที่ถืออยู่นั่นมันคืออะไรล่ะ...?";
				close;
			}
		case 3:
			mes "[Yoop]";
			mes "อะไรนะ...?";
			mes "อย่ามากวนฉันนะ ฉันยุ่งมาก";
			close;
		}
	} else {
		mes "ฉันกำลังมุ่งเน้นไปที่การทำสมาธิ";
		mes "เพื่อที่ฉันจะได้สร้างสรรค์เวทมนตร์ได้";
		next;
		mes "[Yoop]";
		mes "และ... ดูเหมือนคุณจะมีความสามารถในด้านนี้มากกว่าผู้ใช้เวทมนตร์ทั่วไป";
		mes "บางทีถ้าฉันกลายเป็นนักเล่นแร่แปรธาตุ ฉันอาจจะเก่งกว่าที่เป็นอยู่ตอนนี้ก็ได้ คุณไม่คิดอย่างนั้นบ้างเหรอ";
		next;
		mes "[Yoop]";
		mes "ฉันกำลังศึกษาเพื่อพัฒนาตัวเอง";
		next;
		mes "[Yoop]";
		mes "ถ้าฉันอยู่ใน 'ห้องแห่งเวลาและเวทมนตร์' ฉันจะตามทันพลังของคุณ!";
		mes "ฮ่าฮ่าฮ่า...";
		close;
	}
}

spl_in02,84,103,3	script	Assistant#Warlock2	439,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[Assistant]";
		mes "มนุษย์! คุณต้องการอะไร?";
		close;
	} else if (job_wl < 6) {
		mes "[Assistant]";
		mes "ไม่ใช่ฉันที่คุณกำลังมองหาอยู่ตอนนี้";
		close;
	} else if (job_wl == 6) {
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "คุณกำลังทำสิ่งของมากเกินไป";
			mes "คุณควรจะลดน้ำหนักลง";
			close;
		}
		mes "[Yoop]";
		mes "การทดสอบไม่ได้ยากขนาดนั้น";
		mes "เราสามารถสร้างหินวิเศษในห้องวิเศษได้";
		next;
		mes "[Yoop]";
		mes "ระวังสัตว์ประหลาดในห้องวิเศษด้วย";
		mes "นี่คือทับทิมสีรุ้งที่ถูกผนึกด้วยเวทมนตร์ของวอร์ล็อค";
		mes "ใช้สิ่งเหล่านี้ระหว่างการทดสอบ";
		next;
		mes "[Yoop]";
		mes "ฉันจะพาคุณเข้าไปในห้องวิเศษ";
		set job_wl,7;
		changequest 11109,11110;
		getitem 12384,5; //Rainbow_Ruby_Water
		getitem 12386,5; //Rainbow_Ruby_Wind
		getitem 12387,5; //Rainbow_Ruby_Earth
		getitem 12385,5; //Rainbow_Ruby_Fire
		close2;
		warp "job3_war01",23,20;
		end;
	} else if (job_wl == 7) {
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "คุณกำลังทำสิ่งของมากเกินไป";
			mes "คุณควรจะลดน้ำหนักลง";
			close;
		}
		mes "[Yoop]";
		mes "คุณอยากลองอีกครั้งไหม";
		mes "ฉันจะให้ทับทิมเพิ่มเติมแก่คุณ";
		next;
		if (countitem(12384)) delitem 12384,countitem(12384); //Rainbow_Ruby_Water
		if (countitem(12386)) delitem 12386,countitem(12386); //Rainbow_Ruby_Wind
		if (countitem(12387)) delitem 12387,countitem(12387); //Rainbow_Ruby_Earth
		if (countitem(12385)) delitem 12385,countitem(12385); //Rainbow_Ruby_Fire
		getitem 12384,5; //Rainbow_Ruby_Water
		getitem 12386,5; //Rainbow_Ruby_Wind
		getitem 12387,5; //Rainbow_Ruby_Earth
		getitem 12385,5; //Rainbow_Ruby_Fire
		close2;
		warp "job3_war01",23,20;
		end;
	} else if (job_wl == 8) {
		mes "[Yoop]";
		mes "คุณได้ตกผลึกหินกลวงสำเร็จแล้ว";
		mes "ฉันขอโทษที่ฉันสงสัยคุณ";
		if (countitem(12384)) delitem 12384,countitem(12384); //Rainbow_Ruby_Water
		if (countitem(12386)) delitem 12386,countitem(12386); //Rainbow_Ruby_Wind
		if (countitem(12387)) delitem 12387,countitem(12387); //Rainbow_Ruby_Earth
		if (countitem(12385)) delitem 12385,countitem(12385); //Rainbow_Ruby_Fire
		next;
		mes "[Yoop]";
		mes "การทดสอบทั้งหมดเสร็จสิ้นแล้ว ตอนนี้ไปคุยกับ Master Engoloria ได้เลย";
		mes "ฉันดีใจมากที่คุณทำสำเร็จ";
		set job_wl,9;
		changequest 11111,11112;
		close;
	} else if (job_wl == 9) {
		mes "[Yoop]";
		mes "การทดสอบทั้งหมดเสร็จสิ้นแล้ว ตอนนี้ไปคุยกับ Master Engoloria ได้เลย";
		mes "ฉันดีใจมากที่คุณทำสำเร็จ";
		close;
	} else {
		mes "[Yoop]";
		mes "ฉันรู้สึกได้ว่าคุณมีเวทมนตร์อันแข็งแกร่ง";
		mes "ฉันอิจฉาคุณ";
		next;
		mes "[Yoop]";
		mes "มันสมควรได้รับสิ่งที่พิเศษ";
		mes "อย่าใช้มันในทางที่ผิด";
		close;
	}
}

spl_in02,78,110,5	script	Assistant#Warlock3	437,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	}
	mes "[Assistant]";
	mes "......";
	switch(job_wl) {
		case 1: mes "ถ้าคุณไม่มีอะไรจะพูดกับฉัน คุณจะจากไปไหม?"; break;
		case 2: mes "ฉันไม่มีหางม้า..."; break;
		default: mes "โปรดเงียบขณะที่อาจารย์ของฉันกำลังนั่งสมาธิ"; break;
	}
	next;
	switch(select("ออก:อยู่ต่อ")) {
	case 1:
		mes "[Assistant]";
		mes "ก็ระวังไว้ด้วยนะ.";
		close2;
		warp "splendide",200,100;
		end;
	case 2:
		mes "[Assistant]";
		mes "......";
		close;
	}
}

spl_in02,80,108,5	script	Guild Master#Warlock	465,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[Master Egnoloria]";
		mes "คุณมีอะไรจะถามฉันมั้ย?";
		next;
		switch(select("ฉันจะเป็น Warlock ได้อย่างไร?:ยกเลิก")) {
		case 1:
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_wl,2;
				setquest 11106;
				next;
			}
			mes "[Master Egnoloria]";
			mes "คุณจริงจังไหม?";
			mes "คุณหิวกระหายพลังไม่ใช่เหรอ มนุษย์";
			next;
			mes "[Master Egnoloria]";
			mes "ความปรารถนาอันแรงกล้าที่จะมีพลังมากขึ้นทำให้จิตวิญญาณของคุณสวยงามยิ่งขึ้น";
			next;
			mes "[Master Egnoloria]";
			mes "แต่ฉันต้องตรวจสอบก่อนว่าคุณมีคุณสมบัติหรือไม่";
			next;
			mes "[Master Egnoloria]";
			mes "ยู้ป!";
			next;
			mes "[Yoop]";
			mes "เย้!!";
			next;
			mes "[Master Egnoloria]";
			mes "มอบสิ่งนี้ให้กับมนุษย์";
			next;
			mes "[Master Egnoloria]";
			mes "คุยกับผู้ชายที่มีผมประหลาดๆ";
			mes "เขาจะบอกคุณว่าคุณควรทำอย่างไร";
			set job_wl,2;
			setquest 11106;
			close;
		case 2:
			mes "[Master Egnoloria]";
			mes "ดี.";
			mes "คุณเป็นมนุษย์ ดังนั้นนั่นก็เพื่อคุณ";
			close2;
			warp "splendide",200,100;
			end;
		}
	} else if (job_wl == 2) {
		mes "[Master Egnoloria]";
		mes "ฉันกำลังนั่งสมาธิ ฉันบอกให้คุยกับ Yoop โอเค";
		mes "Yoop มีหางม้าตลกๆ";
		mes "และอย่ามารบกวนฉัน";
		close;
	} else if (job_wl == 3 || job_wl == 4) {
		mes "[Master Egnoloria]";
		mes "......";
		next;
		mes "[Assistant]";
		mes "อาจารย์กำลังนั่งสมาธิอยู่";
		mes "เงียบๆ ไว้";
		close;
	} else if (job_wl == 5) {
		mes "[Master Egnoloria]";
		mes "ฉันได้ยินมาจากยูปว่าสามารถสร้างหินที่มีประโยชน์มากได้";
		next;
		mes "[Master Egnoloria]";
		mes "มันสามารถใช้ทำเครื่องประดับเวทย์มนตร์ที่สามารถร่ายเวทย์มนตร์อันทรงพลังได้";
		next;
		mes "[Master Egnoloria]";
		mes "Yoop.";
		next;
		mes "[Yoop]";
		mes "เยี่ยมเลย อาจารย์!";
		next;
		mes "[Master Egnoloria]";
		mes "โปรดพาเขาไปที่ห้องเวทย์มนตร์ด้วย";
		next;
		mes "[Yoop]";
		mes "เยี่ยมเลย อาจารย์";
		mes "โปรดตามฉันมา";
		emotion ET_AHA, getnpcid(0, "Assistant#Warlock2");
		set job_wl,6;
		changequest 11108,11109;
		close;
	} else if (job_wl < 9) {
		mes "[Master Egnoloria]";
		mes "......";
		next;
		mes "[Yoop]";
		mes "ขณะนี้พระอาจารย์ของฉันกำลังนั่งสมาธิอยู่ โปรดติดตามข้าพเจ้าด้วย";
		close;
	} else if (job_wl == 9) {
		if (BaseLevel < 99 || BaseJob != Job_Wizard) {
			mes "[Master Egnoloria]";
			mes "หืม...?";
			mes "คุณมาไกลขนาดนี้ได้ยังไง...";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "คุณมีรายการมากเกินไป";
			mes "โปรดกลับมาอีกครั้งหลังจากลดจำนวนรายการที่คุณมี";
			close;
		}
		mes "[Master Egnoloria]";
		if (SkillPoint != 0) {
			mes "เว้นแต่คุณจะชอบงานปัจจุบันของคุณ คุณควรใช้คะแนนทักษะทั้งหมดของคุณ";
			close;
		}
		if (ismounting()) {
			mes "คุณกำลังขี่สัตว์เลี้ยงอยู่ ดังนั้นคุณจึงไม่สามารถเปลี่ยนงานได้";
			mes "กรุณาถอดสัตว์เลี้ยงขี่ของคุณออกแล้วลองอีกครั้ง!";
			close;
		}
		mes "แสงที่สวยงาม...";
		mes "อย่าทำให้ฉันผิดหวังในตัวคุณ...";
		next;
		mes "[Master Egnoloria]";
		mes "คุณได้เป็นสมาชิกของวอร์ล็อคแล้ว";
		mes "จงภูมิใจ";
		next;
		if (SkillPoint != 0) {
			mes "^0000ffคุณยังคงมีแต้มทักษะเหลืออยู่ กรุณาใช้แต้มทักษะทั้งหมดแล้วลองเริ่มต้นใหม่อีกครั้ง!^000000";
			close;
		}
		mes "[Master Egnoloria]";
		mes "หินวิเศษที่ตกผลึกนี้จะช่วยให้คุณปลอดภัย";
		if (Class == Job_High_Wizard) {
			if (JobLevel > 69) set job_wl,13;
			else if (JobLevel > 59) set job_wl,12;
			else set job_wl,11;
		}
		else set job_wl,10;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11112;
		getitem 5753,1; //Magic_Stone_Hat
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_wl > 9) {
		mes "[Master Egnoloria]";
		mes "คุณปรับตัวกับพลังของวอร์ล็อคได้อย่างไร?";
		mes "ถ้าคุณประมาท คุณจะถูกโค่นล้มด้วยพลังของคุณเอง";
		close;
	}
}

job3_war01,22,22,7	script	Yoop#WRR	439,{
	mes "[Yoop]";
	mes "หากคุณขอให้เอเบนถูกส่งไปที่ห้องเวทมนตร์ เขาจะส่งคุณไปที่นั่น";
	mes "รอในคิวเพื่อเข้าไปในห้องเวทมนตร์";
	close;
}

job3_war01,29,25,0	script	Ebein#WRR	437,{
	mes "[Ebein]";
	mes "คุณอยากจะทดสอบไหม";
	mes "ถ้าคุณรออยู่ในห้องรับรอง คุณจะสามารถเข้าไปในห้องเวทมนตร์ได้";
	next;
	mes "[Ebein]";
	mes "ถ้ามีคนอื่นอยู่ในห้อง คุณก็ต้องรอสักพัก";
	next;
	switch(select("ไปที่ห้องรอ : ออกไปข้างนอก.")) {
	case 1:
		mes "[Ebein]";
		mes "จากนั้นกรุณาเข้าห้องด้วยตนเอง";
		mes "ตรวจสอบให้แน่ใจว่าคุณมียาเพียงพอที่จะเอาชีวิตรอดในการทดสอบ เนื่องจากสัตว์ประหลาดในห้องนั้นแข็งแกร่งมาก";
		close;
	case 2:
		mes "[Ebein]";
		mes "......";
		mes "แล้วฉันจะส่งคุณออกไปข้างนอก";
		mes "ลาก่อน...";
		close2;
		warp "spl_in02",79,102;
		end;
	}
}

job3_war01,29,25,5	script	The chamber of magic#1	437,{
	end;
OnInit:
	//disablenpc  "The chamber of magic#1";
	hideonnpc "The chamber of magic#1";
	waitingroom "ห้องแห่งเวทย์มนตร์",20,"The chamber of magic#1::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "job3_war02",29,25;
	donpcevent "The chamber of magic#2::OnEnable";
	initnpctimer;
	disablewaitingroomevent;
	end;
OnTimer10000:
	if (getmapusers("job3_war02") == 0) {
		donpcevent "The chamber of magic#2::OnReset";
		donpcevent "The chamber of magic#2::OnDisable";
		stopnpctimer "The chamber of magic#2";
		donpcevent "The chamber of magic#3::OnReset";
		donpcevent "The chamber of magic#3::OnDisable";
		stopnpctimer "The chamber of magic#3";
		donpcevent "The chamber of magic#4::OnReset";
		donpcevent "The chamber of magic#4::OnDisable";
		stopnpctimer "The chamber of magic#4";
		donpcevent "Hollow Stone#Warlock::OnReset";
		donpcevent "Hollow Stone#Warlock::OnDisable";
		stopnpctimer "Hollow Stone#Warlock";
		stopnpctimer;
		enablewaitingroomevent;
		end;
	}
	initnpctimer;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

-	script	The chamber of magic#2	-1,{
OnInit:
	disablenpc();
	end;
OnEnable:
	enablenpc();
	monster "job3_war02",23,32,"Magic Monster",2047,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,21,"Magic Monster",2048,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,32,"Magic Monster",2049,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,21,"Magic Monster",2050,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,32,"Magic Monster",2047,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,21,"Magic Monster",2048,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,32,"Magic Monster",2049,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,21,"Magic Monster",2050,1,strnpcinfo(0)+"::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02",strnpcinfo(0)+"::OnMyMobDead";
	end;
OnDisable:
	disablenpc();
	end;
OnMyMobDead:
	if (mobcount("job3_war02",strnpcinfo(0)+"::OnMyMobDead") < 1) {
		stopnpctimer;
		mapannounce "job3_war02","การรุกรานของมอนสเตอร์ได้รับการหยุดแล้ว",bc_map;
		set .@str$, "The chamber of magic#"+((strnpcinfo(0) == "The chamber of magic#2")?"3":"4");
		donpcevent .@str$+"::OnEnable";
	}
	end;
OnTimer3000:
	if (strnpcinfo(0) != "The chamber of magic#2") end;
	mapannounce "job3_war02","ชิ้นส่วนพลังเวทย์มนตร์ได้ถูกสร้างขึ้นในห้องห้องเวทย์มนตร์แล้ว",bc_map;
	end;
OnTimer4000:
	mapannounce "job3_war02","ปกป้องห้องนี้จากมอนสเตอร์เป็นเวลา 5 นาที",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","ทำลายมอนสเตอร์ทั้งหมด และห้องจะคงตัว",bc_map;
	end;
OnTimer63000:
	mapannounce "job3_war02","เหลือเวลาอีก 4 นาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer123000:
	mapannounce "job3_war02","เหลือเวลาอีก 3 นาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer183000:
	mapannounce "job3_war02","เหลือเวลาอีก 2 นาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer243000:
	mapannounce "job3_war02","เหลือเวลาอีก 1 นาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer273000:
	mapannounce "job3_war02","เหลือเวลาอีก 30 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer283000:
	mapannounce "job3_war02","เหลือเวลาอีก 20 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer293000:
	mapannounce "job3_war02","เหลือเวลาอีก 10 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ...",bc_map;
	end;
OnTimer303000:
	mapannounce "job3_war02","การรักษาเสถียรภาพเวทย์มนตร์ล้มเหลว",bc_map;
	donpcevent "The chamber of magic#2::OnReset";
	donpcevent "The chamber of magic#3::OnReset";
	donpcevent "The chamber of magic#4::OnReset";
	end;
OnTimer304000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer305000:
	donpcevent "The chamber of magic#2::OnDisable";
	donpcevent "The chamber of magic#3::OnDisable";
	donpcevent "The chamber of magic#4::OnDisable";
	donpcevent "The chamber of magic#1::OnEnable";
	stopnpctimer;
	end;
}
-	duplicate(The chamber of magic#2)	The chamber of magic#3	-1

-	script	The chamber of magic#4	-1,{
OnInit:
	disablenpc "The chamber of magic#4";
	end;
OnEnable:
	enablenpc "The chamber of magic#4";
	monster "job3_war02",23,32,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",23,21,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",34,32,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",34,21,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",23,32,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",23,21,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",34,32,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	monster "job3_war02",34,21,"Magic Monster",1002,1,"The chamber of magic#4::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02","The chamber of magic#4::OnMyMobDead";
	end;
OnDisable:
	disablenpc "The chamber of magic#4";
	end;
OnMyMobDead:
	if (mobcount("job3_war02","The chamber of magic#4::OnMyMobDead") < 1) {
		mapannounce "job3_war02","เมื่อมันคงที่แล้ว ชิ้นส่วนเหล่านั้นจะเคลื่อนเข้าหาศูนย์กลาง",bc_map;
		stopnpctimer;
		donpcevent "Hollow Stone#Warlock::OnEnable";
	}
	end;
OnTimer4000:
	mapannounce "job3_war02","ปกป้องห้องนี้จากมอนสเตอร์เป็นเวลา 1 นาที",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","ทำลายมอนสเตอร์ทั้งหมด และห้องจะคงตัว",bc_map;
	end;
OnTimer33000:
	mapannounce "job3_war02","เหลือเวลาอีก 30 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ....",bc_map;
	end;
OnTimer43000:
	mapannounce "job3_war02","เหลือเวลาอีก 20 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ....",bc_map;
	end;
OnTimer53000:
	mapannounce "job3_war02","เหลือเวลาอีก 10 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ....",bc_map;
	end;
OnTimer58000:
	mapannounce "job3_war02","เหลือเวลาอีก 5 วินาทีเพื่อทำให้ห้องมีเสถียรภาพ....",bc_map;
	end;
OnTimer63000:
	mapannounce "job3_war02","การรักษาเสถียรภาพเวทย์มนตร์ล้มเหลว ชิ้นส่วนนั้นสูญเสียพลังไปแล้ว",bc_map;
	donpcevent "The chamber of magic#2::OnReset";
	donpcevent "The chamber of magic#3::OnReset";
	donpcevent "The chamber of magic#4::OnReset";
	end;
OnTimer64000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer65000:
	donpcevent "The chamber of magic#2::OnDisable";
	donpcevent "The chamber of magic#3::OnDisable";
	donpcevent "The chamber of magic#4::OnDisable";
	donpcevent "The chamber of magic#1::OnEnable";
	stopnpctimer;
	end;
}

-	script	Hollow Stone#Warlock	-1,{
OnInit:
	disablenpc "Hollow Stone#Warlock";
	end;
OnEnable:
	enablenpc "Hollow Stone#Warlock";
	donpcevent "The chamber of magic#2::OnReset";
	donpcevent "The chamber of magic#2::OnDisable";
	donpcevent "The chamber of magic#3::OnReset";
	donpcevent "The chamber of magic#3::OnDisable";
	donpcevent "The chamber of magic#4::OnReset";
	donpcevent "The chamber of magic#4::OnDisable";
	monster "job3_war02",29,27,"Hollow Stone",2035,1,"Hollow Stone#Warlock::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02","Hollow Stone#Warlock::OnMyMobDead";
	end;
OnDisable:
	disablenpc "Hollow Stone#Warlock";
	end;
OnMyMobDead:
	if (mobcount("job3_war02","Hollow Stone#Warlock::OnMyMobDead") < 1) {
		mapannounce "job3_war02","หินกลวงได้รับการตกผลึกด้วยพลังเวทย์มนตร์ที่เพียงพอ",bc_map;
		set job_wl,8;
		changequest 11110,11111;
		donpcevent "Hollow Stone#Warlock::OnReset";
		donpcevent "Hollow Stone#Warlock::OnDisable";
		stopnpctimer;
		donpcevent "Ebein#E1::OnEnable";
		donpcevent "Ebein#E2::OnEnable";
	}
	end;
OnTimer1000:
	mapannounce "job3_war02","หินกลวงต้องได้รับการตกผลึกด้วยเวทมนตร์",bc_map;
	end;
OnTimer2000:
	mapannounce "job3_war02","Hollow Stone จะดูดซับเวทมนตร์ใดๆ ที่โยนใส่มัน",bc_map;
	end;
OnTimer3000:
	mapannounce "job3_war02","คุณมีเวลา 10 นาทีในการตกผลึก Hollow Stone ด้วยเวทมนตร์ของคุณ",bc_map;
	end;
OnTimer303000:
	mapannounce "job3_war02","เหลือเวลาอีก 5 นาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer363000:
	mapannounce "job3_war02","เหลือเวลาอีก 4 นาทีในการตกผลึกหิน",bc_map;
	end;
OnTimer423000:
	mapannounce "job3_war02","เหลือเวลาอีก 3 นาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer483000:
	mapannounce "job3_war02","เหลือเวลาอีก 2 นาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer543000:
	mapannounce "job3_war02","เหลือเวลาอีก 1 นาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer573000:
	mapannounce "job3_war02","เหลือเวลาอีก 30 วินาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer583000:
	mapannounce "job3_war02","เหลือเวลาอีก 20 วินาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer593000:
	mapannounce "job3_war02","เหลือเวลาอีก 10 วินาทีในการตกผลึกหิน....",bc_map;
	end;
OnTimer603000:
	mapannounce "job3_war02","การตกผลึกของหินกลวงล้มเหลว",bc_map;
	donpcevent "Hollow Stone#Warlock::OnReset";
	end;
OnTimer605000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer607000:
	donpcevent "Hollow Stone#Warlock::OnDisable";
	stopnpctimer;
	donpcevent "The chamber of magic#1::OnEnable";
	end;
}

-	script	Ebein#E1	-1,{
OnInit:
	disablenpc "Ebein#E1";
	end;
OnEnable:
	initnpctimer;
	end;
OnDisable:
	disablenpc "Ebein#E1";
	end;
OnTimer2000:
	mapannounce "job3_war01","ขอแสดงความยินดี! คุณประสบความสำเร็จในการสร้างคริสตัลวิเศษ",bc_map;
	end;
OnTimer3000:
	mapannounce "job3_war01","ห้องเวทย์มนตร์เริ่มจะเต็มไปด้วยพลังเวทย์มนตร์",bc_map;
	end;
OnTimer6000:
	donpcevent "Ebein#E1::OnDisable";
	stopnpctimer;
	donpcevent "The chamber of magic#1::OnEnable";
	end;
}

-	script	Ebein#E2	-1,{
OnInit:
	disablenpc "Ebein#E2";
	end;
OnEnable:
	initnpctimer;
	end;
OnDisable:
	disablenpc "Ebein#E2";
	end;
OnTimer3000:
	mapannounce "job3_war02","ขอแสดงความยินดี! คุณประสบความสำเร็จในการสร้างคริสตัลวิเศษ",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","ห้องเวทย์มนตร์เริ่มจะเต็มไปด้วยพลังเวทย์มนตร์",bc_map;
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer6000:
	donpcevent "Ebein#E2::OnDisable";
	stopnpctimer;
	end;
}

job3_war01,1,2,0	script	Button Girl#wl	66,{
	callfunc "F_GM_NPC";
	switch(select("Open Arena:Close Arena:Open the Chamber of Magic:Close the Chamber of Magic:Hollow Stone On:Hollow Stone Off:Cancel")) {
	case 1:
		mes "Opening the Arena.";
		donpcevent "The chamber of magic#1::OnEnable";
		close;
	case 2:
		mes "Closing the Arena.";
		donpcevent "The chamber of magic#1::OnDisable";
		close;
	case 3:
		mes "Opening the Chamber of Magic.";
		donpcevent "The chamber of magic#2::OnEnable";
		close;
	case 4:
		mes "Closing the Chamber of Magic.";
		donpcevent "The chamber of magic#2::OnReset";
		donpcevent "The chamber of magic#2::OnDisable";
		donpcevent "The chamber of magic#3::OnReset";
		donpcevent "The chamber of magic#3::OnDisable";
		donpcevent "The chamber of magic#4::OnReset";
		donpcevent "The chamber of magic#4::OnDisable";
		close;
	case 5:
		mes "Enabing Hollow Stone.";
		donpcevent "Hollow Stone#Warlock::OnEnable";
		close;
	case 6:
		mes "Disabling Hollow Stone.";
		donpcevent "Hollow Stone#Warlock::OnReset";
		donpcevent "Hollow Stone#Warlock::OnDisable";
		close;
	case 7:
		close;
	}
}

spl_fild02,0,0	monster	Bradium Golem	2049,20,5000
spl_fild02,0,0	monster	Naga	2047,20,5000

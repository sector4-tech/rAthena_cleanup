//===== rAthena Script =======================================
//= Mission WoE TE
//===== Description: =========================================
//= [Official Conversion]
//= Template for mission WoE TE
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

// This npc call the duplicate of pop_mission_TE npc in order to spawn the mobs mission.
-	script	#popswitch	-1,{
OnInit:
		if (strnpcinfo(4) == "") end;
		if (agitcheck3() == 1)
				donpcevent strnpcinfo(0) + "::OnCommandOn";
		else
				donpcevent strnpcinfo(0) + "::OnReset";
		end;

OnCommandOn:
		.@map$ = strnpcinfo(4);
		if (compare( .@map$,"te_alde" ))
				callsub S_Event, "alde0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnCommandOn";// #popa_alde0[1-5]::OnCommandOn
		else
				callsub S_Event, "prt0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnCommandOn";
		end;

OnReset:
		.@map$ = strnpcinfo(4);
		if (compare( .@map$,"te_alde" ))
				callsub S_Event, "alde0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnReset";
		else
				callsub S_Event, "prt0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnReset";
		end;

S_Event:
		.@string$ = getarg(0);
		donpcevent "#popa_"+ .@string$;
		donpcevent "#popb_"+ .@string$;
		donpcevent "#popc_"+ .@string$;
		donpcevent "#popd_"+ .@string$;
		end;
}

-	script	pop_mission_TE	-1,{
OnMyMobDead:
		.@map$ = strnpcinfo(4);
		if (mobcount( .@map$,strnpcinfo(0) +"::OnMyMobDead" ) < 1 && playerattached()) {// playerattached stop the script if killmonster / maprespawnguildid
				callsub S_Coord, .@item_id, .@mob_id, .@coord;
				for ( .@mtem = 0; .@mtem < 20; .@mtem++ )
						makeitem .@item_id,1, .@map$, rand( .@coord[0],.@coord[1] ), rand( .@coord[2],.@coord[3] );
				initnpctimer;
		}
		end;

OnTimer12000:
		stopnpctimer;
OnCommandOn:
		callsub S_Coord, .@item_id, .@mob_id, .@coord;
		monster strnpcinfo(4), rand( .@coord[0],.@coord[1] ), rand( .@coord[2],.@coord[3] ),"--ja--",.@mob_id,1,strnpcinfo(0) + "::OnMyMobDead";
		end;

OnReset:
		killmonster strnpcinfo(4),strnpcinfo(0) +"::OnMyMobDead";
		stopnpctimer;
		end;

S_Coord:
		if (compare( strnpcinfo(4),"te_alde" )) {
				set getarg(0), 7056;// Payroll_Of_Kafra
				set getarg(1), 2450;// THIEF_PORING
				.@hidden_name$ = strnpcinfo(2);

				if (.@hidden_name$ == "popa_alde01") setarray .@coord[0],178,234, 128,175;// area x1,x2, y1,y2
				else if (.@hidden_name$ == "popb_alde01") setarray .@coord[0],14,65, 186,222;
				else if (.@hidden_name$ == "popc_alde01") setarray .@coord[0],46,114, 37,105;
				else if (.@hidden_name$ == "popd_alde01") setarray .@coord[0],198,233, 5,41;
				else if (.@hidden_name$ == "popa_alde02") setarray .@coord[0],29,73, 169,211;
				else if (.@hidden_name$ == "popb_alde02") setarray .@coord[0],175,220, 136,184;
				else if (.@hidden_name$ == "popc_alde02") setarray .@coord[0],196,231, 7,40;
				else if (.@hidden_name$ == "popd_alde02") setarray .@coord[0],53,125, 11,80;
				else if (.@hidden_name$ == "popa_alde03") setarray .@coord[0],43,107, 212,264;
				else if (.@hidden_name$ == "popb_alde03") setarray .@coord[0],181,215, 150,187;
				else if (.@hidden_name$ == "popc_alde03") setarray .@coord[0],59,121, 63,123;
				else if (.@hidden_name$ == "popd_alde03") setarray .@coord[0],192,215, 24,51;
				else if (.@hidden_name$ == "popa_alde04") setarray .@coord[0],154,189, 34,101;
				else if (.@hidden_name$ == "popb_alde04") setarray .@coord[0],21,76, 64,125;
				else if (.@hidden_name$ == "popc_alde04") setarray .@coord[0],120,143, 200,219;
				else if (.@hidden_name$ == "popd_alde04") setarray .@coord[0],13,37, 199,222;
				else if (.@hidden_name$ == "popa_alde05") setarray .@coord[0],145,207, 47,78;
				else if (.@hidden_name$ == "popb_alde05") setarray .@coord[0],13,43, 87,116;
				else if (.@hidden_name$ == "popc_alde05") setarray .@coord[0],11,63, 177,230;
				else if (.@hidden_name$ == "popd_alde05") setarray .@coord[0],130,167, 191,225;
		}
		else {
				set getarg(0), 6592;// Small_Wooden_Chest
				set getarg(1), 2451;// BABY_STRANGE
				.@hidden_name$ = strnpcinfo(2);

				if (.@hidden_name$ == "popa_prt01") setarray .@coord[0],10,90, 10,80;
				else if (.@hidden_name$ == "popb_prt01") setarray .@coord[0],146,190, 64,118;
				else if (.@hidden_name$ == "popc_prt01") setarray .@coord[0],92,128, 170,204;
				else if (.@hidden_name$ == "popd_prt01") setarray .@coord[0],188,204, 180,204;
				else if (.@hidden_name$ == "popa_prt02") setarray .@coord[0],15,95, 162,225;
				else if (.@hidden_name$ == "popb_prt02") setarray .@coord[0],144,172, 152,175;
				else if (.@hidden_name$ == "popc_prt02") setarray .@coord[0],43,99, 23,62;
				else if (.@hidden_name$ == "popd_prt02") setarray .@coord[0],168,204, 24,58;
				else if (.@hidden_name$ == "popa_prt03") setarray .@coord[0],7,76, 48,110;
				else if (.@hidden_name$ == "popb_prt03") setarray .@coord[0],160,204, 32,81;
				else if (.@hidden_name$ == "popc_prt03") setarray .@coord[0],138,190, 183,232;
				else if (.@hidden_name$ == "popd_prt03") setarray .@coord[0],8,36, 202,227;
				else if (.@hidden_name$ == "popa_prt04") setarray .@coord[0],246,270, 233,275;
				else if (.@hidden_name$ == "popb_prt04") setarray .@coord[0],14,53,236,273;
				else if (.@hidden_name$ == "popc_prt04") setarray .@coord[0],13,96, 12,63;
				else if (.@hidden_name$ == "popd_prt04") setarray .@coord[0],259,283, 14,35;
				else if (.@hidden_name$ == "popa_prt05") setarray .@coord[0],17,95, 226,281;
				else if (.@hidden_name$ == "popb_prt05") setarray .@coord[0],243,287, 243,288;
				else if (.@hidden_name$ == "popc_prt05") setarray .@coord[0],16,66, 17,60;
				else if (.@hidden_name$ == "popd_prt05") setarray .@coord[0],205,282, 12,93;
		}
		for ( .@i = 0; .@i < 4; .@i++ )
				set getelementofarray( getarg(2),.@i ), .@coord[.@i];
		return;
}


-	script	Training_Instructor_TE	-1,{
		if (checkweight(1201,5) == 0) {
				mes "คุณมีไอเทมมากเกินไปที่จะดำเนินภารกิจต่อไปได้";
				mes "กรุณาลดภาระของคุณแล้วลองอีกครั้ง";
				close;
		}
		.@map$ = strnpcinfo(4);
		if (.@map$ == "te_alde_gld") {
				.@npc_name$ = "Mana";
				.@fortress$ = "Kafragarten";
				.@menu$ = callsub( S_Menu, .@quest_id,
						7510,"1 Suspicious Poring",
						7511,"10 Suspicious Poring",
						7512,"30 Suspicious Poring",
						7504,"Destroy 1 Emperium",
						7505,"Destroy 3 Emperiums",
						7506,"Destroy 5 Emperiums",
						7513,"10 Kafra's Paychecks",
						7514,"20 Kafra's Paychecks",
						7515,"30 Kafra's Paychecks" );
		}
		else {
				.@npc_name$ = "Rune";
				.@fortress$ = "Gloria";
				.@menu$ = callsub( S_Menu, .@quest_id,
						7501,"1 Suspicious Babee",
						7502,"10 Suspicious Babee",
						7503,"30 Suspicious Babee",
						7504,"Destroy 1 Emperium",
						7505,"Destroy 3 Emperiums",
						7506,"Destroy 5 Emperiums",
						7507,"10 Small Wooden Boxes",
						7508,"20 Small Wooden Boxes",
						7509,"30 Small Wooden Boxes" );
		}
		mes "-"+ .@npc_name$ +"-";
		if (jobcanentermap("te_prtcas01") == 0) {
				mes "อ่า...คุณ...";
				mes "ตามกฏ TE ฉันจะไม่สั่งคุณหรือขอความช่วยเหลือจากคุณอีกเลย น่าเสียดาย";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "เฮ้ ตอนนี้ เนื่องจากสถานที่นี้มีไว้สำหรับคลาสงานหลักและรองเท่านั้น รวมถึงผู้ใช้ที่ไม่มีเลเวลขยายด้วย ถ้าคุณไม่มีอะไรทำที่นี่ คุณจะออกไปจากที่นี่ได้ไหม?";
				close;
		}
		mes "มีภารกิจที่คุณสามารถทำสำเร็จได้ในภูมิภาค TE";
		mes "มาดูกันดีกว่าว่าคุณต้องการอะไร?";
		next;
		switch( select( "Accept/Conform Missions", "What do you mean a mission?", "I don't have any." ) ) {
		case 1:
				mes "-"+ .@npc_name$ +"-";
				if (BaseLevel < 70) {
						mes "อืม ขออภัย แต่ภารกิจนี้มีไว้สำหรับผู้ใช้ที่มีระดับ ^4d4dff70^000000 ขึ้นไป";
						mes "จริงๆ แล้ว มันยากที่จะทำภารกิจให้สำเร็จหากระดับของคุณต่ำเกินไป";
						close;
				}
				.@time_check = WoeTETimeStart(3600);
				.@siege_check = agitcheck3();
				mes "คุณคงทราบอยู่แล้วว่าคุณจะต้องรับภารกิจในช่วงระยะเวลาหนึ่งใช่ไหม?";
				if (.@time_check == 1 || .@siege_check == 1)
						mes "นอกจากนี้ คุณต้องจำไว้ว่าคุณไม่สามารถรับภารกิจระดับสูงและระดับต่ำพร้อมกันได้หากภารกิจเหล่านั้นมีเนื้อหาเดียวกัน";
				else {
						mes "ขออภัย คุณไม่สามารถรับภารกิจได้ในขณะนี้";
						next;
						mes "-"+ .@npc_name$ +"-";
						.@playtime_check2 = checkquest(7516,PLAYTIME);// A Break for Commercial Development
						if (.@playtime_check2 == 2) {
								erasequest 7516;
								mes "อืม คุณพักผ่อนอยู่เหรอ?";
								mes "เอาล่ะ มันไม่สำคัญอีกต่อไปแล้ว";
						}
						mes "คุณสามารถตรวจสอบผลลัพธ์ของภารกิจที่คุณยอมรับแล้วเท่านั้น";
				}
				next;
				.@s = select( .@menu$ + "Cancel" ) -1;
				mes "-"+ .@npc_name$ +"-";
				if (.@s == 9) {
						mes "เจอกันใหม่คราวหน้าครับ.";
						close;
				}

				.@main_quest = .@quest_id[.@s];
				deletearray .@quest_id[.@s],1;
				.@mission01 = isbegin_quest(.@main_quest);
				.@playtime_check = checkquest(.@main_quest,PLAYTIME);
				.@hunting_check = checkquest(.@main_quest,HUNTING);

				.@index = (.@s/3) *3;
				.@mission02 = isbegin_quest(.@quest_id[.@index]);
				.@mission03 = isbegin_quest(.@quest_id[.@index+1]);

				if (.@s == 0 || .@s == 1 || .@s == 2) {// Hunting suspicious quest
						if (.@map$ == "te_alde_gld")
								setarray .@mob_name$[0],"Porings","suspicious Porings";
						else
								setarray .@mob_name$[0],"Orc Babies","suspicious creatures";

						if (.@s == 0)
								.@num_kill = 1;
						else if (.@s == 1) {
								.@num_tool_box = 2;
								.@num_kill = 10;
								setarray .@reward[0],12873,2;
								setarray .@exp[0],20000,10000;
						}
						else if (.@s == 2) {
								.@num_tool_box = 3;
								.@num_kill = 30;
								setarray .@reward[0],12873,3;
								setarray .@exp[0],30000,15000;
						}

						if (.@mission01 == 1) {
								if (.@playtime_check == 0) {
										if (.@hunting_check == 2) {
												mes "คุณทำมันทันเวลา";
												mes "ฉันไม่เข้าใจว่าทำไม"+ .@mob_name$[0] +"ปรากฏขึ้นในเวลานั้นของวัน";
												mes "ยังไงก็ตามทำได้ดีมาก";
												if (.@num_tool_box == 0) {
														erasequest .@main_quest;
														getitem 11558,5;// TE_White_Slim_Potion
														getexp 5000,2500;
														close;
												}
												next;
												mes "-"+ .@npc_name$ +"-";
												mes "เมื่อคุณทำได้ดีแล้ว ฉันจะให้คุณเปิดกล่องเครื่องมือของฉัน" + .@num_tool_box + "ครั้ง";
												mes "แม้ว่าคุณจะไม่พบอะไรเลยส่วนใหญ่...";
												mes "เป็นโบนัส ดังนั้นอย่าบ่นแม้จะไม่พบอะไรเลย";
												next;
												mes "เหมือนกับ"+ .@npc_name$ +"กล่าวว่าคุณได้ผ่านกล่องข้างหน้าแล้วและ...";
												erasequest .@main_quest;
												getitem .@reward[0],.@reward[1];
												getexp .@exp[0],.@exp[1];
												callsub( S_Rentbox, .@num_tool_box );
										}
										callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to eliminate "+ .@mob_name$[1] +" that secretly appear at the fortress in "+ .@fortress$ +"." );
								}
								mes "หมดเวลาแล้ว";
								mes "น่าเสียดาย ภารกิจล้มเหลว!";
								mes "บางทีคราวหน้าก็ได้!";
								erasequest .@main_quest;
								close;
						}
						if (.@time_check != 1 && .@siege_check != 1) {
								mes "คุณไม่ได้อยู่ในภารกิจนี้ในขณะนี้";
								mes "คุณช่วยตรวจสอบอีกครั้งได้ไหม?";
								close;
						}
						if (.@mission02 == 1 || .@mission03 == 1) {
								if (.@num_tool_box == 0) {
										mes "คุณกำลังอยู่ในภารกิจระดับสูงแล้ว!";
										mes "คุณไม่สามารถรับภารกิจระดับต่ำได้หากคุณอยู่ในภารกิจระดับสูงที่มีเนื้อหาเดียวกันอยู่แล้ว!";
								}
								else {
										mes "คุณกำลังอยู่ในภารกิจการกำจัดแล้ว!";
										mes "ฉันต้องการให้คุณดูแลสิ่งที่คุณมีอยู่แล้วเว้นแต่คุณต้องการยกเลิกมัน";
								}
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "มันก็สมเหตุสมผลแค่แบบนั้นเท่านั้น ไม่ใช่หรือ?";
								mes "คุณไม่คิดอย่างนั้นเหรอ?";
								close;
						}
						mes "ฉันได้รับการบอกกล่าวว่า"+ .@mob_name$[1] +"ปรากฏขึ้นระหว่างชั่วโมงการปิดล้อมอันวุ่นวายที่ป้อมปราการใน"+ .@fortress$ +"-";
						mes "ฉันอยากให้คุณ ^4d4dffeliminate"+ .@num_kill +"หรือมากกว่านั้น"+ .@mob_name$[1] +"^000000.";
						callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
				}
				else if (.@s == 3 || .@s == 4 || .@s == 5) {// Breaking Emperiums
						if (.@s == 3) {
								.@num_tool_box = 1;
								.@num_kill = 1;
								setarray .@reward[0],12873,3;
								setarray .@exp[0],30000,10000;
						}
						else if (.@s == 4) {
								.@num_tool_box = 3;
								.@num_kill = 3;
								setarray .@reward[0],12873,9;
								setarray .@exp[0],90000,30000;
						}
						else if (.@s == 5) {
								.@num_tool_box = 5;
								.@num_kill = 5;
								setarray .@reward[0],12873,15;
								setarray .@exp[0],150000,50000;
						}
						if (.@mission01 == 1) {
								if (.@playtime_check == 0) {
										if (.@hunting_check == 2) {
												mes "เหลือเชื่อ!";
												mes "การทำลาย Emperiums นั้นไม่ใช่เรื่องง่ายอย่างแน่นอน";
												mes "คุณคงมีส่วนสนับสนุนกิลด์ของคุณมากทีเดียว ทำได้ดีมาก!";
												next;
												mes "-"+ .@npc_name$ +"-";
												mes "เมื่อคุณทำได้ดีแล้ว ฉันจะให้คุณเปิดกล่องเครื่องมือของฉัน"+ .@num_tool_box +"ครั้ง";
												mes "แม้ว่าคุณจะไม่พบอะไรเลยส่วนใหญ่...";
												mes "เป็นโบนัส ดังนั้นอย่าบ่นแม้จะไม่พบอะไรเลย";
												next;
												mes "เหมือนกับ"+ .@npc_name$ +"กล่าวว่าคุณได้ผ่านกล่องข้างหน้าแล้วและ...";
												erasequest .@main_quest;
												setquest 7516;
												getitem .@reward[0],.@reward[1];
												getexp .@exp[0],.@exp[1];
												callsub( S_Rentbox, .@num_tool_box );
										}
										callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to ^4d4dff destroy Emperiums that exist all over Gloria and Kafragarten regions.^000000" );
								}
								mes "หมดเวลาแล้ว";
								mes "น่าเสียดาย ภารกิจล้มเหลว!";
								mes "บางทีคราวหน้าก็ได้!";
								erasequest .@main_quest;
								close;
						}
						if (.@time_check != 1 && .@siege_check != 1) {
								mes "คุณไม่ได้อยู่ในภารกิจนี้ในขณะนี้";
								mes "คุณช่วยตรวจสอบอีกครั้งได้ไหม?";
								close;
						}
						if (.@mission02 == 1 || .@mission03 == 1) {
								mes "คุณกำลังอยู่ในภารกิจของเนื้อหาเดียวกันอยู่แล้ว";
								mes "ระดับมันต่างกันนะ";
								mes "ฉันต้องการให้คุณดูแลสิ่งที่คุณมีอยู่แล้วเว้นแต่คุณต้องการยกเลิกมัน";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "มันก็สมเหตุสมผลแค่แบบนั้นเท่านั้น ไม่ใช่หรือ?";
								mes "คุณไม่คิดอย่างนั้นเหรอ?";
								close;
						}
						.@playtime_check2 = checkquest(7516,PLAYTIME);
						if (.@playtime_check2 == 0) {
								mes "มีสิ่งที่เรียกว่า 'ระดับการพัฒนาเชิงพาณิชย์' อยู่ในป้อมปราการแต่ละแห่ง";
								mes "เพิ่มขึ้นตามการลงทุนของแต่ละกิลด์";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "อย่างไรก็ตาม สิ่งนี้มีความเกี่ยวข้องอย่างใกล้ชิดกับ Emperiums";
								mes "Destroying Emperiums จะทำให้ระดับการพัฒนาเชิงพาณิชย์ตกต่ำซ้ำแล้วซ้ำเล่า";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "ดังนั้นตอนนี้เราจะหยุดพักเพื่อป้องกันไม่ให้ระดับการพัฒนาเชิงพาณิชย์ลดลงมากเกินไป";
								mes "เจอกันใหม่อีกครั้งเร็วๆ นี้";
								close;
						}
						else if (.@playtime_check2 == 2) {
								erasequest 7516;
								mes "การหยุดพัฒนาเชิงพาณิชย์ของป้อมปราการสิ้นสุดลงแล้ว";
								mes "ดีเลย ลองคิดดูว่าคุณอยากได้ภารกิจแบบไหนแล้วกลับมาทำใหม่";
								close;
						}
						mes "สิ่งที่สำคัญที่สุดในการต่อสู้แบบปิดล้อมคืออะไร?";
						mes "มันกำลังทำลาย Emperiums! ฉันจะบอกภารกิจทำลาย Emperiums ให้คุณฟัง!";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "กลอเรียหรือคาฟราการ์เทน ไม่ว่าอยู่ที่ไหนก็ตาม! ในเวลานี้ เวลานี้ ^4d4dff ทำลายอาณาจักร!^000000";
						mes "^4d4dffหมายเลขเป้าหมายคือ"+ .@num_kill +"หรือมากกว่านั้น!^000000";
						callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
				}
				else if (.@s == 6 || .@s == 7 || .@s == 8) {
						if (.@map$ == "te_alde_gld") {
								.@item_quest = 7056;// Payroll_Of_Kafra
								setarray .@string$[0], "paychecks", "I don't understand why Porings would steal Kafra's Paychecks", "Kafra's stolen paychecks";
						}
						else {
								.@item_quest = 6592;// Small_Wooden_Chest
								setarray .@string$[0], "wooden boxes", "So, those Orc Babies were after this, huh?", "wooden boxes";
						}
						if (.@s == 6) {
								.@num_tool_box = 1;
								.@num_items = 10;
								setarray .@reward[0],12873,1;
								setarray .@exp[0],10000,5000;
						}
						else if (.@s == 7) {
								.@num_tool_box = 2;
								.@num_items = 20;
								setarray .@reward[0],12873,2;
								setarray .@exp[0],20000,10000;
						}
						else if (.@s == 8) {
								.@num_tool_box = 3;
								.@num_items = 30;
								setarray .@reward[0],12873,3;
								setarray .@exp[0],30000,15000;
						}

						if (.@mission01 == 1) {
								if (.@playtime_check == 0) {
										if (countitem(.@item_quest) > 9) {
												mes .@num_items +"หรือมากกว่านั้น"+ .@string$[0] +"มาดูกันว่าคุณทำมันถูกต้องแล้ว";
												mes .@string$[1];
												mes "ยังไงก็ตามทำได้ดีมาก";
												next;
												mes "-"+ .@npc_name$ +"-";
												mes "ให้ฉันรับส่วนที่เหลือ"+ .@string$[0] +"-";
												mes "ฉันจะคืนมันให้กับเจ้าของเดิมของมัน";
												next;
												mes "-"+ .@npc_name$ +"-";
												mes "เมื่อคุณทำได้ดีแล้ว ฉันจะให้คุณเปิดกล่องเครื่องมือของฉัน"+ .@num_tool_box +"ครั้ง";
												mes "แม้ว่าคุณจะไม่พบอะไรเลยส่วนใหญ่...";
												mes "เป็นโบนัส ดังนั้นอย่าบ่นแม้จะไม่พบอะไรเลย";
												next;
												mes "เหมือนกับ"+ .@npc_name$ +"กล่าวว่าคุณได้ผ่านกล่องข้างหน้าแล้วและ...";
												delitem .@item_quest, countitem(.@item_quest);
												erasequest .@main_quest;
												getitem .@reward[0],.@reward[1];
												getexp .@exp[0],.@exp[1];
												callsub( S_Rentbox, .@num_tool_box );
										}
										callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to ^4d4dffcollect "+ .@string$[2] +"^000000 at the fortress in "+ .@fortress$ +"." );
								}
								mes "หมดเวลาแล้ว";
								mes "น่าเสียดาย ภารกิจล้มเหลว!";
								mes "บางทีคราวหน้าก็ได้!";
								erasequest .@main_quest;
								close;
						}
						if (.@time_check != 1 && .@siege_check != 1) {
								mes "คุณไม่ได้อยู่ในภารกิจนี้ในขณะนี้";
								mes "คุณช่วยตรวจสอบอีกครั้งได้ไหม?";
								close;
						}
						if (.@mission02 == 1 || .@mission03 == 1) {
								mes "คุณกำลังอยู่ในภารกิจของเนื้อหาเดียวกันอยู่แล้ว";
								mes "ระดับมันต่างกันนะ";
								mes "ฉันต้องการให้คุณดูแลสิ่งที่คุณมีอยู่แล้วเว้นแต่คุณต้องการยกเลิกมัน";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "มันก็สมเหตุสมผลแค่แบบนั้นเท่านั้น ไม่ใช่หรือ?";
								mes "คุณไม่คิดอย่างนั้นเหรอ?";
								close;
						}
						mes "ฮึม คุณคงไม่เชื่อหรอก แต่ภารกิจนี้เกี่ยวข้องกับการโจรกรรมนะ";
						mes "พวกเขาบอกฉันมากมาย"+ .@string$[2] +"หายไปจากป้อมปราการใน"+ .@fortress$ +"-";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "โจรคงได้ใช้ประโยชน์จากช่วงเวลาที่ถูกล้อมโจมตี";
						mes "เป็นการร้องขอให้ค้นหา ^4d4dff"+ .@num_items +"หรือมากกว่านั้น"+ .@string$[2] +"^000000.";
						callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
				}
		case 2:
				mes "-"+ .@npc_name$ +"-";
				mes "เนื่องจากภูมิภาค TE มีบรรยากาศที่เป็นกันเองมากกว่า";
				mes "การทำแค่การสู้รบแบบปิดล้อมมันก็ไม่สนุกใช่ไหมล่ะ?";
				mes "ดังนั้นฉันจึงนำ ^4d4dff ผลงานบางอย่างที่คุณสามารถทำได้ระหว่างชั่วโมงการปิดล้อม^000000~";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "คุณสามารถรับภารกิจ ^4d4dff เริ่มตั้งแต่ 1 ชั่วโมงก่อนการเริ่มการปิดล้อมจนถึงสิ้นสุดการต่อสู้ปิดล้อม TE^000000";
				mes "และแต่ละภารกิจมีกำหนดเวลาจำกัดในการปฏิบัติภารกิจให้สำเร็จภายใน 4d4dffa 4 ชั่วโมง^000000";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "พูดอย่างง่ายๆ ก็คือ คุณยอมรับภารกิจจากฉันในระหว่างเวลาที่ถูกล้อมและ";
				mes "^4d4dffกลับมารายงานให้ฉันทราบภายในเวลาที่กำหนด^000000";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "ลองเล่นดูเพื่อความสนุก";
				close;
		case 3:
				mes "-"+ .@npc_name$ +"-";
				mes "ฉันเห็น.";
				mes "กลับมาได้ตลอดเวลาหากคุณเปลี่ยนใจ";
				mes "แต่ฉันอาจจะไม่ว่างก็ได้นะ อิอิ";
				close;
		}

S_Cancelquest:
		.@npc_name$ = getarg(1);
		if (WoeTETimeStart(3600) == 1 || agitcheck3()) {
				mes "ดูเหมือนว่าคุณยังไม่บรรลุภารกิจใช่ไหม?";
				mes getarg(2);
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "คุณอยากจะยกเลิกโดยบังเอิญใช่ไหม?";
				next;
				switch( select( "No.","Yes." ) ) {
				case 1:
						mes "-"+ .@npc_name$ +"-";
						mes "ขอให้โชคดี!";
						close;
				case 2:
						mes "-"+ .@npc_name$ +"-";
						mes "ตามที่คุณต้องการ ภารกิจนี้ถูกยกเลิก";
						erasequest getarg(0);
						close;
				}
		}
		else {
				mes "ฮืม คุณยังมีเวลาเหลืออยู่บ้าง แต่ยังไม่ถึงเวลาปิดล้อม";
				mes "ฉันไม่คิดว่าคุณควรไปต่อ";
				mes "คุณควรยกเลิกภารกิจนี้ ฉันคิดว่ามันล้มเหลว";
				next;
				switch( select( "I would like to go on.","Yes, I'll cancel it." ) ) {
				case 1:
						mes "-"+ .@npc_name$ +"-";
						mes "ฉันไม่คิดว่ามันคุ้มหรอก ขอให้โชคดี!";
						close;
				case 2:
						mes "-"+ .@npc_name$ +"-";
						mes "ตามที่คุณต้องการ ภารกิจนี้ถูกยกเลิก";
						erasequest getarg(0);
						close;
				}
		}

S_Acceptquest:
		.@npc_name$ = getarg(1);
		next;
		switch( select( "Accept it.","Do not accept it." ) ) {
		case 1:
				mes "-"+ .@npc_name$ +"-";
				mes "^4d4dffเวลาจำกัดคือ 4 ชั่วโมง^000000";
				mes "ดำเนินการให้สำเร็จภายในเวลาดังกล่าวแล้วรายงานให้ฉันทราบ";
				setquest getarg(0);
				close;
		case 2:
				mes "-"+ .@npc_name$ +"-";
				mes "ลองภารกิจอื่นดูสิ";
				close;
		}

S_Rentbox:
		setarray .@item_id[0], 2950,15067,20709,22012;// Rune_Ring, Rune_Suit, Mana_Manteau, Mana_Boots
		for ( .@count = 0; .@count < getarg(0); .@count++ ) {
				.@miss_item01 = rand(100);
				if (.@miss_item01 > 3)
						mes "คุณไม่พบอะไรเลย.";
				else {
						rentitem .@item_id[ .@miss_item01 ],86400;
						mes "คุณพบ '"+ getitemname(.@item_id[ .@miss_item01 ]) +"-";
				}
		}
		close;

S_Menu:
		for ( .@i = 1; .@i < getargcount(); .@i += 2 ) {
				if ( isbegin_quest(getarg(.@i)) )
						.@menu$ = .@menu$ + getarg(.@i+1) + " ^4d4dffCheck the result^000000:";
				else
						.@menu$ = .@menu$ + getarg(.@i+1) + ":";
				set getelementofarray( getarg(0),.@index ), getarg(.@i);
				.@index++;
		}
		return .@menu$;
}
//===== rAthena Script =======================================
//= God Items Quest
//===== Description: =========================================
//= [Official Conversion]
//= God Item Jormungand Creation script (WoE TE)
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

te_aldecas1,226,34,3	script	Blacksmith Cano#tegod01	4_M_RACHMAN1,{
		if (checkweight(1201,1) == 0) {
				mes "คุณมีไอเทมมากเกินไปที่จะดำเนินภารกิจต่อไปได้";
				mes "กรุณาลดภาระของคุณแล้วลองอีกครั้ง";
				close;
		}

		.@guild_id = getcastledata("te_aldecas1",1);
		.@economy  = getcastledata("te_aldecas1",2);
		.@npc_name$ = "Cano";

		mes "-"+ .@npc_name$ +"-";
		if (.@guild_id != getcharid(2)) {
				mes "คุณ...ดูไม่คุ้นหน้าฉันเลย... ?";
				mes "คุณจะขู่ฉันมั้ย?";
				mes "ไม่มีใช่มั้ย?";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "ฉันมีเหตุผลของฉัน";
				mes "กรุณาทำเป็นไม่เห็นฉัน";
				close;
		}
		if (WoeTETimeStart(14400) == 1 || agitcheck3() == 1) {// cannot start the quest 4 hours before and during WOE TE
				mes "จากนี้ไปฉันจะอยู่ที่นี่อย่างเงียบๆ และไม่รบกวนใครจนกว่าการปิดล้อมจะสิ้นสุดลง";
				mes "อย่ากังวลเกี่ยวกับฉัน";
				close;
		}
		if (is_guild_leader() == false) {
				mes "ฉันรู้สึกขอบคุณมากตลอดเวลาที่คุณให้ฉันอยู่ที่นี่";
				mes "ก็ไม่มีอะไรพิเศษ...แต่ถ้าคุณต้องการ ผมจะสร้างเครื่องมือที่มีประโยชน์ให้กับคุณได้";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "ฉันหมายถึงถ้าหัวหน้ากิลด์ต้องการมัน";
				mes "ผมก็เป็นอาจารย์ที่มุ่งมั่นสู่หนทางอันสูงสุดเช่นกัน";
				mes "ฉันหวังว่าฉันจะสามารถพบวิธีนั้นที่นี่ได้";
				close;
		}
		if ($2012_tegod_kafra == 0) {// event status
				mes "อ่า...อาจารย์ครับ";
				mes "ฉันรู้สึกขอบคุณมากตลอดเวลา";
				mes "เนื่องจากฉันเป็นหนี้คุณมาก ฉันจึงจะช่วยคุณในเรื่องอะไรก็ได้";
				next;
				switch( select( "สร้าง Jormungand.","วัสดุ Fuse.","ผ่อนคลาย." ) ) {
				case 1:
						if (.@economy > 29) {
								if (countitem(6603) > 3 && countitem(6599) > 0 && countitem(6598) > 0 && countitem(6601) > 3 && countitem(6600) > 0 && countitem(6605) > 0 && countitem(6604) > 0 && countitem(1473) > 0) {
										mes "^ff0000 จำเป็นต้องมีไม้เท้าเวทมนตร์ในกระบวนการสร้าง Jormungand";
										mes "คุณไม่ควรครอบครองไอเท็มอื่นใดนอกจากคทาเวทมนตร์เพื่อดำเนินการกับภารกิจต่อไป^ff0000";
										next;
										mes "^ff0000 แม้ว่าระดับการกลั่นและการ์ดจะได้รับการใช้แล้ว กระบวนการนี้จำเป็นต้องใช้คทาเวทมนตร์อย่างใดอย่างหนึ่งที่คุณอาจมี";
										mes "กรุณาลองใหม่อีกครั้งหลังจากจัดระเบียบทรัพย์สินของคุณแล้ว^000000";
										next;
										if (select( "Organize possessions.","ดำเนินต่อไปตามที่เป็นอยู่" ) == 1) {
												mes "คุณได้ตัดสินใจที่จะเก็บเฉพาะคทาเวทมนตร์เท่านั้น";
												close;
										}
										mes "-"+ .@npc_name$ +"-";
										mes "ฉันเห็นว่าคุณพัฒนาป้อมปราการได้ตามที่คุณสัญญาไว้";
										mes "นอกจากนี้คุณยังได้พบกับ Twilight of Trail...";
										next;
										mes "-"+ .@npc_name$ +"-";
										mes "... เอาล่ะ..มาทำงานให้เสร็จกันเถอะ";
										mes "ก่อนที่สาวๆจะเจอเรา...";
										next;
										mes "-" + strcharinfo(0) + "-";
										mes "สาวๆ...?";
										next;
										mes "-"+ .@npc_name$ +"-";
										mes "...ใช่แล้ว มีสาวๆอยู่บ้าง..";
										mes "ฉันจึงต้องวิ่งหนีเพราะพวกเขา...";
										donpcevent "#tequest_master_alde01::OnCommandOn";
										mapannounce "te_aldecas1","เสียงผู้หญิง: เฮ้ คุณกำลังพูดถึงพวกเราเหรอ มันคือ "+ .@npc_name$ +", ใช่มั้ย ฉันไม่เชื่อเลยว่าคุณจะหนีมาที่แบบนี้",bc_map,"0x00FF00",FW_BOLD,20,0,40;
										next;
										mes "-"+ .@npc_name$ +"-";
										mes "ไม่นะ... !";
										mes "พวกเขาอยู่ที่นี่..!!";
										next;
										mes "-" + strcharinfo(0) + "-";
										mes "ปล่อยให้เป็นหน้าที่ของฉันเอง";
										next;
										mes "^4d4dff!- คำสั่ง -!";
										mes "เร็วๆ นี้ 7 สาวที่กำลังตามอยู่"+ .@npc_name$ +"จะพยายามบุกเข้าป้อมปราการ";
										mes "คุณจะต้องปกป้อง"+ .@npc_name$ +"เป็นเวลา 1 ชั่วโมง^000000";
										next;
										mes "^4d4dff!- คำสั่ง -!";
										mes "เมื่อการโจมตีจริงเริ่มขึ้น ประตูทางเข้าภูมิภาคเอ็มเพเรียมจะถูกปิดกั้นเพื่อป้องกัน^000000";
										close;
								}
								mes "-"+ .@npc_name$ +"-";
								mes "Jormungand เป็นไม้เท้าที่มีต้นแบบมาจาก Jormungand บุตรแห่งเทพเจ้าผู้โอบรับโลก";
								mes "ยังหมายถึงแร็กนาร็อกยามสนธยาของเหล่าทวยเทพอีกด้วย";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "แต่ที่นี่เรามีสิ่งของที่เป็นสัญลักษณ์แห่งพลบค่ำของเทพเจ้า...";
								mes "และฉันไม่เชื่อว่าฉันจะได้ใช้มันทำ Jormungand เองด้วยมือของฉันเอง!";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "อย่างไรก็ตามดูเหมือนว่าเราจะต้องการวัสดุเพิ่มเติม";
								mes "ช่วยตรวจสอบอีกครั้งได้ไหม ฉันจะให้รายการสิ่งที่เราต้องการ";
								callsub S_Material, .@npc_name$,1,"เนื่องจาก Jormungand มีพลังอันยิ่งใหญ่";
						}
						mes "-"+ .@npc_name$ +"-";
						mes "แต่จะโอเคไหมถ้าฉันจะพูดอะไรบางอย่าง?";
						mes "Jormungand เป็นหนึ่งในอุปกรณ์รบที่ทรงพลังที่สุดที่ฉันผลิต และคุณสมควรได้รับสิ่งนี้จากฉัน...";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "แต่ผมอยากให้คุณมีคุณสมบัติขั้นต่ำอย่างน้อยที่สุด";
						mes "บริหารป้อมปราการนี้ให้ดีและเพิ่มระดับการค้า ^4d4dffits เป็น 30 หรือมากกว่า^000000";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ยิ่งมีพลังมาก ความรับผิดชอบและหน้าที่ก็ยิ่งมากตามไปด้วย";
						mes "ฉันอยากให้คุณเรียนรู้พื้นฐานก่อน";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ฉันจะทำ Jormungand ภายหลัง";
						mes "ฉันขอรายการวัสดุให้คุณหากคุณต้องการ";
						callsub S_Material, .@npc_name$,0,"เนื่องจาก Jormungand มีพลังอันยิ่งใหญ่";
				case 2:
						callsub S_Fuse, .@npc_name$, "Jormungand";
				case 3:
						mes "-"+ .@npc_name$ +"-";
						mes "คุณใจดีมากที่พูดแบบนั้น";
						mes "ยังไงก็อยากทราบว่าพี่ไก่ของฉันเป็นยังไงบ้างคะ?";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ฉันมั่นใจว่าเขาคงสบายดีเช่นเดียวกับฉัน";
						mes "... ฉันหวังว่าสิ่งต่างๆ จะคงสงบสุขเหมือนตอนนี้";
						close;
				}
		}
		if ($2012_tegod_kafra == 1) {// event started
				mes "คุณได้ยินข่าวมั้ย?";
				mes "สาวๆกำลังมาแล้ว";
				mes "สาววายไร้ความปราณี!!!";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "พวกเขาจะมาถึงที่นี่เมื่อไหร่ก็ได้ เนื่องจากพวกเขาประกาศสงครามไปแล้ว";
				mes "ฉันแน่ใจว่าพวกเขากำลังมาหา Jormungand";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "หยุดพวกมันซะเถอะ ขอร้อง!";
				next;
				switch( select( "ปล่อยให้เป็นหน้าที่ของฉันเอง","'สาวๆ' ที่คุณหมายถึงคืออะไร?" ) ) {
				case 1:
						mes "-"+ .@npc_name$ +"-";
						mes "ขอบคุณ...";
						mes "ฉันขอโทษที่ทำให้คุณลำบาก แต่ฉันมีมือที่ถูกมัดไว้แล้ว";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ขอร้องล่ะ หยุดมันเพื่อฉันเถอะ!";
						close;
				case 2:
						mes "-"+ .@npc_name$ +"-";
						mes "...พวกเธอคือสาวๆที่อยากจะเป็นวาลคิรี...";
						mes "และพวกมันก็จะเดินไปเป็นกลุ่มๆ ละเจ็ดคนเสมอ";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "สิ่งที่พวกเขาต้องการคืออุปกรณ์รบอันทรงพลัง...";
						mes "พวกเขาจะคอยขู่ฉันและไคพี่ชายของฉันเสมอ";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ตอนแรกพวกเขาพอใจกับอาวุธพื้นฐานแต่";
						mes "ไม่นานพวกมันก็เริ่มขู่เราว่าพวกมันต้องมีอะไรบางอย่างที่ทรงพลังกว่านี้เพื่อที่จะกลายเป็นวาลคิรี";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "ฉันทนไม่ได้เลย เลยวิ่งหนี...แต่พวกมันยังอยู่ที่นี่...";
						mes "คุณต้องระวังเมื่อพวกเขาพบว่ามีวัสดุสำหรับ Jormungand อยู่ที่นี่";
						close;
				}
		}
		if ($2012_tegod_kafra == 2) {// after 10 minutes delay
				mes "โอ้ย หยุดตามฉันซะที!";
				mes "ถ้ากำจัดพวกมันได้ก็ดีสิ... ยอร์มุงกานด์ นั่นแหละ! ฉันจะทำให้เธอเป็นยอร์มุงกานด์!";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "เมื่อไหร่เขาจะเลิกติดตามฉันซะที...";
				mes "ตอนนี้ผมเริ่มกลัวผู้หญิงโดยทั่วไปแล้ว... และผมก็กลัวเกินกว่าจะทำอะไรได้...";
				next;
				mes "เขากำลังพูดพล่ามตอนนี้";
				mes "แค่รอแล้วเขาจะสงบลง";
				mes "คุณจะต้องกำจัดสาว ๆ พวกนั้นก่อน";
				close;
		}
		if ($2012_tegod_kafra == 3) {// event failed
				mes "... ถึงแม้ฉันจะสามารถหนีรอดจากพวกมันได้อีกครั้งก็ตาม...";
				mes "มันยาก.";
				mes "ชีวิตมัน...ยากมาก";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "ฉันเหนื่อยมากจนทำอะไรไม่ได้เลย";
				mes "... ฉันไม่คิดว่าจะทำอะไรได้สักพัก ไม่ว่าจะเป็นการผลิต การตีเหล็ก หรืออะไรก็ตาม";
				mes "ขอโทษครับ..ผมจะพักก่อนนะครับ.";
				close;
		}
		if ($2012_tegod_kafra == 4) {
				mes "ในที่สุดฉันก็สามารถหายใจได้แล้ว";
				mes "ฉันจะเริ่มทำ Jormungand ตามที่สัญญาไว้";
				next;
				switch( select( "สร้าง Jormungand.","วัสดุ Fuse.","ไม่ใช่ตอนนี้." ) ) {
				case 1:
						if (countitem(6603) > 3 && countitem(6599) > 0 && countitem(6598) > 0 && countitem(6601) > 3 && countitem(6600) > 0 && countitem(6605) > 0 && countitem(6604) > 0 && countitem(1473) > 0) {
								mes "^ff0000 จำเป็นต้องมีไม้เท้าเวทมนตร์ในกระบวนการสร้าง Jormungand";
								mes "คุณไม่ควรครอบครองไอเท็มอื่นใดนอกจากคทาเวทมนตร์เพื่อดำเนินการกับภารกิจต่อไป^ff0000";
								next;
								mes "^ff0000 แม้ว่าระดับการกลั่นและการ์ดจะได้รับการใช้แล้ว กระบวนการนี้จำเป็นต้องใช้คทาเวทมนตร์อย่างใดอย่างหนึ่งที่คุณอาจมี";
								mes "กรุณาลองใหม่อีกครั้งหลังจากจัดระเบียบทรัพย์สินของคุณแล้ว^000000";
								next;
								if (select( "Organize possessions.","ดำเนินต่อไปตามที่เป็นอยู่" ) == 1) {
										mes "คุณได้ตัดสินใจที่จะเก็บเฉพาะคทาเวทมนตร์เท่านั้น";
										close;
								}
								mes "-"+ .@npc_name$ +"-";
								mes "โอ้...ขอบคุณนะ.";
								mes "ฉันไม่เชื่อเลยว่าในที่สุดฉันก็ได้สัมผัสวัสดุเหล่านี้ด้วยตัวเอง...";
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "นี่มันอยู่นี่";
								mes "ถึงต้องผ่านหลายขั้นตอนก็เร็วใช่ไหม?";
								mes "ฉันรู้สึกประทับใจมากจนรู้สึกเหมือนว่าเป็นเวลาสั้นมาก";
								next;
								mes "Jormungand อาวุธทรงพลังและการรวบรวมร่องรอยทั้งหมดในตำนาน ถูกสร้างขึ้นในที่สุด";
								mes "ในมือของ"+ .@npc_name$ +"-";
								delitem 6603,4;// Skin_Of_Hraesvelg
								delitem 6599,1;// Spirit_Of_Hugin
								delitem 6598,1;// Rib_Of_Jormungand
								delitem 6601,4;// Chisel_Of_Giant
								delitem 6600,1;// Spirit_Of_Munin
								delitem 6605,1;// Muspellium
								delitem 6604,1;// Essence_Of_Rune
								delitem 1473,1;// Wizardy_Staff
								$2012_tegod_kafra = 5;
								getitem 2020,1;// Jormungand
								announce "Twilight of trail [ Jormungand ] ได้รับการแนะนำให้โลกนี้รู้จักโดย ["+ strcharinfo(0) +"] แห่งกิลด์ ["+ getguildname(.@guild_id) +"]",bc_all,"0x70dbdb",FW_NORMAL,12;
								if (.@economy > 20)
										setcastledata "te_aldecas1",2,(.@economy -20);
								else
										setcastledata "te_aldecas1",2,1;// min is 1 for WOE TE
								next;
								mes "-"+ .@npc_name$ +"-";
								mes "เฮ้อ.. ฉันคิดว่าฉันคงทำงานไม่ได้สักพักแล้วล่ะ";
								mes "เหนื่อยจังเลย 555...";
								close;
						}
						mes "-"+ .@npc_name$ +"-";
						mes "อย่างไรก็ตามดูเหมือนว่าเราจะต้องการวัสดุเพิ่มเติม";
						mes "ช่วยตรวจสอบอีกครั้งได้ไหม ฉันจะให้รายการสิ่งที่เราต้องการ";
						callsub S_Material, .@npc_name$,1;
				case 2:
						callsub S_Fuse, .@npc_name$, "Jormungand";
				case 3:
						mes "-"+ .@npc_name$ +"-";
						mes "ตอนนี้คือเวลาที่เหมาะสมที่จะทำมัน";
						mes "เมื่อถึงคราวที่ Emperium ครั้งถัดไปปรากฏ ก็คงไม่มีโอกาสเหลืออยู่อีกแล้ว";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "โปรดทราบไว้ด้วยว่าฉันสามารถทำสิ่งนี้ได้เพียงสัปดาห์ละครั้งเท่านั้น";
						close;
				}
		}
		if ($2012_tegod_kafra == 5) {
				mes "ฉันเหนื่อยนิดหน่อย";
				mes "ฉันคิดว่าฉันควรพักผ่อนสักสองสามวัน";
				mes "ฉันอาจดูดี แต่จริงๆ แล้วฉันเหนื่อยทางจิตใจมาก";
				close;
		}
		mes "มันแปลกนะ.";
		mes "ฉันบอกไม่ได้ว่าฉันรู้สึกอย่างไร";
		mes "ฉันมีปัญหาสุขภาพบางอย่างรึเปล่า?";
		mes "มันเป็นอะไรที่ไม่อาจรักษาได้ใช่ไหม?";
		close;

S_Material:
		.@npc_name$ = getarg(0);
		next;
		mes "-"+ .@npc_name$ +"-";
		mes "^4d4dff4 หนังของเพรสเบิร์ค";
		mes "1 จิตวิญญาณแห่งฮูจิน";
		mes "1. วิญญาณแห่งมุนิน";
		mes "4 สิ่วยักษ์";
		mes "ซี่โครงของ Jormungand 1";
		mes "ไม้เท้าเวทมนตร์ 1 อัน";
		mes "1 มัสเปลเลียมและเอสเซ้นส์แห่งรูน ตามลำดับ^000000";
		next;
		mes "-"+ .@npc_name$ +"-";
		mes "เราต้องมีวัสดุมากเท่านี้";
		mes getarg(2,"");
		mes "อย่างไรก็ตาม Muspellium และ Essence of Rune จะต้องสร้างแยกกันโดยใช้ส่วนผสมเพิ่มเติม";
		next;
		mes "-"+ .@npc_name$ +"-";
		mes "เพื่อที่จะสร้าง Muspellium คุณต้องรวม Elunium จำนวน 50 ชิ้นกับ Oridecon จำนวน 50 ชิ้นเข้าด้วยกัน ^4d4dff^000000";
		mes "สำหรับ Essence of Rune คุณต้องรวม Ancient Rough Runestones 200 ก้อนกับ Mystic Rough Runestones 100 ก้อนเข้าด้วยกัน^000000";
		next;
		mes "-"+ .@npc_name$ +"-";
		mes "ฉันจะทำ Muspellium และ Essence of Rune ให้คุณได้ทุกเมื่อตามที่คุณร้องขอ";
		mes "และแน่นอน ฉันจะสร้าง Jormungand ทันทีที่เรารวบรวมวัสดุที่จำเป็นทั้งหมดได้";
		next;
		mes "-"+ .@npc_name$ +"-";
		if (getarg(1))
				mes "สิ่งที่ฉันทำไม่เคยพอเมื่อเทียบกับสิ่งที่คุณทำเพื่อฉัน";
		else {
				mes "อย่างไรก็ตาม คุณจะไม่สามารถพบ Twilight of Trail ได้";
				mes "...แต่คนเขาว่ากันว่าคนที่อยากได้จริงๆก็เห็นจะเป็นเช่นนั้นจริงๆ ขอให้โชคดีครับ";
		}
		close;

S_Fuse:
		.@npc_name$ = getarg(0);
		mes "-"+ .@npc_name$ +"-";
		mes "คุณอยากทำวัสดุอะไร?";
		next;
		switch( select( "Muspellium","Essence of Rune","ลืมมันไปซะ" ) ) {
		case 1:
				mes "-"+ .@npc_name$ +"-";
				if (countitem(985) > 49 && countitem(984) > 49) {
						mes "ฉันจะสร้าง Muspellium ด้วยวัตถุดิบที่คุณเตรียมไว้";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "มันไม่น่าจะใช้เวลานาน";
						mes "นี่มันอยู่นี่";
						mes "เป็นวัตถุดิบสำคัญในการทำเฮอร์วอร์และยอร์มุงกานด์";
						delitem 985,50;// Elunium
						delitem 984,50;// Oridecon
						getitem 6605,1;// Muspellium
						close;
				}
				mes "เพื่อที่จะสร้างมัสเปลเลียม";
				mes "^4d4dffคุณต้องการ Elunium 50 ชิ้นและ Oridecon 50 ชิ้น^000000";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "เราควรจะมีวัสดุทั้งหมดพร้อมใช่ไหม?";
				close;
		case 2:
				mes "-"+ .@npc_name$ +"-";
				if (countitem(12735) > 199 && countitem(12736) > 99) {
						mes "ฉันจะสร้าง Essence of Rune ด้วยวัตถุดิบที่คุณเตรียมไว้";
						next;
						mes "-"+ .@npc_name$ +"-";
						mes "มันไม่น่าจะใช้เวลานาน";
						mes "นี่มันอยู่นี่";
						mes "เป็นวัตถุดิบสำคัญในการทำเฮอร์วอร์และยอร์มุงกานด์";
						delitem 12735,200;// Runstone_Ancient
						delitem 12736,100;// Runstone_Mystic
						getitem 6604,1;// Essence_Of_Rune
						close;
				}
				mes "สำหรับเอสเซ้นส์แห่งรูน";
				mes "^4d4dffคุณต้องการหินรูนดิบโบราณ 200 ก้อนและหินรูนดิบลึกลับ 100 ก้อน^000000";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "คงต้องใช้เวลาพอสมควร...";
				close;
		case 3:
				mes "-"+ .@npc_name$ +"-";
				mes "บอกฉันเมื่อไหร่ก็ตามที่คุณต้องการ";
				mes "ฉันดีใจที่อย่างน้อยฉันก็ทำสิ่งนี้เพื่อคุณได้";
				next;
				mes "-"+ .@npc_name$ +"-";
				mes "อีกทั้งยังมีสถานที่ให้ค้นหาวัสดุสำหรับ"+ getarg(1) +"-";
				mes "นี่ก็เหมือนสถานที่ในฝันของฉันเหมือนกัน";
				close;
		}

OnInit:
		if ($2012_tegod_kafra == 3 || agitcheck3())// 3 -> quest failed
				disablenpc();
		else
				enablenpc();
		end;
}

te_aldecas1,3,2,0	script	#tequest_master_alde01	CLEAR_NPC,{
		mes "รหัสผ่าน?";
		next;
		if ( callfunc( "F_GM_NPC", 1854,0, 0,9999 ) < 1 ) {
				mes "ออกไปซะ";
				close;
		}
		.@map$ = "te_aldecas1";
		mes "สถานะปัจจุบัน:" + $2012_tegod_kafra;
		mes "0: สามารถดำเนินการภารกิจได้";
		mes "1: ดำเนินการตามภารกิจ รอ 10 นาที";
		mes "2: ภารกิจกำลังดำเนินอยู่";
		mes "3: ภารกิจล้มเหลว";
		mes "4: สามารถผลิตสินค้าได้ , 5: ผลิตสินค้าได้สำเร็จ";
		next;
		mes "สถานะเจ้าหญิง:" + $@2012_tegirls_alde01;
		mes "ระดับการค้าปัจจุบันของป้อมปราการ:" + getcastledata(.@map$,2);
		next;
		switch( select( "Do not proceed","Initialization","Raise Commerce Level","Lower Commerce Level" ) ) {
		case 1:
				end;
		case 2:
				mes "การเริ่มต้น";
				donpcevent "#tequest_master_alde01::OnReset";
				close;
		case 3:
				.@economy = getcastledata(.@map$,2) +30;
				.@economy = .@economy > 100 ? 100 : .@economy;
				setcastledata .@map$,2,.@economy;
				mes "เพิ่มระดับการค้าขึ้น 30";
				mes "ระดับการพาณิชย์ที่ปรับเปลี่ยน:" + .@economy;
				close;
		case 4:
				.@economy1 = getcastledata(.@map$,2);
				.@economy2 = .@economy1 > 30 ? (.@economy1 -30) : 1;
				if (.@economy1 > 30) {
						mes "ระดับพาณิชย์ต่ำลง 30";
						setcastledata .@map$,2,.@economy2;
				}
				else {
						mes "ระดับการพาณิชย์ลดลงเหลือ 0 เนื่องจากระดับปัจจุบันต่ำกว่า 30";
						setcastledata .@map$,2,1;// min is 1 for WOE TE
				}
				mes "ระดับการพาณิชย์ที่ปรับเปลี่ยน:" + .@economy2;
				close;
		}

OnReset:// gm Initialization
		callsub S_EventReset;// kill all quests mobs and stop timer of others npcs
		callsub S_EventOff;// clear setnpcdisplay
		$2012_tegod_kafra = 0;
		$@2012_tegirls_alde01 = 0;
		.npc_name$ = "";
		enablenpc "aldeg-1-11_aldeg-1-15#te";
		enablenpc "Blacksmith Cano#tegod01";
		stopnpctimer;
		end;

OnCommandOn:
		$2012_tegod_kafra = 1;
		.npc_name$ = strnpcinfo(0);// for script readability
		initnpctimer;
		end;

OnTimer10000:
		announce "Girls' voices: Cano~ We already know where you are. It's over!",bc_all,"0xFF0000",FW_NORMAL,10;
		end;
OnTimer15000:
		announce "Girls' Voices: We'll wait just 10 minutes. If you don't show up in the meantime, we'll storm the fortress!",bc_all,"0xFF0000",FW_NORMAL,10;
		end;
OnTimer595000:
		mapannounce "te_aldecas1","A girl's voice: Cano! How dare you ignore our warning! So, you have company there, huh? But how long do you think they'll stay with you?!",bc_map,"0x00ff00",FW_NORMAL,12;
		end;
OnTimer598000:
		mapannounce "te_aldecas1","You are forced out of the fortress by unknown power.",bc_map,"0x00ff00",FW_NORMAL,12;
		end;

OnTimer600000:
		$2012_tegod_kafra = 2;
		mapwarp "te_aldecas1", "te_alde_gld",130,264;
		donpcevent "Aira#fake01_alde01::OnCommandOn";
		donpcevent "Kuluna#fake01_alde01::OnCommandOn";
		donpcevent "Mallina#fake01_alde01::OnCommandOn";
		donpcevent "Ezella#fake01_alde01::OnCommandOn";
		donpcevent "Lune#fake01_alde01::OnCommandOn";
		donpcevent "Morin#fake01_alde01::OnCommandOn";
		donpcevent "Nasarin#fake01_alde01::OnCommandOn";

		disablenpc "aldeg-1-11_aldeg-1-15#te";// warp to emperium room
		end;

OnTimer602000:
		announce "Glaris in Kafragarten region is being attacked by a group of unidentified beings.",bc_all,"0xFF0000",FW_NORMAL,10;
		monster "te_aldecas1",0,0,"Raydric",1163,15, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Khalitzburg",1132,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Abysmal Knight",1219,5, .npc_name$ +"::OnMyMobDead";

		monster "te_aldecas1",207,34,"Dalongee",1272,1, .npc_name$ +"::OnMyMobDead";// DARK_LORD
		monster "te_aldecas1",213,46,"Summoned Reindeer",1251,1, .npc_name$ +"::OnMyMobDead";// KNIGHT_OF_WINDSTORM
		monster "te_aldecas1",218,46,"Summoned Wolf",1252,1, .npc_name$ +"::OnMyMobDead";// GARM

		monster "te_aldecas1",26,224,"Dalongee",1272,1, .npc_name$ +"::OnMyMobDead";// DARK_LORD
		monster "te_aldecas1",24,199,"Summoned Reindeer",1251,1, .npc_name$ +"::OnMyMobDead";// KNIGHT_OF_WINDSTORM
		monster "te_aldecas1",113,93,"Summoned Wolf",1252,1, .npc_name$ +"::OnMyMobDead";// GARM
		end;

OnTimer610000:
		monster "te_aldecas1",0,0,"Raydric",1163,15, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Khalitzburg",1132,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Abysmal Knight",1219,5, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Bloody Knight",1268,5, .npc_name$ +"::OnMyMobDead";
		end;

OnTimer1000000:
OnTimer1300000:
OnTimer1900000:
OnTimer2500000:
OnTimer3100000:
		callsub S_MobRedondant;
		end;
S_MobRedondant:
		monster "te_aldecas1",0,0,"Raydric",1163,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Khalitzburg",1132,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Abysmal Knight",1219,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"High Orc",1213,10, .npc_name$ +"::OnMyMobDead";
		monster "te_aldecas1",0,0,"Bloody Knight",1268,5, .npc_name$ +"::OnMyMobDead";
		end;

OnTimer4200000:
		mapannounce "te_aldecas1","Girls: ... how long do we have to wait?",bc_map,"0x00ff00",FW_NORMAL,12;
		end;
OnTimer4205000:
		mapannounce "te_aldecas1","Girls: Jormungand should be made for us!",bc_map,"0x00ff00",FW_NORMAL,12;
		end;
OnTimer4210000:
		mapannounce "te_aldecas1","Girls: Come on, cut if off! You guys are so persistent!!",bc_map,"0x00ff00",FW_NORMAL,12;
		end;
OnTimer4220000:
		mapannounce "te_aldecas1","Aira: Ah! I can't take it anymore! Girls, come here!",bc_map,"0x00ff00",FW_NORMAL,12;

		$2012_tegod_kafra = 3;// quest off until someone break the emp
		callsub S_EventReset;
		enablenpc "aldeg-1-11_aldeg-1-15#te";
		end;

OnTimer4225000:
		donpcevent "#eproom_girls_alde01::OnReset";
		donpcevent "#end_aira_alde01::OnCommandOn";
		donpcevent "#end_kuluna_alde01::OnCommandOn";
		donpcevent "#end_mallina_alde01::OnCommandOn";
		donpcevent "#end_ezella_alde01::OnCommandOn";
		donpcevent "#end_lune_alde01::OnCommandOn";
		donpcevent "#end_morin_alde01::OnCommandOn";
		donpcevent "#end_nasarin_alde01::OnCommandOn";
		end;

OnTimer4227000:
		npctalk "คาโนะ: โอ้...ไม่นะ! คุณจะทำอะไรกับฉันนะ!", "Blacksmith Cano#tegod01";
		end;
OnTimer4230000:
		npctalk "ไอระ: หืม? เฮ้ คาโนะ เธอจะกลับบ้านแล้ว", "#end_aira_alde01";
		end;
OnTimer4233000:
		npctalk "คูลูน่า: ใช่แล้ว~ คุณต้องซ่อมไม้กายสิทธิ์ที่พังของฉันและสร้างอาวุธทรงพลังให้ฉันเมื่อนายกลับมานะ!", "#end_kuluna_alde01";
		end;
OnTimer4236000:
		npctalk "เอเซลลา: ลองวิ่งหนีอีกครั้งสิ เธอจะต้องเสียใจ เข้าใจไหม?", "#end_ezella_alde01";
		end;
OnTimer4237000:
		npctalk "นาสาริน : ... เอาไปเลยสิ ทำไมพวกคุณถึงคุยกับเขาเยอะจัง", "#end_nasarin_alde01";
		end;
OnTimer4240000:
		npctalk "ไอระ: ถูกต้องแล้ว! โอเค!", "#end_aira_alde01";
		npctalk "คาโนะ: โอ้... ไม่นะ... ไม่นะ!!!!!", "Blacksmith Cano#tegod01";
		end;

OnTimer4243000:
		mapannounce "te_aldecas1","You've failed to recapture the fortress. Cano is now gone.",bc_map,"0x00ff00",FW_NORMAL,12;
		disablenpc "Blacksmith Cano#tegod01";
		callsub S_EventOff;// clear setnpcdisplay
		.npc_name$ = "";
		stopnpctimer;
		end;

S_EventOff:
		donpcevent "#end_aira_alde01::OnCommandOff";
		donpcevent "#end_kuluna_alde01::OnCommandOff";
		donpcevent "#end_mallina_alde01::OnCommandOff";
		donpcevent "#end_ezella_alde01::OnCommandOff";
		donpcevent "#end_lune_alde01::OnCommandOff";
		donpcevent "#end_morin_alde01::OnCommandOff";
		donpcevent "#end_nasarin_alde01::OnCommandOff";
		return;

OnStop:// all girls have been defeated, kill remaining mobs and stop timer
		callsub S_EventReset;
		$@2012_tegirls_alde01 = 0;
		.npc_name$ = "";
		stopnpctimer;
		end;

S_EventReset:
		killmonster "te_aldecas1", .npc_name$ +"::OnMyMobDead";
		donpcevent "#eproom_girls_alde01::OnReset";
		donpcevent "Aira#fake01_alde01::OnReset";
		donpcevent "Kuluna#fake01_alde01::OnReset";
		donpcevent "Mallina#fake01_alde01::OnReset";
		donpcevent "Ezella#fake01_alde01::OnReset";
		donpcevent "Lune#fake01_alde01::OnReset";
		donpcevent "Morin#fake01_alde01::OnReset";
		donpcevent "Nasarin#fake01_alde01::OnReset";

		donpcevent "#aira_real_alde01::OnReset";
		donpcevent "#kuluna_real_alde01::OnReset";
		donpcevent "#mallina_real_alde01::OnReset";
		donpcevent "#ezella_real_alde01::OnReset";
		donpcevent "#lune_real_alde01::OnReset";
		donpcevent "#morin_real_alde01::OnReset";
		donpcevent "#nasarin_real_alde01::OnReset";
		return;

OnMyMobDead:
		end;

OnInit:
		$@2012_tegirls_alde01 = 0;// girls mobs killed count
		if ($2012_tegod_kafra < 3)
				$2012_tegod_kafra = 0;
		end;
}


te_aldecas1,40,226,3	script	Aira#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Aira#fake01_alde01";
		setnpcdisplay "Aira#fake01_alde01",2443;// AIRA
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Aira#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#aira_real_alde01::OnCommandOn";
		npctalk "ไอระ: ลูกบอลน้ำของฉัน! ฉันจะจัดการเธอเอง! ด้วยมือของฉันเอง!!";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Aira#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Aira#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Aira#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",26,224,"Bodyguard",1213,5, "Aira#fake01_alde01::OnMyMobDead";// HIGH_ORC
		monster "te_aldecas1",26,224,"Bodyguard",1309,5, "Aira#fake01_alde01::OnMyMobDead";// GAJOMART
		end;
OnTimer2000:
		monster "te_aldecas1",26,224,"Bodyguard",1367,5, "Aira#fake01_alde01::OnMyMobDead";// BLAZZER
		monster "te_aldecas1",26,223,"Bodyguard",1837,5, "Aira#fake01_alde01::OnMyMobDead";// IMP
		end;
OnTimer3000:
		monster "te_aldecas1",26,223,"Bodyguard",1949,1, "Aira#fake01_alde01::OnMyMobDead";// B_S_GUARDIAN
		monster "te_aldecas1",26,224,"Bodyguard",1833,2, "Aira#fake01_alde01::OnMyMobDead";// KASA
		monster "te_aldecas1",26,224,"Bodyguard",1831,2, "Aira#fake01_alde01::OnMyMobDead";// SALAMANDER
		stopnpctimer;
		end;
}

te_aldecas1,33,197,3	script	Kuluna#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Kuluna#fake01_alde01";
		setnpcdisplay "Kuluna#fake01_alde01",2444;// KULUNA
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Kuluna#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#kuluna_real_alde01::OnCommandOn";
		npctalk "คูลูน่า: หืม? แล้วจะเป็นแบบนี้เหรอ? เตรียมตัวไว้ให้ดีล่ะ!";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Kuluna#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Kuluna#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Kuluna#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",24,199,"Guardian",1143,10, "Kuluna#fake01_alde01::OnMyMobDead";// MARIONETTE
		end;
OnTimer2000:
		monster "te_aldecas1",24,199,"Guardian",1693,10, "Kuluna#fake01_alde01::OnMyMobDead";// PLASMA_Y
		end;
OnTimer3000:
		monster "te_aldecas1",24,199,"Guardian",1869,5, "Kuluna#fake01_alde01::OnMyMobDead";// FLAME_SKULL
		monster "te_aldecas1",24,199,"Guardian",1704,1, "Kuluna#fake01_alde01::OnMyMobDead";// THA_ODIUM
		monster "te_aldecas1",24,199,"Guardian",1705,1, "Kuluna#fake01_alde01::OnMyMobDead";// THA_DESPERO
		monster "te_aldecas1",24,199,"Guardian",1706,1, "Kuluna#fake01_alde01::OnMyMobDead";// THA_MAERO
		monster "te_aldecas1",24,199,"Guardian",1707,1, "Kuluna#fake01_alde01::OnMyMobDead";// THA_DOLOR
		stopnpctimer;
		end;
}

te_aldecas1,90,69,3	script	Mallina#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Mallina#fake01_alde01";
		setnpcdisplay "Mallina#fake01_alde01",2445;// MALLINA
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Mallina#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#mallina_real_alde01::OnCommandOn";
		npctalk "มัลลิน่า : หยุดเลย!! หยุดกวนฉันซะ!!";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Mallina#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Mallina#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Mallina#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",83,70,"The Absolute",1993,10, "Mallina#fake01_alde01::OnMyMobDead";// NAGA
		end;
OnTimer3000:
		monster "te_aldecas1",83,70,"The Absolute",1695,10, "Mallina#fake01_alde01::OnMyMobDead";// PLASMA_G
		end;
OnTimer5000:
		monster "te_aldecas1",83,71,"The Absolute",1717,1, "Mallina#fake01_alde01::OnMyMobDead";// FERUS_
		monster "te_aldecas1",82,71,"The Absolute",1717,1, "Mallina#fake01_alde01::OnMyMobDead";// FERUS_
		monster "te_aldecas1",81,71,"The Absolute",1717,1, "Mallina#fake01_alde01::OnMyMobDead";// FERUS_
		monster "te_aldecas1",80,71,"The Absolute",1717,1, "Mallina#fake01_alde01::OnMyMobDead";// FERUS_
		monster "te_aldecas1",79,71,"The Absolute",1717,1, "Mallina#fake01_alde01::OnMyMobDead";// FERUS_

		monster "te_aldecas1",83,70,"The Absolute",1991,2, "Mallina#fake01_alde01::OnMyMobDead";// TENDRILRION
		monster "te_aldecas1",83,70,"The Absolute",1989,2, "Mallina#fake01_alde01::OnMyMobDead";// HILLSRION
		stopnpctimer;
		end;
}

te_aldecas1,109,91,3	script	Ezella#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Ezella#fake01_alde01";
		setnpcdisplay "Ezella#fake01_alde01",2446;// EZELLA
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Ezella#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#ezella_real_alde01::OnCommandOn";
		npctalk "เอเซลล่า: อ่า... มันยิ่งทำให้เราทั้งสองคนเหนื่อยมากขึ้นเท่านั้น...";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Ezella#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Ezella#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Ezella#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",113,93,"Convoy",1692,10, "Ezella#fake01_alde01::OnMyMobDead";// BREEZE
		end;
OnTimer2000:
		monster "te_aldecas1",113,93,"Convoy",1627,2, "Ezella#fake01_alde01::OnMyMobDead";// ANOPHELES
		monster "te_aldecas1",113,93,"Convoy",1656,2, "Ezella#fake01_alde01::OnMyMobDead";// KAVAC
		monster "te_aldecas1",113,93,"Convoy",1315,6, "Ezella#fake01_alde01::OnMyMobDead";// ASSULTER
		end;
OnTimer3000:
		monster "te_aldecas1",113,93,"Convoy",1669,5, "Ezella#fake01_alde01::OnMyMobDead";// DIMIK
		monster "te_aldecas1",113,93,"Convoy",1259,2, "Ezella#fake01_alde01::OnMyMobDead";// GRYPHON
		monster "te_aldecas1",113,93,"Convoy",1783,1, "Ezella#fake01_alde01::OnMyMobDead";// GALION
		monster "te_aldecas1",113,93,"Convoy",1950,1, "Ezella#fake01_alde01::OnMyMobDead";// B_B_GUARDIAN
		stopnpctimer;
		end;
}

te_aldecas1,85,108,3	script	Lune#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Lune#fake01_alde01";
		setnpcdisplay "Lune#fake01_alde01",2447;// LUNE
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Lune#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#lune_real_alde01::OnCommandOn";
		npctalk "ลูน : ..อยากจมน้ำตายในทะเลลึกเหรอ ห้ะ?";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Lune#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Lune#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Lune#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",74,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";// GAZETI
		monster "te_aldecas1",75,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",76,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",77,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",78,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",79,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",80,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",81,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",82,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",83,83,"Bodyguard",1778,1, "Lune#fake01_alde01::OnMyMobDead";
		end;
OnTimer2000:
		monster "te_aldecas1",84,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";// AQUA_ELEMENTAL
		monster "te_aldecas1",85,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",86,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",87,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",88,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",89,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",90,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",91,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",92,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		monster "te_aldecas1",93,83,"Bodyguard",2016,1, "Lune#fake01_alde01::OnMyMobDead";
		end;
OnTimer3000:
		monster "te_aldecas1",82,95,"Bodyguard",1777,5, "Lune#fake01_alde01::OnMyMobDead";// ICE_TITAN
		monster "te_aldecas1",82,95,"Bodyguard",1681,1, "Lune#fake01_alde01::OnMyMobDead";// GEMINI
		monster "te_aldecas1",82,95,"Bodyguard",2021,1, "Lune#fake01_alde01::OnMyMobDead";// PHYLLA
		monster "te_aldecas1",82,95,"Bodyguard",2020,2, "Lune#fake01_alde01::OnMyMobDead";// RHYNCHO
		stopnpctimer;
		end;
}

te_aldecas1,207,156,3	script	Morin#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Morin#fake01_alde01";
		setnpcdisplay "Morin#fake01_alde01",2448;// MORIN
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Morin#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#morin_real_alde01::OnCommandOn";
		npctalk "มอริน: ... . ...";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Morin#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Morin#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Morin#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",207,148,"Nightmare Gang",1379,10, "Morin#fake01_alde01::OnMyMobDead";// NIGHTMARE_TERROR
		end;
OnTimer2000:
		monster "te_aldecas1",207,148,"Nightmare Gang",1701,5, "Morin#fake01_alde01::OnMyMobDead";// SHELTER
		monster "te_aldecas1",207,148,"Nightmare Gang",1753,5, "Morin#fake01_alde01::OnMyMobDead";// FRUS
		end;
OnTimer3000:
		monster "te_aldecas1",207,148,"Nightmare Gang",1752,5, "Morin#fake01_alde01::OnMyMobDead";// SKOGUL
		monster "te_aldecas1",207,148,"Nightmare Gang",1733,1, "Morin#fake01_alde01::OnMyMobDead";// KIEL
		monster "te_aldecas1",207,148,"Nightmare Gang",1205,1, "Morin#fake01_alde01::OnMyMobDead";// EXECUTIONER
		monster "te_aldecas1",207,148,"Nightmare Gang",2040,1, "Morin#fake01_alde01::OnMyMobDead";// TIRFING_R
		monster "te_aldecas1",207,148,"Nightmare Gang",1203,1, "Morin#fake01_alde01::OnMyMobDead";// MYSTELTAINN
		stopnpctimer;
		end;
}

te_aldecas1,214,176,3	script	Nasarin#fake01_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "Nasarin#fake01_alde01";
		setnpcdisplay "Nasarin#fake01_alde01",2449;// NASARIN
		initnpctimer;
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","Nasarin#fake01_alde01::OnMyMobDead" )) end;
		donpcevent "#nasarin_real_alde01::OnCommandOn";
		npctalk "นาซาริน: ..อ่า.. มันยากมากเลยนะที่จะกลายมาเป็นวาลคิรี!";
OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "Nasarin#fake01_alde01::OnMyMobDead";
		setnpcdisplay "Nasarin#fake01_alde01",CLEAR_NPC;
OnInit:
		disablenpc "Nasarin#fake01_alde01";
		end;

OnTimer1000:
		monster "te_aldecas1",212,170,"Gang of Poison",1987,10, "Nasarin#fake01_alde01::OnMyMobDead";// CENTIPEDE
		end;
OnTimer2000:
		monster "te_aldecas1",212,170,"Gang of Poison",2015,10, "Nasarin#fake01_alde01::OnMyMobDead";// PINGUICULA_D
		end;
OnTimer3000:
		monster "te_aldecas1",212,170,"Gang of Poison",1618,3, "Nasarin#fake01_alde01::OnMyMobDead";// UNGOLIANT
		monster "te_aldecas1",212,170,"Khalitz",1949,1, "Nasarin#fake01_alde01::OnMyMobDead";// B_S_GUARDIAN
		stopnpctimer;
		end;
}

-	script	real_alde01	-1,{
		end;
OnCommandOn:
		.@npc_name$ = strnpcinfo(0);
		if (.@npc_name$ == "#aira_real_alde01")
				monster "te_aldecas1",26,224,"Aira in a blaze",2443,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#kuluna_real_alde01")
				monster "te_aldecas1",24,199,"Passionate Kuluna",2444,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#mallina_real_alde01")
				monster "te_aldecas1",83,70,"Mallina of the Earth",2445,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#ezella_real_alde01")
				monster "te_aldecas1",113,93,"Ezella of Verdure",2446,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#lune_real_alde01")
				monster "te_aldecas1",82,95,"Lune of the Eternal Glacier",2447,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#morin_real_alde01")
				monster "te_aldecas1",207,148,"Morin of the Abyss",2448,1, .@npc_name$ +"::OnMyMobDead";
		else if (.@npc_name$ == "#nasarin_real_alde01")
				monster "te_aldecas1",212,170,"Fatal Nasarin",2449,1, .@npc_name$ +"::OnMyMobDead";
		end;

OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", strnpcinfo(0)+"::OnMyMobDead";
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1",strnpcinfo(0) +"::OnMyMobDead" ) == 0)
				initnpctimer;
		end;

OnTimer1000:
		if (mobcount( "te_aldecas1",strnpcinfo(0) +"::OnMyMobDead" ) == 0) {
				$@2012_tegirls_alde01++;
				if ($@2012_tegirls_alde01 == 7) {
						mapannounce "te_aldecas1","Cano: aaahh!! The girls broke the shields and made their way into where Emperium is!!!!",bc_map,"0x00ff00",FW_NORMAL,12;
						donpcevent "#eproom_girls_alde01::OnCommandOn";
				}
				else {
						.@npc_name$ = strnpcinfo(0);
						if (.@npc_name$ == "#aira_real_alde01")
								mapannounce "te_aldecas1","Aira: You think I lost? You're wrong! We force our way to where Cano is!!",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#kuluna_real_alde01")
								mapannounce "te_aldecas1","Kuluna: ... This is called a strategic retreat.",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#mallina_real_alde01")
								mapannounce "te_aldecas1","Mallina: ... Why are you disturbing us so much? But victory will be ours in the end!!",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#ezella_real_alde01")
								mapannounce "te_aldecas1","Ezella: Oh you, a weakling that would just disappear like the wind... !!!",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#lune_real_alde01")
								mapannounce "te_aldecas1","Lune: ..Alright. Let the battle begin!",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#morin_real_alde01")
								mapannounce "te_aldecas1","Morin: It's not... interesting anymore.",bc_map,"0x00ff00",FW_NORMAL,12;
						else if (.@npc_name$ == "#nasarin_real_alde01")
								mapannounce "te_aldecas1","Nasarin: .. I wasted my time. Cano is in the center of this fortress?",bc_map,"0x00ff00",FW_NORMAL,12;
				}
		}
		stopnpctimer;
		end;
}
te_aldecas1,4,2,0	duplicate(real_alde01)	#aira_real_alde01	CLEAR_NPC
te_aldecas1,5,2,0	duplicate(real_alde01)	#kuluna_real_alde01	CLEAR_NPC
te_aldecas1,6,2,0	duplicate(real_alde01)	#mallina_real_alde01	CLEAR_NPC
te_aldecas1,7,2,0	duplicate(real_alde01)	#ezella_real_alde01	CLEAR_NPC
te_aldecas1,8,2,0	duplicate(real_alde01)	#lune_real_alde01	CLEAR_NPC
te_aldecas1,9,2,0	duplicate(real_alde01)	#morin_real_alde01	CLEAR_NPC
te_aldecas1,10,2,0	duplicate(real_alde01)	#nasarin_real_alde01	CLEAR_NPC


te_aldecas1,11,2,0	script	#eproom_girls_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		enablenpc "aldeg-1-11_aldeg-1-15#te";
		monster "te_aldecas1",207,34,"Fatal Nasarin",2449,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",216,26,"Morin of the Abyss",2448,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",205,21,"Lune of the Eternal Glacier",2447,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",215,34,"Ezella of Verdure",2446,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",226,24,"Mallina of the Earth",2445,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",213,46,"Passionate Kuluna",2444,1, "#eproom_girls_alde01::OnMyMobDead";
		monster "te_aldecas1",218,46,"Aira in a blaze",2443,1, "#eproom_girls_alde01::OnMyMobDead";
		end;

OnReset:
		stopnpctimer;
		killmonster "te_aldecas1", "#eproom_girls_alde01::OnMyMobDead";
		end;

OnMyMobDead:
		if (mobcount( "te_aldecas1","#eproom_girls_alde01::OnMyMobDead" ) == 0)
				initnpctimer;
		end;

OnTimer3000:
		if (mobcount( "te_aldecas1","#eproom_girls_alde01::OnMyMobDead" ) == 0) {
				donpcevent "#tequest_master_alde01::OnStop";
				$2012_tegod_kafra = 4;
				mapannounce "te_aldecas1","You have defeated the girls and recaptured the fortress successfully.",bc_map,"0x00ff00",FW_NORMAL,12;
		}
		stopnpctimer;
		end;
}

te_aldecas1,225,34,5	script	#end_aira_alde01	CLEAR_NPC,{
		end;
OnCommandOn:
		.@npc_name$ = strnpcinfo(0);
		enablenpc .@npc_name$;
		if (compare(.@npc_name$,"aira")) setnpcdisplay .@npc_name$,2443;
		else if (compare(.@npc_name$,"kuluna")) setnpcdisplay .@npc_name$,2444;
		else if (compare(.@npc_name$,"mallina")) setnpcdisplay .@npc_name$,2445;
		else if (compare(.@npc_name$,"ezella")) setnpcdisplay .@npc_name$,2446;
		else if (compare(.@npc_name$,"lune")) setnpcdisplay .@npc_name$,2447;
		else if (compare(.@npc_name$,"morin")) setnpcdisplay .@npc_name$,2448;
		else if (compare(.@npc_name$,"nasarin")) setnpcdisplay .@npc_name$,2449;
		end;

OnCommandOff:
		setnpcdisplay strnpcinfo(0),CLEAR_NPC;
OnInit:
		disablenpc();
		end;
}
te_aldecas1,227,35,3	duplicate(#end_aira_alde01)	#end_kuluna_alde01	CLEAR_NPC
te_aldecas1,226,33,7	duplicate(#end_aira_alde01)	#end_mallina_alde01	CLEAR_NPC
te_aldecas1,225,35,5	duplicate(#end_aira_alde01)	#end_ezella_alde01	CLEAR_NPC
te_aldecas1,227,34,3	duplicate(#end_aira_alde01)	#end_lune_alde01	CLEAR_NPC
te_aldecas1,226,35,4	duplicate(#end_aira_alde01)	#end_morin_alde01	CLEAR_NPC
te_aldecas1,227,33,1	duplicate(#end_aira_alde01)	#end_nasarin_alde01	CLEAR_NPC
//===== rAthena Script =======================================
//= Dewata Quest NPCs
//===== By: ==================================================
//= Gennosuke Kouga, Muad_Dib
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quest NPCs related to Dewata:
//== "Dewata Legend"
//== "Help Out the Old Man"
//== "Traditional Weapon"
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardized. [Euphy]
//= 1.2 Updated to match the official script. [Lemongrass/Euphy]
//============================================================

// Dewata Legend Quest :: dewata_legend
//============================================================
dew_fild01,57,274,0	script	#hideout_legend	111,6,6,{
OnTouch:
		if (BaseLevel > 59 && dew_legend < 1) {
				mes "- คุณได้ยินบทสนทนาเบาๆ เกิดขึ้นภายใน -";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันล้มเหลวอีกแล้ว";
				mes "สร้าง ^8B4513Jaty Crown^000000...";
				mes "ฉันมั่นใจว่าคราวนี้ฉันจะทำได้";
				mes "ทำไมมันถึงใช้ไม่ได้?";
				next;
				mes "[ฤๅษีกาศป]";
				mes "^006400มงกุฎเจตี้^000000 เป็นที่รู้จักกันว่ามีพลังมหัศจรรย์มาตั้งแต่สมัยโบราณ";
				mes "แต่มีการกล่าวไว้ว่ามีเพียงผู้ที่ได้รับเกียรติจากชนเผ่าเท่านั้น";
				mes "ก็จะสามารถใช้งานได้";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "^0000FFกสิปา^000000!!";
				mes "คุณกำลังจะบอกว่าถึงแม้ว่าฉันจะเป็นผู้นำของเผ่าของเรา แต่ฉันยังไม่ได้รับเกียรตินั้นเลยเหรอ?";
				next;
				mes "[ฤๅษีกาศป]";
				mes "ใจเย็นๆ ^0000FFChief Paiko^000000...";
				mes "ในฐานะผู้นำที่ยิ่งใหญ่และทรงอำนาจ คุณควรมีคุณสมบัติมากเกินพอที่จะทำเช่นนั้น";
				next;
				mes "[ฤๅษีกาศป]";
				mes "ตอนผมยังหนุ่มๆ ผมสามารถแก้ปัญหานี้ได้อย่างง่ายดาย แต่จิตใจของชายชราคนนี้ไม่สามารถเข้าใจได้ว่าอะไรหายไป";
				next;
				mes "บทสนทนาค่อยๆเงียบลง และคุณไม่ได้ยินอะไรอีกต่อไป";
				set dew_legend,1;
				close;
		}
		end;
}

dew_in01,15,33,0	script	#hidein_legend	111,5,5,{
OnTouch:
		if (dew_legend == 3) {
				mes "กาศยปะกับหัวหน้าเผ่าไพโกเริ่มพูดคุยกันขณะที่คุณกำลังจะออกไป...";
				next;
				mes "[ฤๅษีกาศป]";
				mes "ฉันไม่แน่ใจว่าขาดอะไรไป แต่ผู้เดินทางรายนี้อาจช่วยสร้าง ^006400Jaty Crown^000000 ได้มาก";
				mes "ฉันก็แน่ใจเรื่องนี้";
				mes "เหตุผลก็คือ...";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "สาเหตุเป็นเพราะอะไร?";
				next;
				mes "[ฤๅษีกาศป]";
				mes "^8B4513Cendrawasih Feather^000000 นี้เป็นของหัวหน้าเผ่าคนแรกของเราในตอนที่มงกุฎถูกสร้างขึ้นครั้งแรก";
				mes "มันถูกเก็บรักษาไว้เป็นมรดกอันล้ำค่าของชนเผ่าของเรา";
				next;
				mes "[ฤๅษีกาศป]";
				mes "แต่เมื่อนักเดินทางเข้ามาใกล้ ขนนกก็เริ่มเปล่งประกาย และฉันก็คิดอยู่ตลอดว่านี่ไม่ใช่แค่เรื่องบังเอิญ";
				mes "บุคคลนี้จะเป็นกุญแจสำคัญในการช่วยเราทำมงกุฎ";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "มันจะเกิดขึ้นได้แน่นอนใช่ไหม?";
				mes "ถ้าอย่างนั้นฉันก็จะให้ทุกอย่าง";
				mes "เพื่อสนับสนุนพวกเขา...";
				set dew_legend,4;
				close;
		}
		end;
}

dew_in01,22,48,3	script	Sage Kasyapa#dew	541,{
		if (dew_legend == 0) {
				mes "[ฤๅษีกาศป]";
				mes "โอ้ ผู้แปลกหน้าในดินแดนของเรา";
				mes "คุณหลงทางใช่ไหม?";
				mes "ฉันหวังว่าคุณจะไม่พบเจออันตรายมากนักในระหว่างการเดินทางมาที่นี่";
				close;
		} else if (dew_legend == 1) {
				mes "[ฤๅษีกาศป]";
				mes "การแสดงออกที่จริงจังของคุณบอกฉันว่า";
				mes "ที่คุณมีอะไรจะพูด";
				mes "คุณอาจจะบอกฉันว่ามันคืออะไรก็ได้";
				next;
				if(select("I overheard talk about a crown?:Just looking for the gift shop.") == 2) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "จริงหรือ";
						mes "ดูเหมือนว่าตาของคุณ";
						mes "อยากจะพูดอะไรบางอย่าง.";
						close;
				}
				mes "[ฤๅษีกาศป]";
				mes "โอ้ คุณเคยได้ยินเรื่องมงกุฎใช่มั้ย?";
				mes "อ๋อ ลุงคนนี้ปากห้อย";
				mes "เราควรจะใช้ความรอบคอบมากกว่านี้สักหน่อย...";
				next;
				select("Anything I can do to help?");
				mes "[ฤๅษีกาศป]";
				mes "ฉันมองเห็นในดวงตาของคุณ";
				mes "ว่าคุณเป็นคนน่าเชื่อถือ...แต่ว่า...";
				next;
				mes "มีแสงสว่างกระพริบสั้นๆ ^0000FFKasyapa^000000 จู่ๆ เขาก็ดูเหมือนจะตกตะลึง";
				specialeffect2 EF_PNEUMA;
				next;
				mes "[ฤๅษีกาศป]";
				mes "อ๋อ ขนนก......!";
				mes "แทนที่จะพูดกับฉัน ทำไมคุณไม่พูดกับหัวหน้าเผ่าของเราล่ะ ^0000FFPaiko^000000";
				set dew_legend,2;
				close;
		} else if (dew_legend == 2) {
				mes "[ฤๅษีกาศป]";
				mes "แทนที่จะพูดกับฉัน ทำไมคุณไม่พูดกับหัวหน้าเผ่าของเราล่ะ ^0000FFChief Paiko^000000";
				close;
		} else if (dew_legend == 3) {
				mes "[ฤๅษีกาศป]";
				mes "หากนั่นคือสิ่งที่หัวหน้า ^0000FFPaiko^000000 ต้องการ";
				mes "ฉันไม่มีอะไรจะพูดอีกแล้ว";
				mes "อืม....";
				mes "แต่......นี่....";
				next;
				mes "[ฤๅษีกาศป]";
				mes "ไม่เป็นไร เที่ยวให้สนุกนะ";
				close;
		} else if (dew_legend == 4) {
				mes "[ฤๅษีกาศป]";
				mes "ฉันกำลังรอคุณอยู่นะนักเดินทาง";
				mes "หัวหน้าเผ่าของเรามีพระคุณ";
				mes "ที่จะถามคุณ.";
				mes "กรุณาพูดคุยกับหัวหน้า ^0000FFPaiko^000000";
				close;
		} else if (dew_legend == 5) {
				mes "[ฤๅษีกาศป]";
				mes "ฉันได้ยินมาว่าคุณช่วยทำมงกุฎได้";
				mes "สำหรับหัวหน้าเผ่าของเรา";
				mes "คุณจะต้องเป็นอย่างแน่นอน";
				mes "เป็นประโยชน์กับเรามาก...";
				mes "ฉันเชื่อสายตาตัวเอง!";
				mes "และยังมีแสงระยิบระยับ...";
				next;
				mes "[ฤๅษีกาศป]";
				mes "สัญลักษณ์ของเผ่าดายัค";
				mes "และสิ่งมีชีวิตลึกลับที่โด่งดัง";
				mes "พบ 15 จาก ^006400เซ็นต์ขนนก^000000";
				mes "และนำพวกเขาไปหาหัวหน้า ^0000FFPaiko^000000";
				close;
		} else if (dew_legend == 6) {
				mes "[ฤๅษีกาศป]";
				mes "ด้วยขนนกที่คุณนำมา การสร้าง ^006400Jaty Crown^000000 ดำเนินไปได้อย่างราบรื่นมากขึ้น";
				mes "ไปหาหัวหน้า ^0000FFPaiko^000000";
				close;
		} else if (dew_legend == 7) {
				mes "[ฤๅษีกาศป]";
				mes "มีอะไรบางอย่าง";
				mes "ฉันอยากจะมอบให้คุณ";
				mes "เพื่อเป็นรางวัลสำหรับความช่วยเหลือของคุณ";
				mes "ขอร้องละ เราคงจะรู้สึกเป็นเกียรติหากท่านจะรับมันไป";
				next;
				if (checkweight(6406,1) == 0) {
						if (Weight + getiteminfo(6406, ITEMINFO_WEIGHT) > MaxWeight) {
								mes "[ฤๅษีกาศป]";
								mes "คุณมีน้ำหนักเกินกำหนดมากเกินไป";
								mes "ฉันไม่สามารถมอบรางวัลให้คุณได้จนกว่าคุณจะลดจำนวนสิ่งของหนักๆ ที่คุณถืออยู่ลง";
								close;
						} else {
								mes "[ฤๅษีกาศป]";
								mes "คุณมีของในคลังมากเกินไป";
								mes "ฉันจะให้รางวัลแก่คุณเมื่อคุณปลดภาระและกลับมา";
								close;
						}
				}
				set dew_legend,8;
				getitem 6406,1; //Cendrawasih_SF
				completequest 9157;
				erasequest 9157;
				mes "[ฤๅษีกาศป]";
				mes "เหตุผลที่ฉันขอให้คุณเข้าร่วมภารกิจที่สำคัญนี้ก็เพราะขนนกอันนี้";
				mes "ต้นกำเนิดในตำนานของมงกุฎนี้ถูกสืบทอดจากรุ่นสู่รุ่น...";
				specialeffect2 EF_PNEUMA;
				next;
				mes "[ฤๅษีกาศป]";
				mes "ฉันเห็นขนนกนี้เริ่มเปล่งประกายเมื่อคุณมาที่นี่";
				mes "ฉันรู้ว่าคุณเป็นคนที่";
				mes "ก็จะพยายามช่วยเรา";
				mes "ในนามของประชาชนของฉัน";
				mes "ฉันขอขอบพระคุณ.";
				next;
				mes "[ฤๅษีกาศป]";
				mes "นอกจากนี้ ขนนที่คุณได้รับไม่ได้มีคุณค่าเพียงตัวของมันเอง แต่ยังเป็นการยอมรับจากชนเผ่าของเราด้วย";
				next;
				mes "[ฤๅษีกาศป]";
				mes "เหมือนกับภูเขาไฟ ^8B4513Krakatau^000000 เมื่อคุณเข้าไปในพื้นที่อันตรายแบบนั้น หากคุณแสดงขนนนี้ออกมา คุณก็จะได้รับความช่วยเหลือจากเผ่าได้";
				mes "ยังไงก็ตามผมขอขอบคุณคุณมากจริงๆ";
				close;
		} else {
				mes "[ฤๅษีกาศป]";
				mes "นอกจากนี้ ขนนที่คุณได้รับไม่ได้มีคุณค่าเพียงตัวของมันเอง แต่ยังเป็นการยอมรับจากชนเผ่าของเราด้วย";
				next;
				mes "[ฤๅษีกาศป]";
				mes "เหมือนกับภูเขาไฟ ^8B4513Krakatau^000000 เมื่อคุณเข้าไปในพื้นที่อันตรายแบบนั้น หากคุณแสดงขนนนี้ออกมา คุณก็จะได้รับความช่วยเหลือจากเผ่าได้";
				mes "ยังไงก็ตามผมขอขอบคุณคุณมากจริงๆ";
				close;
		}
}

dew_in01,15,49,4	script	Tribal Chief Paiko#dew	540,{
		if (dew_legend < 2) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันเป็นหัวหน้าเผ่านี้";
				mes "^0000FFไพโกะ^000000";
				close;
		} else if (dew_legend == 2) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันเห็นว่าคุณเป็นนักเดินทาง";
				mes "จากดินแดนอื่น";
				mes "อะไรทำให้คุณมาที่นี่?";
				next;
				if(select("I overheard the story by accident:I have nothing to say.") == 2) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "คุณกำลังเสียเวลาอยู่ที่นี่";
						mes "รอบหมู่บ้านของเรายังมีสถานที่ท่องเที่ยวอื่นๆ อีกมากมาย";
						close;
				}
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คุณหมายความว่าอย่างไร?";
				mes "คุณเคยได้ยินเรื่องราวเกี่ยวกับมงกุฎมั้ย?";
				mes "คุณคงได้ยินไม่ถูกต้อง";
				mes "ฉันไม่มีอะไรจะพูดกับคุณ";
				mes "เกี่ยวกับเรื่องดังกล่าว";
				next;
				select("Kasyapa was talking about a crown...");
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ดูเหมือนว่า ^0000FFKasyapa^000000 จะเล่าเรื่องยาวๆ ให้คุณฟัง และฉันก็ชื่นชมที่คุณใส่ใจเรื่องของเรา";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "มันไม่ใช่เรื่องที่คนแปลกหน้าอย่างคุณจะเข้าใจได้ ดังนั้น จงทำตัวเหมือนต้นไม้แล้วออกไปจากที่นี่";
				set dew_legend,3;
				close;
		} else if (dew_legend == 3) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "รอบหมู่บ้านของเรายังมีสถานที่ท่องเที่ยวอื่นๆ อีกมากมาย";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ถ้าเป็นเรื่องมงกุฎ ฉันอยากให้คุณหยุดถามถึงมัน";
				mes "กรุณาออกไปก่อน.";
				close;
		} else if (dew_legend == 4) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันรอคุณอยู่นะนักเดินทาง";
				mes "ตามปราชญ์ ^0000FFKasyapa^000000";
				mes "คุณเต็มใจที่จะช่วยเราสร้างมงกุฎให้กับชนเผ่าของเรา";
				select("Crown-a-whatsis?");
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "หัวหน้าเผ่ามีหน้าที่สร้างและปกป้อง ^006400Jaty Crown^000000 เพื่อรักษาความภาคภูมิใจและเกียรติยศของเผ่า";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "แต่ตั้งแต่ฉันได้เป็นหัวหน้าเผ่าก็พบกับความยากลำบากในการก้าวขึ้นมาเป็นหัวหน้าเผ่า";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ตามที่ Sage ^0000FFKasyapa^000000 คุณสามารถช่วยเราสร้าง ^006400Jaty Crown^000000 ได้";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คุณคิดว่าคุณสามารถช่วยได้หรือไม่?";
				mes "ความพยายามของคุณจะไม่ได้รับผลตอบแทน";
				next;
				if(select("Ok, I will help:I am sorry, I cannot help now.") == 2) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "นั่นมันน่าเศร้าจริงๆ.";
						mes "หากคุณเปลี่ยนแปลง";
						mes "โปรดคิดหน่อย";
						mes "มาอีกครั้ง.";
						mes "ฉันจะรอ.";
						close;
				}
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ขอบคุณมากนะคนแปลกหน้า";
				mes "หากกาสิปไว้ใจคุณ ฉันก็รู้ว่าคุณสามารถช่วยเราทำได้";
				mes "^006400เจตี้ คราวน์^000000";
				mes "สิ่งที่คุณสามารถทำได้เพื่อ";
				mes "การช่วยเหลือเราก็ง่ายมาก";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "สัญลักษณ์ของเผ่าจาตียังเป็นสิ่งมีชีวิตลึกลับที่โด่งดังอีกด้วย";
				mes "หา 15 ^006400Cendrawasih Feather^000000s แล้วนำมาให้ฉัน";
				mes "ศรัทธาของเราอยู่ที่ตัวคุณ คนแปลกหน้า";
				set dew_legend,5;
				setquest 9155;
				close;
		} else if (dew_legend == 5) {
				if (countitem(6405) < 15) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "ดูเหมือนว่าคุณยังไม่พบวัตถุดิบที่จะสร้าง ^006400Jaty Crown^000000";
						next;
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "สัญลักษณ์ของเผ่า Dayak ยังเป็นสิ่งมีชีวิตลึกลับที่โด่งดังอีกด้วย";
						mes "หา 15 ^006400Cendrawasih Feather^000000s แล้วนำมาให้ฉัน";
						close;
				}
				set dew_legend,6;
				delitem 6405,15; //Cendrawasih_F
				changequest 9155,9156;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "โอ้! คุณเจอพวกมันหมดแล้ว ดูสิ! ขนนกที่คุณเจอเป็นประกายแวววาวเลยไม่ใช่เหรอ?";
				mes "เอาล่ะ ฉันจะเริ่มสร้าง ^006400Jaty Crown^000000 ฉันรู้สึกว่าพร้อมแล้ว";
				next;
				mes "ทันใดนั้น เสียงดังอันดังกึกก้องก็เริ่มดังขึ้นขณะที่คุณและ ^0000FFPaiko^000000 ถูกล้อมรอบไปด้วยพลังงานประกายอันอบอุ่นและสดใส เหมือนกับหิ่งห้อยนับพันตัวที่ส่งเสียงกริ๊งกริ๊ง";
				specialeffect2 EF_BASH3D2;
				close;
		} else if (dew_legend == 6) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ด้วยความช่วยเหลือของคุณ ^006400Jaty Crown^000000 กำลังดำเนินไปอย่างดี";
				mes "คงจะต้องใช้เวลาสักพักจนกว่างานจะเสร็จสิ้น ดังนั้นกรุณารอสักนิด";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "หลังจากมงกุฎ ^006400jaty ^000000 เสร็จสมบูรณ์มีของขวัญที่ฉันต้องการมอบให้คุณ";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "อ้อ คุณควรไปพบ Sage ^0000FFKasyapa^000000 ที่อยากจะตอบแทนคุณด้วยบางสิ่งบางอย่าง";
				set dew_legend,7;
				changequest 9156,9157;
				close;
		} else if (dew_legend == 7) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "อ๋อ คุณควรไปคุยกับ Sage ^0000FFKasyapa^000000 ที่อยากจะตอบแทนคุณด้วยบางสิ่งบางอย่าง";
				close;
		} else if (dew_legend == 8) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ด้วยการสนับสนุนของคุณ ฉันไม่ต้องกังวลเกี่ยวกับการสร้าง ^006400Jaty Crown^000000";
				mes "ฉันซาบซึ้งใจมากจริงๆ";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "วันอันแสนสุขเช่นนี้ควรได้รับการเฉลิมฉลอง คุณไม่คิดเหรอ?";
				mes "ฉันจึงเตรียมตัวชิมอาหารพิเศษประจำเผ่าที่เรียกว่า ^006400Satay^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "แต่ซาเต๊ะอันนี้อร่อยเกินกว่าที่เราจะแบ่งกันกินกันแค่สองคน ดังนั้นเราเลยอยากแบ่งกับคนในหมู่บ้านด้วย";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คุณคิดอย่างไร?";
				mes "เพื่อแบ่งปันข่าวสารที่";
				mes "^006400Jaty Crown^000000 กำลังได้รับการประดิษฐ์ขึ้นอย่างสำเร็จแล้ว และเพื่อที่จะแบ่งปัน ^006400Satay^000000 อันแสนอร่อยนี้กับเพื่อนๆ ฉันต้องการความช่วยเหลือจากคุณ";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คุณพร้อมสำหรับงานส่งมอบระยะสั้นหรือไม่?";
				next;
				if(select("Of course, I can help:Sorry, I am too busy to help.") == 2) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "ฉันเชื่อว่าคุณจะช่วยฉันได้แน่นอน...";
						mes "ก็ผิดหวังนิดหน่อย.";
						mes "หากคุณเปลี่ยนใจเมื่อไร";
						mes "แล้วมาคุยกับฉันอีกครั้ง";
						close;
				}
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "โอ้! ฉันรู้ว่าคุณจะช่วย";
				mes "นักรบของเผ่าของเราอยู่ในความคิดของฉันก่อนที่เราจะแบ่งปันกับผู้อื่น";
				mes "โปรดแบ่งปันซาเตย์ให้กับนักรบในหมู่บ้าน";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ในขณะนี้ฉันจะให้คุณ 2 ^006400ซาเต๊ะ^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คุณอาจจะรู้สึกถูกดึงดูดด้วยกลิ่นหอมของ ^006400Satay^000000 ของเราและคุณอาจจะกินมันไปหนึ่งชิ้น แต่ถ้าคุณกินมากกว่านั้น คุณจะไม่สามารถแบ่ง ^006400Satay^000000 ได้";
				mes "กับเพื่อนๆหมู่บ้านของเรา";
				set dew_legend,9;
				getitem 11533,2; //Satay
				setquest 9158;
				close;
		} else if (dew_legend == 9) {
				callsub L_Delivery,0;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันนึกถึงนักรบของเผ่าของเราที่นำอาหารมาแบ่งซาเต๊ะให้กับนักรบในหมู่บ้าน";
				close;
		} else if (dew_legend == 10) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "อ่า คนต่อไปที่ฉันอยากให้ส่งซาเต๊ะไปให้คือคนที่พักอยู่ฝั่งตรงข้ามแม่น้ำ ชื่อว่าผู้จัดการเผ่า^0000FF^00000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "เพื่อจุดประสงค์นี้ ฉันจะให้คุณเพิ่มอีก 2 เสิร์ฟของ ^006400Satay^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "หากคุณถูกกลิ่นหอมของ ^006400Satay^000000 ดึงดูดใจ คุณอาจจะกินมันได้หนึ่งชิ้น แต่หากกินมากกว่านี้ คุณจะไม่สามารถแบ่ง ^006400Satay^000000 ให้กับเพื่อน ๆ ได้";
				set dew_legend,11;
				getitem 11533,2; //Satay
				changequest 9159,9160;
				close;
		} else if (dew_legend == 11) {
				callsub L_Delivery,0;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ผู้ที่ถูกส่งมอบคนที่สอง ^006400ซาเตย์^000000 คือผู้ที่อยู่ฝั่งตรงข้ามแม่น้ำ";
				next;
				mes "นั่นคือ ^0000FFTribe Manager^000000";
				mes "กรุณาส่งให้ก่อนจะเย็นลงครับ";
				close;
		} else if (dew_legend == 12) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "อ้าว ส่งมาแล้วเหรอคะ?";
				mes "บุคคลที่สามที่จะส่ง ^006400Satay^000000 ให้คือ ^0000FFGatekeeper ของภูเขาไฟ Krakatau^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันจะให้คุณเพิ่มอีก 2 เสิร์ฟ ^006400Satay^000000 สำหรับการจัดส่งครั้งนี้";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "และเหมือนเช่นเคยครับ อย่าไปหลงกลิ่น และอย่าพยายามกิน ^006400Satay^000000";
				set dew_legend,13;
				getitem 11533,2; //Satay
				changequest 9161,9162;
				close;
		} else if (dew_legend == 13) {
				callsub L_Delivery,1;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "บุคคลที่สามที่จะส่ง ^006400Satay^000000 ให้คือ ^0000FFGatekeeper ของภูเขาไฟ Krakatau^000000";
				mes "กรุณาส่งให้ก่อนที่จะเย็นลง";
				close;
		} else if (dew_legend == 14) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "โอ้ คุณส่งมันมาให้แล้ว";
				mes "ผู้ที่จะมอบ ^006400Satay^000000 คนสุดท้ายคือ ^0000FFผู้จัดการวัดโบโรบูดูร์^000000 ซึ่งอยู่ที่ ^8B4513เกาะเดวาตะ^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ฉันจะให้คุณอีก 2 ^006400ซาเต๊ะ^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "และเหมือนเช่นเคยครับ อย่าไปหลงกลิ่น และอย่าพยายามกิน ^006400Satay^000000";
				set dew_legend,15;
				getitem 11533,2; //Satay
				changequest 9163,9164;
				close;
		} else if (dew_legend == 15) {
				callsub L_Delivery,1;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "คนสุดท้ายที่ส่ง ^006400ซาเตย์^000000 คือ";
				mes "^0000FFผู้จัดการวัดโบโรบูดูร์^000000 ใครอยู่ใน";
				mes "^8B4513เกาะเดวาตา^000000.";
				mes "กรุณาส่งให้ก่อนที่จะเย็นลง";
				close;
		} else if (dew_legend == 16) {
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "โอ้ คุณส่งมันมาแล้ว";
				mes "คุณได้ทำผลงานดีมาก.";
				mes "เป็นช่วงเวลาที่ดีสำหรับเราทุกคนที่จะได้แบ่งปันและสนุกสนานกับโอกาสอันน่ายินดีนี้ร่วมกัน ขอบคุณ";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ขณะที่คุณกำลังเดินทางกลับจากการส่ง ^006400Satay^000000,";
				mes "Jaty Crown ถูกสร้างขึ้นแล้ว บรรยากาศและความเปล่งประกายทำให้มงกุฎนี้ไร้ที่ติอย่างแท้จริง";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "นอกจากนี้ คุณยังนำวัสดุมามากเกินพอ ดังนั้น หลังจากสร้าง ^006400Jaty Crown^000000 แล้ว เราจึงสามารถสร้างอันที่คล้ายกันสำหรับคุณเก็บไว้ได้";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ถึงจะไม่เหมือนต้นฉบับแต่ก็มีเอกลักษณ์เฉพาะตัว เป็นรางวัลตอบแทนที่ต้องขอบคุณในสิ่งที่คุณทำ";
				next;
				if (checkweight(18520,1) == 0) {
						mes "[หัวหน้าเผ่าไพโกะ]";
						mes "คุณมีน้ำหนักมากเกินไปจนไม่ได้รับรางวัล";
						mes "กรุณาลดน้ำหนักที่แบกไว้แล้วกลับมาใหม่อีกครั้ง";
						close;
				}
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ทำด้วยน้ำหมักสูตรลับของชนเผ่าเรา ลองทำ^006400ซาเต๊ะ^000000";
				mes "เราทำด้วยใจจริงเพื่อคุณโดยเฉพาะ แต่ก็อย่าลืมแบ่งปันกับเพื่อน ๆ ด้วยนะ";
				set dew_legend,17;
				getitem 18520,1; //Jaty_C
				getitem 11533,5; //Satay
				completequest 9165;
				erasequest 9165;
				next;
		}
		mes "[หัวหน้าเผ่าไพโกะ]";
		mes "คุณได้ทำผลงานดีมาก.";
		mes "ไม่ว่าคุณจะทำอะไรในอนาคต ฉันขอให้คุณมีชีวิตที่เต็มไปด้วยโชคลาภและความสุข";
		mes "ในฐานะหัวหน้าเผ่า ^8B4513Jaty^000000 ฉันขอให้คุณโชคดี!!";
		close;
L_Delivery:
		mes "[หัวหน้าเผ่าไพโกะ]";
		if (getarg(0) == 0) {
				mes "ดูเหมือนคุณยังไม่ได้ส่งมอบ ^006400Satay^000000.";
				mes "คุณประสบปัญหาอะไรไหม?";
		} else {
				mes "คุณยังไม่ได้ส่ง ^006400Satay^000000 มาให้เหรอ?";
				mes "มีอะไรผิดพลาดหรือเปล่า?";
		}
		next;
		switch(select("I ate them all.:Deliver to whom.:Nothing.")) {
		case 1:
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "ห๊ะ... แปลว่าคุณต้านทานตัวเองไม่ได้แล้วใช่มั้ยล่ะ?";
				mes "อย่าโทษตัวเองมากเกินไป ยากที่จะต้านทานกลิ่นหอมอันน่าติดใจของ ^006400Satay^000000";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "เผ่าของเราขาดแคลนเสบียงอาหาร ดังนั้นเราจึงไม่สามารถแจกซาเต๊ะได้มากนัก แต่มีข่าวลือว่าเจ้าของร้านอาหารบนเกาะเดวาตะกำลังทำและขายซาเต๊ะของตัวเองอยู่";
				next;
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "มันอาจจะไม่อร่อยเท่าสูตรของเผ่าเรา แต่ว่านี่เป็นงานเร่งด่วน ดังนั้นต้องหามันแล้วส่งมอบไปเลย";
				close;
		case 2:
				return;
		case 3:
				mes "[หัวหน้าเผ่าไพโกะ]";
				mes "โอเค ส่งเลยตอนนี้!";
				close;
		}
}

dew_fild01,78,288,6	script	Jaty Tribe Warrior#dew	541,{
		mes "[นักรบเผ่าเจตี้]";
		mes "เราเคารพความเข้มแข็งและความกล้าหาญ";
		mes "พวกเราคือเผ่า ^FF0000Jaty^000000";
		if (dew_legend != 9 || !countitem(11533))
				close;
		next;
		mes "[นักรบเผ่าเจตี้]";
		mes "ดวงตาที่ผ่านการฝึกฝนของฉันมองเห็นว่า";
		mes "คุณดูเหมือนกำลังจะพูดอะไรบางอย่าง";
		mes "พูดและระบุธุรกิจของคุณ";
		next;
		select("^0000FFTribal Chief Paiko^000000");
		mes "[นักรบเผ่าเจตี้]";
		mes "ฉันได้ยินเกี่ยวกับคุณ";
		mes "ความดีที่ทำ...";
		mes "ในฐานะตัวแทนของชนเผ่าของฉัน";
		mes "ฉันขอแสดงความขอบคุณต่อคุณ.";
		next;
		mes "[นักรบเผ่าเจตี้]";
		mes "ขอขอบคุณสำหรับ ^006400Satay^000000 ด้วย";
		mes "น้ำลายไหลแล้วค่ะ ขอบคุณค่ะ.";
		mes "มีบางอย่างที่ฉันอยากจะแบ่งปันกับคุณเกี่ยวกับชนเผ่าของเรา";
		delitem 11533,1; //Satay
		set dew_legend,10;
		changequest 9158,9159;
		next;
		mes "[นักรบเผ่าเจตี้]";
		mes "โลกในปัจจุบันนี้สงบสุขมากขึ้น ต่างจากในสมัยโบราณที่นักรบของเผ่าเราต่อสู้และนองเลือด แต่ดินแดนของเผ่าเรากลับหดตัวลงอย่างมาก";
		next;
		mes "[นักรบเผ่าเจตี้]";
		mes "อย่างไรก็ตาม เราจะยังคงรักษาประเพณีนักรบของชนเผ่าของเราไว้ และเราจะรักษาความภาคภูมิใจที่เราได้รับจากการเสียสละของนักรบในอดีตเหล่านั้นไว้";
		next;
		mes "[นักรบเผ่าเจตี้]";
		mes "ฉันหวังว่าความเพียรพยายามและความภาคภูมิใจของคุณจะคงอยู่กับคุณตลอดการเดินทางของคุณ";
		close;
}

dew_fild01,185,300,6	script	Tribe Manager#dew	541,{
		mes "[ผู้จัดการเผ่า]";
		mes "การรักษาไว้ซึ่งวัฒนธรรมและประเพณีถือเป็นสิ่งที่สำคัญมาก";
		if (dew_legend != 11 || !countitem(11533)) {
				if (dew_legend < 11)
						mes "แม้ว่ามันจะน่าเบื่อนิดหน่อย...";
				close;
		}
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "ฉันคิดว่าคุณมีอะไรบางอย่าง";
		mes "จะพูดว่าไงดีล่ะ?";
		next;
		select("^0000FFTribal Chief Paiko^000000");
		mes "[ผู้จัดการเผ่า]";
		mes "ฉันไม่รู้ว่ามี";
		mes "ความพยายามที่เกิดขึ้นดังกล่าว";
		mes "และที่คุณมีบทบาทสำคัญในการพยายามนี้...";
		mes "คุณคือฮีโร่ของเผ่า^8B4513Jaty^000000";
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "ว้าว~ กลิ่นหอมชวนกิน";
		mes "โฮมเมดแสนอร่อย ^006400ซาเต๊ะ^000000!!";
		mes "ขอบคุณสำหรับมื้ออาหาร";
		mes "ขอแสดงความนับถือ";
		mes "แก่หัวหน้าเผ่าของเรา";
		delitem 11533,1; //Satay
		set dew_legend,12;
		changequest 9160,9161;
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "มีการติดต่อระหว่างกันน้อยมาก";
		mes "^8B4513เกาะเดวาตา^000000 และ ^8B4513เผ่าจาตี^000000";
		mes "เราเคยมีพันธะที่แน่นแฟ้นมากขึ้น";
		mes "ในสมัยก่อน";
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "แต่ด้วยวัฒนธรรมใหม่และชาวต่างชาติที่หลั่งไหลเข้ามา";
		mes "^8B4513เกาะเดวาตา^000000 กลายเป็นแหล่งท่องเที่ยว และเราก็ขาดการติดต่อกับพวกเขาไปแล้ว";
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "แหล่งท่องเที่ยวมีการขยายตัวอย่างต่อเนื่อง แต่สาเหตุที่ไม่ส่งผลกระทบต่อเราที่นี่ ก็เพราะวัดเล็กๆ แห่งนี้ที่ผมดูแลอยู่";
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "ฉันไม่สามารถปล่อยให้ประเพณีของเราจางหายไปได้ง่ายๆ เช่นนั้นได้";
		mes "ฉันหวังว่าการรักษาประเพณีและวัฒนธรรมของเราให้คงอยู่ จะทำให้ผู้คนทราบว่าสิ่งเหล่านี้สำคัญต่อเรามากเพียงใด";
		next;
		mes "[ผู้จัดการเผ่า]";
		mes "จากนี้ไปฉันหวังว่าผู้คนจาก ^8B4513Dewata Island^000000 และ ^8B4513Jaty Tribe^000000 จะสามารถอยู่ร่วมกันและพูดคุยกันบ่อยยิ่งขึ้น";
		close;
}

//Original name: Gatekeeper of Krakatau Volcano
dewata,235,56,4	script	Gatekeeper of Krakatau#1	534,{
		mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
		mes "^8B4513ภูเขาไฟกรากะตัว^000000 คือ";
		mes "พื้นที่อันตรายมาก";
		mes "ฉันไม่สามารถยอมให้ใครเข้าไปที่นั่นได้";
		next;
		if (dew_legend == 13 && countitem(11533))
				set .@menu$, ":^0000FFTribal Chief Paiko^000000";
		switch(select("Allow me through:I don't want to go in"+.@menu$)) {
		case 1:
				if (dew_legend < 8) {
						mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
						mes "^8B4513ภูเขาไฟกรากะตัว^000000 คือ";
						mes "พื้นที่อันตรายมาก";
						mes "ฉันเลยไม่สามารถให้คุณเข้ามาได้";
						mes "เว้นแต่คุณจะมีคุณสมบัติสำหรับการเข้า";
						close;
				}
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "คุณคือผู้เดินทางที่ได้รับการยอมรับจากหัวหน้าเผ่าว่าเป็นเพื่อนของเผ่าและได้รับ ^006400Shining Cendrawasih Feather^000000";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "เอาล่ะ ผมจะปล่อยให้คุณผ่านไป แต่ว่า ^8B4513ภูเขาไฟกรากะตัว^000000 เป็นอันตราย ดังนั้นโปรดระมัดระวังด้วย";
				close2;
				warp "dew_dun01",289,160;
				end;
		case 2:
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "คุณได้เลือกด้วยความชาญฉลาด";
				mes "^8B4513ภูเขาไฟกรากะตัว^000000 คือ";
				mes "พื้นที่อันตรายอย่างยิ่ง.";
				close;
		case 3:
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "โอ้! ฉันไม่รู้เรื่องนั้นเลย!";
				mes "นี่เป็นข่าวที่ดีที่สุด";
				mes "ฉันได้ยินมาเมื่อเร็ว ๆ นี้";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "เอิ่ม นี่มันกลิ่นหอมอะไรเหรอ?";
				mes "แน่นอนว่ามันเป็น ^006400ซาเต๊ะ^000000 ของเรา!";
				mes "ฉันหิวเล็กน้อย";
				mes "แล้วนี่น่าจะทำให้ฉันอิ่มได้";
				delitem 11533,1; //Satay
				set dew_legend,14;
				changequest 9162,9163;
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "ตั้งแต่คุณมาถึงที่นี่";
				mes "ฉันจะแบ่งปันเรื่องราวที่น่าสนใจให้คุณฟัง";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "มันได้กลายมาเป็นประเพณีที่สูญหายไปในช่วงไม่กี่ปีที่ผ่านมา แต่จนกระทั่งเมื่อ 10 ปีหรือมากกว่านั้น เพื่อที่จะกลายเป็นนักรบของเผ่า ^8B4513Jaty^000000 คุณต้องปีนภูเขาไฟกรากะตัวเพื่อพิสูจน์ความอดทนของคุณ";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "ดวงตาของนักรบเหล่านั้นเต็มไปด้วยความวิตกกังวลและมุ่งมั่นก่อนที่จะขึ้นไป ^8B4513ภูเขาไฟกรากะตัว^000000";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "ฉันยังไม่สามารถลืมแววตานั้นของพวกเขาได้ ฉันรู้สึกมีอารมณ์เล็กๆ น้อยๆ เหมือนกันกับนายนะนักผจญภัย";
				next;
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "ฉันอยากให้คุณมีอายุยืนยาวและเจริญรุ่งเรือง";
				mes "มีสุขภาพแข็งแรงและปลอดภัย นักเดินทางจะได้มีชีวิตอยู่เพื่อบอกเล่าเรื่องราวการผจญภัยของคุณได้";
				close;
		}
}

//Original name: Gatekeeper of Krakatau Volcano
dew_dun01,292,164,4	script	Gatekeeper of Krakatau#2	541,{
		mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
		mes "อยากกลับไปอีกครั้ง";
		mes "ไปเดวาตะเหรอ?";
		next;
		switch(select("Yes:No")) {
		case 1:
				close2;
				warp "dewata",232,53;
				end;
		case 2:
				mes "[ผู้เฝ้าประตูภูเขาไฟกรากาตัว]";
				mes "^8B4513ภูเขาไฟกรากะตัว^000000 คือ";
				mes "สถานที่อันตราย.";
				mes "คุณควรจะกลับตอนนี้";
				close;
		}
}

dewata,78,192,6	script	Borobudur Temple Manager	536,{
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "วันนี้อากาศดีมากเลย";
		if (dew_legend != 15 || !countitem(11533))
				close;
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "แล้วคุณมี";
		mes "มีอะไรจะบอกฉันไหม?";
		next;
		select("^0000FFTribal Chief Paiko^000000");
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "ฉันไม่เคยรู้ว่ามีเรื่องแบบนี้เกิดขึ้น";
		mes "หากคุณบอกฉันล่วงหน้า ฉันคงช่วยได้สักนิดหน่อย";
		mes "ทุกอย่างยังคงดำเนินไปได้ดีด้วยความช่วยเหลือของคุณ";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "อ๋อ มันคือ ^006400Satay^00000 ที่ฉันชิมตอนที่เผ่าเชิญฉันเมื่อไม่นานมานี้";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "ดีเลย~~ รสชาติเดียวกับที่ฉันจำได้เลย";
		mes "เพื่อแสดงความขอบคุณ ฉันจะส่งเรื่องราวที่อาจสนใจคุณต่อไป";
		delitem 11533,1; //Satay
		set dew_legend,16;
		changequest 9164,9165;
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "ตำนานเล่าว่ามีนกประหลาดตัวหนึ่งชื่อว่า ^FF0000Garuda^000000";
		mes "มันมีลำตัว แขนและขาเหมือนมนุษย์แต่มีหัวและจะงอยปากเหมือนนกอินทรี";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "เรื่องราวของนกตัวนี้เล่าว่ามันช่วยเทพเจ้ากำจัดปีศาจและงูที่ชั่วร้าย";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "นิทานเรื่องนี้ได้ถูกสืบทอดจากรุ่นสู่รุ่น";
		mes "น่าสนใจมากเลยใช่ไหมล่ะ?";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "ฉันมั่นใจว่าในหมู่บ้านของเผ่า ^8B4513Jaty^000000 มีเรื่องเล่าที่เป็นตำนานมากมาย";
		mes "เมื่อผมได้พบหัวหน้า ผมคงจะได้ยินเรื่องราวแบบนี้อีกมากมายแน่นอน";
		next;
		mes "[ผู้จัดการวัดโบโรบูดูร์]";
		mes "ฉันไม่แน่ใจว่าเราจะได้พบกันอีกเมื่อใด แต่ฉันหวังว่าเราคงจะได้พบกันอีกครั้งสักวันหนึ่ง";
		mes "ขอให้เดินทางอย่างสนุกสนานนะครับ นักเดินทางหนุ่มๆ";
		close;
}

// Help Out the Old Man :: dewata_drink
//============================================================
dewata,109,262,4	script	Memo#dew_drink	534,{
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
				if (dew_drink) {
						mes "[บันทึก]";
						mes "ทำไมแบกเยอะขนาดนี้ หลังไม่ปวดเหรอ";
						close;
				} else {
						mes "[บันทึก]";
						mes "วัยรุ่นไทยเริ่มถือของหนักแล้วเหรอ?";
						close;
				}
		}
		set .@playtime, checkquest(5044,PLAYTIME);
		if (.@playtime == 0) {
				mes "[บันทึก]";
				mes "พรุ่งนี้คุณจะช่วยฉันอีกไหม?";
				mes "โพบี้ยังมีอาการปวดหลังอยู่";
				mes "อาการปวดหลังไม่หายไปภายในวันเดียว";
				close;
		} else if (.@playtime == 1) {
				erasequest 5044;
		} else if (.@playtime == 2) {
				completequest 5044;
				erasequest 5044;
		}
		if (BaseLevel < 60) {
				mes "[บันทึก]";
				mes "เดวาตะมักจะมีผู้คนพลุกพล่านอยู่เสมอ แต่ในวันนี้กลับเงียบสงบ";
				close;
		}
		if (dew_drink == 0) {
				if (rand(3)) {
						mes "[บันทึก]";
						mes "โอ้พระเจ้า! เราไม่ได้รับข้อความจากเผ่าเจตี้เลย";
						emotion ET_PROFUSELY_SWEAT;
						next;
						mes "[บันทึก]";
						mes "พวกเขาไม่เคยผิดสัญญาเลยตลอด 10 ปีที่ผ่านมา ฉันไม่ทราบสาเหตุ เกิดอะไรขึ้น?";
						next;
						mes "[บันทึก]";
						mes "ฉันอาจจะทำให้ของเหลวเสียหมดถ้ามันเกิดขึ้นแบบนี้";
						emotion ET_CRY;
						close;
				}
				mes "[บันทึก]";
				mes "จะต้องทำยังไงดีคะ ยังไม่มีข่าวคราวจากชาวเผ่าเจติเลยค่ะ";
				emotion ET_PROFUSELY_SWEAT;
				next;
				mes "[บันทึก]";
				mes "โอ้ไม่~ ของเหลวจะเสียหมดนะ...";
				next;
				select("What happened?");
				mes "[บันทึก]";
				mes "คุณเป็นใคร?";
				emotion ET_QUESTION;
				next;
				mes "[บันทึก]";
				mes "คุณเป็นนักผจญภัยใช่ไหม?";
				mes "อย่างที่คุณเห็นจากขวดโหลรอบๆ ตัวฉัน ฉันกำลังผลิตไวน์แบบดั้งเดิมเพื่อเลี้ยงชีพ";
				next;
				mes "[บันทึก]";
				mes "ทุกคนรู้เรื่องนี้ ดังนั้นเกาะ ^0000FFDewata^000000 จึงดึงดูดนักท่องเที่ยวจำนวนมาก";
				next;
				mes "[บันทึก]";
				mes "โดยเฉพาะไวน์สูตรดั้งเดิมนี้ที่ผลิตกันมาสี่ชั่วอายุคน! แต่ ^FF0000Ark^000000 ขาดแคลน";
				mes "อาร์คต้องการ ^0000FFน้ำมันปาล์ม^000000 ผลิตจากผลปาล์มน้ำมัน";
				next;
				mes "[บันทึก]";
				mes "น่าเสียดายที่เราไม่ได้รับ ^0000FFPalm Oil^000000 จากหมู่บ้านชนเผ่า Jaty เมื่อเร็ว ๆ นี้";
				mes "ส่วนผสมอาจจะไร้ประโยชน์ถ้าส่งมาช้าเกินไป";
				next;
				select("Sounds like a big problem.");
				mes "[บันทึก]";
				mes "มันเป็นปัญหาใหญ่กว่าที่คิด ยอดขายคือเครดิต แต่ถ้าเราไม่ทำวันนี้ให้เสร็จ เราก็จะเสียเครดิต วัตถุดิบ และนักท่องเที่ยวที่ไม่ได้รับหีบ ดังนั้นมันจึงสร้างความเสียหายอย่างมาก";
				next;
				mes "[บันทึก]";
				mes "-";
				emotion ET_THINK;
				next;
				emotion ET_SURPRISE;
				mes "[บันทึก]";
				mes "อ๋อ ใช่แล้ว! ตอนนี้คุณยุ่งอยู่เหรอ?";
				next;
				if(select("Sorry, I am busy.:No, I am not busy.") == 1) {
						mes "[บันทึก]";
						mes "อืม ฉันไม่มีทางเลือก";
						mes "คุณไม่สนใจว่าคนอื่นจะเดือดร้อน โลกนี้มันกลายเป็นอะไรไปแล้ว";
						emotion ET_HNG;
						close;
				}
				mes "[บันทึก]";
				mes "ดีใจที่ได้ยินแบบนั้น";
				mes "ดังนั้นเมื่อคุณกำลังเดินมาที่นี่...";
				next;
				mes "[บันทึก]";
				mes "คุณรังเกียจไหมถ้าฉันจะขอร้องคุณหน่อย~ จริงๆ แล้วฉันต้องไปเยี่ยมเผ่า Jaty แต่เจ้า ^FF0000Ark^000000 ที่ยังไม่โตเต็มวัยนี้ไม่ยอมให้ฉันไป";
				next;
				mes "[บันทึก]";
				mes "คุณจะไปที่หมู่บ้าน Jaty และถาม ^0000FFLero^000000 ว่าเกิดอะไรขึ้น?";
				next;
				mes "[บันทึก]";
				mes "ไอ้นี่มันไม่เคยผิดสัญญาเลยมา 10 ปีแล้ว ดังนั้นมันคงมีเหตุผลดีๆ มากที่จะไม่มา";
				set dew_drink,1;
				setquest 5035;
				next;
				mes "[บันทึก]";
				mes "ค้นหาผู้เชี่ยวชาญด้านน้ำมันที่ดีที่สุด";
				mes "ชื่อของเขาอีกครั้งคือ ^0000FFLero^000000";
				close;
		} else if (dew_drink < 5) {
				mes "[บันทึก]";
				mes "แล้วเลโรพูดว่าอะไรล่ะ?";
				next;
				mes "[บันทึก]";
				mes "-";
				emotion ET_THINK;
				next;
				mes "[บันทึก]";
				mes "คุณไม่รู้ว่าเกิดอะไรขึ้นใช่ไหม?";
				next;
				mes "[บันทึก]";
				mes "ค้นหาปรมาจารย์ด้านน้ำมันที่เก่งที่สุด ชื่อของเขาคือ ^0000FFLero^000000";
				close;
		} else if (dew_drink == 5) {
				mes "[บันทึก]";
				mes "แล้วเลโรพูดว่าอะไรล่ะ?";
				next;
				select("Palm tree... became.");
				mes "- คุณอธิบายสถานการณ์ให้คุณเมโมทราบ -";
				next;
				mes "[บันทึก]";
				mes "เลโร่บอกว่าขอโทษเหรอ?";
				mes "อืม ไม่เป็นไรนะ";
				next;
				mes "[บันทึก]";
				mes "นั่นต้องเป็น ^0000FFPoby^000000 ถ้าเขาได้รับบาดเจ็บ นั่นจึงเป็นเหตุผลว่าทำไมถึงไม่มีข้อความใดๆ เลโรใส่ใจเขามากจริงๆ...";
				next;
				mes "[บันทึก]";
				mes "เขาอาจจะสร้างปัญหา แต่โปรดไปเยี่ยมหมู่บ้านจาตี เลโรต้องทำน้ำมันให้เสร็จ";
				next;
				mes "[บันทึก]";
				mes "ถ้าไม่ใช่ก็รออยู่ที่นั่นแล้วนำขวดน้ำมัน ^FF000030^000000 มาให้ฉัน";
				set dew_drink,6;
				changequest 5040,5041;
				close;
		} else if (dew_drink == 6) {
				mes "[บันทึก]";
				mes "เขาอาจจะสร้างปัญหา แต่โปรดไปเยี่ยมหมู่บ้านจาตี เลโรต้องทำน้ำมันให้เสร็จ";
				next;
				mes "[บันทึก]";
				mes "ถ้าไม่ใช่ก็รออยู่ที่นั่นแล้วนำขวดน้ำมัน ^FF000030^000000 มาให้ฉัน";
				close;
		} else if (dew_drink == 7) {
				if (countitem(6401) < 30) {
						mes "[บันทึก]";
						mes "อ้าว ฉันไม่ได้บอกให้เอาน้ำมันมา 30 ขวดเหรอ คุณลืมเหรอ";
						next;
						mes "[บันทึก]";
						mes "กลับไปหาเลโรแล้วเอามาเพิ่ม เขาจะมอบเพิ่มให้คุณหากนั่นเป็นความผิดพลาดของคุณ";
						close;
				}
				mes "[บันทึก]";
				mes "คุณนำน้ำมันมาให้ ทำได้ดีมาก และขอบคุณนะ";
				next;
				mes "[บันทึก]";
				mes "ขอโทษนะครับ ผมมีเรื่องด่วนต้องจัดการ คุณรอผมสักครู่แล้วคุยกับผมได้ไหมเมื่อผมเสร็จธุระแล้ว?";
				delitem 6401,30; //Palm_O
				changequest 5042,5043;
				set dew_drink,8;
				close;
		} else if (dew_drink == 8) {
				if (checkquest(5043,PLAYTIME) == 0) {
						mes "[บันทึก]";
						mes "-";
						emotion ET_PROFUSELY_SWEAT;
						next;
						mes "- เมโม่กำลังทำเรือโนอาห์ -";
						close;
				} else if (checkquest(5043,PLAYTIME) == -1) {
						mes "- มีข้อผิดพลาดในการทำภารกิจ -";
						close;
				}
				mes "[บันทึก]";
				mes "ฉันทำให้คุณต้องรอนานเกินไปหรือเปล่า?";
				next;
				mes "[บันทึก]";
				mes "ครอบครัวของฉันทำ Arks มาสี่รุ่นแล้ว";
				next;
				mes "[บันทึก]";
				mes "ปู่มีชีวิตที่ยากลำบากเนื่องจากไม่ได้ขายของดีๆ มากนัก";
				next;
				mes "[บันทึก]";
				mes "ชาวเกาะเดวาตะไม่ค่อยดื่มน้ำมากนักในช่วงอากาศร้อน";
				next;
				mes "[บันทึก]";
				mes "เมื่อตอนผมยังเด็ก Dewata ได้กลายเป็นแหล่งท่องเที่ยว และนั่นคือช่วงเวลาที่ ^FF00FFtour package^000000 Ark เริ่มขายดี";
				next;
				mes "[บันทึก]";
				mes "ของเหลวที่ดีต้องมีน้ำมันใหม่ ฉันทำของเหลวมาสิบปีและส่งมอบให้ทันเวลา";
				next;
				mes "[บันทึก]";
				mes "เนื่องจากน้ำมันวันนี้ไม่มาถึงตรงเวลาผมจึงคิดว่าคงจะลำบากครับ";
				next;
				mes "[บันทึก]";
				mes "คุณช่วยให้ฉันทำของเหลวได้ถูกต้อง";
				next;
				mes "[บันทึก]";
				mes "สุภาษิตนี้ ^0000FF'Mengatakan mudah, mengerjakan susah.'^000000 แปลว่า พูดง่ายแต่ทำอะไรยาก";
				next;
				mes "[บันทึก]";
				mes "กล่าวอีกนัยหนึ่ง การพูดว่าเราจะช่วยเหลือผู้อื่นนั้นเป็นเรื่องง่าย แต่การกระทำจริงนั้นเป็นเรื่องยาก";
				next;
				emotion ET_THANKS;
				mes "[บันทึก]";
				mes "คุณทำได้ดีมาก ฉันชอบคุณเพราะคุณแตกต่างจากเด็กรุ่นใหม่ในปัจจุบัน";
				changequest 5043,5044;
				set dew_drink,10;
				getexp 50000,10000;
				getitem 617,1; //Old_Violet_Box
				next;
				mes "[บันทึก]";
				mes "คุณจะช่วยฉันทำงานบ้างเป็นครั้งคราวไหม?";
				mes "ฉันจะตอบแทนคุณสำหรับสิ่งที่คุณทำเพื่อฉัน";
				close;
		} else if (dew_drink == 10) {
				for(set .@i,0; .@i<7; set .@i,.@i+1)
						if (checkquest(5045+.@i) > -1)
								set .@j, .@j | (1<<.@i);
				if (.@j == 0) {
						mes "[บันทึก]";
						mes strcharinfo(0)+"ฉันต้องยินดีเพราะอะไร?";
						next;
						switch(select("Just came by to say hello.:I will help you.")) {
						case 1:
								mes "[บันทึก]";
								mes "ฉันเดาว่าคุณยุ่งอยู่";
								mes "ถ้าเป็นไปได้จะเอาน้ำมันจากเลโรไปให้ได้ไหมครับ ขอบคุณครับ...";
								close;
						case 2:
								mes "[บันทึก]";
								mes "ขอบคุณ ฉันรู้ว่าคุณยุ่งและการช่วยเหลือฉันอาจเป็นเรื่องยุ่งยาก";
								next;
								mes "[บันทึก]";
								mes "ช่วยเลโรนำน้ำมันปาล์มกลับมาได้ไหม...?";
								setquest 5045;
								close;
						}
				} else if ((.@j & 1) || (.@j & 2) || (.@j & 8) || (.@j & 32)) {
						mes "[บันทึก]";
						mes "ช่วยเลโรนำน้ำมันปาล์มกลับมาได้ไหม...?";
						close;
				} else if ((.@j & 4) || (.@j & 16) || (.@j & 64)) {

						// .@i[]: <required amount>,<quest ID>,<reward amount>
						if (.@j & 4)
								setarray .@i[0],10,5047,1;
						else if (.@j & 16)
								setarray .@i[0],15,5049,2;
						else
								setarray .@i[0],20,5051,3;

						if (countitem(6401) < .@i[0]) {
								mes "[บันทึก]";
								mes "คุณไม่ได้นำน้ำมันมาเพียงพอ";
								next;
								mes "-";
								next;
								mes "[บันทึก]";
								mes "โปบี้เป็นหนี้ฉันอยู่สองสามอย่าง เขาก็จะให้คุณบ้าง";
								close;
						}
						mes "[บันทึก]";
						mes "โอ้ คุณนำน้ำมันมาด้วย งานดีมาก";
						next;
						mes "[บันทึก]";
						mes "คุณคงจะลำบากในการนำน้ำมันมา ดังนั้นเอาเครื่องดื่มนี้มาเลย";
						delitem 6401,.@i[0]; //Palm_O
						erasequest .@i[1];
						setquest 5044;
						getitem 11534,.@i[2]; //Coco_Juice
						next;
						switch(rand(4)) {
						case 0:
								mes "[บันทึก]";
								mes "อาร์ค โด่งดังด้านนักท่องเที่ยว มีผู้ชายคนหนึ่งซื้อขวดหลายขวดด้วยตัวเอง";
								next;
								mes "[บันทึก]";
								mes "กังวลว่าจะดื่มคนเดียวหรือเปล่า ดื่มมากเกินไปก็อาจเป็นพิษได้";
								break;
						case 1:
								mes "[บันทึก]";
								mes "พระอาทิตย์ขึ้นทางทิศตะวันออกเสมอ ความขี้เกียจที่คุ้นเคยจะตื่นแต่เช้า";
								next;
								mes "[บันทึก]";
								mes "เขามาถึงความเป็นจริงแล้วหรือ เขาเป็นคนดีแต่ขี้เกียจ";
								break;
						case 2:
								mes "[บันทึก]";
								mes "การโจมตีครั้งแรกเป็นเพียงครึ่งหนึ่งของการต่อสู้";
								next;
								mes "[บันทึก]";
								mes "ฉันคิดว่าคุณถูกกำหนดมาให้ทำสิ่งที่ยิ่งใหญ่";
								break;
						case 3:
								mes "[บันทึก]";
								mes "มีผู้ชายคนหนึ่งพูดทั้งร้อนทั้งหนาว คำพูดดูเหมือนง่ายแต่กลับไม่เป็นเช่นนั้น";
								next;
								mes "[บันทึก]";
								mes "ฉันเชื่อว่าคุณจะรักษาคำสัญญาของคุณ";
								break;
						}
						next;
						mes "[บันทึก]";
						mes "ฉันต้องการให้คุณช่วยฉันครั้งนี้";
						next;
						mes "[บันทึก]";
						mes "โพบี้ยังมีอาการปวดหลัง และจะต้องใช้เวลาอีกหลายวันจึงจะฟื้นตัว";
						close;
				}
		} else { // Should never happen.
				mes "[บันทึก]";
				mes "เกาะเดวาตะมีผู้คนพลุกพล่านอยู่เสมอแต่ในวันนี้กลับเงียบสงบ";
				close;
		}
}

dew_fild01,114,275,4	script	Lero#dew_drink	536,{
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
				mes "[เลโร]";
				mes "คุณพกสิ่งของมาเยอะเกินไป...";
				close;
		}
		if (dew_drink == 1) {
				mes "[เลโร]";
				mes "โอ้ไม่! ฉันมีออเดอร์เยอะมากและวัตถุดิบก็ใกล้จะหมดแล้ว";
				mes "ยังไม่มาเหรอ เกิดอะไรขึ้นเหรอ โพบี้ ไปไหนมา";
				emotion ET_FRET;
				next;
				select("Excuse me, but are you Lero?");
				mes "[เลโร]";
				mes "เมื่อโพบี้กลับมาที่นี่ ฉันจะซ้อมเขาให้หนักจนเขาไม่มีวันลืม!";
				emotion ET_ANGER;
				next;
				mes "- เลโรดูเหมือนจะโกรธใครบางคนมากและไม่สนใจคุณ -";
				next;
				mes "[เลโร]";
				mes "อุ๊ย คุณเป็นใคร?";
				emotion ET_QUESTION;
				next;
				select("Memo sent me here.");
				mes "[เลโร]";
				mes "อ๋อ ลืมไปว่ามีเมโม่ เพราะคนงานเก็บถั่วยังไม่มา ขอโทษทีครับ";
				next;
				mes "[เลโร]";
				mes "ในการผลิตน้ำมันปาล์มสด ฉันต้องการน้ำมันปาล์มสดจากผลปาล์ม เพื่อที่ฉันจะได้น้ำมันสดจากผลปาล์มสดได้มากขึ้น";
				next;
				mes "[เลโร]";
				mes "ระหว่างคุณกับฉัน คุณเมโมเป็นคนเรื่องมากถึงขนาดไม่ยอมใช้น้ำมันที่ไม่สดเลย";
				next;
				mes "[เลโร]";
				mes "เพื่อผลิตน้ำมันสดเขาจะส่งคนงานไปที่ป่าปาล์มเสมอ แต่... คนที่ส่งมาตอนรุ่งสางก็ยังไม่กลับมาเลย";
				next;
				mes "[เลโร]";
				mes "ในตอนแรกฉันคิดว่าเขาขี้เกียจและทำให้ฉันรู้สึกไม่สบายใจมาก";
				mes "คุณสามารถพบคนงานในป่าปาล์มได้หรือไม่?";
				next;
				select("Yes, I will find them.");
				mes "[เลโร]";
				mes "ขอบคุณมาก.";
				mes "ค้นหาโพบี้";
				next;
				mes "[เลโร]";
				mes "โพบี้อาจจะอยู่ในป่าปาล์ม ^0000FF^000000 พืชผลจะดีที่สุดในส่วนทางตอนใต้ของป่า";
				set dew_drink,2;
				changequest 5035,5036;
				close;
		} else if (dew_drink == 2) {
				mes "[เลโร]";
				mes "คุณเจอโปบี้แล้วหรือยัง?";
				emotion ET_QUESTION;
				next;
				mes "[เลโร]";
				mes "-";
				next;
				mes "[เลโร]";
				mes "โอ้ คุณยังไม่ได้ไปเยี่ยมชมทางตะวันออกของ ^0000FFPalm Forest^000000 เลย ฉันค่อนข้างกังวล";
				close;
		} else if (dew_drink == 3) {
				mes "[เลโร]";
				mes "กลับมาแล้วจ้า...และ...โพบี้...";
				mes "โพบี้ เป็นอย่างไรบ้าง?";
				emotion ET_QUESTION;
				next;
				select("Poby.....");
				mes "[เลโร]";
				mes "อ่า! ฉันไม่รู้ว่า ฉันมักจะบอกโพบี้เสมอ ^0000FF'เกมาเรา เศตะฮุน รุศักดิ์เซฮารี.'^000000";
				next;
				mes "[เลโร]";
				mes "เขาปีนต้นไม้โดยไม่ได้ติดตั้งอุปกรณ์ป้องกันความปลอดภัยใดๆ ดังนั้นเขาอาจจะล้มลงและได้รับบาดเจ็บได้";
				next;
				mes "[เลโร]";
				mes "อืม...ขอโทษที ตอนนี้ฉันไม่สามารถหาน้ำมันปาล์มได้";
				next;
				mes "[เลโร]";
				mes "จะส่งสารถึงคุณเมโมว่าหาไม่ได้เพราะส่วนผสมไม่ครบใช่ไหมครับ?";
				mes "ช่างฝีมือไม่มีทางสร้างสิ่งใดได้เลยหากไม่มีส่วนผสม";
				next;
				select("Why don't I get the ingredients?");
				mes "[เลโร]";
				mes "นั่นอาจเป็นวิธีที่ดีได้ แต่การรวบรวมถั่วเป็นจุดเริ่มต้นอาจเป็นเรื่องยาก";
				next;
				mes "[เลโร]";
				mes "อืม!!!";
				mes "ฉันมีความคิดที่ดี";
				next;
				mes "[เลโร]";
				mes "คุณสามารถถาม Poby เกี่ยวกับวิธีการเก็บถั่วที่ดีที่สุดที่คุณพบเขาในป่าได้ เขาน่าจะสามารถสอนเทคนิคนี้ให้คุณได้";
				set dew_drink,4;
				changequest 5037,5038;
				next;
				mes "[เลโร]";
				mes "แม้ว่าเขาจะขี้เกียจและค่อนข้างประมาท แต่เขาก็เป็นคนทำงานที่ดีที่สุด";
				close;
		} else if (dew_drink == 4) {
				if (checkquest(5038) > -1) {
						mes "[เลโร]";
						mes "คุณสามารถถาม Poby เกี่ยวกับวิธีการเก็บถั่วที่ดีที่สุดที่คุณพบเขาในป่าได้ เขาน่าจะสามารถสอนเทคนิคนี้ให้คุณได้";
						next;
						mes "[เลโร]";
						mes "แม้ว่าเขาจะขี้เกียจและค่อนข้างประมาท แต่เขาก็เป็นคนทำงานที่ดีที่สุด";
						close;
				} else if (checkquest(5039) > -1) {
						if (countitem(6402) < 60) {
								mes "[เลโร]";
								mes "คุณเก็บถั่วมาแล้วหรือยัง?";
								next;
								mes "[เลโร]";
								mes "หนึ่งสอง...";
								next;
								mes "[เลโร]";
								mes "นี่ไม่ใช่หมายเลขที่ฉันสั่งจาก Poby";
								next;
								mes "[เลโร]";
								mes "ถ้าจะล่าช้ากว่านี้ก็จะลำบากแล้วรีบเอามา 60 มาให้หน่อยนะคะ";
								close;
						}
						mes "[เลโร]";
						mes "หนึ่ง, สอง, สาม, สี่, . . .";
						next;
						mes "[เลโร]";
						mes "ถั่ว 60 เม็ดพอดีค่ะ เดี๋ยวจะคั้นน้ำมันให้ค่ะ";
						next;
						emotion ET_SORRY;
						mes "[เลโร]";
						mes "ต้องใช้เวลาสักพัก กลับไปหาคุณเมโมแล้วเล่าสถานการณ์ให้เขาฟัง และบอกเขาด้วยว่าขอโทษที่ล่าช้า";
						delitem 6402,60; //Oil_Palm_F
						set dew_drink,5;
						changequest 5039,5040;
						close;
				}
		} else if (dew_drink == 5) {
				mes "[เลโร]";
				mes "ต้องใช้เวลาสักพัก กลับไปหาคุณเมโมแล้วเล่าสถานการณ์ให้เขาฟัง และบอกเขาด้วยว่าขอโทษที่ล่าช้า";
				close;
		} else if (dew_drink == 6) {
				if (checkweight(1201,1) == 0 || MaxWeight - Weight < 15000) {
						mes "[เลโร]";
						mes "ตอนนี้คุณแบกรับมากเกินไปจนไม่สามารถดำเนินการต่อได้";
						close;
				}
				mes "[เลโร]";
				mes "มาถูกเวลาแล้วค่ะ เพิ่งทำน้ำมันปาล์มเสร็จค่ะ";
				next;
				mes "[เลโร]";
				mes "มันหนักกว่าที่คิด ดังนั้นต้องระวังในการพกพา";
				next;
				emotion ET_SORRY;
				mes "[เลโร]";
				mes "โอ้ บอกคุณเมโมด้วยว่าฉันขอโทษที่ส่งช้า และอธิบายว่ามันไม่ใช่ความผิดของฉัน";
				changequest 5041,5042;
				set dew_drink,7;
				getitem 6401,30; //Palm_O
				close;
		} else if (dew_drink == 7) {
				if (checkquest(5042) > -1 && countitem(6401) > 29) {
						mes "[เลโร]";
						mes "มันหนักกว่าที่คิด ดังนั้นต้องระวังในการพกพา";
						next;
						emotion ET_SORRY;
						mes "[เลโร]";
						mes "โอ้ บอกคุณเมโมด้วยว่าฉันขอโทษที่ส่งช้า และอธิบายว่ามันไม่ใช่ความผิดของฉัน";
						close;
				} else {
						set .@num, 30 - countitem(6401);
						if (checkweight(6401,.@num) == 0) {
								mes "[เลโร]";
								mes "ตอนนี้คุณแบกรับมากเกินไปจนไม่สามารถดำเนินการต่อได้";
								close;
						}
						mes "[เลโร]";
						mes "ฉันจำได้ชัดเจนว่าฉันให้น้ำมันปาล์มแก่คุณ 30 ขวด คุณทำมันหายหมดเลยเหรอ?";
						next;
						select("Sorry, it's my fault...");
						emotion ET_FRET;
						mes "[เลโร]";
						mes "นี่คือน้ำมันปาล์มที่ขาดแคลน โปรดระวังด้วย มันหายากจริงๆ...";
						getitem 6401,.@num; //Palm_O
						close;
				}
		} else if (dew_drink == 8) {
				mes "[เลโร]";
				mes "คุณเมโมพูดว่าอะไรคะ?";
				emotion ET_QUESTION;
				next;
				mes "-"+strcharinfo(0)+"-";
				emotion ET_BLABLA, playerattached();
				mes "-";
				next;
				mes "[เลโร]";
				mes "อ่า... ฉันเข้าใจแล้ว...";
				emotion ET_OK;
				close;
		} else if (dew_drink == 10) {
				for(set .@i,0; .@i<7; set .@i,.@i+1)
						if (checkquest(5045+.@i) > -1)
								set .@j, .@j | (1<<.@i);
				if (.@j == 0) {
						mes "[เลโร]";
						mes "เกิดอะไรขึ้น? ฉันไม่ได้รับบันทึกจากคุณเมโมว่าเขาจะส่งใครมาที่นี่";
						close;
				} else if (.@j & 1) {
						mes "[เลโร]";
						mes "เฮ้ ฉันรอเธออยู่นะ ฉันไม่รู้ว่าอาการบาดเจ็บของโพบี้จะทำให้ชีวิตฉันลำบากขนาดนี้";
						next;
						set .@quest,rand(3);
						mes "[เลโร]";
						mes "ฉันอายที่จะถามคุณเรื่องนี้ แต่คุณช่วยนำ ^0000FF มาได้ไหม"+(20+(.@quest*10))+"ขอชิ้นเมล็ดปาล์มน้ำมันหน่อยคะ^000000";
						changequest 5045,5046+(.@quest*2);
						next;
						mes "[เลโร]";
						mes "เมื่อนำเมล็ดปาล์มน้ำมันมาแล้ว คุณเมโมจะแลกเปลี่ยนกับน้ำมันให้";
						close;
				} else if ((.@j & 2) || (.@j & 8) || (.@j & 32)) {

						// .@i[]: <required amount>,<quest ID>,<reward amount>,<free weight>
						if (.@j & 2)
								setarray .@i[0],20,5046,10,5000;
						else if (.@j & 8)
								setarray .@i[0],30,5048,15,7500;
						else
								setarray .@i[0],40,5050,20,10000;

						if (countitem(6402) < .@i[0]) {
								mes "[เลโร]";
								mes "เมื่อคุณนำ"+.@i[0]+"ชิ้นส่วนเมล็ดปาล์มน้ำมัน คุณเมโมจะแลกเปลี่ยนกับน้ำมัน";
								close;
						}
						if (checkweight(1201,1) == 0 || MaxWeight - Weight < .@i[3]) {
								mes "[เลโร]";
								mes "ตอนนี้คุณแบกรับมากเกินไปจนไม่สามารถดำเนินการต่อได้";
								close;
						}
						mes "[เลโร]";
						mes "หนึ่ง, สอง, สาม, สี่, . . .";
						next;
						mes "[เลโร]";
						mes "อย่างแน่นอน"+.@i[0]+"ชิ้นปาล์มน้ำมัน เอามาแลกกับน้ำมันได้เลย";
						changequest .@i[1], .@i[1]+1;
						delitem 6402,.@i[0]; //Oil_Palm_F
						getitem 6401,.@i[2]; //Palm_O
						next;
						mes "[เลโร]";
						mes "อย่างที่บอกไปว่ามันหนักกว่าที่คิด ดังนั้นต้องระวังตอนพกพาไปด้วย";
						close;
				} else if ((.@j & 4) || (.@j & 16) || (.@j & 64)) {
						mes "[เลโร]";
						mes "อย่างที่บอกไปว่ามันหนักกว่าที่คิด ดังนั้นต้องระวังตอนพกพาไปด้วย";
						close;
				}
		}
		mes "[เลโร]";
		mes "เกิดอะไรขึ้นกับโพบี้...ทำไมเขาไม่กลับมา...?";
		next;
		mes "- ดูเหมือนว่าเขากำลังรอใครบางคนอยู่-";
		close;
}

dew_fild01,259,48,4	script	Poby#dew_drink	538,{
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 500) {
				if (dew_drink > 0) {
						mes "[โพบี้]";
						mes "ทำไมคุณถึงแบกเยอะขนาดนี้?";
						close;
				} else {
						mes "[โพบี้]";
						mes "ถ้าพกมาเยอะขนาดนี้ต้องเสียดาย";
						close;
				}
		}
		if (dew_drink == 2) {
				mes "[โพบี้]";
				mes "อ๊า~ปวดหลัง...";
				emotion ET_PROFUSELY_SWEAT;
				next;
				select("Are you Poby?");
				mes "[โพบี้]";
				mes "คุณเป็นใคร?";
				next;
				mes "[โพบี้]";
				mes "ฉันไม่รู้จักคุณแต่";
				mes "คุณเป็นแฟนของฉันหรือเปล่า?";
				mes "ฉันชื่อโพบี้ นักล่าผลไม้ที่เก่งที่สุดในเผ่าจาตี้";
				next;
				mes "[โพบี้]";
				mes "คุณมาที่นี่เพื่อขอลายเซ็นของฉันเหรอ?";
				emotion ET_QUESTION;
				next;
				select("Lero made me come here.");
				mes "[โพบี้]";
				mes "อ๋อ คุณเลโรเป็นคนทำให้เธอต้องมาที่นี่ ฉันขยับตัวไม่ได้เพราะปีนต้นไม้แล้วปวดหลัง";
				next;
				mes "[โพบี้]";
				mes "!#@$*&%!@$&!$%!$#*&!@#$";
				emotion ET_CRY;
				next;
				mes "[โพบี้]";
				mes "แล้วฉันก็ไม่เคยมีแฟนเลย...";
				next;
				mes "[โพบี้]";
				mes "!#@$*&%!@$&!$%!$#*&!@#$";
				emotion ET_CRY;
				next;
				mes "[โพบี้]";
				mes "อ๋อ ขอโทษที บอกคุณเลโรว่าฉันจะกลับไปถ้าหาทางย้ายออกไปได้";
				changequest 5036,5037;
				set dew_drink,3;
				next;
				mes "[โพบี้]";
				mes "ฉันขยับตัวไม่ได้เลยเพราะปวดหลัง...";
				close;
		} else if (dew_drink == 3) {
				mes "[โพบี้]";
				mes "อ๋อ ขอโทษที บอกคุณเลโรว่าฉันจะกลับไปถ้าหาทางย้ายออกไปได้";
				next;
				mes "[โพบี้]";
				mes "ฉันขยับตัวไม่ได้เลยเพราะปวดหลัง...";
				emotion ET_PROFUSELY_SWEAT;
				close;
		} else if (dew_drink == 4) {
				if (checkquest(5039) > -1) {
						if (countitem(6402) > 59) { // Oil_Palm_F
								mes "[โพบี้]";
								mes "คุณได้ผลไม้ทั้งหมดแล้ว เอาไปให้คุณเลโรสิ";
								close;
						} else {
								mes "[โพบี้]";
								mes "สำหรับผู้เริ่มต้นอย่างคุณ การเขย่าหรือเตะต้นไม้เป็นวิธีที่ง่ายที่สุด";
								next;
								mes "[โพบี้]";
								mes "อาจจะเป็นอันตรายได้ ดังนั้นควรใส่ ^0000FFหมวกนิรภัย^000000 ไว้เสมอ";
								next;
								mes "[โพบี้]";
								mes "รวบรวม ^FF000060 ^0000FFผลน้ำมันปาล์ม^000000 และนำมาให้คุณเลโร";
								close;
						}
				}
				mes "[โพบี้]";
				mes "อ๊า~ปวดหลัง...";
				emotion ET_PROFUSELY_SWEAT;
				next;
				mes "[โพบี้]";
				mes "อ๋อ!! คุณ--ให้ฉันเดาดูนะ...";
				next;
				mes "[โพบี้]";
				mes "-";
				emotion ET_THINK;
				next;
				mes "[โพบี้]";
				mes "...กลับมาแล้ว! คุณวางแผนจะจับ ^0000FFผลปาล์มน้ำมัน^000000 แทนฉันใช่ไหม?";
				next;
				mes "[โพบี้]";
				mes "สำหรับมือใหม่อย่างผมคงจับผลไม้ได้ยาก เป็นไปไม่ได้";
				next;
				mes "[โพบี้]";
				mes "ปกติแล้วปรมาจารย์จะปีนต้นไม้เพื่อจับผลไม้ แต่คุณเป็นมือใหม่...";
				next;
				mes "[โพบี้]";
				mes "สำหรับผู้เริ่มต้น การเขย่าหรือเตะต้นไม้เป็นวิธีที่ง่ายที่สุด อาจเป็นอันตรายได้ ดังนั้นอย่าลืมสวม ^0000FFหมวกนิรภัย^000000";
				changequest 5038,5039;
				next;
				mes "[โพบี้]";
				mes "โอเค! รวบรวม ^FF000060^0000FF ^0000FFผลปาล์มน้ำมัน^000000 และนำจำนวนนั้นไปให้คุณเลโร";
				emotion ET_SURPRISE;
				close;
		} else if (dew_drink == 10) {
				mes "[โพบี้]";
				mes "โอ้!"+strcharinfo(0)+", เราพบกันอีกครั้ง มีอะไรเกิดขึ้น?";

				if (checkquest(5047) > -1)
						set .@count,10;
				else if (checkquest(5049) > -1)
						set .@count,15;
				else if (checkquest(5051) > -1)
						set .@count,20;
				if (.@count) {
						if (countitem(6401) >= .@count) {
								// Custom translation
								mes "[โพบี้]";
								mes "งานดีมากกก ส่งของต้องระวังนะคะ~";
								close;
						}
						set .@new_count, .@count-countitem(6401);
						mes "- คุณอธิบายสถานการณ์ -";
						next;
						mes "[โพบี้]";
						mes "ใช่แล้ว... ฉันเป็นหนี้ปู่ของเมโม เดี๋ยวนะ ฉันซ่อนน้ำมันไว้แถวนี้";
						next;
						if (checkweight(6401,.@new_count) == 0) {
								mes "[โพบี้]";
								mes "ฉันมีอยู่บ้าง แต่คุณดูเหมือนมีของเยอะเกินไป";
								close;
						}
						mes "- ค้นหา... ค้นหา... -";
						next;
						mes "[โพบี้]";
						mes "อ้อ นี่อีกอันนะ ระวังอย่าให้มันหายอีกนะ และเก็บเป็นความลับจากคุณเลโรด้วย";
						getitem 6401,.@new_count; //Palm_O
						next;
						mes "[โพบี้]";
						mes "ขอร้องนะ~"+strcharinfo(0)+"-";
						close;
				}
				next;
				switch(select("Why'd you come here?:I want to know how to get fruit.")) {
				case 1:
						mes "[โพบี้]";
						mes "ฉันไปเดินเล่นเพราะคิดว่าจะทำให้ปวดหลังน้อยลง";
						close;
				case 2:
						mes "[โพบี้]";
						mes "สำหรับผู้เริ่มต้น วิธีที่ง่ายที่สุดคือเขย่าหรือเตะต้นไม้";
						next;
						mes "[โพบี้]";
						mes "อาจจะเป็นอันตรายได้ ดังนั้นควรใส่ ^0000FFหมวกนิรภัย^000000 ไว้เสมอ";
						close;
				}
		}
		mes "[โพบี้]";
		mes "โอ้ย~ หลังฉันปวดมากเลย";
		emotion ET_CRY;
		close;
}

-	script	::DewataPalm	-1,{
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
				mes "คุณแบกมันไว้มากเกินไป";
				close;
		}

		if (checkquest(5039) > -1) set .@count,60;
		else if (checkquest(5046) > -1) set .@count,20;
		else if (checkquest(5048) > -1) set .@count,30;
		else if (checkquest(5050) > -1) set .@count,40;
		if (.@count == 0) {
				mes "มีต้นปาล์ม";
				mes "เผชิญลมเย็นสบาย";
				close;
		} else if (countitem(6402) >= .@count) {
				mes "ต้นปาล์มปฏิเสธ";
				mes "มืออันโลภของคุณ";
				close;
		}

		if (isequipped(5009)) { // Safety_Helmet
				mes "มีต้นปาล์มที่มีผลและดอกไม้ห้อยลงมาจากกิ่งก้าน";
				mes "อย่าลืมสวมหมวกนิรภัยขณะพยายามเก็บผลไม้";
		} else {
				mes "มีต้นปาล์ม";
				mes "คุณสามารถเตะหรือเขย่ามันได้";
				mes "คุณอยากทำอะไร?";
		}
		next;
		switch(select("Shake the tree:Kick the tree")) {
		case 1:
				specialeffect EF_HITLINE2;
				set .@message$, "ขณะที่คุณเขย่าต้นปาล์ม...";
				mes "คุณเขย่าต้นปาล์ม";
				break;
		case 2:
				specialeffect EF_BASH;
				set .@message$, "ในขณะที่คุณเตะต้นปาล์มอย่างรวดเร็วเข้าที่หน้าแข้งอันโด่งดังของมัน...";
				mes "คุณเตะต้นปาล์มอย่างรวดเร็วด้วยพลังทั้งหมดของคุณ";
				break;
		}
		next;
		mes "-";
		next;
		mes .@message$;
		switch(rand(1,20)) {
		case 1:
		case 2:
		case 3:
				mes "หิน ^FF0000^000000 ตกลงมา";
				getitem 7049,1; //Stone
				break;
		case 6:
		case 7:
				mes "^FF0000Great Leaf^000000 หล่นออกมา";
				getitem 7198,1; //Great_Leaf
				break;
		case 8:
		case 9:
				mes "เสียง ^FF0000Worm Peeling^000000 หลุดออกมา";
				getitem 955,1; //Worm_Peelings
				break;
		case 4:
		case 5:
		case 11:
		case 12:
		case 13:
				mes "ผลปาล์มน้ำมัน^000000หลุดออกมา";
				getitem 6402,1; //Oil_Palm_F
				break;
		case 14:
		case 16:
		case 20:
				specialeffect2 EF_BASH;
				if (isequipped(5009)) { //Safety_Helmet
						mes "มีอะไรบางอย่างตกลงมาบนหัวคุณ แต่เนื่องจากคุณสวมหมวกนิรภัย คุณจึงปลอดภัย";
						emotion ET_BEST, playerattached();
						break;
				} else {
						mes "something smashes down on your head, right where that soft spot is, $#*@! that's a one in a million shot.";
						percentheal -30,0;
						close2;
						donpcevent strnpcinfo(0)+"::OnDisable";
						warp "dew_fild01",371,211;
						end;
				}
		case 10:
		case 17:
				if (countitem(6402) <= .@count-5) {
						mes "ผลปาล์มน้ำมันจำนวนหลายตันหล่นลงมาจากต้น";
						getitem 6402,1; //Oil_Palm_F
						getitem 6402,1; //Oil_Palm_F
						getitem 6402,1; //Oil_Palm_F
						getitem 6402,1; //Oil_Palm_F
						getitem 6402,1; //Oil_Palm_F
				} else {
						mes "ผลปาล์มน้ำมัน^000000หลุดออกมา";
						getitem 6402,1; //Oil_Palm_F
				}
				break;
		default:
				mes "แต่ก็ไม่มีอะไรเกิดขึ้น";
				break;
		}
		close2;
		donpcevent strnpcinfo(0)+"::OnDisable";
		end;
OnEnable:
		enablenpc();
		stopnpctimer;
		end;
OnDisable:
		disablenpc();
		initnpctimer;
		end;
OnTimer60000:
OnTimer180000:
OnTimer120000:
OnTimer240000:
		if(rand(2))
				donpcevent strnpcinfo(0)+"::OnEnable";
		end;
OnTimer300000:
		donpcevent strnpcinfo(0)+"::OnEnable";
		end;
}
dew_fild01,247,62,0	duplicate(DewataPalm)	Palm Tree#drink01	844
dew_fild01,276,73,0	duplicate(DewataPalm)	Palm Tree#drink02	844
dew_fild01,278,63,0	duplicate(DewataPalm)	Palm Tree#drink03	844
dew_fild01,301,88,0	duplicate(DewataPalm)	Palm Tree#drink04	844
dew_fild01,314,72,0	duplicate(DewataPalm)	Palm Tree#drink05	844
dew_fild01,320,68,0	duplicate(DewataPalm)	Palm Tree#drink06	844
dew_fild01,334,107,0	duplicate(DewataPalm)	Palm Tree#drink07	844
dew_fild01,345,88,0	duplicate(DewataPalm)	Palm Tree#drink08	844
dew_fild01,344,71,0	duplicate(DewataPalm)	Palm Tree#drink09	844
dew_fild01,358,93,0	duplicate(DewataPalm)	Palm Tree#drink10	844
dew_fild01,338,121,0	duplicate(DewataPalm)	Palm Tree#drink11	844
dew_fild01,358,136,0	duplicate(DewataPalm)	Palm Tree#drink12	844
dew_fild01,326,147,0	duplicate(DewataPalm)	Palm Tree#drink13	844
dew_fild01,307,146,0	duplicate(DewataPalm)	Palm Tree#drink14	844
dew_fild01,325,159,0	duplicate(DewataPalm)	Palm Tree#drink15	844
dew_fild01,339,163,0	duplicate(DewataPalm)	Palm Tree#drink16	844
dew_fild01,346,193,0	duplicate(DewataPalm)	Palm Tree#drink17	844
dew_fild01,355,193,0	duplicate(DewataPalm)	Palm Tree#drink18	844
dew_fild01,363,219,0	duplicate(DewataPalm)	Palm Tree#drink19	844
dew_fild01,341,261,0	duplicate(DewataPalm)	Palm Tree#drink20	844
dew_fild01,289,302,0	duplicate(DewataPalm)	Palm Tree#drink21	844
dew_fild01,70,196,0	duplicate(DewataPalm)	Palm Tree#drink22	844
dew_fild01,97,145,0	duplicate(DewataPalm)	Palm Tree#drink23	844
dew_fild01,119,42,0	duplicate(DewataPalm)	Palm Tree#drink24	844
dew_fild01,188,72,0	duplicate(DewataPalm)	Palm Tree#drink25	844
dew_fild01,238,80,0	duplicate(DewataPalm)	Palm Tree#drink26	844
dew_fild01,252,97,0	duplicate(DewataPalm)	Palm Tree#drink27	844
dew_fild01,256,98,0	duplicate(DewataPalm)	Palm Tree#drink28	844
dew_fild01,279,93,0	duplicate(DewataPalm)	Palm Tree#drink29	844
dew_fild01,299,107,0	duplicate(DewataPalm)	Palm Tree#drink30	844
dew_fild01,355,113,0	duplicate(DewataPalm)	Palm Tree#drink31	844
dew_fild01,278,136,0	duplicate(DewataPalm)	Palm Tree#drink32	844
dew_fild01,258,128,0	duplicate(DewataPalm)	Palm Tree#drink33	844
dew_fild01,240,125,0	duplicate(DewataPalm)	Palm Tree#drink34	844
dew_fild01,225,117,0	duplicate(DewataPalm)	Palm Tree#drink35	844
dew_fild01,219,121,0	duplicate(DewataPalm)	Palm Tree#drink36	844
dew_fild01,226,99,0	duplicate(DewataPalm)	Palm Tree#drink37	844
dew_fild01,209,137,0	duplicate(DewataPalm)	Palm Tree#drink38	844
dew_fild01,206,156,0	duplicate(DewataPalm)	Palm Tree#drink39	844
dew_fild01,209,201,0	duplicate(DewataPalm)	Palm Tree#drink40	844

// Traditional Weapon Quest :: dewata_weapon
//============================================================
dew_fild01,127,240,4	script	Gatti#weapon	813,{
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 10000) {
				mes "- จับไว้ตรงนั้นสิ!! -";
				mes "- คุณมีไอเทมมากเกินไปและไม่มีพื้นที่เก็บของเพียงพอที่จะรับรางวัลใดๆ";
				mes "- โปรดเพิ่มพื้นที่เก็บของให้มากขึ้นและกลับมารับความท้าทายนี้อีกครั้ง";
				close;
		}
		if (BaseLevel < 60) {
				mes "[กัตติ]";
				mes "ฉันอยู่ไหนวะเนี่ย คนแถวนี้กำลังหลบฉันกันหมด";
				close;
		}
		if (dew_weapon == 0) {
				mes "[กัตติ]";
				mes "เฮ้ หนุ่มน้อย"+((Sex == SEX_MALE)?"ผู้ชาย":"คุณผู้หญิง")+"~ คุณมาจากแถวๆนี้หรอ?";
				next;
				select("No, I'm not.");
				mes "[กัตติ]";
				mes "ฉันอยากจะถาม";
				mes "คุณมีคำถาม";
				mes "ที่นี่คือที่ไหน?";
				next;
				mes "- ดูเหมือนเขาจะถามถึงชื่อเกาะนะ -";
				next;
				input .@island_name$;
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "มันเรียกว่า"+ .@island_name$ +". มีอะไรเหรอ?";
				next;
				if (.@island_name$ == "Dewata Island") {
						mes "[กัตติ]";
						mes "นี่คือเกาะเดวาตะใช่ไหม คุณแน่ใจว่านี่คือเกาะเดวาตะใช่ไหม?";
						next;
						mes "[กัตติ]";
						mes "ฮ่าฮ่าฮ่าฮ่าฮ่า...!!!";
						next;
						mes "- ชายชราหัวเราะอย่างคนบ้าเมื่อได้ยินชื่อเกาะ คุณจึงตัดสินใจถามในเวลาต่อมา";
						set dew_weapon,1;
						close;
				} else {
						mes "[กัตติ]";
						mes "เป็นอย่างนั้นจริงเหรอ..."+ .@island_name$ +"-"+ .@island_name$ +"-"+ .@island_name$ +"-"+ .@island_name$ +"-";
						next;
						mes "- ชายชราถอนหายใจยาวอย่างยอมแพ้แล้วพูดต่อ -";
						next;
						mes "[กัตติ]";
						mes "บ้าเอ๊ย... แผนของฉันพังหมดแล้ว ฉันทุ่มเงินทั้งหมดเพื่อไปถึงจุดนั้น แต่ฉันก็ล้มเหลว ฉันทำอะไรไม่ได้แล้ว เรือของฉันพังไปแล้ว";
						next;
						mes "- ชายชราก็เริ่มร้องไห้ -";
						mes "- เพราะรู้สึกอึดอัด คุณก็รีบออกไป -";
						close;
				}
		} else if (dew_weapon == 1) {
				select("Well... Sir...");
				mes "[กัตติ]";
				mes "ไอ...ไอ...";
				next;
				mes "[กัตติ]";
				mes "โอ้... ขอโทษที~ ขอโทษที ฉันดีใจมากที่ในที่สุดก็มาถึงเกาะเดวาตะ";
				next;
				mes "[กัตติ]";
				mes "นี่ไม่ใช่เรื่องสั้นนะ ฉันต้องแนะนำตัวก่อน ฉันชื่อมาสเตอร์สมิธ กัตติ และเป็นนักผจญภัยเหมือนกัน";
				next;
				mes "[กัตติ]";
				mes "ฉันพบแบบแปลนอาวุธลึกลับ แต่ฉันจำไม่ได้ว่าได้มาเมื่อใด";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "แบบแปลนอาวุธเหรอ?";
				next;
				mes "[กัตติ]";
				mes "ใช่แล้ว! ฉันเดินทางไปทั่วเพื่อหาวัตถุดิบสำหรับทำอาวุธ แต่...";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "แต่?";
				next;
				mes "[กัตติ]";
				mes "ฉันหาเนื้อหาสุดท้ายไม่เจอเลย";
				next;
				mes "[กัตติ]";
				mes "ฉันเกือบจะยอมแพ้แล้ว แต่มีข้อความเล็กๆ น้อยๆ บนพิมพ์เขียวที่บอกว่าสามารถพบได้ในเกาะเดวาตะ";
				next;
				mes "[กัตติ]";
				mes "ฉันไม่รู้ว่าทำไมใครบางคนถึงเขียนสิ่งสำคัญไว้ในตัวหนังสือเล็กๆ ขนาดนั้น หลังจากอ่านข้อความนั้นแล้ว ฉันก็ทุ่มเงินทั้งหมดที่มีเพื่อตามหาเกาะนั้น";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "อ๋อ...อย่างนั้นเหรอ?";
				next;
				mes "[กัตติ]";
				mes "เกาะนี้แปลกมาก ฉันเพิ่งมาถึงที่นี่เมื่อสัปดาห์ที่แล้ว และทุกคนก็หลบเลี่ยงฉัน";
				next;
				mes "[กัตติ]";
				mes "สัปดาห์ที่แล้วยากมาก แต่คุณช่วยฉันไว้ด้วยการบอกว่าที่นี่คือเกาะเดวาตะ";
				mes "ขอบคุณที่บอกฉัน และขอบคุณที่คุณผ่านมาที่นี่ในเวลาที่เหมาะสม";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "เอ่อ... ไม่มีปัญหาครับ";
				next;
				mes "[กัตติ]";
				mes "เอ่อ...มีปัญหานิดหน่อยเรื่องเรือของฉันอับปาง แต่ก็ไม่ใช่เรื่องใหญ่อะไร";
				next;
				mes "[กัตติ]";
				mes "เมื่อตอนนี้ฉันรู้แล้วว่านี่คือเกาะเดวาตะ ฉันควรจะค้นหาส่วนประกอบสำหรับอาวุธอันยิ่งใหญ่ชิ้นนี้";
				next;
				mes "[กัตติ]";
				mes "-";
				next;
				if (dew_legend < 9) {
						mes "[กัตติ]";
						mes "อ่อ! วัสดุที่ผมกำลังมองหาคือหนัง Comodo ครับ.";
						next;
						mes "-"+strcharinfo(0)+"-";
						mes "หนังโคโมโด?";
						next;
						mes "[กัตติ]";
						mes "ใช่แล้ว ในพิมพ์เขียวเขียนไว้ว่ามีเพียงชาวพื้นเมืองเดวาตะที่ได้รับการยอมรับไม่กี่คนเท่านั้นที่คู่ควรที่จะต่อสู้กับมังกรโคโมโด";
						next;
						mes "[กัตติ]";
						mes "หากต้องการต่อสู้กับมังกรโคโมโด คุณต้องได้รับการยอมรับจากชาวพื้นเมืองก่อน ฉันคิดว่าคงยากที่จะได้รับการยอมรับ";
						next;
						mes "-"+strcharinfo(0)+"-";
						mes "แน่นอนว่าต้องเป็น...";
						next;
						mes "[กัตติ]";
						mes "ฉันขอรบกวนคุณหน่อยนะคะ ถ้าคุณได้รับการยอมรับก่อนก็อย่าลืมมาเยี่ยมฉันด้วย";
						close2;
						setquest 5052;
						set dew_weapon,2;
						end;
				}
				mes "[กัตติ]";
				if(Sex == SEX_FEMALE)
						mes "คุณผู้หญิง คุณมีพลังที่แข็งแกร่งมาก";
				else
						mes "คุณมีพลังที่แข็งแกร่ง";
				mes "คุณเคยเห็นสัตว์ประหลาดที่เรียกว่ามังกรโคโมโดบนเกาะนี้ไหม?";
				next;
				select("Yes, I have.");
				mes "[กัตติ]";
				mes "โอ้จริงเหรอ?";
				mes "ฉันโชคดีมากที่ได้พบคุณ ทุกอย่างกำลังดำเนินไปด้วยดี";
				next;
				mes "[กัตติ]";
				mes "คุณช่วยฉันได้ไหม?";
				next;
				mes "[กัตติ]";
				mes "วัสดุอาวุธที่ฉันกำลังมองหาคือ ^0000FFComodo Leather^000000 เอามาให้ฉัน 100 ชิ้น แล้วฉันจะทำอาวุธเก๋ๆ ให้คุณ";
				setquest 5053;
				set dew_weapon,3;
				next;
				mes "[กัตติ]";
				mes "ฉันได้ยินมาว่ามังกรโคโมโดเป็นสัตว์ประหลาดที่มีพลังมาก โปรดระวังด้วย";
				close;
		} else if (dew_weapon == 2) {
				if (dew_legend < 9) {
						mes "[กัตติ]";
						mes "คุณได้รับการยอมรับจากชาวพื้นเมืองแล้วหรือยัง?";
						next;
						mes "[กัตติ]";
						mes "-";
						next;
						mes "[กัตติ]";
						mes "ฉันเดาว่ายังไม่แน่ ฉันถามไปทั่วแล้วแต่ก็ยังหาทางให้คนในพื้นที่จำได้ไม่เจอ ฉันไม่รู้ว่าจะทำอาวุธนี้เสร็จเมื่อไหร่";
						next;
						mes "[กัตติ]";
						mes "หากคุณได้รับการยอมรับก่อนฉันโปรดอย่าลืมมาเยี่ยมฉัน";
						close;
				}
				mes "[กัตติ]";
				mes "แล้วคุณได้รับการจดจำจากชาวพื้นเมืองแล้วหรือยัง?";
				next;
				mes "[กัตติ]";
				mes "-";
				next;
				mes "[กัตติ]";
				mes "โอ้ ใช่แล้ว! ในที่สุด...";
				if (Sex == SEX_FEMALE)
						mes "คุณผู้หญิงคุณก็มีพลังที่แข็งแกร่งเช่นกัน";
				else
						mes "คุณยังมีพลังที่แข็งแกร่งอีกด้วย";
				mes "คุณได้รับการยอมรับจากคนในพื้นที่แล้วหรือยัง?";
				next;
				select("Yes.");
				mes "[กัตติ]";
				mes "โอ้! เป็นไปตามที่คาดไว้เลย!";
				mes "ฉันโชคดีมากที่ได้พบคุณ ทุกอย่างกำลังไปได้สวย";
				next;
				mes "[กัตติ]";
				mes "ฉันรู้ว่าฉันไม่มีสิทธิ์ที่จะขอร้องเธอแบบนี้ แต่การได้พบกันแบบนี้มันคือพรหมลิขิต...";
				next;
				mes "- กัตติพึมพำ -";
				next;
				mes "[กัตติ]";
				mes "วัสดุอาวุธที่ฉันใฝ่ฝันมานานคือ ^0000FFComodo Leather^000000 ช่วยหามาให้หน่อย 100 ชิ้น";
				next;
				mes "[กัตติ]";
				mes "ฉันแทบรอไม่ไหวที่จะสร้างอาวุธตามแบบพิมพ์เขียวของอาวุธแล้ว";
				changequest 5052,5053;
				set dew_weapon,3;
				next;
				mes "[กัตติ]";
				mes "ขอร้องเถอะ ก่อนที่ความหลงใหลของฉันจะจางหายไป";
				next;
				mes "[กัตติ]";
				mes "โอ้! และมังกรโคโมโดเป็นสัตว์ประหลาดที่มีพลังมาก ดังนั้นโปรดระวังด้วย";
				close;
		} else if (dew_weapon == 3) {
				if (countitem(6403) > 99) {
						mes "[กัตติ]";
						mes "ดูจากท่าทางของคุณแล้ว คุณคงนำหนังโคโมโดมาให้ฉัน 100 เหรียญแน่ๆ ส่งมาให้ฉันหน่อย";
						delitem 6403,100;
						erasequest 5053;
						set dew_weapon,4;
						next;
						mes "- กัตติเอาหนังโคโมโดจากคุณไป -";
						close;
				} else if (countitem(6403)) {
						mes "[กัตติ]";
						mes "นั่นก็คือ Comodo Leather แต่นั่นยังไม่เพียงพอ";
						next;
						mes "[กัตติ]";
						mes "ฉันรู้ว่าฉันกำลังขอร้องคุณ แต่ฉันต้องการ 100 อัน";
						close;
				} else {
						mes "[กัตติ]";
						mes "โปรดระวังเพราะมังกรโคโมโดเป็นสัตว์ประหลาดที่มีพลังมาก และอย่าลืมนำหนังโคโมโด 100 ชิ้นมาให้ฉันด้วยเมื่อเดินทางกลับ";
						close;
				}
		} else if (dew_weapon == 4) {
				mes "[กัตติ]";
				mes "โอ้~ ดูสิ ยืดหยุ่นดีเหลือเกิน นุ่มนิ่มจริงๆ";
				next;
				mes "[กัตติ]";
				mes "หนัง Comodo เป็นวัสดุที่ดีที่สุด";
				next;
				mes "[กัตติ]";
				mes "ใช่แล้ว ฉันจะต้องดำเนินการอย่างไร?";
				next;
				mes "[กัตติ]";
				mes "อืม... ชอบแบบนี้เหรอ?";
				next;
				mes "[กัตติ]";
				mes "หรือแบบนั้น? ใช่ทางนี้แหละ!";
				next;
				mes "- อาจารย์สมิธ กัตติกำลังเพลิดเพลินกับการประมวลผลหนังที่คุณนำมาทำอาวุธ";
				mes "- คุณควรจะรอสักนิด -";
				set dew_weapon,5;
				close;
		} else if (dew_weapon == 5) {
				mes "-"+strcharinfo(0)+"-";
				mes "เอ่อ... กัตติเหรอ?";
				next;
				mes "[กัตติ]";
				mes "โอ้ ขอโทษ ขอโทษ หนังเรื่องนี้มันน่าสนใจจริงๆ";
				next;
				mes "[กัตติ]";
				mes "ฉันทำด้ามดาบจากหนังที่คุณนำมาให้ฉัน";
				next;
				mes "[กัตติ]";
				mes "-";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "ด้ามจับ?";
				next;
				mes "[กัตติ]";
				mes "คุณไม่รู้จักด้ามจับเหรอ? นั่นแหละที่เรียกว่าด้ามจับของอาวุธ";
				next;
				mes "- กัตติโชว์สิ่งที่ดูเหมือนด้ามจับอาวุธ -";
				next;
				mes "[กัตติ]";
				mes "ฉันพยายามทำให้มันเหมือนที่เขียนไว้ในพิมพ์เขียว แต่ฉันต้องการวัสดุเพิ่มเติม";
				next;
				mes "-"+strcharinfo(0)+"-";
				mes "จริงหรือ";
				next;
				mes "[กัตติ]";
				mes "-";
				next;
				mes "[กัตติ]";
				mes "ใช่แล้ว... ถ้าจะพูดตรงๆ กับคุณ ฉันทำลายหลายสิ่งหลายอย่างไปด้วยการลองทำหลายอย่าง";
				next;
				mes "[กัตติ]";
				mes "ฉันรู้ว่านี่เป็นการขอมากเกินไป แต่คุณช่วยหาหนัง Comodo อีก 100 ชิ้น, เหล็ก 20 ชิ้น และ Oridecon 10 ชิ้นให้ฉันได้ไหม?";
				next;
				mes "[กัตติ]";
				mes "ฉันจะทำให้คุณเป็นอาวุธที่ดีที่สุด";
				setquest 5054;
				set dew_weapon,6;
				getitem 6404,1; //Caress_H
				next;
				mes "[กัตติ]";
				mes "อ๋อ! ฉันเกือบลืมไป อย่าลืมเอาด้ามที่ฉันเพิ่งทำมาให้คุณด้วยนะ";
				next;
				mes "[กัตติ]";
				mes "อาวุธจะต้องมีด้ามจับ";
				close;
		} else if (dew_weapon == 6) {
				disable_items;
				mes "[กัตติ]";
				mes "คุณนำวัสดุมาครบแล้วหรือยัง?";
				mes "คุณก็คงอยากรู้เกี่ยวกับอาวุธที่เสร็จแล้วเหมือนกันสินะ ฮ่าๆ";
				next;
				mes "[กัตติ]";
				mes "-";
				next;
				if (countitem(6403) < 100 || countitem(999) < 20 || countitem(984) < 10 || countitem(6404) < 1) {
						mes "[กัตติ]";
						mes "อืม... ฉันต้องการมันเพิ่มอีก ถ้าเรือของฉันไม่พัง ฉันคงไปเองได้ แต่...";
						next;
						mes "- กัตติพึมพำกับตัวเอง -";
						next;
						mes "[กัตติ]";
						mes "หนัง Comodo 100 ชิ้น, เหล็ก 20 ชิ้น และ Oridecon 10 ชิ้น คือสิ่งที่ฉันต้องการ";
						next;
						mes "[กัตติ]";
						mes "หากคุณนำพวกมันมาทั้งหมด ฉันจะสร้างอาวุธที่ดีที่สุดเท่าที่เคยมีมา";
						close;
				}
				mes "[กัตติ]";
				mes "เยี่ยมเลย เรามีพอแล้ว รออีกนิด";
				next;
				specialeffect2 EF_REPAIRWEAPON;
				next;
				emotion ET_PROFUSELY_SWEAT;
				next;
				mes "[กัตติ]";
				mes "ฉันทำอาวุธเสร็จแล้ว เรียกว่า Caress ตามแบบแปลน";
				next;
				mes "[กัตติ]";
				mes "ฉันค้นพบสิ่งใหม่ๆ อยู่เสมอเมื่อฉันได้เห็นพิมพ์เขียวนี้";
				next;
				mes "[กัตติ]";
				mes "ตอนนี้ฉันได้เรียนรู้วิธีทำอาวุธแล้ว ถามฉันได้ตลอดเวลาหากคุณต้องการ";
				erasequest 5054;
				set dew_weapon,7;
				delitem 6403,100; //Comodo_L
				delitem 999,20; //Steel
				delitem 984,10; //Oridecon
				delitem 6404,1; //Caress_H
				getitem 13067,1; //Caress
				close;
		} else if (dew_weapon >= 7) {
				mes "[กัตติ]";
				mes "คุณเป็น"+strcharinfo(0)+"-";
				mes "แล้วความห่วงใยที่ฉันทำให้เธอเป็นอย่างไรบ้าง?";
				switch(select("It's great!:I need another Caress.:Can I make it stronger?")) {
				case 1:
						mes "[กัตติ]";
						mes "ใช่ครับ แน่นอนครับมันเยี่ยมมาก";
						mes "ฉันสร้างอาวุธนั้นขึ้นมาแล้วมันน่าจะดี";
						close;
				case 2:
						disable_items;
						if (countitem(6404)) {
								if (countitem(6403) < 70 || countitem(984) < 10 || countitem(999) < 20 || countitem(6404) < 1) {
										mes "[กัตติ]";
										mes "คุณมีแค่ด้ามดาบเท่านั้น คุณต้องนำ Oridecon 10 อัน, Steel 20 อัน และ Comodo Leather 70 อัน มาให้ฉัน";
										next;
										mes "[กัตติ]";
										mes "ถ้าเอามาให้ผมจะได้ทำ Caress ใหม่ให้เลย";
										close;
								}
								mes "[กัตติ]";
								mes "คุณมีวัสดุทั้งหมดแล้ว รอสักครู่";
								next;
								specialeffect2 EF_REPAIRWEAPON;
								next;
								emotion ET_PROFUSELY_SWEAT;
								next;
								mes "[กัตติ]";
								mes "นี่คือความห่วงใย ถามฉันได้ตลอดเวลา";
								delitem 6403,70; //Comodo_L
								delitem 999,20; //Steel
								delitem 984,10; //Oridecon
								delitem 6404,1; //Caress_H
								getitem 13067,1; //Caress
								close;
						} else {
								if (countitem(6403) < 50 || countitem(7201) < 1) {
										mes "[กัตติ]";
										mes "คุณอยากให้ฉันทำ Caress ให้คุณอีกอันไหม ถ้าอย่างนั้น ฉันต้องการด้ามดาบ หนัง Comodo 50 ชิ้น และไม้ซุง 1 ชิ้น นี่คือสิ่งที่ฉันต้องการเพื่อทำด้ามดาบ";
										close;
								}
								mes "[กัตติ]";
								mes "คุณนำวัสดุสำหรับด้ามมา ฉันจะทำมันทันที";
								next;
								mes "[กัตติ]";
								mes "เอาอันนี้มาวางแล้วก็ทำแบบนี้...";
								next;
								mes "[กัตติ]";
								mes "นี่เลย ฉันจะได้ Caress ถ้าคุณนำ Oridecon 10 อัน, Steel 20 อัน และ Comodo Leather 70 อันมาให้ฉัน";
								delitem 6403,50; //Comodo_L
								delitem 7201,1; //Log
								getitem 6404,1; //Caress_H
								close;
						}
				case 3:
						mes "[กัตติ]";
						mes "คุณต้องการเสริมกำลังอาวุธของคุณหรือไม่ อาวุธอาจเสียหายได้เป็นระยะๆ ในระหว่างกระบวนการ แต่ไม่ได้เกิดขึ้นบ่อยนัก";
						if(select("Please reinforce the weapon.:May be next time.") == 2) {
								mes "[กัตติ]";
								mes "เมื่อไหร่ก็ตามที่คุณต้องการเสริมความแข็งแกร่งให้กับความห่วงใย เพียงแค่นำความห่วงใยและเงิน 100,000 เซนีมาให้ฉัน";
								close;
						}
						mes "[กัตติ]";
						mes "ถ้าหากคุณต้องการทำ ฉันจะต้องบอกคุณบางสิ่งที่ควรระวังก่อนที่จะเสริมความห่วงใย";
						next;
						mes "[กัตติ]";
						mes "^FF0000ก่อนอื่นเลย!^009977 ฉันต้องการ 1 Caress และ 100,000 Zeny เพื่อเสริมความแข็งแกร่งให้กับ Caress อย่างที่คุณรู้ ตอนนี้ฉันไม่มีเงินเลยเพราะเรือของฉันพัง^000000";
						next;
						mes "[กัตติ]";
						mes "^FF0000ที่สอง!^009977 ถ้าคุณมี Caress ที่ได้รับการขัดเกลาหรือแทรกด้วยการ์ดในคลังสินค้าของคุณซึ่งคุณไม่ต้องการเสริมกำลัง มันก็อาจถูกทำลายหรือการอัปเกรดของมันสามารถเปลี่ยนแปลงได้ระหว่างกระบวนการเสริมกำลัง";
						next;
						mes "[กัตติ]";
						mes "หากคุณไม่อยากให้สิ่งนั้นเกิดขึ้น จงวางการเอาใจใส่อันประณีตไว้ที่อื่นก่อนการเสริมกำลัง^000000";
						next;
						mes "[กัตติ]";
						mes "^FF0000ที่สาม!^009977 บางครั้งการเสริมกำลังก็ล้มเหลว อาวุธจะไม่ถูกทำลายทุกครั้งที่คุณล้มเหลว อย่างไรก็ตาม คุณควรทราบว่าอาวุธจะถูกทำลายเป็นครั้งคราวหากคุณล้มเหลว^000000";
						next;
						if(select("Please reinforce the weapon.:Maybe next time.") == 2) {
								mes "[กัตติ]";
								mes "เมื่อไหร่ก็ตามที่คุณต้องการเสริมความแข็งแกร่งให้กับความห่วงใย เพียงแค่นำความห่วงใยและเงิน 100,000 เซนีมาให้ฉัน";
								close;
						}
						break;
				}

				if (countitem(13067) < 1 || Zeny < 100000) {
						mes "[กัตติ]";
						mes "คุณไม่มีวัสดุทั้งหมด เป็นไปไม่ได้ที่จะเสริมความแข็งแกร่งหากคุณไม่มีทั้งหมด หากคุณต้องการเสริมความแข็งแกร่ง ให้เตรียมเงิน 100,000 เซนีและ 1 Caress";
						close;
				}
				specialeffect2 EF_REPAIRWEAPON;
				progressbar "0xFFFF00",3;
				set Zeny, Zeny - 100000;

				// Socket 3
				set .@i, rand(4300);
		     		     if (.@i < 150)  set .@s3,4700; // STR + 1
				else if (.@i < 300)  set .@s3,4730; // AGI + 1
				else if (.@i < 450)  set .@s3,4740; // VIT + 1
				else if (.@i < 600)  set .@s3,4710; // INT + 1
				else if (.@i < 750)  set .@s3,4720; // DEX + 1
				else if (.@i < 900)  set .@s3,4750; // LUK + 1
				else if (.@i < 1050) set .@s3,4761; // MATK + 2%
				else if (.@i < 1200) set .@s3,4764; // CRIT + 5
				else if (.@i < 1350) set .@s3,4786; // MDEF + 2
				else if (.@i < 1500) set .@s3,4791; // DEF + 3
				else if (.@i < 1650) set .@s3,4795; // HP + 100
				else if (.@i < 1750) set .@s3,4800; // SP + 50
				else if (.@i < 1850) set .@s3,4797; // HP + 300
				else if (.@i < 1950) set .@s3,4793; // DEF + 9
				else if (.@i < 2050) set .@s3,4788; // MDEF + 6
				else if (.@i < 2150) set .@s3,4752; // LUK + 3
				else if (.@i < 2250) set .@s3,4722; // DEX + 3
				else if (.@i < 2350) set .@s3,4712; // INT + 3
				else if (.@i < 2450) set .@s3,4742; // VIT + 3
				else if (.@i < 2550) set .@s3,4732; // AGI + 3
				else if (.@i < 2650) set .@s3,4702; // STR + 3
				else if (.@i < 2700) set .@s3,4703; // STR + 4
				else if (.@i < 2750) set .@s3,4733; // AGI + 4
				else if (.@i < 2800) set .@s3,4743; // VIT + 4
				else if (.@i < 2850) set .@s3,4713; // INT + 4
				else if (.@i < 2900) set .@s3,4723; // DEX + 4
				else if (.@i < 2950) set .@s3,4753; // LUK + 4
				else if (.@i < 3000) set .@s3,4789; // MDEF + 8
				else if (.@i < 3001) set .@s3,4704; // STR + 5
				else if (.@i < 3002) set .@s3,4734; // AGI + 5
				else if (.@i < 3003) set .@s3,4744; // VIT + 5
				else if (.@i < 3004) set .@s3,4714; // INT + 5
				else if (.@i < 3005) set .@s3,4724; // DEX + 5
				else if (.@i < 3006) set .@s3,4754; // LUK + 5
				else if (.@i < 3007) set .@s3,4806; // MATK + 3%
				else if (.@i < 3008) set .@s3,4765; // CRIT + 7
				else if (.@i < 3009) set .@s3,4766; // ATK + 2%
				else if (.@i < 3010) set .@s3,4801; // SP + 100
				else if (.@i < 3140) set .@s3,4796; // HP + 200
				else if (.@i < 3270) set .@s3,4792; // DEF + 6
				else if (.@i < 3400) set .@s3,4787; // MDEF + 4
				else if (.@i < 3530) set .@s3,4751; // LUK + 2
				else if (.@i < 3660) set .@s3,4721; // DEX + 2
				else if (.@i < 3790) set .@s3,4711; // INT + 1
				else if (.@i < 3920) set .@s3,4741; // VIT + 2
				else if (.@i < 4050) set .@s3,4731; // AGI + 2
				else if (.@i < 4180) set .@s3,4701; // STR + 2
				else set .@s3,0;

				// Socket 4
				set .@j, rand(4100);
		     		     if (.@j < 130)  set .@s4,4700; // STR + 1
				else if (.@j < 260)  set .@s4,4730; // AGI + 1
				else if (.@j < 390)  set .@s4,4740; // VIT + 1
				else if (.@j < 520)  set .@s4,4710; // INT + 1
				else if (.@j < 650)  set .@s4,4720; // DEX + 1
				else if (.@j < 780)  set .@s4,4750; // LUK + 1
				else if (.@j < 910)  set .@s4,4760; // MATK + 1%
				else if (.@j < 1040) set .@s4,4764; // CRIT + 5
				else if (.@j < 1170) set .@s4,4766; // ATK + 2%
				else if (.@j < 1300) set .@s4,4786; // MDEF + 2
				else if (.@j < 1430) set .@s4,4791; // DEF + 3
				else if (.@j < 1560) set .@s4,4795; // HP + 100
				else if (.@j < 1690) set .@s4,4800; // SP + 50
				else if (.@j < 1790) set .@s4,4796; // HP + 200
				else if (.@j < 1890) set .@s4,4792; // DEF + 6
				else if (.@j < 1990) set .@s4,4787; // MDEF + 4
				else if (.@j < 2090) set .@s4,4767; // ATK + 3%
				else if (.@j < 2190) set .@s4,4765; // CRIT + 7
				else if (.@j < 2290) set .@s4,4751; // LUK + 2
				else if (.@j < 2390) set .@s4,4721; // DEX + 2
				else if (.@j < 2490) set .@s4,4711; // INT + 2
				else if (.@j < 2590) set .@s4,4741; // VIT + 2
				else if (.@j < 2690) set .@s4,4731; // AGI + 2
				else if (.@j < 2790) set .@s4,4701; // STR + 2
				else if (.@j < 2860) set .@s4,4702; // STR + 3
				else if (.@j < 2930) set .@s4,4732; // AGI + 3
				else if (.@j < 3000) set .@s4,4742; // VIT + 3
				else if (.@j < 3140) set .@s4,4712; // INT + 3
				else if (.@j < 3210) set .@s4,4722; // DEX + 3
				else if (.@j < 3280) set .@s4,4788; // MDEF + 8
				else if (.@j < 3350) set .@s4,4793; // DEF + 9
				else if (.@j < 3420) set .@s4,4797; // HP + 300
				else if (.@j < 3490) set .@s4,4801; // SP + 100
				else if (.@j < 3540) set .@s4,4798; // HP + 400
				else if (.@j < 3590) set .@s4,4789; // MDEF + 8
				else if (.@j < 3640) set .@s4,4753; // LUK + 4
				else if (.@j < 3690) set .@s4,4723; // DEX + 4
				else if (.@j < 3740) set .@s4,4713; // INT + 4
				else if (.@j < 3790) set .@s4,4743; // VIT + 4
				else if (.@j < 3840) set .@s4,4733; // AGI + 4
				else if (.@j < 3890) set .@s4,4703; // STR + 4
				else if (.@j < 3900) set .@s4,4704; // STR + 5
				else if (.@j < 3910) set .@s4,4734; // AGI + 5
				else if (.@j < 3920) set .@s4,4744; // VIT + 5
				else if (.@j < 3930) set .@s4,4714; // INT + 5
				else if (.@j < 3940) set .@s4,4724; // DEX + 5
				else if (.@j < 3950) set .@s4,4754; // LUK + 5
				else set .@s4,0;

				delitem 13067,1; //Caress
				if (.@s3 == 0 && .@s4 == 0) {
						mes "[กัตติ]";
						mes "โอ้โห~~ เกือบจะสำเร็จแล้ว แต่ก็ล้มเหลว ขอโทษ ขอโทษ...";
						emotion ET_HUK;
						close;
				}
				getitem2 13067,1,1,0,0,0,0,.@s3,.@s4;
				mes "[กัตติ]";
				mes "การเสริมความห่วงใยประสบความสำเร็จ พบกันใหม่";
				close;
		}
}
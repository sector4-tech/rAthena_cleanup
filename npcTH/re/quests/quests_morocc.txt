//===== rAthena Script =======================================
//= Morocc Quest NPCs
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quest NPCs related to Morocc.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.0a Gathered remaining dialogues. [Euphy]
//= 1.1 Added new warpers for Satan Morocc maps. [Euphy]
//============================================================

// Stop Post Quest
// ============================================================
morocc,152,167,4	duplicate(William)	William#moc	89

// Binoculars Quest
// ============================================================
morocc,169,168,4	duplicate(Alchemist)	Alchemist#moc	64

// Resurrection of Satan Morocc
// ============================================================
-	script	Continental Guard#man	-1,{
		mes "[กองกำลังรักษาแผ่นดิน]";
		mes "เราได้รับคำสั่งจากสำนักงานใหญ่ให้ปิดกั้นการเข้าถึงพื้นที่นี้ เนื่องจากตรวจพบปรากฏการณ์เวลาและอวกาศที่ผิดปกติ";
		next;
		mes "[กองกำลังรักษาแผ่นดิน]";
		mes "หากอยากไปพื้นที่ถัดไปจริง ๆ ก็ต้องรับผิดชอบต่อความปลอดภัยของตัวเอง";
		next;
		switch(select("Nevermind.:Enter the Field.")) {
		case 1:
				mes "[กองกำลังรักษาแผ่นดิน]";
				mes "การเลือกที่ชาญฉลาด";
				mes "เดินทางปลอดภัยนะครับ.";
				close;
		case 2:
				mes "[กองกำลังรักษาแผ่นดิน]";
				mes "ฉันเห็น.";
				mes "ฉันจะส่งคุณไปที่สนามเร็วๆ นี้";
				close2;
				warp "moc_fild20",208,207;
				end;
		}
}
moc_fild07,380,202,1	duplicate(Continental Guard#man)	Continental Guard#man1	852
moc_fild11,189,360,6	duplicate(Continental Guard#man)	Continental Guard#man2	852
prt_fild10,263,23,1	duplicate(Continental Guard#man)	Continental Guard#man3	852
prt_fild09,95,19,1	duplicate(Continental Guard#man)	Continental Guard#man4	852
prt_fild09,246,17,1	duplicate(Continental Guard#man)	Continental Guard#man5	852
moc_fild13,32,171,6	duplicate(Continental Guard#man)	Continental Guard#man6	852
moc_fild16,124,381,4	duplicate(Continental Guard#man)	Continental Guard#man7	852
moc_fild16,333,380,4	duplicate(Continental Guard#man)	Continental Guard#man8	852
moc_fild01,84,19,1	duplicate(Continental Guard#man)	Continental Guard#man9	852

morocc,176,101,3	duplicate(Continental Official)	Continental Official#mor	754

// Pyramids Nightmare Mode :: nightmare_pry
//============================================================
moc_prydb1,103,54,3	script	Suspicious Cat#night	547,{
		mes "[แมวน่าสงสัย]";
		mes "ฉันไม่เชื่อว่าแม่จะทำแบบนั้นกับฉันได้เลย มันเจ็บมาก ไม่มีใครควรรู้เรื่องนี้หรอก...";
		next;
		if(select("Mummy? What happened?:Pretend not to know anything.") == 2)
				close;
		mes "[แมวน่าสงสัย]";
		mes "คุณจับฉันได้แล้ว! บ้าเอ๊ย...ฉันไม่คาดคิดมาก่อนว่าชื่อเสียงของฉันในฐานะผู้ปล้นสุสานจะเสียหายแบบนี้...";
		next;
		mes "[แมวน่าสงสัย]";
		mes "แต่แปลกจริงๆ นะ มันคือมัมมี่ต่างหาก มัมมี่ธรรมดาๆ น่ะรู้ไหม มันไม่ควรจะแข็งแรงขนาดนั้น";
		next;
		if(select("Strong mummies? Where are they?:End the conversation.") == 2) {
				mes "[แมวน่าสงสัย]";
				mes "อะไรนะ~ เธอไม่อยากฟังเรื่องของฉันเหรอ? ก็ได้! เธออย่าเอ่ยชื่อฉันที่ไหนอีกดีกว่า!";
				close;
		}
		mes "[แมวน่าสงสัย]";
		mes "ฮ่าๆ มันเป็นความลับ";
		mes "ผมเพิ่งรู้เรื่องนี้ตอนกำลังขโมย... ไม่ใช่ๆ ผมหมายถึงแค่เดินผ่านพีระมิดน่ะ";
		next;
		mes "[แมวน่าสงสัย]";
		mes "เมื่อฉันเห็นแม่เดินมาหาฉัน ฉันก็แบบว่า 'อะไรก็ได้' และไม่สนใจ แต่แล้วเขาก็ตบหน้าฉันจนล้ม! เขาทำแบบนั้นกับแมวน่ารักอย่างฉันได้ยังไง! คุณพันผ้าพันแผลประหลาด... คอยดูสิ!";
		next;
		if(select("I would like to go, too.:End the conversation.") == 2)
				close;
		mes "[แมวน่าสงสัย]";
		mes "ไม่มีทาง! ฉันเสียเวลามากมายในการค้นหาเส้นทางนั้น!!";
		next;
		mes "[แมวน่าสงสัย]";
		mes "อืม...แต่ถ้าคุณยืนกรานก็ให้ฉัน 5,000 เซนีก็พอ ฉันจะนำทางคุณเอง";
		mes "อะไรนะ~ อย่ามองฉันแบบนั้นสิ! คุณรู้ไหมว่าแมวเอาชีวิตรอดในมิดการ์ดได้ยากขนาดไหน!";
		next;
		mes "[แมวน่าสงสัย]";
		mes "^FF0000แต่คิดดูดีๆ คนอ่อนแออย่างเธอคงไปไม่รอดหรอก ถ้าโชคดีก็อาจจะรอดไปได้สักครั้ง^000000";
		mes "ไม่ใช่เรื่องของฉันหรอก แต่ฉันเตือนคุณแล้วนะ!";
		next;
		switch(select("Pay 5,000 zeny and set out.:Forget it.")) {
		case 1:
				if (Zeny < 5000) {
						mes "[แมวน่าสงสัย]";
						mes "คิดว่าจะไปไหนเนี่ย! หมดตัวซะแล้ว!";
						close;
				}
				set Zeny, Zeny-5000;
				mes "[แมวน่าสงสัย]";
				mes "บ้าเอ้ย เงินเก่าๆ นี่มันเป็นไรวะ... ไม่มีประกายแวววาวหรืออะไรเลยเหรอ?!";
				mes "อ่าาา แค่ตามฉันมา!";
				close2;
				warp "moc_prydn1",93,96;
				end;
		case 2:
				mes "[แมวน่าสงสัย]";
				mes "โอ้โห คุณไม่มีแม้แต่ 5,000 Zeny เลยเหรอ?";
				close;
		}
}

moc_prydn1,94,98,3	script	Suspicious Cat#night2	547,{
		mes "[แมวน่าสงสัย]";
		mes "แม่งเอ๊ยตบกูอีกแล้วทั้งๆที่มึงหันหน้าหนี ฮึ่ย ฮึ่ย...กลับไปซะ เหมียว!";
		next;
		switch(select("Go back to Thief Guild:Eliminate Verit:Eliminate Ancient Mummy:It's nothing")) {
		case 1:
				mes "[แมวน่าสงสัย]";
				mes "เออ เออ...กลับไปกันเถอะ.";
				close2;
				warp "moc_prydb1",100,57;
				end;
		case 2:
				set .@playtime, checkquest(2290,PLAYTIME);
				if (.@playtime == 0 || .@playtime == 1) {
						mes "[แมวน่าสงสัย]";
						mes "ดูสิเวริต เขากลัวพวกเรา! ฮ่าฮ่าฮ่า! พวกเราปลอดภัยดี อย่างน้อยก็สักพักหนึ่ง!";
						close;
				} else if (.@playtime == 2)
						erasequest 2290;
				set .@hunting, checkquest(2289,HUNTING);
				if (.@hunting == -1) {
						mes "[แมวน่าสงสัย]";
						mes "คุณรู้ไหมว่าแมวกับสุนัขไม่ถูกกัน?";
						next;
						mes "[แมวน่าสงสัย]";
						mes "ดูสิเวริตที่น่าเกลียดน่ากลัวตัวนั้น มันทำหน้าบูดบึ้ง กระดิกหาง และพยายามเข้ามาใกล้";
						mes "อ๋อ คุณไม่มีหางใช่มั้ย เขาคิดว่าฉันเป็นหนามยอกอกเขา และตอนนี้เขากำลังพยายามหาเรื่องทะเลาะอยู่!";
						next;
						mes "[แมวน่าสงสัย]";
						mes "ดูเขาสิ โลภมาก น้ำลายไหล... ฉันก็ไม่ชอบการหายใจของเขาเหมือนกัน ฉันทนไม่ได้ที่เขาส่งเสียงกินจุบจิบแบบนั้น...";
						mes "มันชัดเจนมากว่าเขากำลังรอที่จะโจมตีฉันจากด้านหลัง";
						next;
						mes "[แมวน่าสงสัย]";
						mes "โอ้โห ถ้าไม่ใช่เพราะปวดท้อง ฉันคงข่วนหน้าเขาแรงขนาดนั้นได้เลยนะ!";
						mes "เฮ้ยไม่ใช่เพราะกลัวมาโจรูรอสนะ";
						next;
						mes "[แมวน่าสงสัย]";
						mes "ถ้าจะกลับบ้านก็ต้องตีเจ้าหมาขี้เหร่ตัวนั้นซะ ไม่งั้นมันจะกัดคุณ!";
						next;
						switch(select("Help him:Stay away")) {
						case 1:
								mes "[แมวน่าสงสัย]";
								mes "ดีเลย เป้าหมายคือ 20 โชคดีนะ!";
								setquest 2289;
								close;
						case 2:
								mes "[แมวน่าสงสัย]";
								mes "เหอะ! แกไม่สนใจฉันเลยรึไง? ได้สิไอ้คนทรยศ!";
								close;
						}
				} else if (.@hunting == 0 || .@hunting == 1) {
						mes "[แมวน่าสงสัย]";
						mes "การฝึกของ Verit ดำเนินไปด้วยดีหรือเปล่า?";
						next;
						switch(select("Sure.:I want to stop.")) {
						case 1:
								mes "[แมวน่าสงสัย]";
								mes "ดีมากครับ ทำดีต่อไปนะครับ!";
								close;
						case 2:
								mes "[แมวน่าสงสัย]";
								mes "อะไรนะ ไร้ความรับผิดชอบจัง!";
								erasequest 2289;
								close;
						}
				} else if (.@hunting == 2) {
						mes "[แมวน่าสงสัย]";
						mes "เฮ้ คุณมีประโยชน์จริงๆ นะ ทำได้ดีมาก!";
						mes "Verit คงไม่กล้ามาที่นี่สักพักใช่มั้ย หึๆ";
						changequest 2289,2290;
						getexp 300000,100000;
						close;
				}
		case 3:
				set .@playtime, checkquest(2291,PLAYTIME);
				if (.@playtime == 0 || .@playtime == 1) {
						mes "[แมวน่าสงสัย]";
						mes "เอาล่ะ ฉันจะเอาเวลาที่เธอได้มาใช้ประโยชน์ให้คุ้มค่า! แต่ก่อนอื่นฉันต้องทำอะไรสักอย่างเกี่ยวกับอาการปวดท้องนี้ก่อน.....";
						close;
				} else if (.@playtime == 2)
						erasequest 2291;
				set .@hunting, checkquest(2292,HUNTING);
				if (.@hunting == -1) {
						mes "[แมวน่าสงสัย]";
						mes "สมบัติอันล้ำค่าควรจะถูกซ่อนอยู่ในสถานที่ลับลึกที่นี่!";
						mes "ฉันแน่ใจว่าสมบัติล้ำค่าของมัมมี่ถูกซ่อนอยู่ในชั้นใต้ดินที่สอง";
						next;
						mes "[แมวน่าสงสัย]";
						mes "แต่แล้วมัมมี่โบราณก็เข้ามาจับฉันโดยไม่ทันตั้งตัวและฟาดหัวฉันก่อนที่ฉันจะรู้ตัว!";
						next;
						mes "[แมวน่าสงสัย]";
						mes "ฉันมั่นใจว่าพวกเขากำลังพยายามหยุดฉันจากการค้นหาสมบัติโดยการลดจำนวนเซลล์สมองอันเหนือกว่าของฉัน!!";
						next;
						mes "[แมวน่าสงสัย]";
						mes "พวกมันหยุดฉันไม่ได้แล้ว! ไปกำจัดมัมมี่โบราณกันเถอะ!!";
						next;
						mes "[แมวน่าสงสัย]";
						mes "อย่าถามว่าทำไม! ฉันแน่ใจว่าคุณคงไม่อยากให้สมบัติอันสวยงามเหล่านั้นถูกซ่อนอยู่ในความมืดเช่นกัน!";
						next;
						switch(select("Help him:Stay away")) {
						case 1:
								mes "[แมวน่าสงสัย]";
								mes "เอาล่ะ เป้าหมายคือ 20 ค่ะ โชคดีนะคะ!";
								setquest 2292;
								close;
						case 2:
								mes "[แมวน่าสงสัย]";
								mes "บ้าเอ๊ย คุณนี่เย็นชาจริงๆ";
								close;
						}
				} else if (.@hunting == 0 || .@hunting == 1) {
						mes "[แมวน่าสงสัย]";
						mes "การต่อสู้กับมัมมี่โบราณเป็นไปด้วยดีหรือไม่?";
						next;
						switch(select("Sure.:I want to stop.")) {
						case 1:
								mes "[แมวน่าสงสัย]";
								mes "ดีมากครับ ทำดีต่อไปนะครับ!";
								close;
						case 2:
								mes "[แมวน่าสงสัย]";
								mes "อะไรนะ ไร้ความรับผิดชอบจัง!";
								erasequest 2292;
								close;
						}
				} else if (.@hunting == 2) {
						mes "[แมวน่าสงสัย]";
						mes "เฮ้ คุณมีประโยชน์จริงๆ นะ ทำได้ดีมาก!";
						mes "มัมมี่โบราณนั่นคงไม่กล้ามาที่นี่สักพักหรอกใช่มั้ย หึๆ หึๆ!";
						changequest 2292,2291;
						getexp 600000,200000;
						close;
				}
		case 4:
				close;
		}
}
//===== rAthena Script =======================================
//= NPCs related to Glastheim (Nightmare)
//===== By: ==================================================
//= Ridley8819
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Additional Comments: =================================
//= 1.0 Initial release [Ridley8819]
//= 1.1 Dialogue cleanup [Aleos]
//============================================================

glast_01,195,273,3	script	Hugin's follower#1	2_F_MAGICMASTER,{
		mes "[ผู้ติดตามฮูกิน]";
		mes "ฉันไม่สามารถเคลื่อนที่ไปมาระหว่างเวลาและมิติได้เหมือนกับอาจารย์ฮูกิน แต่ฉันสามารถแกล้งทำเป็นทำได้";
		next;
		mes "[ผู้ติดตามฮูกิน]";
		mes "อะไรทำให้ Glast Heim ถูกพลังแห่งความมืดอันทรงพลังครอบงำเช่นนี้?";
		next;
		if( select( "I don't care:That is interesting, please tell me more" ) == 2 ){
				mes "[ผู้ติดตามฮูกิน]";
				mes "ด้วยความขอบคุณต่อ Hugin ฉันจึงพบวิธีเข้าถึง Glast Heim เก่าแห่งนี้ซึ่งเต็มไปด้วยพลังแห่งความมืดอันทรงพลัง";
				next;
				mes "[ผู้ติดตามฮูกิน]";
				mes "ว่าไงครับ อยากไปเที่ยวที่นั่นมั้ย?";
				next;
				if( select( "No exploration for me:I really want to explore!" ) == 2 ){
						mes "[ผู้ติดตามฮูกิน]";
						mes "แน่นอน แต่ว่านี่ไม่ใช่บริการฟรีนะ! ฉันต้องได้สิ่งตอบแทนบ้าง ฮ่าๆ...";
						next;
						mes "[ผู้ติดตามฮูกิน]";
						mes "ฉันส่งคุณได้สองที่ คุณอยากให้ฉันส่งคุณไปที่ไหนดี";
						next;
						switch( select( "Cancel:Monastery Cemetery (Lv100~130) - 5000z:2nd floor Corridor (Over Lv140) - 10000z" ) ){
								case 1:
										mes "[ผู้ติดตามฮูกิน]";
										mes "เอาล่ะ โปรดกลับมาใหม่เมื่อคุณเปลี่ยนใจ ฉันคิดว่าคุณคงมีเรื่องมากมายที่จะคุยกับฉัน...";
										close;
								case 2:
										.@map$ = "gl_chyard_";
										.@x = 148;
										.@y = 144;
										.@zeny = 5000;
										break;
								case 3:
										.@map$ = "gl_cas02_";
										.@x = 104;
										.@y = 31;
										.@zeny = 10000;
										break;
						}
						if( Zeny < .@zeny ){
								mes "[ผู้ติดตามฮูกิน]";
								mes "อะไรนะ? คุณคาดหวังว่าบริการนี้จะฟรีเหรอ?";
								close;
						}
						Zeny -= .@zeny;
						warp .@map$, .@x, .@y;
						end;
				}
		}
		mes "[ผู้ติดตามฮูกิน]";
		mes "ถ้าคุณไม่สนใจก็ไม่เป็นไร";
		close;
}

gl_chyard_,148,149,4	script	Hugin's Follower#n_gh1	123,{
		mes "[ผู้ติดตามฮูกิน]";
		mes "คุณต้องการที่จะกลับมาใช่ไหม?";
		mes "โอเค เราออกไปจากที่นี่กันเถอะ";
		next;
		if( select( "I didn't mean to talk to you:Please get me out of here" ) == 1 ){
				mes "[ผู้ติดตามฮูกิน]";
				mes "ตกลง บอกฉันเมื่อไหร่ก็ได้ที่คุณอยากจะไป";
				close;
		}
		warp "glast_01", 210, 269;
		end;
}

gl_cas02_,104,36,4	duplicate(Hugin's Follower#n_gh1)	Hugin's Follower#n_gh2	123
//===== rAthena Script =======================================
//= Kagerou/Oboro Equipment Quests
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quests for Kagerou/Oboro weapons and armor.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Little clean-up. [Capuche]
//============================================================

// Armor and Weapon NPCs :: kaboro_gearmix
//============================================================
que_ng,21,72,6	script	Master Armor Craftsman	4_M_RASWORD,{
		mes "[เกียว]";
		mes "คุณจะนิยามชัยชนะว่าอย่างไร?";
		mes "เพื่อล้มศัตรู...";
		mes "อย่างไรก็ตามสิ่งที่สำคัญที่สุดก็คือ...";
		mes "อย่าล้มตัวเองเด็ดขาด!";
		next;
		mes "[เกียว]";
		mes "ดังนั้นคุณต้องปกป้องตัวเองให้ดีขึ้น";
		mes "คุณไม่สามารถปกป้อง ผู้อื่น ได้ ในขณะที่คุณขัดขวางการปกป้อง ตัวคุณเอง";
		next;
		if(select("Combine armor.:End conversation.") == 2) {
				mes "[เกียว]";
				mes "ความรอบคอบก็เป็นคุณธรรมที่สำคัญในสนามรบเช่นกัน";
				close;
		}
		switch(select("Wolf Armguard[1]:Crescent Armguard[1]:Ninja Scale Armor[1]:Tenebris Latitantes[1]:Quit.")) {
		case 1:
				mes "[เกียว]";
				mes "Wolf Armguard... ชุดเกราะอันโหดร้ายชิ้นหนึ่ง";
				next;
				callsub L_Select,2172;
				if (countitem(2171) > 0 && countitem(6252) > 9) {  //custom translation
						delitem 2171,1; //Fox_Armguard
						delitem 6252,10; //Wolf_Blood
						getitem 2172,1; //Wolf_Armguard
						mes "[เกียว]";
						mes "คุณรู้สึกถึงธรรมชาติของหมาป่าไหม?";
						mes "ทั้งดุร้ายและมีเสน่ห์...";
						close;
				}
				break;
		case 2:
				mes "[เกียว]";
				mes "Crescent Armguard... สิ่งที่สวยงามและแข็งแกร่ง... ที่ไม่น้อยหน้าใคร";
				next;
				callsub L_Select,2173;
				if (countitem(2171) > 0 && countitem(7321) > 99) {  //custom translation
						delitem 2171,1; //Fox_Armguard
						delitem 7321,100; //Fragment_Of_Crystal
						getitem 2173,1; //Crescent_Armguard
						mes "[เกียว]";
						mes "งดงามมาก...";
						mes "ราวกับทั้งร่างกายเต็มไปด้วยความมหัศจรรย์";
						close;
				}
				break;
		case 3:
				mes "[เกียว]";
				mes "เกราะเกล็ดนินจา... ว่ากันว่าครั้งหนึ่งมีนินจาในตำนานเคยสวมมันอยู่";
				next;
				callsub L_Select,15054;
				if (countitem(7451) > 29 && countitem(7562) > 29 && countitem(6091) > 29) {  //custom translation
						delitem 7451,30; //Scale_Of_Red_Dragon
						delitem 7562,30; //Ice_Scale
						delitem 6091,30; //Dark_Red_Scale
						getitem 15054,1; //Ninja_Scale_Armor
						mes "[เกียว]";
						mes "นี่เป็นเกราะในตำนานโดยแท้จริง...";
						mes "แม้ว่าฉันจะไม่เชื่อในเรื่องตำนานก็ตาม...";
						close;
				}
				break;
		case 4:
				mes "[เกียว]";
				mes "เกราะของราชาเงา... มันถูกสร้างขึ้นมาเพื่อปกป้องผู้อื่น";
				next;
				callsub L_Select,15055;
				if (countitem(15056) > 0 && countitem(6089) > 9) {  //custom translation
						delitem 15056,1; //Special_Ninja_Suit_
						delitem 6089,10; //Piece_Of_Darkness
						getitem 15055,1; //Tenebris_Latitantes
						mes "[เกียว]";
						mes "เป็นยังไงบ้าง?";
						mes "เกราะมันไม่ดูเหมือนมีสองอันหรอ?";
						close;
				}
				break;
		case 5:
				close;
		}
		mes "[เกียว]";
		mes "คุณมีส่วนผสมไม่เพียงพอ";
		mes "ดูอีกครั้งอีกครั้ง...";
		close;

L_Select:
		.@string1$ = "Check Ingredients.";
		.@string2$ = "Check Stats.";
		while(1) {
				switch(select(.@string1$,.@string2$,"Combine.","Quit.")) {
				case 1:
						.@string1$ = "";
						callsub L_Ingredients, getarg(0);
						break;
				case 2:
						.@string2$ = "";
						callsub L_Stats, getarg(0);
						break;
				case 3:
						callsub L_Combine;
						return;
				case 4:
						mes "[เกียว]";
						mes "ดูเหมือนว่าคุณจะต้องการเวลาเพิ่มเติมเพื่อการตัดสินใจ";
						close;
				}
		}

L_Ingredients:
		mes "[เกียว]";
		switch (getarg(0)) {
		case 2172:	//Wolf_Armguard
				mes "ในการสร้าง ^ff0000Wolf Armguard คุณต้องมี Fox Armguard 1 ชิ้นและ Blood of Wolf 10 ชิ้น^000000";
				mes "คุณรู้สึกได้ไหม? การเต้นระรัวของเลือดหมาป่า...";
				break;
		case 2173:	//Crescent_Armguard
				mes "ในการสร้าง ^ff0000Crescent Armguard คุณต้องมี Fox Armguard 1 ชิ้นและ Crystal Fragments 100 ชิ้น^000000";
				mes "จะมีการสลักรูปจันทร์เสี้ยวที่งดงามใสสะอาดเหมือนคริสตัลไว้";
				break;
		case 15054:	//Ninja_Scale_Armor
				mes "ในการสร้างชุดเกราะเกล็ดนินจา คุณต้องมีเกล็ดมังกรไฟ 30 ชิ้น เกล็ดน้ำแข็ง 30 ชิ้น และชิ้นส่วนเกล็ดสีแดงเข้ม 30 ชิ้น^000000";
				mes "คุณสามารถสัมผัสถึงพลังอันเป็นตำนานของมันได้";
				break;
		case 15055:	//Tenebris_Latitantes
				mes "ในการสร้างชุดเกราะ Shadow King คุณจะต้องมีชุดนินจาพิเศษพร้อมช่องและชิ้น Dark Pieces จำนวน 10 ชิ้น^000000";
				mes "ความมืดทำให้การดำรงอยู่หายไป...";
				break;
		}
		next;
		return;

L_Stats:
		mes "-" + getitemname(getarg(0)) + "-";
		switch (getarg(0)) {
		case 2172:	//Wolf_Armguard
				mes "มีโอกาสได้รับ ATK+100 เป็นเวลา 5 วินาที, FLEE -50 จากการโจมตีระยะประชิด";
				mes "ประเภทโล่, ป้องกัน 45, ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 100 ประเภทนินจาเท่านั้น";
				break;
		case 2173:	//Crescent_Armguard
				mes "ลดความล่าช้าในการโพสต์ทักษะลง 2% สำหรับทุกการปรับปรุง";
				mes "ประเภทโล่, ป้องกัน 70, ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 100 ประเภทนินจาเท่านั้น";
				break;
		case 15054:	//Ninja_Scale_Armor
				mes "แม็กซ์แรงม้า+15%, แม็กซ์แรงม้า-30%";
				mes "ประเภทเกราะ, ป้องกัน 90, ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 100 ประเภทนินจาเท่านั้น";
				break;
		case 15055:	//Tenebris_Latitantes
				mes "มีโอกาสเปิดใช้งานภาพลวงตาระดับ 1 - เงา เมื่อถูกโจมตีด้วยการโจมตีระยะประชิด";
				mes "ประเภทเกราะ, ป้องกัน 60, ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 100 ประเภทนินจาเท่านั้น";
				break;
		}
		next;
		return;

L_Combine:
		mes "[เกียว]";
		mes "ตรวจสอบให้แน่ใจว่าคุณมีส่วนผสมและอุปกรณ์ที่ถูกต้องอยู่ในมือ^000000";
		mes "อย่ามาร้องไห้ทีหลังเลย";
		next;
		switch(select("Combine.:Quit.")) {
		case 1:
				if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
						mes "- เดี๋ยว!! -";
						mes "- ไม่สามารถรับสินค้าได้-";
						mes "- เพราะคุณพกมันมากเกินไป -";
						mes "- กรุณาลองอีกครั้ง -";
						mes "- หลังจากผ่อนภาระของคุณลงแล้ว -";
						close;
				}
				return;
		case 2:
				mes "[เกียว]";
				mes "ดูเหมือนว่าคุณจะต้องการเวลาเพิ่มเติมเพื่อการตัดสินใจ";
				close;
		}
		end;
}

que_ng,23,70,3	script	Master Weapon Craftsman	4_DST_SOLDIER,{
		mes "[กี้]";
		mes "การโจมตีเป็นวิธีการป้องกันที่ดีที่สุด";
		mes "อาวุธทรงพลังจะทำให้คุณสมบูรณ์แบบได้อย่างแท้จริง";
		next;
		mes "[กี้]";
		mes "แน่นอนว่ามันคงจะดีกว่าถ้ามีอะไรพิเศษอยู่ในมือ";
		next;
		if(select("Combine weapon.:End conversation.") == 2) {
				mes "[กี้]";
				mes "การระมัดระวังมากเกินไปบางครั้งอาจถึงแก่ชีวิตได้";
				close;
		}
		switch(select("Raksasa Dagger[1]:Mikatsuki[1]:Huuma Swirling Petal[2]:Quit.")) {
		case 1:
				mes "[กี้]";
				mes "Raksasa Dagger สามารถใช้ได้หลังผ่านการฝึกฝนอย่างเพียงพอเท่านั้น";
				next;
				callsub L_Select,13076;
				if (countitem(13013) > 0 && countitem(718) > 0) {  //custom translation
						delitem 13013,1; //Murasame_
						delitem 718,1; //Dark_Red_Jewel
						getitem 13076,1; //Raksasa_Dagger
						mes "[กี้]";
						mes "อ่า...";
						mes "ช่างเป็นประกายแวววาวที่งดงามเหลือเกิน...";
						close;
				}
				mes "[กี้]";
				mes "อืม... คุณไม่มีเงินพอที่จะทำ Raksasa Dagger";
				break;
		case 2:
				mes "[กี้]";
				mes "มิคาสึกิ...ส่วนโค้งอันงดงามดุจจันทร์เสี้ยว...";
				mes "มีน้อยคนที่สามารถทำมันได้";
				next;
				callsub L_Select,13078;
				if (countitem(13015) > 0 && countitem(727) > 0) {  //custom translation
						delitem 13015,1; //Hakujin_
						delitem 727,1; //White_Jewel
						getitem 13078,1; //Mikatsuki
						mes "[กี้]";
						mes "งดงามจริงๆครับ...";
						mes "ฉันโชคดีมากที่มีโอกาสได้เห็นมีดที่งดงามเช่นนี้";
						close;
				}
				mes "[กี้]";
				mes "อืม...คุณไม่มีเงินพอจะทำ Mikatsuki ได้";
				break;
		case 3:
				mes "[กี้]";
				mes "กลีบดอกหมุนวน Huuma...คุณเคยเห็นกลีบดอกไม้หมุนวนบ้างไหม?";
				next;
				callsub L_Select,13313;
				if (countitem(13304) > 0 && countitem(7156) > 99) {  //custom translation
						delitem 13304,1; //Huuma_Calm_Mind
						delitem 7156,100; //Broken_Shuriken
						getitem 13313,1; //Huuma_Swirling_Petal
						mes "[กี้]";
						mes "กลีบดอกลอย...ลอย!!";
						close;
				}
				mes "[กี้]";
				mes "อืม... คุณไม่มีเงินพอที่จะทำ Huuma Swirling Petal ได้";
				break;
		case 4:
				close;
		}
		mes "ทำไมไม่ลองตรวจสอบอีกครั้งล่ะ?";
		mes "บางคนก็ไม่สามารถละทิ้งสิ่งที่พวกเขาถือเอาไว้ได้";
		close;

L_Select:
		.@string1$ = "Check Ingredients.";
		.@string2$ = "Check Stats.";
		while(1) {
				switch(select(.@string1$,.@string2$,"Combine.","Quit.")) {
				case 1:
						.@string1$ = "";
						callsub L_Ingredients, getarg(0);
						break;
				case 2:
						.@string2$ = "";
						callsub L_Stats, getarg(0);
						break;
				case 3:
						callsub L_Combine;
						return;
				case 4:
						mes "[กี้]";
						mes "คิดให้ดีก่อน~~";
						close;
				}
		}

L_Ingredients:
		mes "[กี้]";
		switch (getarg(0)) {
		case 13076:	//Raksasa_Dagger
				mes "สำหรับ ^ff0000Raksasa Dagger คุณต้องมี Murasame 1 อันที่มีช่อง 2 ช่องและ Garnet 1 อัน^000000";
				mes "ทับทิมทำให้ Raksasa Dagger มีสีที่เป็นเอกลักษณ์";
				break;
		case 13078:	//Mikatsuki
				mes "สำหรับ ^ff0000Mikatsuki คุณต้องมี Hakujin ที่มีช่องและ Opal^000000";
				mes "โอปอลเป็นส่วนที่มีประกายแสงโปร่งแสง";
				break;
		case 13313:	//Huuma_Swirling_Petal
				mes "สำหรับ ^ff0000Huuma Swirling Petal คุณต้องมี Huuma Calm Mind Shuriken หนึ่งอันและ Broken Shurikens 100 อัน^000000";
				mes "ดูเหมือนว่าจะต้องใช้ดาวกระจายมากเกินไป... แต่มันมีความจำเป็นสำหรับเอฟเฟกต์กระพือปีกอันสวยงาม";
				break;
		}
		next;
		return;

L_Stats:
		mes "-" + getitemname(getarg(0)) + "-";
		switch (getarg(0)) {
		case 13076:	//Raksasa_Dagger
				mes "INT+3 MATK+100.";
				mes "ประเภทมีดสั้น, ATK 120, ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 110 ประเภทนินจาเท่านั้น";
				break;
		case 13078:	//Mikatsuki
				mes "MATK + 120, การใช้การร่ายคาถาและ SP ที่ผันผวนลดลง 5% เมื่อใช้ทักษะ";
				mes "ประเภทมีดสั้น, ATK 50, อาวุธระดับ 4 ช่อง 1";
				mes "จำเป็นต้องมีเลเวล 100 ประเภทนินจาเท่านั้น";
				break;
		case 13313:	//Huuma_Swirling_Petal
				mes "MATK + 50, ความเสียหายจากทักษะ Huuma Swirling Petal เพิ่มขึ้น 20%";
				mes "ประเภทชูริเคน พลังโจมตี 150";
				mes "อาวุธระดับ 3 ช่อง 2";
				mes "จำเป็นต้องมีเลเวล 110 ประเภทนินจาเท่านั้น";
				break;
		}
		next;
		return;

L_Combine:
		mes "[กี้]";
		mes "ตรวจสอบให้แน่ใจว่าคุณมีส่วนผสมและอุปกรณ์ที่ถูกต้องอยู่ในมือ^000000";
		mes "เมื่อทำเสร็จแล้วไม่สามารถย้อนกลับได้";
		next;
		switch(select("Combine.:Quit.")) {
		case 1:
				if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
						mes "- เดี๋ยว!! -";
						mes "- ไม่สามารถรับสินค้าได้-";
						mes "- เพราะคุณพกมันมากเกินไป -";
						mes "- กรุณาลองอีกครั้ง -";
						mes "- หลังจากผ่อนภาระของคุณลงแล้ว -";
						close;
				}
				return;
		case 2:
				mes "[กี้]";
				mes "คิดให้ดีก่อน~~";
				close;
		}
		end;
}
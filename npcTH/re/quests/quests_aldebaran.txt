//===== rAthena Script =======================================
//= NPCs related to Aldebaran Clock Tower (Nightmare)
//===== By: ==================================================
//= Ridley8819
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Additional Comments: =================================
//= 1.0 Initial release [Ridley8819]
//= 1.1 Dialogue cleanup [Aleos]
//============================================================

c_tower1,232,222,5	script	Belljamin Button#ct	968,{
		mes "[เบลจามิน บัตตัน]";
		mes "เหล่าสัตว์ประหลาดที่นี่สามารถควบคุมเวลาได้";
		if( BaseLevel < 150 ){
				mes "กลับมาเมื่อคุณถึง ^0000FFระดับ 150^000000";
				close;
		}
		mes "เวลา ความทรงจำ สภาพแวดล้อม... ทุกอย่างอาจบิดเบือนได้ที่นี่";
		next;
		mes "[เบลจามิน บัตตัน]";
		mes "ฉันขอร้องคุณอย่าถามเรื่องนี้อีกเลย มันอาจเป็นอันตรายต่อชีวิตคุณได้";
		next;
		.@menu = select("Don't ask:Go to 2nd floor of warped tower:Go to 3rd floor of warped tower");
		switch(.@menu){
				case 1:
						break;
				case 2:
				case 3:
						if( .@menu == 2 ){
								.@map$ = "c_tower2_";
								.@x = 268;
								.@y = 26;
						}else{
								.@map$ = "c_tower3_";
								.@x = 65;
								.@y = 147;
						}
						mes "[เบลจามิน บัตตัน]";
						mes "โปรดระมัดระวังในการเลือกของคุณ";
						next;
						mes "[เบลจามิน บัตตัน]";
						mes "ฉันต้องให้ ^0000FF10,000z^000000 เพื่อปล่อยคุณเข้ามา มันเป็นสถานที่ที่ค่อนข้างอันตราย";
						next;
						switch( select( "Quit:Pay 10,000z:Why do you need money?" ) ){
								case 1:
										break;
								case 2:
										if( Zeny < 10000 ){
												mes "[เบลจามิน บัตตัน]";
												mes "จริงเหรอ? คุณไม่มีเงินแล้วมาขอให้ฉันพาไปเหรอ?";
												close;
										}
										Zeny -= 10000;
										mes "[เบลจามิน บัตตัน]";
										mes "ฉันไม่แน่ใจว่าคุณจะติดตามเวลาในนั้นได้อย่างไร แต่จงระวังให้มากที่สุดเท่าที่ทำได้";
										close2;
										warp .@map$, .@x, .@y;
										end;
								case 3:
										mes "[เบลจามิน บัตตัน]";
										mes "ฉันอยู่ที่นี่ตลอดทั้งวันเพื่อความปลอดภัยของนักผจญภัย ฉันคอยแจ้งให้พวกเขาทราบถึงอันตรายอยู่เสมอ นั่นเป็นเหตุผลว่าทำไมฉันถึงต้องการการชำระเงินในรูปแบบใดรูปแบบหนึ่ง";
										emotion ET_SWEAT;
										close;
						}
						break;
		}
		mes "[เบลจามิน บัตตัน]";
		mes "เฮ้อ...คุณเลือกถูกแล้ว ที่นี่โหดร้ายมาก คุณควรจะกลับไปที่ที่ปลอดภัย";
		close;
}

c_tower2_,274,26,3	script	Belljamin Button#nct1	962,{
		mes "[เบลจามิน บัตตัน]";
		mes "บางทีคุณอาจเคยเห็นฉันมาก่อนแล้วใช่ไหม?";
		next;
		mes "[เบลจามิน บัตตัน]";
		mes "คุณเห็นฉันอยู่ในสถานที่ที่แตกต่างออกไป ด้วยรูปลักษณ์ที่แตกต่างออกไป";
		next;
		if( select( "Who are you?:Get me out of here!" ) == 1 ){
				mes "[เบลจามิน บัตตัน]";
				mes "คุณอธิบายให้ฉันฟังได้ไหมว่าคุณได้เห็นอะไรและทำอะไร?";
				next;
				mes "[เบลจามิน บัตตัน]";
				mes "ฉันก็เหมือนกัน คุณอาจเป็นคนหายไปก็ได้ ไม่รู้ตัว ไม่รู้จุดประสงค์หรือความคิด";
				next;
				mes "[เบลจามิน บัตตัน]";
				mes "ถึงเวลาต้องกลับแล้ว โปรดใช้เวลาให้คุ้มค่ามากขึ้น";
				next;
				if( select( "Stay a little bit more:Please take me back" ) == 1 ){
						mes "[เบลจามิน บัตตัน]";
						mes "คุณคงชอบที่นี่มากแน่ ๆ เลย จะทำอะไรก็ได้ตามใจชอบ";
						close;
				}
		}else{
				mes "[เบลจามิน บัตตัน]";
				mes "ถึงเวลาต้องก้าวต่อไปแล้ว คุณมีเครื่องดื่มแก้วโปรดหรือยัง?";
				next;
		}
		mes "[เบลจามิน บัตตัน]";
		mes "ลองนึกถึงเครื่องดื่มในถ้วย ค่อยๆ คนถ้วยด้วยช้อนและมองดูเครื่องดื่มที่เปลี่ยนสี";
		close2;
		warp "c_tower1", 235, 223;
		end;
}

c_tower3_,59,148,5	duplicate(Belljamin Button#nct1)	Belljamin Button#nct2	574